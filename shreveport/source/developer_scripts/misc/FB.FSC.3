************************************************************************
* Program name : FB.FSC.3
* Author       : David R. Friend
* Date written : 10/02/00
* Library      : FBBP
* COPYRIGHT (C) 2016 - AFS LOGISTICS LLC - ALL RIGHTS RESERVED.
*
* enter FSC codes and amounts by date
* Last updated by AFS (gbino) at 07:57:18 on 12/20/2017.
*
* Start modification history
* 012215 - DRF01 - SysAid 14856 Gave Ken access
* 031615 - DRF02 - SysAid 15959 Gave April the same access as Cynthia
* 081117 - DRF03 - Ticket 5671 Gave Kaitlin access to the program
* End modification history
**********************************************************************
$OPTIONS HEADER.EJECT
$OPTIONS EXEC.EQ.PERF
$OPTIONS INFO.ABORT
      ERRMSG=''

* Initialize variables.
*
20:   PROG.NAME='FB.FSC.3'
      PROG.DESC='FUEL INDEX TABLE'
      PACK.NAME='FBBP'
      CALL AFS.SCR.HEAD(CO.ID,FILE.ID,'',PROG.NAME,PROG.DESC,PACK.NAME,CO.NAME,TIME.DATE,1)
      UPL.VIN = 'FBBP' ; UPL.VIN<2> = 'FB.FSC.3' ; UPL.VOUT = ''       ; * NPR_UPL 04/23/2010
      CALL UPD.PROGRAM.LOG(UPL.VIN,UPL.VOUT)       ; * NPR_UPL 04/23/2010
      PROMPT''
      PAGE.LEN=18
      MODE=''
      FIND.LOCKED.NAME=''
*
* Open files.
*
      OPEN '','FB.TABLES' TO F.FB.TABLES ELSE CALL OPEN.ABORT("FB.TABLES",PROG.NAME)
      OPEN '','OPERATOR' TO F.OP ELSE CALL OPEN.ABORT("OPERATOR",PROG.NAME)      ; * GRB01
      CALL GET.USER.NEW(USER,CH.NUM)
      READV OP.FLG FROM F.OP,USER,56 ELSE
         OP.FLG = "N"
      END
      IF OP.FLG # "Y" THEN
         CALL FB.PRINT.CENT(1,80,12,'Restricted Access Application - Access Denied')
         CALL FB.PRINT.CENT(1,80,14,'Request access through david, greg')
         CALL ELINE('')
         GOTO 9999
      END
      
      READ FSC.HEAD FROM F.FB.TABLES,'FSC*FSC' ELSE FSC.HEAD=''
********************************************************      	
50:   * enter rec code
********************************************************
      HELP='Enter The Record Code To Update or FSC'
      CALL GEN.IN(0,22,'Enter Table Code -----','',Q,0,5,'','',0,-5,22,0,QI,HELP,0,23)
      QI=OCONV(QI,'MCU')
      BEGIN CASE
         CASE QI='X' OR QI='EX' OR QI=''
            STOP
         CASE QI='FSC'
         CASE 1
            LOCATE QI IN FSC.HEAD<1> SETTING POS ELSE
               CALL ELINE('Record: ':QI:' is not setup in the FSC*FSC')
               GOTO 50
            END
      END CASE
      REC.NUM=QI
      PROG.DESC=PROG.DESC:' - ':REC.NUM
      IF REC.NUM#'FSC' THEN
         PROG.DESC=PROG.DESC:' - ':FSC.HEAD<2,POS>'L#30'
      END
      CALL AFS.SCR.HEAD(CO.ID,FILE.ID,'',PROG.NAME,PROG.DESC,PACK.NAME,CO.NAME,TIME.DATE,1)
*
* GET FSC RECORD
*
      READU FSC.REC FROM F.FB.TABLES,'FSC*':REC.NUM LOCKED
         FIND.LOCKED.NAME = 'FB.TABLES,FSC*':REC.NUM
         GOSUB FIND.LOCKED
         CALL ELINE(FL.USER:' has ':FIND.LOCKED.NAME:' locked. Press enter to exit program.')
         GOTO 9999
      END ELSE
         FSC.REC=''
      END
      OLD.FSC.REC=FSC.REC
      PAGE=1
      GOSUB 9000

********************************************************
100:* Enter selection code.
********************************************************
      HELP="Enter line number 'A'=Add 'B'=Back 'T'=Top ''=Next page 'E'=End 'EX'=Exit."
      CALL GEN.IN(0,22,"Enter line number please. ###",'',Q,0,3,'','',1,-3,22,0,QI,HELP,0,23)
      QI=OCONV(QI,'MCU')
      BEGIN CASE
         CASE QI=''
            IF FSC.REC<1,PAGE*PAGE.LEN>='' THEN
               CALL ELINE('Error! You are on the last page!')
               GO 100
            END
            PAGE=PAGE+1
            GOSUB 9000
         CASE QI='A'
            MODE='ADD'
            ADDING=1
            OLD.PAGE=0
            LOOP
            WHILE ADDING
               NUM.ITEM=DCOUNT(FSC.REC<2>,@VM)
               SEL=NUM.ITEM+1
               IF MOD(SEL,PAGE.LEN) > 0 AND MOD(SEL,PAGE.LEN) < PAGE.LEN/2 THEN
                  PAGE=OCONV(SEL/PAGE.LEN,'MR0')+1
               END ELSE
                  PAGE=OCONV(SEL/PAGE.LEN,'MR0')
               END
               IF OLD.PAGE#PAGE THEN
                  GOSUB 9000
                  OLD.PAGE=PAGE
               END
               GOSUB 8000
            REPEAT
            MODE=''
         CASE QI='B'
            PAGE=PAGE-1
            IF PAGE=0 THEN PAGE=1
            GOSUB 9000
         CASE QI='T'
            PAGE=1
            GOSUB 9000
         CASE QI='EX'
            IF OLD.FSC.REC#FSC.REC THEN
               GOSUB ASK.FOR.CONFIRMATION
               IF NOT(CONFIRMATION) THEN GOTO 100
            END
            GOTO 9999
         CASE NUM(QI)
            SEL=QI
            IF SEL LT START ! SEL GT STOP THEN
               CALL ELINE('Invalid selection.')
               GOTO 100
            END
            IF SEL = 1 ELSE
               IF FSC.REC<1,SEL-1> = '' THEN
                  CALL ELINE('Invalid entry. Use next available line.')
                  GOTO 100
               END
            END
            GOSUB 8000
         CASE QI='E'
            X=DCOUNT(FSC.REC<1>,CHAR(253))
            FOR I=X TO 1 STEP -1
               IF FSC.REC<2,I>='' THEN
                  FSC.REC=DELETE(FSC.REC,1,I)
                  FSC.REC=DELETE(FSC.REC,2,I)
               END
            NEXT I
            VIN=''
            VIN<1>=1
            IF REC.NUM='FSC' THEN
               VIN<2>='L'
            END ELSE
               VIN<2>='R'
               VIN<3>='D'
            END
            AIN=FSC.REC
            CALL SORT.ARRAY(VIN,VOUT,AIN,AOUT)
            FSC.REC=AOUT
            WRITE FSC.REC ON F.FB.TABLES,'FSC*':REC.NUM
            GOTO 20
      END CASE
      GOTO 100

********************************************************
8000:* EDIT REC
********************************************************
      ROW=3+REM(SEL,PAGE.LEN)
      IF ROW=3 THEN ROW=21
      IF REC.NUM='FSC' THEN
         CODE=FSC.REC<1,SEL>
         DESC=FSC.REC<2,SEL>
      END ELSE
         FSC.DATE=FSC.REC<1,SEL>
         FSC.PERCENT=FSC.REC<2,SEL>
      END
*
********************************************************
8015:
********************************************************
      IF REC.NUM='FSC' THEN
         HELP="Enter the Code. ''=Accept. 'D'=Delete. 'EX'=Exit."
         IF CODE='' THEN MASK=STR('-',3) ELSE MASK=CODE'L#3'
         CALL GEN.IN(5,ROW,MASK,'',Q,0,3,'','',2,5,ROW,0,QI,HELP,0,23)
         IF QI='' THEN QI=CODE
         QI=OCONV(QI,'MCU')
         IF MODE='ADD' THEN
            IF QI='' AND SEL#1 THEN
               ADDING=0                  ; * Finished adding
               RETURN
            END
         END
         BEGIN CASE
            CASE QI='D'
               FSC.REC=DELETE(FSC.REC,1,SEL)
               FSC.REC=DELETE(FSC.REC,2,SEL)
               GOSUB 9000
               GOTO 100
            CASE QI='EX'
               IF OLD.FSC.REC#FSC.REC THEN
                  GOSUB ASK.FOR.CONFIRMATION
                  IF NOT(CONFIRMATION) THEN GOTO 8015
               END
               GOTO 9999
            CASE QI=''
               IF SEL#1 THEN
                  CALL ELINE('Invalid entry. Data required in this field.')
                  GOTO 8015
               END
            CASE INDEX(QI,' ',1)
               CALL ELINE('Invalid entry. No spaces allowed.')
               GOTO 8015
               RETURN
         END CASE
         LOCATE QI IN FSC.REC<1> SETTING TEST.POS THEN
            IF TEST.POS # SEL THEN
               CALL ELINE('Code ':QI:' is already on file on line number ':TEST.POS)
               GO 8015
            END
         END
         CODE=QI
         CRT @(5,ROW):CODE'L#3'
*
********************************************************
8050:
********************************************************
         HELP="Enter the Description. ''=Accept. [EX]-Exit."
         IF DESC='' THEN MASK=STR('-',35) ELSE MASK=DESC'L#35'
         CALL GEN.IN(10,ROW,MASK,'',Q,0,35,'','',2,10,ROW,0,QI,HELP,0,23)
         QI=OCONV(QI,'MCU')
         IF QI='' THEN
            IF DESC#'' THEN
               Q=DESC
            END ELSE
               CALL ELINE('Entry is required for this field')
               GOTO 8050
            END
         END
         BEGIN CASE
            CASE QI='X'
               CRT @(10,ROW):DESC'L#35'
               GOTO 8015
            CASE QI='EX'
               IF OLD.FSC.REC#FSC.REC THEN
                  GOSUB ASK.FOR.CONFIRMATION
                  IF NOT(CONFIRMATION) THEN GOTO 8050
               END
               GOTO 9999
         END CASE
         DESC=Q
         CRT @(10,ROW):DESC'L#35'
*
      END ELSE
********************************************************     	
8100:
********************************************************
         HELP="Enter the Monday Date. ''=Accept. 'D'=Delete. 'EX'=Exit."
         IF FSC.DATE='' THEN MASK='##/##/##' ELSE MASK=OCONV(FSC.DATE,'D2/')'L#8'
         CALL GEN.IN(5,ROW,MASK,'DATE',Q,0,8,'','',2,5,ROW,0,QI,HELP,0,23)
         IF QI='' THEN QI=FSC.DATE
         QI=OCONV(QI,'MCU')
         IF MODE='ADD' THEN
            IF QI='' AND SEL#1 THEN
               ADDING=0                  ; * Finished adding
               RETURN
            END
         END
         BEGIN CASE
            CASE QI='D'
               FSC.REC=DELETE(FSC.REC,1,SEL)
               FSC.REC=DELETE(FSC.REC,2,SEL)
               GOSUB 9000
               GOTO 100
            CASE QI='EX'
               IF OLD.FSC.REC#FSC.REC THEN
                  GOSUB ASK.FOR.CONFIRMATION
                  IF NOT(CONFIRMATION) THEN GOTO 8015
               END
               GOTO 9999
            CASE QI=''
               IF SEL#1 THEN
                  CALL ELINE('Invalid entry. Data required in this field.')
                  GOTO 8100
               END
            CASE INDEX(QI,' ',1)
               CALL ELINE('Invalid entry. No spaces allowed.')
               GOTO 8100
               RETURN
            CASE NOT(NUM(Q))
               CALL ELINE('Date must be numeric')
               GO 8100
         END CASE
         IF MOD(Q,7)#1 THEN
            *** This CASE statement makes exceptions for days immediately after a holiday that falls on a Monday
            *** Requested by Wayne Pardue
            BEGIN CASE
               CASE Q = 14240            ; * JCW 01/23/07, for Tuesday after Christmas '06
               CASE Q = 14247            ; * JCW 01/30/07, for Tuesday after New Years '07
               CASE Q = 14394            ; * JCW 05/30/07, for Tuesday after Memorial Day '07, C200705300003
               CASE MOD(Q,7)=2
                  CALL ELINE('Warning Date is not a Monday')
               CASE 1
8150:             HELP='Date is not a Monday or Tuesday. Is this correct?'
                  CALL GEN.IN(0,22,'Are You Sure The Date ':OCONV(Q,'D2/'):' is Correct: N','',Q.TEMP,0,1,'','',2,-1,22,0,QI.TEMP,HELP,0,23)
                  QI.TEMP=OCONV(QI.TEMP,'MCU')
                  BEGIN CASE
                     CASE QI.TEMP='N' OR QI.TEMP=''
                        GO 8100
                     CASE QI.TEMP='Y'
                     CASE 1
                        CALL ELINE('Invalid Entry')
                        GOTO 8150
                  END CASE
                  SUBJ='Fuel Index Table for a date other than Monday or Tuesday'
                  TEXT=USER:' has entered a fuel index table ':REC.NUM:' for ':OCONV(Q,'DWA'):' ':OCONV(Q,'D2/'):', this may affect when this entry is active because it is usually entered on Mondays.'
                  MVIN=''
                  MVIN<2>='DFriend@afs.net'
                  MVIN<2,-1>='SWinstead@afs.net'
                  MVIN<2,-1>='SSelman@afs.net'
                  MVIN<4>='AFS'
                  MVIN<5>=SUBJ
                  MVIN<6>=TEXT
                  CALL SENDMAIL(MVIN,MVOUT)
            END CASE
         END
         LOCATE Q IN FSC.REC<1> SETTING TEST.POS THEN
            IF TEST.POS # SEL THEN
               CALL ELINE('Date ':OCONV(Q,'D2/'):' is already on file on line number ':TEST.POS)
               GO 8100
            END
         END
         FSC.DATE=Q
         CRT @(5,ROW):OCONV(FSC.DATE,'D2/')'L#8'
*
********************************************************
8200:* index pct
********************************************************
         HELP="Enter the Fuel Index Percent. ''=Accept. [EX]-Exit."
         IF FSC.PERCENT='' THEN MASK='#.###' ELSE MASK=FSC.PERCENT'R37#5'
         CALL GEN.IN(15,ROW,MASK,'AMT3',Q,0,5,'','',2,15,ROW,0,QI,HELP,0,23)
         QI=OCONV(QI,'MCU')
         IF QI='' THEN
            IF FSC.PERCENT#'' THEN
               Q=FSC.PERCENT
            END ELSE
               CALL ELINE('Entry is required for this field')
               GOTO 8200
            END
         END
         BEGIN CASE
            CASE QI='X'
               CRT @(15,ROW):FSC.PERCENT'R37#5'
               GOTO 8100
            CASE QI='EX'
               IF OLD.FSC.REC#FSC.REC THEN
                  GOSUB ASK.FOR.CONFIRMATION
                  IF NOT(CONFIRMATION) THEN GOTO 8100
               END
               GOTO 9999
         END CASE
         FSC.PERCENT=Q
         CRT @(15,ROW):FSC.PERCENT'R37#5'
      END
*
      IF REC.NUM='FSC' THEN
         FSC.REC<1,SEL>=CODE
         FSC.REC<2,SEL>=DESC
      END ELSE
         FSC.REC<1,SEL>=FSC.DATE
         FSC.REC<2,SEL>=FSC.PERCENT
      END
      RETURN
*
********************************************************
9000: * DISPLAY EXPENSE ARRAY
********************************************************
      CRT @(00,3):@(-3):"Line":
      IF REC.NUM='FSC' THEN
         CRT @(05,3):"Code":
         CRT @(10,3):"Description":
      END ELSE
         CRT @(05,3):"  Date  "
         CRT @(15,3):"Percent"
      END
      START=(PAGE*PAGE.LEN)-17
      STOP=START+17
      FOR I=START TO STOP STEP 1
         ROW=3+REM(I,PAGE.LEN)
         IF ROW=3 THEN ROW=21
         PRINT @(00,ROW):I"R#3":
         IF REC.NUM='FSC' THEN
            CODE=FSC.REC<1,I>
            DESC=FSC.REC<2,I>
            PRINT @(05,ROW):CODE'L#3':
            PRINT @(10,ROW):DESC'L#35':
         END ELSE
            FSC.DATE=FSC.REC<1,I>
            FSC.PERCENT=FSC.REC<2,I>
            PRINT @(05,ROW):OCONV(FSC.DATE,'D2/')'L#8'
            IF FSC.PERCENT='' THEN
               PRINT @(15,ROW):SPACE(5)
            END ELSE
               PRINT @(15,ROW):OCONV(FSC.PERCENT,'MR3')'R#5'
            END
         END
      NEXT I
      RETURN

********************************************************
FIND.LOCKED:
********************************************************
      STMT = 'LIST.READU EVERY'
      EXECUTE STMT CAPTURING OUTPUT
      FL.LEN = DCOUNT(OUTPUT,@AM)
      FL.USER = 'Someone'
      FOUND = 0

      FOR X = 1 TO FL.LEN UNTIL FOUND
         T.USER = OCONV(TRIM(OUTPUT<X>[44,9]),'MCU')
         T.FILE = TRIM(OUTPUT<X>[53,17])
         IF T.FILE = FIND.LOCKED.NAME THEN
            FL.USER = T.USER
            FOUND = 1
         END
      NEXT

      RETURN
*
********************************************************
ASK.FOR.CONFIRMATION:***
********************************************************
      HELP='Enter <Y>es to exit without saving or <N>o to return to program'
      CALL GEN.IN(0,22,'Are You Sure You Want To Exit: N','',Q,0,1,'','',2,-1,22,0,QI,HELP,0,23)
      QI=OCONV(QI,'MCU')
      BEGIN CASE
         CASE QI='N' OR QI=''
            CONFIRMATION=0
         CASE QI='Y'
            CONFIRMATION=1
         CASE 1
            CALL ELINE('Invalid Entry')
            GOTO ASK.FOR.CONFIRMATION
      END CASE
      CRT @(0,22):@(-3):
      RETURN
********************************************************
9999:* EXIT PROGRAM
********************************************************
      STOP
