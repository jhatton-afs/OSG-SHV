* PROGRAM FB.TL.FSC.LIST
* PROGRAM TO MODIFY FREIGHT BILL STATUS
*
* FREIGHT BILLING PACKAGE
*
* AUTHOR: Stephen B. Cook
* DATE:   09.21.94
*
*---- cloned from FB.1.16
* Initialize variables
*
      $OPTIONS HEADER.EJECT
      $OPTIONS EXEC.EQ.PERF
      $OPTIONS INFO.ABORT
      ERRMSG=''
      PROMPT''
      PROG.NAME='FB.TL.FSC.LIST'
      PROG.DESC='EDIT TL CARRIER FSC LIST'
      PACK.NAME='FREIGHT BILLING'
      CALL AFS.SCR.HEAD(CO.ID,FILE.ID,'',PROG.NAME,PROG.DESC,PACK.NAME,CO.NAME,TIME.DATE,1)
       UPL.VIN = 'FBBP' ;  UPL.VIN<2> = 'FB.TL.FSC.LIST' ; UPL.VOUT = ''                    ;* NPR_UPL 04/23/2010
       CALL UPD.PROGRAM.LOG(UPL.VIN,UPL.VOUT)                                                           ;* NPR_UPL 04/23/2010
      CALL GET.USER(USER)
      CALL CHANNEL(CH.NUM)
      BEGIN CASE
         CASE USER='BRIAN'
         CASE USER='KAREN'
         CASE USER='OWEN'
         CASE USER='NPRATT'
         CASE USER='MKERBOW'
         CASE USER='KRISTY'
         CASE USER='BTYOUNG'
         CASE 1
            CALL ELINE ('Unauthorized access')
            STOP
      END CASE
*
* Open files
*
      OPEN '','CARRIERS' TO F.CARRIERS ELSE CALL OPEN.ABORT("CARRIERS",PROG.NAME)
      OPEN '','FB.TABLES' TO F.TABLES ELSE CALL OPEN.ABORT("FB.TABLES",PROG.NAME)
*
* Select codes and build display array
*
      READ FB.LIST.REC FROM F.TABLES,'FB.TL.FSC.LIST' ELSE FB.LIST.REC=''
      DISP.REC=''
      NVAL=DCOUNT(FB.LIST.REC<1>,@VM)
      FOR VAL=1 TO NVAL
         CARRIER.ID=FB.LIST.REC<1,VAL>
         READ CARRIER.REC FROM F.CARRIERS,CARRIER.ID ELSE CARRIER.REC=''
         IF CARRIER.REC#'' THEN
            CARRIER.NAME=CARRIER.REC<1>
         END ELSE
            CARRIER.NAME='Unknown'
         END
         DISP.REC<1,VAL>=CARRIER.ID
         DISP.REC<2,VAL>=CARRIER.NAME
      NEXT VAL
*
* First page of codes
*
      PAGE=1
      GOSUB 9000
*
* Enter selection code
*
100   HELP="Enter line number. 'B'=Back. 'T'=Top, ''=Next page. 'E'=End. 'EX'=Exit."
      CALL GEN.IN(0,22,"Enter line number please. ###",'',Q,0,3,'','',1,-3,22,0,QI,HELP,0,23)
      QI=OCONV(QI,'MCU')
      BEGIN CASE
         CASE QI=''
            PAGE=PAGE+1
            GOSUB 9000
         CASE QI='B'
            PAGE=PAGE-1
            IF PAGE=0 THEN PAGE=1
            GOSUB 9000
         CASE QI='T'
            PAGE=1
            GOSUB 9000
         CASE QI='EX'
            STOP
         CASE NUM(QI)
            SEL=QI
            IF SEL LT START ! SEL GT STOP THEN
               CALL ELINE('Invalid selection.')
               GOTO 100
            END
            GOSUB 8000
         CASE QI='E'
            X=DCOUNT(DISP.REC<1>,@VM)
            FOR I=X TO 1 STEP -1
               IF DISP.REC<1,I>='' THEN
                  DISP.REC=DELETE(DISP.REC,1,I)
                  DISP.REC=DELETE(DISP.REC,2,I)
               END
            NEXT I
*
            NEW.DISP.REC=''
            NVAL=DCOUNT(DISP.REC<1>,@VM)
            FOR VAL=1 TO NVAL
               LOCATE DISP.REC<2,VAL> IN NEW.DISP.REC<2> BY 'AR' SETTING CPOS ELSE
                  INS DISP.REC<1,VAL> BEFORE NEW.DISP.REC<1,CPOS>
                  INS DISP.REC<2,VAL> BEFORE NEW.DISP.REC<2,CPOS>
               END
            NEXT VAL
            DISP.REC=NEW.DISP.REC
*
            FB.LIST.REC=DISP.REC<1>
            IF FB.LIST.REC='' THEN
               DELETE F.TABLES,'FB.TL.FSC.LIST'
            END ELSE
               WRITE FB.LIST.REC ON F.TABLES,'FB.TL.FSC.LIST'
            END
            STOP
      END CASE
      GOTO 100
*
*------------------------------------------------------------------------
* INTERNAL SUBROUTINES
*------------------------------------------------------------------------
*
* Edit code
*
8000  ROW=3+REM(SEL,18)
      IF ROW=3 THEN ROW=21
      CODE=DISP.REC<1,SEL>
      DESC=DISP.REC<2,SEL>
8010  HELP="Enter carrier number, 'name' for search, [L]ist, [EX]it."
      CALL GEN.IN(5,ROW,CODE'L#5','',Q,0,5,'','',2,-5,ROW,0,QI,HELP,0,23)
      QI=OCONV(QI,'MCU')
      BEGIN CASE
         CASE QI='X'
            CRT @(5,ROW):CODE'L#5'
            RETURN
         CASE QI='L'
            EXECUTE 'SORT CARRIERS BY 1 1'
            CALL ELINE('')
         CASE QI=''
            Q=CODE
         CASE QI='D'
            DISP.REC=DELETE(DISP.REC,1,SEL)
            DISP.REC=DELETE(DISP.REC,2,SEL)
            GOSUB 9000
            RETURN
         CASE NUM(QI)
            CARRIER.ID=Q'R%5'
            READ CARRIER.REC FROM F.CARRIERS,CARRIER.ID ELSE
               CALL ELINE('Invalid Carrier. Please try again.')
               GO 8010
            END
            CODE=CARRIER.ID
            DESC=CARRIER.REC<1>
         CASE 1
            CALL SOUNDEX.DISPLAY(Q,'CARRIERS','SDX.CARRIER,NAME','1,3,4',QX)
            CT=DCOUNT(QX<1>,@VM)
            IF CT=1 THEN
               DATA QX<1,1>
            END
            CALL AFS.SCR.HEAD(CO.ID,FILE.ID,'',PROG.NAME,PROG.DESC,PACK.NAME,CO.NAME,TIME.DATE,1)
            GOSUB 9000
            GO 8010
      END CASE
      DISP.REC<1,SEL>=CODE
      DISP.REC<2,SEL>=DESC
      GOSUB 9000
      RETURN
*
* Display array
*
9000  CRT @(0,3):@(-3):"  #":@(5,3):"Code   Description"
      START=(PAGE*18)-17
      IF START GT 108 THEN
         START=1
         PAGE=1
      END
      STOP=START+17
      FOR I=START TO STOP STEP 1
         ROW=3+REM(I,18)
         IF ROW=3 THEN ROW=21
         CODE=DISP.REC<1,I>
         DESC=DISP.REC<2,I>
         PRINT @(0,ROW):I"R#3":'  ':CODE"L#5":'  ':DESC"L#40"
      NEXT I
      RETURN
