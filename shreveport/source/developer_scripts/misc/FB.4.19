********************************************************************************
*  FB.4.19 Mark Old Accounts Inactive
*  Copyright (C) 2001 by AFS Logistics, LLC.
*  Author:  James Barberousse
********************************************************************************
* Modifications
* 083112 - RLM01 - SysAid#3073 - Replace former employees' email addresses with current employees' email addresses
*
********************************************************************************

      CO.NAME='AFS Logistics, LLC'
      PROG.NAME='FB.4.19'
      PROG.DESC='Mark Old Accounts Inactive'
      PACK.NAME='Freight Payment'
      MASK.CHAR='-'
       UPL.VIN = 'FBBP' ;  UPL.VIN<2> = 'FB.4.19' ; UPL.VOUT = ''                    ;* NPR_UPL 04/23/2010
       CALL UPD.PROGRAM.LOG(UPL.VIN,UPL.VOUT)                                                           ;* NPR_UPL 04/23/2010
      CALL GET.USER(USER.NAME)
      USER.NAME=OCONV(USER.NAME,'MCU')

      CALL AFS.SCR.HEAD(CO.ID,FILE.ID,'',PROG.NAME,PROG.DESC,PACK.NAME,CO.NAME,TIME.DATE,1)
      CRT @(0,2):STR('=',79)

      CRT @(0,5):'This program will read the client file and mark all clients inactive'
      CRT @(0,6):'that HAVE NOT been processed within the last 30 days.  A list of'
      CRT @(0,7):'these clients will be emailed to Brian, Karen, Owen, SWinstead, and Myra.'

      VALID.USER=1
      BEGIN CASE
         CASE USER.NAME='BRIAN'
            NULL
         CASE USER.NAME='KAREN'
            NULL
         CASE USER.NAME='OWEN'
            NULL
         CASE USER.NAME='SWinstead'
            NULL
         CASE USER.NAME='MYRA'
            NULL
         CASE USER.NAME='DAVID'
            NULL
         CASE 1
            VALID.USER=0
      END CASE
      IF NOT(VALID.USER) THEN 
         CRT @(0,10):'You do not have permission to run this program.'
         GOTO THE.END; * Abort program
      END

      LN='Do you want to continue? '
      MASK='N':STR(MASK.CHAR,1)
      HELP='[Y]=Yes [N]=No [EX]=Exit'
100:  CALL GEN.IN(0,22,LN:MASK,'',Q,0,2,'','',2,-2,22,1,QI,HELP,0,23)
      QI=OCONV(QI,'MCU')
      BEGIN CASE
         CASE QI='EX' OR QI='N' OR QI=''
            GOTO THE.END
         CASE QI='Y'
            NULL
         CASE 1
            GOTO 100
      END CASE

      OPEN '','CLIENTS' TO F.CLIENTS ELSE
         CALL OPEN.ABORT("CLIENTS",PROG.NAME)
      END

      INACTIVE.CLIENTS=''
      ACTIVE.CLIENTS=''
      END.DATE=DATE()-30
      
      STMT= 'SSELECT CLIENTS WITH 30 = "A" AND WITH 82 NE "" AND WITH 82 LE '
      STMT:=END.DATE'D2/':' '
      STMT:='BY NAME'
      EXECUTE STMT PASSLIST CAPTURING OUTPUT
      NUM.ITMS=@SELECTED
      DONE=0
      LOOP
         READNEXT CLIENT.ID ELSE DONE=1
      UNTIL DONE=1 DO
         IF NOT(CLIENT.ID[1,2]='99' OR CLIENT.ID[1,2]='98' OR CLIENT.ID='80000' OR CLIENT.ID='00000') THEN
            READ CLIENT.REC FROM F.CLIENTS,CLIENT.ID THEN
               LINE=CLIENT.ID:@VM:CLIENT.REC<2>:@VM:CLIENT.REC<82>
               CLIENT.REC<30>='I'
               WRITE CLIENT.REC ON F.CLIENTS,CLIENT.ID THEN
                  INACTIVE.CLIENTS<-1>=LINE
               END ELSE
                  ACTIVE.CLIENTS<-1>=LINE
               END
            END
         END
      REPEAT

      * Send email showing inactive clients
      EMAIL.OUT=''
      EMAIL.OUT<1,-1>='Brian@afs.net';     * To: Email addresses
*     EMAIL.OUT<1,-1>='KGilbert@afs.net'  ; * RLM01
      EMAIL.OUT<1,-1>='brose@afs.net'  ; * RLM01
*     EMAIL.OUT<1,-1>='oweninfo@afs.net'  ; * RLM01
*     EMAIL.OUT<1,-1>='bfranklin@afs.net'  ; * RLM01
      EMAIL.OUT<1,-1>='SMatthews@afs.net'
****      EMAIL.OUT<1,-1>='Myra@afs.net'
*     EMAIL.OUT<1,-1>='J-ames@afs.net'
      EMAIL.OUT<2>   ='';                           * CC: Email addresses
      EMAIL.OUT<3>   ='';                           * BCC: Email addresses
      EMAIL.OUT<4>   ='Reports@afs.net';   * From Email address
      EMAIL.OUT<5,1> ='Clients marked as INACTIVE'; * Subject of Email

      * Email body
      EMAIL.OUT<6,-1>='The following ACTIVE clients have not been processed within'
      EMAIL.OUT<6,-1>='the last 30 days and have been marked as INACTIVE:'
      EMAIL.OUT<6,-1>=' '
      EMAIL.OUT<6,-1>='Item Acct#           Client Name          Last Processed Date'
      EMAIL.OUT<6,-1>='---- ----- ------------------------------ -------------------'
      IF INACTIVE.CLIENTS#'' THEN
         NUM=DCOUNT(INACTIVE.CLIENTS,@AM)
         FOR X=1 TO NUM
            EMAIL.OUT<6,-1>=X'R#4':' ':INACTIVE.CLIENTS<X,1>:' ':INACTIVE.CLIENTS<X,2>'L#30':' ':INACTIVE.CLIENTS<X,3>'D2/'
         NEXT X
      END ELSE
         EMAIL.OUT<6,-1>='       *** There are none ***'
      END

      IF ACTIVE.CLIENTS#'' THEN
         EMAIL.OUT<6,-1>=' '
         EMAIL.OUT<6,-1>='The following ACTIVE clients have not been processed within the last'
         EMAIL.OUT<6,-1>='30 days but have not been marked as INACTIVE due to a write error:'
         EMAIL.OUT<6,-1>=' '
         NUM=DCOUNT(ACTIVE.CLIENTS,@AM)
         FOR X=1 TO NUM
            EMAIL.OUT<6,-1>=X'R#4':' ':ACTIVE.CLIENTS<X,1>:' ':ACTIVE.CLIENTS<X,2>'L#30':' ':ACTIVE.CLIENTS<X,3>'D2/'
         NEXT X
      END

      EMAIL.OUT<7>=1;                              * Auto format (add cr to end of text)
      VIN=EMAIL.OUT
      CALL SENDMAIL(VIN,VOUT)


THE.END:
*     End of program
      CRT @(0,22)
      END
