* PROGRAM EMPLOYEE LIST FOR ENTRY TO SQL TABLE
*
* AUTHOR: ROB GAUDET
* DATE: APRIL 2, 2002
* MODIFIED: JOSH MCADAMS - 29 APRIL 2002
* MODIFIED: ROB GAUDET - 18 AUGUST 2003 ADDED TERM DATE.
* 051305 - KWM01 - CHANGED METHOD OF GETTING SALESMAN ID 
*                  SINCE IT WAS ADDED TO THE EMPLOYEE FILE
* 060805 - JMK01 - ADD LOCATION ID TO EXPORT RECORD
* 080305 - JMK02 - ADD SS ACCESS LIST TO EXPORT RECORD
* 120905 - KWM02 - ADD EXEMPT FLAG TO EXPORT RECORD
* 091208 - JMK03 - C082129 - Enforce new user e-mail
*
* THIS PROGRAM IS A PROPRIETARY PRODUCT OF AUTOMATED FREIGHT SYSTEMS
* AND MAY NOT BE USED, COPIED OR MODIFIED WITHOUT THE EXPRESS
* WRITTEN CONSENT OF SAME.
*
      PROG.NAME=''
      PROG.DESC='EMPLOYEE LISTING'

      OPEN 'BCTRL' TO F.BCTRL ELSE
      END

*    OPEN 'AR.SLSMAN.1000.1' TO SLSMAN ELSE
*    END

      METADATA = ""
      METADATA<1,1> = "VARCHAR" ; METADATA<1,2> = "username" ; METADATA<1,3> = 0
      METADATA<2,1> = "VARCHAR" ; METADATA<2,2> = "password" ; METADATA<2,3> = 0
      METADATA<3,1> = "VARCHAR" ; METADATA<3,2> = "fullname" ; METADATA<3,3> = 0
      METADATA<4,1> = "VARCHAR" ; METADATA<4,2> = "deptartment" ; METADATA<4,3> = 0
      METADATA<5,1> = "VARCHAR" ; METADATA<5,2> = "status" ; METADATA<5,3> = 0
      METADATA<6,1> = "VARCHAR" ; METADATA<6,2> = "email" ; METADATA<6,3> = 0
      METADATA<7,1> = "VARCHAR" ; METADATA<7,2> = "hire_date" ; METADATA<7,3> = 0
      METADATA<8,1> = "VARCHAR" ; METADATA<8,2> = "supervisor" ; METADATA<8,3> = 0
      METADATA<9,1> = "VARCHAR" ; METADATA<9,2> = "term_date" ; METADATA<9,3> = 0
      METADATA<10,1> = "VARCHAR" ; METADATA<10,2> = "sales_id" ; METADATA<10,3> = 0
      METADATA<11,1> = "VARCHAR" ; METADATA<11,2> = "location" ; METADATA<11,3> = 0
      METADATA<12,1> = "VARCHAR" ; METADATA<12,2> = "ss_access" ; METADATA<12,3> = 0
      METADATA<13,1> = "VARCHAR" ; METADATA<13,2> = "exempt" ; METADATA<13,3> = 0
      CONVERT @AM TO CHAR(30) IN METADATA
      *PRINT "%METADATA:":METADATA

      BCTRL.LIST = ""
      TWO.WEEKS.AGO=DATE()-14

*     STMT = 'SSELECT BCTRL BY 13'       ; * JMK03
*DF      STMT = 'SSELECT BCTRL BY 13 WITH 16 # "I"'   ; * JMK03
*DF       STMT = 'SSELECT BCTRL WITH 16 # "I" OR WITH 16 = "I" AND WITH 22 BY 13'
       STMT = 'SSELECT BCTRL WITH 16 # "I" OR WITH 16 = "I" AND WITH 22 GE "':OCONV(TWO.WEEKS.AGO,'D2/'):'"  BY 13'

      EXECUTE STMT RTNLIST BCTRL.LIST CAPTURING OUTPUT
      NUMFILES = @SELECTED
      IF NUMFILES = 0 THEN
      END

      ROW = ''
      DONE=0
      LOOP
         READNEXT BCTRL.ID FROM BCTRL.LIST ELSE DONE=1
      UNTIL DONE=1 DO
         READ DBCTRL FROM F.BCTRL,BCTRL.ID THEN
            ROW<1> = BCTRL.ID            ; * USERNAME
            ROW<2> = DBCTRL<1>           ; * PASSWORD
            ROW<3> = DBCTRL<13>          ; * FULLNAME
            ROW<4> = DBCTRL<15>          ; * DEPT
            ROW<5> = DBCTRL<16>          ; * STATUS
            ROW<6> = DBCTRL<20>          ; * EMAIL
            ROW<7> = OCONV(DBCTRL<17>,'D2/')       ; * HIRE DATE
            ROW<8> = DBCTRL<21>          ; * SUPERVISOR
            ROW<9> = OCONV(DBCTRL<22>,'D2/')       ; * TERM_DATE
            ROW<10> = DBCTRL<25>         ; * SALES REP ID --KM 5-13-05
            IF NUM(DBCTRL<23>) THEN      ; * JMK01
               ROW<11> = DBCTRL<23>      ; * JMK01 - LOCATION ID
            END                          ; * JMK01
            ROW<12> = CHANGE(DBCTRL<24>,@VM,'|')   ; * JMK02
            ROW<13> = DBCTRL<4>          ; * KWM02
         END
         CONVERT @AM TO CHAR(30) IN ROW
         CRT ROW
      REPEAT
      STOP
   END
