* PROGRAM TO CREATE EXCEL FILE OF LAST CHECK RUN
*
PROG.NAME = ""
CALL GET.USER(USER)
*
OPEN '','FB.PAY.LAST' TO F.PAY ELSE
   CALL OPEN.ABORT("FB.PAY.LAST",PROG.NAME)
END
OPEN '','CLIENTS' TO F.CLIENTS ELSE
   CALL OPEN.ABORT("CLIENTS",PROG.NAME)
END
OPEN '','CARRIERS' TO F.CARRIERS ELSE
   CALL OPEN.ABORT("CARRIERS",PROG.NAME)
END
*
OPEN '','FB.CHECKS.HDR' TO F.CHECKS.HDR ELSE
   CALL OPEN.ABORT("FB.CHECKS.HDR",PROG.NAME)
END
*
OPEN '','BILLDATA.':USER TO F.DATA ELSE STOP
*
*
W.SEN = \SELECT FB.PAY.LAST\
*W.SEN = \SELECT FB.PAY.LAST WITH CLIENT# = "01433"\
*W.SEN = \SSELECT FB.PAY WITH 15 = "12/31/07"\
EXECUTE W.SEN
*
EOF = "N"
CNT = 0
CRT @(-1)
*
XLS.REC = "Carrier #"
XLS.REC<1,2> = "Carrier Name"
XLS.REC<1,3> = "Client #"
XLS.REC<1,4> = "Client Name"
XLS.REC<1,5> = "Check #"
XLS.REC<1,6> = "Bank"
XLS.REC<1,7> = "Check Date"
XLS.REC<1,8> = "Amount"
XLS.REC<1,9> = "Pmt Selected Dt."
*
CHECK.LIST = ""
CARRIER.LIST = ""
DATE37.LIST = ""
DATE133.LIST = ""
*
LOOP
UNTIL EOF = "Y" DO
   READNEXT ID.PAY ELSE EOF = 'Y'
*
   READ PAY.REC FROM F.PAY, ID.PAY ELSE PAY.REC = ""
*
   CNT += 1
*DF   CRT @(0,0):CNT
*
   ID.CL = FIELD(ID.PAY,"*",1)
   ID.CARRIER = FIELD(ID.PAY,"*",2)
   BANK.TYPE = PAY.REC<40>
   N.CKS = DCOUNT(PAY.REC<14>,@VM)
   FOR P = 1 TO N.CKS
      ID.CHECK = PAY.REC<14,P>:"*":BANK.TYPE
      LOCATE ID.CHECK IN CHECK.LIST SETTING POS ELSE
         CHECK.LIST<-1> = ID.CHECK
         CARRIER.LIST<-1> = ID.CARRIER
         DATE37.LIST<-1> = PAY.REC<37,P>
         DATE133.LIST<-1> = PAY.REC<133,P>
      END
   NEXT P
*
REPEAT
*
CRT @(-1)
*
N.PMTS = DCOUNT(CHECK.LIST,@AM)
FOR CK = 1 TO N.PMTS
*DF   CRT @(0,0):"***":N.PMTS
   ID.CHECK = CHECK.LIST<CK>
   PROCESS.DT = DATE37.LIST<CK>
   PMT.SELECT.DT = DATE133.LIST<CK>
   READ CHECK.REC FROM F.CHECKS.HDR, ID.CHECK ELSE CHECK.REC = ""
   ID.CARRIER = CARRIER.LIST<CK>
   READ CARRIER.REC FROM F.CARRIERS, ID.CARRIER ELSE CARRIER.REC = ""
   CHECK.DATE = CHECK.REC<2>
   N.CC = DCOUNT(CHECK.REC<41>,@VM)
   FOR CC = 1 TO N.CC
      ID.CL = CHECK.REC<41,CC>
      READ CL.REC FROM F.CLIENTS, ID.CL ELSE CL.REC = ""
      XL = ID.CARRIER
      XL<1,2> = CARRIER.REC<1>
      XL<1,3> = CHECK.REC<41,CC>
      XL<1,4> = CL.REC<2>
      XL<1,5> = FIELD(ID.CHECK,"*",1)
      XL<1,6> = FIELD(ID.CHECK,"*",2)
      XL<1,7> = OCONV(CHECK.DATE,"D2/")
      XL<1,8> = OCONV(CHECK.REC<42,CC>,"MR2")
      XL<1,9> = OCONV(PMT.SELECT.DT,"D2/")
      XLS.REC<-1> = XL
   NEXT CC
NEXT CK
*
CONVERT @VM TO CHAR(9) IN XLS.REC
*
W.DATE = OCONV(DATE(),"D2")
CONVERT "/" TO "" IN W.DATE
W.TIME = OCONV(TIME(),"MTS")
CONVERT ":" TO "" IN W.TIME
*
ID.XLS = "LAST_CHECKRUN_":W.DATE:"_":W.TIME:".XLS"
WRITE XLS.REC ON F.DATA, ID.XLS
CALL ELINE("Spreadsheet ":ID.XLS:" Created - Check your U: drive")
