* PROGRAM: FB.5.5
* PROGRAM TO PRINT OPEN FREIGHT BILL REPORT
*
* FREIGHT PAYMENT PACKAGE
*
* AUTHOR: BRIAN M. QUEEN
* DATE: April 16, 1992
*
* THIS PROGRAM IS A PROPRIETARY PRODUCT OF:
*
* VARSITY COMPUTING, INC.
* 6060 NORTH CENTRAL EXPRESSWAY, SUITE 538
* DALLAS, TX  75206
* (214)692-8484
*
* AND MAY NOT BE USED, COPIED OR MODIFIED WITHOUT THE
* EXPRESS WRITTEN CONSENT OF SAME.
*
* 102704 - JMK01 - ADD BANK CODE FOR 'D''H' AND HEADING TO REPORT
* 121404 - JMK02 - ADD POST.CC.AR FLAG TO FILTER
* 100605 - JMK03 - ADD OPTION TO SEND TO DOWNLOAD FILE
* 061810 - JMK04 - C100535 - OFB Report Change Requested
*********************************************************************
*
* Last updated by AFS.SOURCE (valerie) at 12:54:12 on 06/17/1994.
*   PROMPT FOR DATE TO PUT ON REPORT.
* Last updated by AFS (valerie) at 11:00:16 on 03/19/1994.
*   UPDATED FOR OSF, TOOK OUT DUE DATE, AND ADDED PROCESS DATE TO
*   THE REPORT.
*
      $INCLUDE PROG.ADMIN INC_OPTIONS
      $INCLUDE FILE.DEFS INC_FB.OPEN.BILLS
*
* Initialize variables
*
      CLIENT.TOTAL=0
      CARRIER.TOTAL=0
      GRAND.TOTAL=0
      PROC.DATE=''
      ERRMSG=''
      FILE.BRK.TOTAL=0                   ; * JMK03
      EQU TB TO CHAR(9)                  ; * JMK03
      CALL GET.USER(USER)                ; * JMK03
*
      PROMPT ''
      CALL CHANNEL(CH.NUM)
      PREV.CLIENT=''
      PREV.CARRIER=''
      PREV.CLIENT.NAME=''                ; * JMK03
      PREV.CARRIER.NAME=''               ; * JMK03
      PREV.FILE.BRK=''                   ; * JMK03
      CLIENT.NAME=''                     ; * JMK03
      CARRIER.NAME=''                    ; * JMK03
      MAT OPEN.REC = ''
*
      PROG.NAME='FB.5.5'
      PROG.DESC='OPEN FREIGHT BILLS REPORT'
      PACK.NAME='FREIGHT PAYMENT'
      CALL AFS.SCR.HEAD(CO.ID,FILE.ID,'',PROG.NAME,PROG.DESC,PACK.NAME,CO.NAME,TIME.DATE,1)
      UPL.VIN = 'FBBP' ; UPL.VIN<2> = 'FB.5.5' ; UPL.VOUT = ''         ; * NPR_UPL 04/23/2010
      CALL UPD.PROGRAM.LOG(UPL.VIN,UPL.VOUT)       ; * NPR_UPL 04/23/2010
*
*  SET-UP FILES
*
      OPEN '','FB.OPEN.BILLS' TO F.OPEN ELSE
         CALL OPEN.ABORT("FB.OPEN.BILLS",PROG.NAME)
      END
      OPEN '','CLIENTS' TO F.CLIENTS ELSE
         CALL OPEN.ABORT("CLIENTS",PROG.NAME)
      END
      OPEN '','CARRIERS' TO F.CARRIER ELSE
         CALL OPEN.ABORT("CARRIERS",PROG.NAME)
      END
      OPEN '','FB.BANKS' TO F.BANK ELSE  ; * JMK01
         CALL OPEN.ABORT("FB.BANKS",PROG.NAME)     ; * JMK01
      END                                ; * JMK01
      OPEN '','BILLDATA.':USER TO F.BILLDATA ELSE  ; * JMK03
         OPEN 'VOC' TO F.VOC ELSE CALL OPEN.ABORT('VOC',PROG.NAME)     ; * JMK03
         READ BILLDATA.REC FROM F.VOC,"BILLDATA" ELSE        ; * JMK03
            CALL ELINE('UNABLE TO OPEN "VOC BILLDATA"')      ; * JMK03
            STOP                         ; * JMK03
         END                             ; * JMK03
         BILLDATA.REC<2>:='\':USER       ; * JMK03
         WRITE BILLDATA.REC ON F.VOC,"BILLDATA.":USER ELSE   ; * JMK03
            CALL ELINE("YOUR USER NAME '":USER:"' IS NOT IN THE BILLDATA FOLDER - PLEASE SEE OWEN/DAVID/MIKE")           ; * JMK03
            STOP                         ; * JMK03
         END                             ; * JMK03
         OPEN '','BILLDATA.':USER TO F.BILLDATA ELSE         ; * JMK03
            CALL ELINE('UNABLE TO OPEN "BILLDATA.":USER')    ; * JMK03
            STOP                         ; * JMK03
         END                             ; * JMK03
      END                                ; * JMK03
      CLIENT.ID=''
      BANK.ID=''
      BANK.DESC=''                       ; * JMK01
      SUM.DET.FLAG=''
      RPT.DATE=DATE() -1
*
* GET CLIENT NUMBER OR ALL
*
50: ***
      HELP="Enter client number for report or <cr>=all. X=Exit."
      IF CLIENT.ID='' THEN
         MASK='#####'
      END ELSE
         IF CLIENT.ID='ALL' THEN
            MASK='ALL  '
         END ELSE
            MASK=CLIENT.ID'R%5'
         END
      END
      CALL GEN.IN(0,3,'Enter client number: ':MASK,'',Q,0,5,'','0',1,-5,3,0,QI,HELP,0,23)
      QI=OCONV(QI,'MCU')
      IF QI='X' ! QI='EX' THEN STOP
      IF QI='' THEN QI=CLIENT.ID
      IF QI=' ' THEN QI=''
      CLIENT.ID=QI
      IF CLIENT.ID='' THEN CLIENT.ID='ALL'
      IF CLIENT.ID#'ALL' THEN
         CLIENT.ID=CLIENT.ID'R%5'
         READV CLIENT.NAME FROM F.CLIENTS,CLIENT.ID,2 ELSE
            CALL ELINE('Invalid client number please try again')
            GOTO 50
         END
         CRT @(21,3):CLIENT.ID:' ':CLIENT.NAME'L#25':@(-4)
      END ELSE
         CRT @(21,3):CLIENT.ID'L#5':@(-4)
      END
*
* Get Bank ID
*
100: ***
*     HELP="Enter bank ID. F=Freight Payment Checks. V=Voucher. X=Back. EX=Exit."          ; * JMK04
      HELP="Enter bank ID. F/M=Freight Payment Checks. V=Voucher. X=Back. EX=Exit."        ; * JMK04
      IF BANK.ID='' THEN MASK='#' ELSE MASK=BANK.ID
      CALL GEN.IN(0,7,'Enter Bank: ':MASK,'',Q,0,2,'','',1,-1,7,0,QI,HELP,0,23)
      QI=OCONV(QI,'MCU')
      IF QI='EX' THEN STOP
      IF QI='X' THEN
         CRT @(12,7):BANK.ID'L#1'
         GOTO 50
      END
      IF QI='' THEN QI=BANK.ID
      BANK.ID=QI
      READ BANK.INFO FROM F.BANK,'BANK*':BANK.ID ELSE        ; * JMK01
         CALL ELINE('Invalid Entry. ')   ; * JMK01
         GOTO 100                        ; * JMK01
      END                                ; * JMK01
*     IF BANK.ID # 'F' AND BANK.ID # 'V' AND BANK.ID#'S' THEN          ; * JMK01
*        CALL ELINE('Invalid Entry. Must enter a <F>, <V>, or <X>')    ; * JMK01
*        GOTO 100                        ; * JMK01
*     END                                ; * JMK01
*     CRT @(12,7):BANK.ID'L#1'           ; * JMK01
      BANK.DESC=BANK.INFO<1>             ; * JMK01
      CRT @(12,7):BANK.ID:' ':BANK.DESC'L#25'      ; * JMK01
*
* SUMMARY OR DETAIL
*
150: ***
*     HELP = "Enter S for Summary or D for Detail.  X = Back. EX = Exit."        ; * JMK04
      HELP = "Enter S for Summary, D for Detail or F for Final.  X = Back. EX = Exit."     ; * JMK04
      IF SUM.DET.FLAG='' THEN SUM.DET.FLAG='S'
      MASK=SUM.DET.FLAG
      CALL GEN.IN(0,9,'Summary or Detail: ':MASK,'',Q,0,2,'','',1,-1,9,0,QI,HELP,0,23)
      QI = OCONV(QI,'MCU')
      IF QI = 'EX' THEN STOP
      IF QI = 'X' THEN
         CRT @(19,9):SUM.DET.FLAG'L#1'
         GOTO 100
      END
      IF QI='' THEN QI=SUM.DET.FLAG
*     IF QI#'S' AND QI#'D' THEN          ; * JMK04
*        CALL ELINE("Invalid entry must be either 'S' or 'D'")         ; * JMK04
      IF QI#'S' AND QI#'D' AND QI#'F' THEN         ; * JMK04
         CALL ELINE("Invalid entry must be either 'S', 'D' or 'F'")    ; * JMK04
         GOTO 150
      END
      SUM.DET.FLAG = QI
      CRT @(19,9):SUM.DET.FLAG'L#1'
      IF SUM.DET.FLAG='D' THEN
         CALL ELINE('WARNING: You are about to run a detail report. This report may be long')
      END
      IF SUM.DET.FLAG='F' THEN           ; * JMK04
         SUM.DET.FLAG='S'                ; * JMK04
         FINAL.FLAG=1                    ; * JMK04
      END ELSE                           ; * JMK04
         FINAL.FLAG=0                    ; * JMK04
      END                                ; * JMK04
*
*  REPORT DATE
*
200: ***
      HELP="Enter report date. nn=Day. nn/nn=Month & Day. X=Exit."
      LN = 'Report Date: ':RPT.DATE'D2/'
      CALL GEN.IN(0,11,LN,'DATE',Q,0,8,'','',0,-8,11,0,QI,HELP,0,23)
      QI=OCONV(QI,'MCU')
      BEGIN CASE
         CASE QI = 'X'
            CRT @(13,11):RPT.DATE 'D2/'
            GOTO 150
         CASE QI='EX'
            STOP
         CASE 1
            RPT.DATE = Q
            CRT @(13,11) : RPT.DATE 'D2/' :
      END CASE
*
* Output to file or printer.
*
250: * ; * JMK03
      HELP = "Output to [F]ile or [P]rinter"       ; * JMK03
      LN = 'Enter selection please.                 P-'      ; * JMK03
      CALL GEN.IN(0,13,LN,'',Q,0,2,'','',0,-2,13,0,QI,HELP,0,23)       ; * JMK03
      QI = OCONV(QI,'MCU')               ; * JMK03
      IF QI='' THEN QI='P'               ; * JMK03
      BEGIN CASE                         ; * JMK03
         CASE QI = 'X'                   ; * JMK03
            CRT @(0,13):@(-3):           ; * JMK03
            GOTO 200                     ; * JMK03
         CASE QI = 'EX'                  ; * JMK03
            STOP                         ; * JMK03
         CASE QI = '' OR QI='F' OR QI='P'          ; * JMK03
            NULL                         ; * JMK03
         CASE 1                          ; * JMK03
            GOTO 250                     ; * JMK03
      END CASE                           ; * JMK03
      EXP.OUT=QI                         ; * JMK03
      IF EXP.OUT = 'P' THEN              ; * JMK03
         CRT @(40,13):'P - Printer'      ; * JMK03
      END ELSE                           ; * JMK03
         CRT @(40,13):'F - File'         ; * JMK03
      END                                ; * JMK03
*
* Ask if data correct
*
      HELP="Enter Y if information correct or N if incorrect."
      CALL GEN.IN(0,22,'Is the information correct? Y','',Q,0,2,'','',2,-1,22,1,QI,HELP,0,23)
      QI=OCONV(QI,'MCU')
      IF QI='EX' THEN STOP
      IF QI='X' THEN
         GOTO 200
      END
      IF QI='Y' ! QI='' ELSE GOTO 50
*
* SORT AND SELECT PAY RECORDS
*
*     EX.REC='SSELECT FB.OPEN.BILLS'     ; * JMK02
      EX.REC='SSELECT FB.OPEN.BILLS WITH POST.CC.AR = "Y"'   ; * JMK02
      IF CLIENT.ID # 'ALL' THEN
*        EX.REC := ' WITH CLIENT# = "':CLIENT.ID:'"'         ; * JMK02
         EX.REC := ' AND WITH CLIENT# = "':CLIENT.ID:'"'     ; * JMK02
      END
      IF BANK.ID # '' THEN
         IF INDEX(EX.REC,' WITH ',1) THEN
            EX.REC := ' AND WITH BANK = "':BANK.ID:'"'
         END ELSE
            EX.REC := ' WITH BANK = "':BANK.ID:'"'
         END
      END
*
      EX.REC := ' BY CLIENT.NAME BY CARRIER.NAME'
      IF EXP.OUT='F' THEN                ; * JMK03
         EX.REC:=' BY 37'                ; * JMK03
      END                                ; * JMK03
      IF SUM.DET.FLAG # 'S' THEN EX.REC:=' BY CARRIER# BY 2 BY 1 BY PRO'
      EXECUTE EX.REC PASSLIST CAPTURING OUTPUT
      IF SYSTEM(11) ELSE
         CALL ELINE('No transactions in FB.OPEN.BILLS file.')
         STOP
      END
      NUM.ITMS = @SELECTED
      COUNTER=0
*
*  SET UP PAGE HEADING
*
      IF EXP.OUT='P' THEN                ; * JMK03
         VIN=''
         VIN<3>='R6'
         CALL LASER.CODE(VIN,VOUT)
         IF VOUT<1>#'' THEN
            CALL ELINE(VOUT<1>)
            STOP
         END
         IF VOUT<3>#'' THEN EXECUTE VOUT<3>
         PRINTER ON
         IF VOUT<2>#'' THEN PRINT VOUT<2>:
         O.RPT.DATE=OCONV(RPT.DATE,'D2/')
         HDG1=''                         ; * JMK01
         HDG2=''                         ; * JMK01
         BEGIN CASE                      ; * JMK01
            CASE BANK.ID='F' OR BANK.ID='V'        ; * JMK01
               HDG1='AFS Logistics, LLC'           ; * JMK01
            CASE 1                       ; * JMK01
               HDG1=BANK.DESC            ; * JMK01
               HDG2='A Division of AFS Logistics, LLC'       ; * JMK01
         END CASE                        ; * JMK01
*        CALL AFS.PAGE.HEAD(PROG.NAME,'AFS Logistics, LLC','','Open Freight Bills Report',O.RPT.DATE,132,HED) ; * JMK01
         CALL AFS.PAGE.HEAD(PROG.NAME,HDG1,HDG2,PROG.DESC,O.RPT.DATE,132,HED)    ; * JMK01
         IF SUM.DET.FLAG = 'S' THEN
            HED=HED:" 'L'Client Name               Carrier Name                                        Summary Report                         Total Charges"
         END ELSE
            HED=HED:" 'L'Client Name               Carrier Name              Pro Number                Bill of Lading    Date   Prc Date  Bank      Charges"
         END
         HEADING HED
      END ELSE                           ; * JMK03
         EXP.CNT=1                       ; * JMK03
         EXP.SEQ=0                       ; * JMK03
         EXP.ID='OFB_':CLIENT.ID:'_':SUM.DET.FLAG:'_':RPT.DATE'D2-'    ; * JMK03
         EXP.REC = ''                    ; * JMK03
         EXP.REC<-1> = PROG.DESC:' for bank ':BANK.ID:' ':BANK.DESC    ; * JMK03
         EXP.REC<-1> = ' '               ; * JMK03
         LINE.FMT = 'HDG'                ; * JMK03
         GOSUB EXP.LINE                  ; * JMK03
      END                                ; * JMK03
*
* Read open bill item
*
      DONE = 0
      LOOP
         READNEXT OPEN.ID ELSE DONE = 1
      UNTIL DONE = 1 DO
         COUNTER=COUNTER+1
         CALL PROG.BAR(22,COUNTER,NUM.ITMS,PER)
         MATREAD OPEN.REC FROM F.OPEN, OPEN.ID ELSE MAT OPEN.REC = ''
         CARRIER=FIELD(OPEN.ID,'*',2)
         CLIENT=FIELD(OPEN.ID,'*',1)
         PRO=FIELD(OPEN.ID,'*',3)
         IF FBO.PRO.DATE = '' THEN
            PROC.DATE = SPACE(8)
         END ELSE
            PROC.DATE = FBO.PRO.DATE 'D2/'
         END
*        IF EXP.OUT='P' THEN             ; * JMK03
         IF CARRIER # PREV.CARRIER OR CLIENT # PREV.CLIENT THEN
            GOSUB 8200
         END
         IF CLIENT # PREV.CLIENT THEN GOSUB 8100
*        END                             ; * JMK03

         IF EXP.OUT='F' THEN             ; * JMK03
            FILE.BRK=CLIENT:'*':CARRIER:'*':PROC.DATE        ; * JMK03
            IF FILE.BRK#PREV.FILE.BRK THEN         ; * JMK03
               PREV.CLIENT=FIELD(PREV.FILE.BRK,'*',1)        ; * JMK03
               READV PREV.CLIENT.NAME FROM F.CLIENTS,PREV.CLIENT,2 ELSE          ; * JMK03
                  PREV.CLIENT.NAME='~~~ Unknown ~~~'         ; * JMK03
               END                       ; * JMK03
               PREV.CARRIER=FIELD(PREV.FILE.BRK,'*',2)       ; * JMK03
               READV PREV.CARRIER.NAME FROM F.CARRIER,PREV.CARRIER,1 ELSE        ; * JMK03
                  PREV.CARRIER.NAME='~~~ Unknown ~~~'        ; * JMK03
               END                       ; * JMK03
               PREV.PROC.DATE=FIELD(PREV.FILE.BRK,'*',3)     ; * JMK03
               IF PREV.FILE.BRK#'' AND FILE.BRK.TOTAL+0#0 AND SUM.DET.FLAG='S' THEN        ; * JMK03
                  LINE.FMT=SUM.DET.FLAG  ; * JMK03
                  IF FINAL.FLAG=0 THEN   ; * JMK04
                     GOSUB EXP.LINE      ; * JMK03
                  END                    ; * JMK04
               END                       ; * JMK03
               PREV.FILE.BRK=FILE.BRK    ; * JMK03
               FILE.BRK.TOTAL=0          ; * JMK03
            END                          ; * JMK03
         END                             ; * JMK03
*
         IF SUM.DET.FLAG # 'S' THEN
            IF EXP.OUT='P' THEN          ; * JMK03
               PRINT CLIENT.NAME'L#25':' ':CARRIER.NAME'L#25':' ':
               PRINT PRO'L#25':' ' : FBO.BOL<1,1>'L#15':
               X=DCOUNT(FBO.BOL<1>,CHAR(253))
               IF X LE 1 THEN
                  PRINT ' ':FBO.BILL.DATE'D2/':' ':PROC.DATE:
                  PRINT FBO.BANK'R#6':FBO.ACT.CHG'R26,#13'
               END ELSE
                  FOR I=2 TO X UNTIL I GT X
                     PRINT
                     PRINT SPACE(78):FBO.BOL<1,I>'L#15':
                  NEXT I
                  PRINT ' ':FBO.BILL.DATE'D2/':' ':PROC.DATE:
                  PRINT FBO.BANK'R#6':FBO.ACT.CHG'R26,#13'
               END
            END ELSE                     ; * JMK03
               LINE.FMT=SUM.DET.FLAG     ; * JMK03
               GOSUB EXP.LINE            ; * JMK03
            END                          ; * JMK03
         END
         CLIENT.TOTAL=CLIENT.TOTAL+FBO.ACT.CHG
         CARRIER.TOTAL=CARRIER.TOTAL+FBO.ACT.CHG
         FILE.BRK.TOTAL=FILE.BRK.TOTAL+FBO.ACT.CHG           ; * JMK03
         GRAND.TOTAL=GRAND.TOTAL+FBO.ACT.CHG
      REPEAT
*
* End of program
*
      IF EXP.OUT='P' THEN                ; * JMK03
         IF FINAL.FLAG=0 THEN            ; * JMK04
            GOSUB 8200
            GOSUB 8100
            PRINT
            GOSUB 8900
         END                             ; * JMK04
         PRINT 'Grand totals         ':SPACE(94):GRAND.TOTAL'R26,#15'
         PRINT SPACE(116):'=============='
         PRINTER CLOSE
      END ELSE                           ; * JMK03
         IF SUM.DET.FLAG='S' THEN        ; * JMK03
            PREV.CLIENT=FIELD(PREV.FILE.BRK,'*',1)           ; * JMK03
            READV PREV.CLIENT.NAME FROM F.CLIENTS,PREV.CLIENT,2 ELSE   ; * JMK03
               PREV.CLIENT.NAME='~~~ Unknown ~~~'  ; * JMK03
            END                          ; * JMK03
            PREV.CARRIER=FIELD(PREV.FILE.BRK,'*',2)          ; * JMK03
            READV PREV.CARRIER.NAME FROM F.CARRIER,PREV.CARRIER,1 ELSE           ; * JMK03
               PREV.CARRIER.NAME='~~~ Unknown ~~~'           ; * JMK03
            END                          ; * JMK03
            PREV.PROC.DATE=FIELD(PREV.FILE.BRK,'*',3)        ; * JMK03
            IF PREV.FILE.BRK#'' AND FILE.BRK.TOTAL+0#0 THEN  ; * JMK03
               LINE.FMT=SUM.DET.FLAG     ; * JMK03
               GOSUB EXP.LINE            ; * JMK03
            END                          ; * JMK03
         END                             ; * JMK03
         IF EXP.SEQ#0 THEN               ; * JMK03
            RECORD.NAME=EXP.ID:'_':EXP.SEQ:'.XLS'  ; * JMK03
         END ELSE                        ; * JMK03
            RECORD.NAME=EXP.ID:'.XLS'    ; * JMK03
         END                             ; * JMK03
         WRITE EXP.REC ON F.BILLDATA,RECORD.NAME   ; * JMK03
      END                                ; * JMK03
      STOP
*----------------------------------------------------------------------
*
* INTERNAL SUBROUTINES
*
*----------------------------------------------------------------------
*
* Print current client totals
*
8100: IF PREV.CLIENT='' ELSE
         IF EXP.OUT='P' THEN             ; * JMK03
            IF FINAL.FLAG=0 THEN         ; * JMK04
               GOSUB 8900
               PRINT 'Totals for client ':CLIENT.NAME'L#30':SPACE(67):CLIENT.TOTAL'R26,#15':' **'
               PRINT
            END                          ; * JMK04
         END                             ; * JMK03
      END
      PREV.CLIENT=CLIENT
      CLIENT.TOTAL=0
      READV CLIENT.NAME FROM F.CLIENTS,CLIENT,2 ELSE
         CLIENT.NAME='~~~ Unknown ~~~'
      END
      RETURN
*
* Print current carrier totals
*
8200: IF PREV.CARRIER='' ELSE
         IF FINAL.FLAG=0 THEN            ; * JMK04
            IF SUM.DET.FLAG = 'S' THEN
               IF EXP.OUT='P' THEN       ; * JMK03
                  PRINT CLIENT.NAME 'L#30' : CARRIER.NAME 'L#30' : SPACE(55) :
                  PRINT CARRIER.TOTAL 'R26,#15' :
                  PRINT
               END                       ; * JMK03
            END ELSE
               IF EXP.OUT='P' THEN       ; * JMK03
                  GOSUB 8900
                  PRINT 'Totals for carrier ':CARRIER.NAME'L#30':SPACE(66):CARRIER.TOTAL'R26,#15':' *'
                  PRINT
               END                       ; * JMK03
            END
         END                             ; * JMK04
      END
      CARRIER.TOTAL=0
      PREV.CARRIER=CARRIER
      READV CARRIER.NAME FROM F.CARRIER,CARRIER,1 ELSE
         CARRIER.NAME='~~~ Unknown ~~~'
      END
      RETURN
*
* Print single underscore
*
8900: PRINT SPACE(116):'--------------'
      RETURN
*
***********************************************************************
*
*
* Export Line
*
EXP.LINE: ***
      EXP.LN=''
      BEGIN CASE
         CASE LINE.FMT='HDG'
            EXP.LN<1,1>='Client #'
         CASE LINE.FMT='S'
            EXP.LN<1,1>='=':QUOTE(PREV.CLIENT)
         CASE LINE.FMT='D'
            EXP.LN<1,1>='=':QUOTE(CLIENT)
      END CASE
      BEGIN CASE
         CASE LINE.FMT='HDG'
            EXP.LN<1,2>='Client Name'
         CASE LINE.FMT='S'
            EXP.LN<1,2>=PREV.CLIENT.NAME
         CASE LINE.FMT='D'
            EXP.LN<1,2>=CLIENT.NAME
      END CASE
      BEGIN CASE
         CASE LINE.FMT='HDG'
            EXP.LN<1,3>='Carrier #'
         CASE LINE.FMT='S'
            EXP.LN<1,3>='=':QUOTE(PREV.CARRIER)
         CASE LINE.FMT='D'
            EXP.LN<1,3>='=':QUOTE(CARRIER)
      END CASE
      BEGIN CASE
         CASE LINE.FMT='HDG'
            EXP.LN<1,4>='Carrier Name'
         CASE LINE.FMT='S'
            EXP.LN<1,4>=PREV.CARRIER.NAME
         CASE LINE.FMT='D'
            EXP.LN<1,4>=CARRIER.NAME
      END CASE
      BEGIN CASE
         CASE LINE.FMT='HDG'
            EXP.LN<1,5>='Process Date'
         CASE LINE.FMT='S'
            EXP.LN<1,5>=FIELD(PREV.FILE.BRK,'*',3)
         CASE LINE.FMT='D'
            EXP.LN<1,5>=PROC.DATE
      END CASE
      BEGIN CASE
         CASE LINE.FMT='HDG'
            EXP.LN<1,6>='Bill Date'
         CASE LINE.FMT='S'
            EXP.LN<1,6>=''
         CASE LINE.FMT='D'
            EXP.LN<1,6>=OCONV(FBO.BILL.DATE,'D2/')
      END CASE
      BEGIN CASE
         CASE LINE.FMT='HDG'
            EXP.LN<1,7>='Pro'
         CASE LINE.FMT='S'
            EXP.LN<1,7>=''
         CASE LINE.FMT='D'
            EXP.LN<1,7>='=':QUOTE(PRO)
      END CASE
      BEGIN CASE
         CASE LINE.FMT='HDG'
            EXP.LN<1,8>='Amount'
         CASE LINE.FMT='S'
            EXP.LN<1,8>=FILE.BRK.TOTAL 'R26,'
         CASE LINE.FMT='D'
            EXP.LN<1,8>=FBO.ACT.CHG 'R26,'
      END CASE
      CONVERT @VM TO TB IN EXP.LN
      EXP.REC<-1>=EXP.LN
      EXP.CNT=EXP.CNT+1
      IF EXP.CNT >= 65000 THEN
         RECORD.NAME=EXP.ID:'_':EXP.SEQ:'.XLS'
         WRITE EXP.REC ON F.BILLDATA,RECORD.NAME
         EXP.SEQ=EXP.SEQ+1
         EXP.REC=''
         EXP.CNT=0
      END
      RETURN
      END
