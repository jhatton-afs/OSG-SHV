* FREIGHT BILLING PACKAGE

*---- PROGRAM: VC.ZIP.CHECK
* AUTHOR: JAY WEBERT
* DATE: MARCH 14, 2007
* DESCRIPTION: GATHER AND SAVE DATA ABOUT THE ZIP CODES IN VEND.CUST.
*              ONLY VEND.CUST RECORDS WITHOUT VALID RELATIONSHIPS TO
*              VEND.CUST.XREF WILL BE INCLUDED IN THIS PROCESSING.
*
* THIS PROGRAM IS A PROPRIETARY PRODUCT OF AUTOMATED FREIGHT SYSTEMS
* AND MAY NOT BE USED, COPIED OR MODIFIED WITHOUT THE EXPRESS
* WRITTEN CONSENT OF SAME.
*-----------------------------------------------------------------------
* TOTAL - Number of VEND.CUST records with no valid VEND.CUST.XREF relation
* CAN.LONG - 6-Character Canadian format zip
* CAN.SHORT - 3-Character Candian format zip
* STANDARD - 5-Digit US format zip
* NINE.DIGIT - 9-Digit US format zip with hyphen
* HYPHEN.END - 5-Digit US format zip with hyphen on the end
* NULL - zip code field is blank
* UNDERSCORE - 5-Digit or 9-Digit US format zip with underscore (_) where hyphen should be
* HYPHEN.ONLY - zip is the hyphen character
* CAN.PAIR - 6-Character Canadian format zip with a space added after the third character
* OTHER - zip value that fits none of the above descriptions
*-----------------------------------------------------------------------
      PROG.NAME = 'VC.ZIP.CHECK'
      OPEN 'ZIP.DATA' TO F.ZIP ELSE
        CALL OPEN.ABORT('ZIP.DATA',PROG.NAME)
      END
      ********************
      *** 'SELECT CLIENT ID'S
      ********************
      STMT = 'SELECT VOC "VEND.CUST" *A7'
      EXECUTE STMT RTNLIST CAPTURING MSG
      IF @SELECTED < 1 THEN
        CRT 'NO CLIENTS SELECTED.'
        STOP
      END
      STMT = 'SELECT CLIENTS'
      EXECUTE STMT PASSLIST RTNLIST CLIENT.LIST CAPTURING MSG
      IF @SELECTED < 1 THEN
        CRT 'NO CLIENT RECORDS SELECTED.'
        STOP
      END
      ********************
      *** LOOP THROUGH ALL CLIENTS
      ********************
      DONE = 0
      LOOP UNTIL DONE DO
        READNEXT CLIENT.ID FROM CLIENT.LIST THEN
          CRT CLIENT.ID
          *** INITIALIZE COUNTERS
          TOTAL = 0
          CAN.LONG = 0
          CAN.SHORT = 0
          STANDARD = 0
          NINE.DIGIT = 0
          HYPHEN.END = 0
          NULL = 0
          UNDERSCORE = 0
          HYPHEN.ONLY = 0
          CAN.PAIR = 0
          OTHER = 0
          STMT = 'SELECT VEND.CUST,':CLIENT.ID
          EXECUTE STMT RTNLIST VC.LIST CAPTURING MSG
          IF @SELECTED < 1 THEN GOTO NEXT.CLIENT
          READ ZIP.REC FROM F.ZIP, CLIENT.ID ELSE
            CRT 'COULD NOT READ ZIP.DATA RECORD ':CLIENT.ID
            GOTO NEXT.CLIENT
          END
          OPEN 'VEND.CUST,':CLIENT.ID TO F.VC ELSE
            CRT 'COULD NOT OPEN VEND.CUST,':CLIENT.ID
            GOTO NEXT.CLIENT
          END
          OPEN 'VEND.CUST.XREF,':CLIENT.ID TO F.XREF ELSE
            CRT 'COULD NOT OPEN VEND.CUST.XREF,':CLIENT.ID
            GOTO NEXT.CLIENT
          END
          DONE1 = 0
          LOOP UNTIL DONE1 DO
            READNEXT VC.ID FROM VC.LIST THEN
              READ VC.REC FROM F.VC, VC.ID THEN
                ZIP = VC.REC<7>
                DESC = VC.REC<1>
                DESC = TRIM(OCONV(DESC,'MCU'))
                IF ZIP = '99999' THEN
                  XREF.ID = DESC
                END ELSE
                  XREF.ID = DESC:'*':ZIP
                END
                READ XREF.REC FROM F.XREF, XREF.ID THEN GOTO NEXT.VC
                TOTAL += 1
                BEGIN CASE
                  CASE ZIP MATCHES '1A1N1A1N1A1N'
                    CAN.LONG += 1
                  CASE ZIP MATCHES '1A1N1A'
                    CAN.SHORT += 1
                  CASE NUM(ZIP) AND LEN(ZIP) = 5
                    STANDARD += 1
                  CASE LEN(ZIP) = 6 AND ZIP[6,1] = '-' AND NUM(ZIP[1,5])
                    HYPHEN.END += 1
                  CASE ZIP[6,1] = '-' AND NUM(FIELD(ZIP,'-',1)) AND NUM(FIELD(ZIP,'-',2))
                    NINE.DIGIT += 1
                  CASE ZIP = ''
                    NULL += 1
                  CASE ZIP[6,1] = '_'
                    UNDERSCORE += 1
                  CASE ZIP = '-'
                    HYPHEN.ONLY += 1
                  CASE ZIP MATCHES '1A1N1A 1N1A1N'
                    CAN.PAIR += 1
                  CASE 1
                    OTHER += 1
                END CASE
              END ELSE
                NO.REC += 1
              END
            END ELSE
              DONE1 = 1
            END
NEXT.VC:
          REPEAT
          *** Save client results
          ZIP.REC<13> = TOTAL
          ZIP.REC<14> = CAN.LONG
          ZIP.REC<15> = CAN.SHORT
          ZIP.REC<16> = STANDARD
          ZIP.REC<17> = NINE.DIGIT
          ZIP.REC<18> = HYPHEN.END
          ZIP.REC<19> = NULL
          ZIP.REC<20> = UNDERSCORE
          ZIP.REC<21> = HYPHEN.ONLY
          ZIP.REC<22> = CAN.PAIR
          ZIP.REC<23> = OTHER
          WRITE ZIP.REC TO F.ZIP, CLIENT.ID ELSE
            CRT '****** COULD NOT WRITE ZIP RECORD!**********'
            INPUT X
          END
        END ELSE
          DONE = 1
        END
NEXT.CLIENT:
      REPEAT
STOP

