*******************************************************
* Program name : VEND.CUST.XREF.REBUILD
* Author       : JAY WEBERT
* Date written : MARCH 19, 2007
* Library      : FBBP
* COPYRIGHT (C) 2016 - AFS LOGISTICS LLC - ALL RIGHTS RESERVED.
*
* REBUILD VEND.CUST.XREF FILE BASED ON DATA IN VEND.CUST
*
* Last updated by afs (AFSLOGISTICS\nmeddaugh) at 09:22:51 on 04/03/2019.
*
* Start modification history
* 04/03/19 : NKH01 : ACCU-316 Modified CREATE-FILE file sizes
* End modification history
*
*******************************************************
      PROG.NAME = 'VEND.CUST.XREF.REBUILD'

*---- Restrict program usage   Added by NPR Feb. 2011 

      CALL GET.USER(USER)
      BEGIN CASE
        CASE USER='OWEN'
        CASE USER='MKERBOW'
***        CASE USER='MARY'
***        CASE USER='MYRA'
        CASE USER='DAVID'
        CASE USER='NPRATT'
        CASE 1
          TEMP='**  This is a restricted access application - Access Denied  **'
          CALL CENTER(TEMP,80)
          CRT @(0,12):TEMP
          CALL ELINE('')
          STOP
      END CASE

      ********************
      *** 'SELECT CLIENT ID'S
      ********************
      STMT = 'SELECT VOC "VEND.CUST" *A7'
      EXECUTE STMT RTNLIST CAPTURING MSG
      IF @SELECTED < 1 THEN
        CRT 'NO CLIENTS SELECTED.'
        STOP
      END
      STMT = 'SELECT CLIENTS'
      EXECUTE STMT PASSLIST RTNLIST CLIENT.LIST CAPTURING MSG
      IF @SELECTED < 1 THEN
        CRT 'NO CLIENT RECORDS SELECTED.'
        STOP
      END
      ********************
      *** LOOP THROUGH ALL CLIENTS
      ********************
      DONE = 0
      LOOP UNTIL DONE DO
        READNEXT CLIENT.ID FROM CLIENT.LIST THEN
          CRT CLIENT.ID
          STMT = 'CLEAR.FILE VEND.CUST.XREF,':CLIENT.ID
          EXECUTE STMT CAPTURING MSG
          STMT = 'SELECT VEND.CUST,':CLIENT.ID
          EXECUTE STMT RTNLIST VC.LIST CAPTURING MSG
          IF @SELECTED < 1 THEN GOTO NEXT.CLIENT
          OPEN 'VEND.CUST,':CLIENT.ID TO F.VC ELSE GOTO NEXT.CLIENT
          OPEN 'VEND.CUST.XREF,':CLIENT.ID TO F.VC.XREF ELSE 
            STMT = 'CREATE.FILE DATA VEND.CUST.XREF,':CLIENT.ID:' 401,1'      ; * NKH01
            EXECUTE STMT CAPTURING MSG
            OPEN 'VEND.CUST.XREF,':CLIENT.ID TO F.VC.XREF ELSE
              CRT 'COULD NOT CREATE AND OPEN VEND.CUST.XREF,':CLIENT.ID
              GOTO NEXT.CLIENT
            END
          END
          DONE1 = 0
          LOOP UNTIL DONE1 DO
            READNEXT VC.ID FROM VC.LIST THEN
              READ VC.REC FROM F.VC, VC.ID ELSE GOTO NEXT.VC.REC
              VC.DESC = VC.REC<1>
              VC.ZIP = VC.REC<7>
              VC.DESC = TRIM(OCONV(VC.DESC,'MCU'))
              VC.XREF.ID = VC.DESC
              IF VC.ZIP # '99999' THEN VC.XREF.ID := '*':VC.ZIP
              VC.XREF.REC = ''
              VC.XREF.REC<1> = VC.ID
              WRITE VC.XREF.REC TO F.VC.XREF, VC.XREF.ID
            END ELSE
              DONE1 = 1
            END
NEXT.VC.REC:
          REPEAT
        END ELSE
          DONE = 1
        END
NEXT.CLIENT:
      REPEAT
STOP

