****************************************************
* Program name : FB.SLC.PARCEL.BLOCK
* Author       : MIKE KERBOW
* Date written : 10-03-13
* Library      : FBBP
* COPYRIGHT (c) : AFS, LLC 2014, All Rights Reserved.
*
* This process will place invoicing blocks for clients
* with SLC Parcel activity.Update operator table
* Last updated by afs (tgorton) at 11:44:30 on 09/14/2016.
*
* Start modification history
* 091416 - TAG01 - Sysaid 27036 Add Timestamp To Eod Processes
* End modification history

************************************************************************

      TODAY=DATE()
      END.DATE=TODAY
      BEG.DATE=END.DATE-45
      PROG.NAME='FB.SLC.PARCEL.BLOCK'
      BLOCK.MSG='Waiting on Parcel Import from SLC'
* Begin TAG01 
      CALL GET.USER(USER)
      VIN1=""
      VIN1<1> = "FB.SLC.PARCEL.BLOCK"
      VIN1<2> = USER
      VIN1<3> = SYSTEM(18)
      VIN1<4> = "S"
      CALL PROGTRACK(VIN1,VOUT1)
* End TAG01 

      OPEN '','FB.TABLES' TO F.TABLES ELSE
         CALL OPEN.ABORT('FB.TABLES',PROG.NAME)
      END
      OPEN '','PCL.BILL.DATE.XREF' TO F.XREF ELSE
         CALL OPEN.ABORT('PCL.BILL.DATE.XREF',PROG.NAME)
      END
*
* First, clear out old block records.
*
      STMT=''
      STMT<-1>='SELECT FB.TABLES WITH F1 = "':BLOCK.MSG:'"'
      EXECUTE STMT RTNLIST TABLES.LIST CAPTURING RESULTS
      NUM.ITMS=@SELECTED
      IF NUM.ITMS>0 THEN
         EOF=0
         CNT=0
         LOOP
            READNEXT TABLE.ID FROM TABLES.LIST ELSE EOF=1
         UNTIL EOF DO
            CNT+=1
            DELETE F.TABLES,TABLE.ID
         REPEAT
      END
*
* Next, block client activity from past four weeks
*
      STMT=''
      STMT<-1>='SELECT PCL.BILL.DATE.XREF WITH INV.DATE >= "':BEG.DATE'D4/':'"'
      EXECUTE STMT RTNLIST XREF.LIST CAPTURING RESULTS
      NUM.ITMS=@SELECTED
      IF NUM.ITMS>0 THEN
         CLIENT.LIST=''
         EOF=0
         CNT=0
         LOOP
            READNEXT XREF.ID FROM XREF.LIST ELSE EOF=1
         UNTIL EOF DO
            CNT+=1
            READ XREF.REC FROM F.XREF,XREF.ID THEN
               NATT=DCOUNT(XREF.REC,@AM)
               FOR ATT=1 TO NATT
                  CLIENT.ID=XREF.REC<ATT>
                  LOCATE CLIENT.ID IN CLIENT.LIST SETTING CPOS ELSE
                     TABLE.ID='NO-PROCESS*':CLIENT.ID
                     READ TABLE.REC FROM F.TABLES,TABLE.ID ELSE
                        WRITE BLOCK.MSG ON F.TABLES,TABLE.ID
                     END
                     CLIENT.LIST<-1>=CLIENT.ID
                  END
               NEXT ATT
            END
         REPEAT

         EXECUTE "BBSEND GREGB SHV Invoice Block for SLC Parcel Activity has been applied."
      END
* Begin TAG01 
      VIN1=""
      VIN1<1> = "FB.SLC.PARCEL.BLOCK"
      VIN1<2> = USER
      VIN1<3> = SYSTEM(18)
      VIN1<4> = "E"
      CALL PROGTRACK(VIN1,VOUT1)
* End TAG01 
   END
