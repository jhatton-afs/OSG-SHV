      $OPTIONS HEADER.EJECT
      ERRMSG=''
*
      $OPTIONS EXEC.EQ.PERF
      $OPTIONS INFO.ABORT
*************************************************************************
* ROUTINE TO ENTER CLIENT/CARRIER VERIFY DATES FOR LOGISTICS
*
* FREIGHT BILLING PACKAGE
*
* AUTHOR: TAMI SEAGO
* DATE: SEPTEMBER 21, 1999
*
* THIS PROGRAM IS A PROPRIETARY PRODUCT OF AUTOMATED FREIGHT SYSTEMS
* AND MAY NOT BE USED, COPIED OR MODIFIED WITHOUT THE EXPRESS
* WRITTEN CONSENT OF SAME.
*
********************************************************************
* 111111 - JMK01 - ADJUST VIDEO ATTRIBUTES FOR ACCUTERM
*
************************************************************************
* Initialize variables
************************************************************************
      $INCLUDE PROG.ADMIN INC_OPTIONS
      $INCLUDE FILE.DEFS INC_CLIENTS
      $INCLUDE FILE.DEFS INC_ADDS60.CODES     ;* These require specific PROCOMM settings!

*
* Equates
*
      TERM.TYPE=SYSTEM(7)                ; * JMK01
      BEGIN CASE                         ; * JMK01
         CASE TERM.TYPE='REGENT60'       ; * JMK01
            REV.RED.ON = TERM.ESC.SEQ:CHAR(TERM.REV+TERM.BLINK)
            REV.GRN.ON = TERM.ESC.SEQ:CHAR(TERM.REV)
            TERM.NORM = TERM.ESC.SEQ:CHAR(TERM.NORMAL)
         CASE 1                          ; * JMK01
            REV.RED.ON = @(-13):@(-5)    ; * JMK01
            REV.GRN.ON = @(-13)          ; * JMK01
            TERM.NORM = @(-14):@(-6)     ; * JMK01
      END CASE                           ; * JMK01

      EQU REV.ON TO REV.GRN.ON           ; * Reverse Text ON
      EQU REV.OFF TO TERM.NORM           ; * Reverse Text OFF

      EQU TRUE TO 1
      EQU FALSE TO 0

      EQU ENABLE.STR TO "ENABLE"
      EQU DISABLE.STR TO "DISABLE"

      EQU PAGE.SIZE TO 13
      EQU START.Y TO 7

      EQU WRITE.DATA TO TRUE             ; * SET TO FALSE FOR TESTING...

      PROMPT ''
      PROG.NAME="FB.11.6"
      PROG.DESC="Client/Carrier Quick Verify"
      PACK.NAME="Freight Payment"
      CO.NAME='AFS Logistics, LLC'
      UPL.VIN = 'FBBP' ;  UPL.VIN<2> = 'FB.11.6' ; UPL.VOUT = ''                    ;* NPR_UPL 04/23/2010
      CALL UPD.PROGRAM.LOG(UPL.VIN,UPL.VOUT)                                                           ;* NPR_UPL 04/23/2010
      GOSUB INIT.SCREEN
      DEBUG.MSG = ''

      WIDTH = 80
      CTR = INT(WIDTH/2)
      REVON = REV.ON
      REVOFF = REV.OFF
      EXIT.IT = FALSE
      PAGE.NUM = 1
      ADD.MODE = FALSE
      LAST.EDIT.MASK = "1"
      EDIT.NUMBER = 1

************************************************************************
* Open files
************************************************************************
      OPEN '','CLIENTS' TO F.CLIENTS ELSE CALL OPEN.ABORT("CLIENTS",PROG.NAME)
      OPEN '','FB.QUICK.VERIFY' TO F.VERIFY ELSE CALL OPEN.ABORT("FB.QUICK.VERIFY",PROG.NAME)
      OPEN '','CARRIERS' TO F.CARRIERS ELSE CALL OPEN.ABORT("CARRIERS",PROG.NAME)
      CALL GET.USER(USER)
      VIN=USER
      CALL SUPER.USER(VIN,VOUT)
      SUPER.USER=VOUT<1>
      IF SUPER.USER ELSE
         CALL ELINE('Invalid access for this process.')
         GO TRASH.PROGRAM
      END

100: * START
      CARRIER.NAME='' ; CARRIER.ID = '' ; CARRIER.CITY='' ; CARRIER.STATE=''
      CLIENT.NAME='' ; CLIENT.ID = '' ; CLIENT.CITY='' ; CLIENT.STATE=''
      GOSUB DSP.CARRIER.SCN
200: * GET CARRIER AND CLIENT
      GOSUB GET.CLIENT.CARRIER
      IF EXIT.IT THEN GOTO TRASH.PROGRAM

************************************************************************
* Check for existing verify setting
************************************************************************
      VERIFY.ID = CLIENT.ID'R%5':'*':CARRIER.ID'R%5'
      READ VERIFY.REC FROM F.VERIFY,VERIFY.ID ELSE VERIFY.REC = ''
      VERIFY.FLG = VERIFY.REC<1>

************************************************************************
* Enter client/carrier verify setting.
************************************************************************

300:  CRT @(0,7): 'Enter the quick verify setting for this client and carrier.'
      IF VERIFY.FLG = '' THEN
         LN = '##'
      END ELSE
         LN = VERIFY.FLG'L#2'
      END
      HELP="Enter verify setting for client/carrier. [Y]es [N]o [X]Back [EX]it"
      CALL GEN.IN(0,8,LN,'',Q,0,2,'','',0,-2,8,0,QI,HELP,0,23)
      QI=OCONV(QI,'MCU')
      BEGIN CASE
         CASE QI='X'
            CARRIER.NAME='' ; CARRIER.ID = '' ; CARRIER.CITY='' ; CARRIER.STATE=''
            CLIENT.NAME='' ; CLIENT.ID = '' ; CLIENT.CITY='' ; CLIENT.STATE=''
            GOSUB DSP.CARRIER.SCN
            CRT @(0,8):@(-3):
            GOTO 200
         CASE QI='EX'
            STOP
         CASE QI='Y'
            Q=QI
         CASE QI='N'
            Q=QI
         CASE QI=''
            Q=VERIFY.FLG
         CASE 1
            CALL ELINE('ERROR - Invalid Answer.')
            GO 300
      END CASE
      VERIFY.FLG=Q
      CRT @(0,8):VERIFY.FLG'L#2'


************************************************************************
* Verify selection criteria is correct.
************************************************************************
400:  HELP="X=Change. EX=Exit. RETURN=Continue."
      CALL GEN.IN(0,22,'Enter selection please. --','',Q,0,2,'','',0,-2,22,0,QI,HELP,0,23)
      QI=OCONV(QI,'MCU')
      BEGIN CASE
         CASE QI='X'
            CRT @(0,22):@(-3):
            GOTO 300
         CASE QI='EX'
            STOP
         CASE QI=''
            NULL
         CASE 1
            CALL ELINE('Invalid entry. Must be X, EX, or RETURN.')
            GOTO 400
      END CASE

************************************************************************
* Save VERIFY Date
************************************************************************
      IF VERIFY.FLG # VERIFY.REC<1> THEN
         VERIFY.REC<5,-1> = VERIFY.REC<1>
         VERIFY.REC<1> = VERIFY.FLG
         VERIFY.REC<2,-1> = USER
         VERIFY.REC<3,-1> = DATE()
         VERIFY.REC<4,-1> = TIME()
         WRITE VERIFY.REC ON F.VERIFY,VERIFY.ID
      END
      GO 100

TRASH.PROGRAM:
      CRT @(-1):

      STOP

*----------------------------------------------------------------------
* PROCEDURE:  Initialize Screen
*----------------------------------------------------------------------
INIT.SCREEN: ***

      CALL AFS.SCR.REFRESH(PROG.NAME,PROG.DESC,PACK.NAME,CO.NAME,TIME.DATE,1)
      CRT @(0,2):STR('=',79):

      RETURN

*----------------------------------------------------------------------
* Display Edit Screen
*----------------------------------------------------------------------
DSP.CARRIER.SCN: ***

      GOSUB INIT.SCREEN
      CRT @(0, 3):'CARRIER : ':@(-4)
      CRT @(0, 4):'CLIENT  : ':@(-4)
      CRT @(0, 5):STR('=',79):
      CRT @(0,21):STR('=',79):
      CRT @(0,22):@(-4):
      CRT @(0,23):@(-4):

      RETURN

*-----------------------------------------------------------------------
* GET.CLIENT.CARRIER:
*-----------------------------------------------------------------------
GET.CLIENT.CARRIER:

GC.1:
      IF CARRIER.ID THEN
         MASK = CARRIER.ID'R%5'
      END ELSE
         MASK = '#####'
      END
      HELP = "Enter the carrier number. 'L'=List Setup Carriers/Clients 'X'=Back 'EX'=Exit"
      CRT @(10,3):@(-4)
      CALL GEN.IN(10,3,MASK,'',Q,0,20,'','',2,10,3,1,QI,HELP,0,23)
      QI = OCONV(QI,'MCU')
      IF QI='' AND CARRIER.ID THEN QI=CARRIER.ID
      BEGIN CASE
         CASE QI = 'X' OR QI='EX' OR QI=''
            CARRIER.NAME='' ; CARRIER.ID = '' ; CARRIER.CITY='' ; CARRIER.STATE=''
            CLIENT.NAME='' ; CLIENT.ID = '' ; CLIENT.CITY='' ; CLIENT.STATE=''
            EXIT.IT = TRUE
            RETURN

         CASE QI='L'
            GOSUB LIST.CARRIERS
            GOSUB DSP.CARRIER.SCN
            GOTO GET.CLIENT.CARRIER

         CASE NOT(QI MATCHES "0N")
            CALL SOUNDEX.DISPLAY(QI,'CARRIERS','SDX.CARRIER,NAME','1,3,4',ITEM.LIST)
            BEGIN CASE
               CASE ITEM.LIST = ''
                  GOTO GET.CLIENT.CARRIER
               CASE DCOUNT(ITEM.LIST<1>,@VM) GT 1
                  GOTO GET.CLIENT.CARRIER
               CASE ITEM.LIST # ''
                  IF NUM(ITEM.LIST<1,1>) THEN DATA ITEM.LIST<1,1>
                  GOTO GET.CLIENT.CARRIER
            END CASE

         CASE NUM(QI)
            T.CARRIER.ID = QI'R%5'
            CALL CLR.CONTROL(T.CARRIER.ID)
            GOSUB GET.CARRIER.INFO
            IF T.CARRIER.ID = '' THEN GOTO GC.1
            CARRIER.ID = T.CARRIER.ID

         CASE 1
            GOTO GC.1
      END CASE

      IF CARRIER.ID THEN
         CARRIER.DSP = CARRIER.ID'R%5':', ':CARRIER.NAME:' (':CARRIER.CITY:', ':CARRIER.STATE:')'
      END ELSE
         CARRIER.DSP = ''
      END
      CRT @(0, 3):'CARRIER : ':CARRIER.DSP:@(-4)

*-------------------------------------
* Enter Client Id/Name
*-------------------------------------

GC.2:
      MASK = '#####'
      HELP = "Enter the client number. 'L'=List Setup Carriers/Clients 'X'=Back 'EX'=Exit"
      CRT @(10,4):@(-4)
      CALL GEN.IN(10,4,MASK,'',Q,0,20,'','',2,10,4,1,QI,HELP,0,23)
      QI = OCONV(QI,'MCU')
      BEGIN CASE
         CASE QI = 'X'
            CARRIER.NAME='' ; CARRIER.ID = '' ; CARRIER.CITY='' ; CARRIER.STATE=''
            CLIENT.NAME='' ; CLIENT.ID = '' ; CLIENT.CITY='' ; CLIENT.STATE=''
            GOTO GC.1

         CASE QI='EX'
            EXIT.IT = TRUE
            RETURN

         CASE QI = ''

         CASE QI='L'
            GOSUB LIST.CARRIERS
            GOSUB DSP.CARRIER.SCN
            CRT @(0, 3):'CARRIER : ':CARRIER.DSP:@(-4)
            GOTO GC.2

         CASE NOT(QI MATCHES "0N")
            CALL SOUNDEX.DISPLAY(QI,'CLIENTS','SDX.CLIENTS,NAME','1,3,4',ITEM.LIST)
            BEGIN CASE
               CASE ITEM.LIST = ''
                  GOSUB DSP.CARRIER.SCN
                  GOTO GC.2
               CASE DCOUNT(ITEM.LIST<1>,@VM) GT 1
                  GOSUB DSP.CARRIER.SCN
                  GOTO GC.2
               CASE ITEM.LIST # ''
                  IF NUM(ITEM.LIST<1,1>) THEN DATA ITEM.LIST<1,1>
                  GOSUB DSP.CARRIER.SCN
                  GOTO GC.2
            END CASE

         CASE NUM(QI)
         CASE 1
            GOTO GC.2
      END CASE

*
* Get the optional client info
*
      IF QI # '' THEN
         T.CLIENT.ID = QI'R%5'
         GOSUB GET.CLIENT.INFO
         IF T.CLIENT.ID = '' THEN GOTO GC.2
         CLIENT.ID = T.CLIENT.ID
         CLIENT.DSP = CLIENT.ID'R%5':', ':CLIENT.NAME:' (':CLIENT.CITY:', ':CLIENT.STATE:')'
      END ELSE
         CLIENT.ID = ''
         CLIENT.DSP = '(All Clients)'
      END

      CRT @(0, 4):'CLIENT  : ':CLIENT.DSP:@(-4)

*
* Load VERIFY Record
*
      T.ID = CARRIER.ID
      IF CLIENT.ID THEN T.ID:='*':CLIENT.ID
      READ VERIFY.REC FROM F.VERIFY,T.ID ELSE
         VERIFY.REC=''
      END

      RETURN
*-----------------------------------------------------------------------
* PROCEDURE: GET.CLIENT.INFO
*-----------------------------------------------------------------------
GET.CLIENT.INFO: ***

      MATREAD CLIENT.REC FROM F.CLIENTS,T.CLIENT.ID THEN
         CLIENT.NAME = CL.NAME
         CLIENT.CITY = CL.CITY
         CLIENT.STATE = CL.ST
         ZIP.LEN = CL.ZIP.FLAG
      END ELSE
         CALL ELINE("ERROR - Client ":T.CLIENT.ID:" is not a valid client.")
         T.CLIENT.ID = ''
      END

      RETURN



*-----------------------------------------------------------------------
* PROCEDURE: GET.CARRIER.INFO
*-----------------------------------------------------------------------
GET.CARRIER.INFO: ***

      READ CARRIER.REC FROM F.CARRIERS,T.CARRIER.ID THEN
         CARRIER.NAME = CARRIER.REC<1>
         CARRIER.CITY = CARRIER.REC<3>
         CARRIER.STATE = CARRIER.REC<4>
      END ELSE
         CALL ELINE("ERROR - Carrier ":T.CARRIER.ID:" is not a valid carrier.")
         T.CARRIER.ID = ''
      END

      RETURN

*----------------------------------------------------------------------
* LIST.CARRIERS
*----------------------------------------------------------------------
LIST.CARRIERS:

      CRT @(0,0):@(-1)
      EXECUTE 'SORT FB.QUICK.VERIFY CARRIER CLIENT'
      CALL ELINE('')
      CALL FB.DRAW.BOX(0,5,80,PAGE.SIZE+2,' ','')

      RETURN
