$OPTIONS EXEC.EQ.PERF
$OPTIONS INFO.ABORT
*
*
      SUBROUTINE FB.2.1.VAR.DIST(F.VAR,VARIANCE,VAR.REC,QI)
$OPTIONS HEADER.EJECT
ERRMSG=''
* SUBROUTINE TO ENTER VARIANCE DISTRIBUTIONS
*
* FREIGHT BILLING PACKAGE
*
* AUTHOR: Brian M. Queen
* DATE: August 22, 1993
*
* Initialize variables
*
      PROMPT''
      OPT=QI
      IF OPT='I' THEN
        INQUIRE=1
        MODE='EDIT'
      END ELSE
        INQUIRE=0
        MODE='INPUT'
      END
      LN=0
      CALL FB.CLEAR.WINDOW
*
* Display window heading
*
      CRT @(36,6):"-------- Variance Distribution ---------"
      IF OPT NE 'V' THEN
        CRT @(36,7):"Lin Code Description.........     Amount"
      END ELSE
        CRT @(36,7):"    Code Description.........     Amount"
      END
      CRT @(36,8):STR("-",40)
*
* Start input
*
      PAGE=1
      GOSUB 9000; * Display page
*
* Enter variance distribution line number.
*
100   IF MODE='INPUT' THEN
        LN=LN+1
        QI=LN
        IF LN GT STOP THEN
          PAGE=PAGE+1
          GOSUB 9000; * Display page
        END
      END ELSE
        HELP="Enter Line Num. [cr]=End. [B]=Back. [F]=Forward page. [EX]=Exit."
        CALL GEN.IN(36,18,'Enter line number. ###','',Q,0,3,'','',0,-3,18,0,QI,HELP,0,23)
      END
      QI=OCONV(QI,'MCU')
      BEGIN CASE
        CASE QI='B'
          PAGE=PAGE-1
          IF PAGE=0 THEN PAGE=1
          GOSUB 9000; * Display page
        CASE QI='F'
          PAGE=PAGE+1
          GOSUB 9000; * Display page
        CASE QI=''
          GOTO 9900
        CASE QI='X'
          CALL FB.CLEAR.WINDOW
          RETURN
        CASE QI='EX'
          STOP
        CASE NUM(QI)
          IF INQUIRE THEN
            CALL ELINE('Can not access detail in inquiry.')
          END ELSE
            SEL=QI
            GOSUB 8000
            IF QI='' & OPT='V' THEN RETURN
IF QI='X' THEN
  CALL FB.CLEAR.WINDOW
  RETURN
END
          END
      END CASE
      GOTO 100
*-----------------------------------------------------------------------
*
* INTERNAL SUBROUTINES
*
*-----------------------------------------------------------------------
*
* Enter variance codes
*
8000  IF SEL LT START ! SEL GT STOP THEN
        CALL ELINE('Invalid entry.')
        RETURN
      END
      GOSUB 9100; * Calculate line
*
8010  IF OPT='V' THEN
        MASK=SPACE(4)
        HELP="Enter Code. [cr]=End. [X]=Back. [EX]=Exit."
      END ELSE
        HELP="Enter Code. [cr]=Accept. [D]=Delete. [E]=Change Mode. [X]=Back. [EX]=Exit."
        MASK=VAR.REC<1,SEL>'L#4'
      END
      CALL GEN.IN(40,LINE,MASK,'',Q,0,4,'','',2,-4,LINE,0,QI,HELP,0,23)
      QI=OCONV(QI,'MCU')
      BEGIN CASE
        CASE QI='EX'
          STOP
        CASE QI='X'
          IF OPT NE 'V' THEN
            CRT @(40,LINE):VAR.REC<1,SEL>'L#4'
            IF MODE='INPUT' THEN LN=LN-2
          END
          RETURN
        CASE QI='D' & OPT NE 'V'
          VAR.REC=DELETE(VAR.REC,1,SEL)
          VAR.REC=DELETE(VAR.REC,2,SEL)
          VAR.REC=DELETE(VAR.REC,3,SEL)
          GOSUB 9000; * Display page
          RETURN
        CASE QI='E' & OPT NE 'V'
          MODE='EDIT'
          CRT @(40,LINE):VAR.REC<1,SEL>'L#4'
          RETURN
        CASE QI='' & OPT NE 'V'
          Q=VAR.REC<1,SEL>
          IF Q='' & MODE='INPUT' THEN
            MODE='EDIT'
            RETURN
          END
          IF Q='' THEN RETURN
        CASE QI='' & OPT = 'V'
          X=DCOUNT(VAR.REC<1>,CHAR(253))
          FOR I=1 TO X
            IF VAR.REC<3,I> NE '*' THEN
              CALL ELINE('All codes have not been verified.')
              GOTO 8010
            END
          NEXT I
          RETURN
        CASE OPT NE 'V'
          VAR.REC<1,SEL>=Q
      END CASE
*
      IF OPT NE 'V' THEN
        CODE=VAR.REC<1,SEL>
        GOSUB 8090
        IF ACK='ERR' THEN GOTO 8010
VAR.REC<3,SEL>=DESC
        CRT @(40,LINE):VAR.REC<1,SEL>'L#4'
        CRT @(45,LINE):VAR.REC<3,SEL>'L#20'
      END ELSE
        VAR.CODE=Q
        LOCATE VAR.CODE IN VAR.REC<1> BY 'AR' SETTING VERIFIED ELSE VERIFIED=0
        IF VERIFIED THEN
          READV DESC FROM F.VAR,VAR.REC<1,SEL>,1 ELSE DESC='Unknown'
          CRT @(40,LINE):VAR.REC<1,VERIFIED>'L#4'
          CRT @(45,LINE):VAR.REC<3,VERIFIED>'L#20'
        END ELSE
          CALL ELINE("Invalid code.")
          GOTO 8010
        END
        BACK.FLAG=0
      END
*
8020  IF OPT='V' THEN
        MASK=SPACE(10)
      END ELSE
        MASK=VAR.REC<2,SEL>'R26#10'
      END
      HELP="Enter Variance Amount. [cr]=Accept. [X]=Back to code. [EX]=Exit."
      CALL GEN.IN(66,LINE,MASK,'AMT2',Q,0,10,'','',2,-10,LINE,0,QI,HELP,0,23)
      QI=OCONV(QI,'MCU')
      BEGIN CASE
        CASE QI='X'
          IF OPT NE 'V' THEN
            CRT @(66,LINE):VAR.REC<2,SEL>'R26#10'
          END ELSE
            CRT @(66,LINE):SPACE(10)
          END
          GOTO 8010
        CASE QI='EX'
          STOP
        CASE QI='' & OPT NE 'V'
          NULL
        CASE OPT NE 'V'
          VAR.REC<2,SEL>=Q
        CASE OPT='V'
          VAR.AMT=Q'R0'
          IF VAR.AMT NE VAR.REC<2,VERIFIED> THEN
            CALL ELINE('Invalid variance amount.')
            GOTO 8020
          END
          BACK.FLAG=0
          VAR.REC<3,VERIFIED>='*'
      END CASE
      CRT @(66,LINE):VAR.REC<2,SEL>'R26#10'
      RETURN
*
* Check for valid variance code
*
8090  IF CODE='' ELSE
        READV DESC FROM F.VAR,CODE,1 ELSE
8095      CRT @(0,22):CHAR(7):CODE:" not on file. Do you want to add?(Y/N) ":
          INPUT ACK,1
          CRT @(0,22):@(-4):
          ACK=OCONV(ACK,'MCU')
          IF ACK='Y' ! ACK='N' ELSE GOTO 8095
          IF ACK='N' THEN
            ACK='ERR'
            RETURN
          END
*
8099      CRT @(0,22):@(-4):"Enter description ":
          INPUT DESC:
          CRT @(0,22):@(-4):'Is the description - ':DESC:' correct?(Y/N) :':
          INPUT ACK,1:
          ACK=OCONV(ACK,'MCU')
          CRT @(0,22):@(-4):
          IF ACK='N' THEN GOTO 8099
          IF ACK='Y' THEN WRITEV DESC ON F.VAR,CODE,1
        END
      END
      ACK=''
      RETURN
*
* Print page
*
9000  START=(PAGE*9)-8
      STOP=START+8
      FOR SEL=START TO STOP
        GOSUB 9100; * Calculate line
        IF OPT NE 'V' THEN
          IF VAR.REC<1,SEL>='' THEN
            CRT @(36,LINE):SEL'L#3':@(-4):
          END ELSE
*           READV DESC FROM F.VAR,VAR.REC<1,SEL>,1 ELSE DESC='Unknown'
            CRT @(36,LINE):SEL'L#3':' ':VAR.REC<1,SEL>'L#4':' ':VAR.REC<3,SEL>'L#20':' ':VAR.REC<2,SEL>'R26#10'
          END
        END
      NEXT SEL
      RETURN
*
* Calculate line
*
9100  X=REM(SEL,9)
      IF X=0 THEN X=9
      LINE=8+X
      RETURN
*
* Compress variance codes and return
*
9900  IF INQUIRE ELSE
        X=DCOUNT(VAR.REC<1>,CHAR(253))
        FOR I=X TO 1 STEP -1
          IF VAR.REC<1,I>='' THEN
            VAR.REC=DELETE(VAR.REC,1,I)
            VAR.REC=DELETE(VAR.REC,2,I)
            VAR.REC=DELETE(VAR.REC,3,I)
          END
        NEXT I
*
* Check for equal totals
*
        TOT=0
        X=DCOUNT(VAR.REC<1>,CHAR(253))
        FOR I=1 TO X
          TOT=TOT+VAR.REC<2,I>
        NEXT I
        IF TOT'R0' NE VARIANCE'R0' THEN
          CALL ELINE('Distributions are not equal to variance.')
          GOSUB 9000; * Display page
          GOTO 100
        END
      END
      CALL FB.CLEAR.WINDOW
      RETURN
