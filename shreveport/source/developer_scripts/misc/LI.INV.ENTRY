$OPTIONS EXEC.EQ.PERF
$OPTIONS INFO.ABORT
*
*
SUBROUTINE LI.INV.ENTRY( CLIENT.ID )
$OPTIONS HEADER.EJECT
ERRMSG=''
* PROGRAM LI.INV.ENTRY
* PROGRAM TO CREATE OR MODIFY INVENTORY FOR LOGISTICS INVOICING
*
* FREIGHT BILLING PACKAGE
*
* AUTHOR: VALERIE DOTY
* DATE:   JANUARY 21, 1994
*
***********************************************************************
*
* Initialize variables
*
      PROMPT''
      ADD.FLAG = 0
      PROG.NAME = 'LI.INV.ENTRY'
      PROG.DESC = 'INVENTORY MAINTENANCE'
      PACK.NAME = 'LOGISTICS INVOICING'
*
       UPL.VIN = 'FBBP' ;  UPL.VIN<2> = 'LI.INV.ENTRY' ; UPL.VOUT = ''                    ;* NPR_UPL 04/23/2010
       CALL UPD.PROGRAM.LOG(UPL.VIN,UPL.VOUT)                                                           ;* NPR_UPL 04/23/2010
* Open files
*
      OPEN '', 'LI.INVENTORY' TO LI.INV ELSE
        CALL OPEN.ABORT("LI.INVENTORY", PROG.NAME)
      END
*
* Display page heading
*
      CALL AFS.SCR.HEAD(CO.ID, FILE.ID, PERIOD, PROG.NAME, PROG.DESC, PACK.NAME, CO.NAME, TIME.DATE, 1)
      TEXT = 'Client number ':CLIENT.ID
      CALL CENTER(TEXT, 80)
      SCREEN = @(0,2) : @(-3) : TEXT
      SCREEN := @(0,4) : 'Part Number   : '
      SCREEN := @(0,5) : 'Description   : '
      CRT SCREEN
*
***********************************************************************
* Enter inventory number,  if not on file then create if desired
***********************************************************************
*
100   CRT @(16,4):
      ADD.FLAG = 0
      HELP = "Enter part number. [N] = New. [cr] = Exit."
      CALL GEN.IN(16,4,'#######','',Q,0,7,'','',0,-7,4,0,QI,HELP,0,23)
      QI = OCONV(QI, 'MCU')
      BEGIN CASE
        CASE QI = ''
          RETURN
        CASE QI = 'N'
          ID = 100
          LOOP
            ID = ID+1
            READ XX FROM LI.INV, CLIENT.ID : '*' : ID ELSE XX = '**'
          UNTIL XX = '**' DO REPEAT
          Q = ID
        CASE NUM(QI)
          Q = QI
        CASE 1
          GOTO 100
      END CASE
*
* Read client item
*
      INV.ID = CLIENT.ID : '*' : Q
      READU INV.REC FROM LI.INV, INV.ID THEN
        CRT @(16,5) : INV.REC<1> :
      END ELSE
        HELP = "Enter either Y for Yes or N for No."
        LN = 'Part number ':Q:' not on file. Do you want to create.? N'
        CALL GEN.IN(0,22,LN,'',ACK,0,1,'','',1,-1,22,1,QI,HELP,0,23)
        QI = OCONV(QI, 'MCU')
        IF QI # 'Y' THEN
          RELEASE
          GOTO 100
        END
        INV.REC = ''
        ADD.FLAG = 1
      END
*
* Display client data
*
      IF ADD.FLAG THEN
        GOSUB 1000
      END
*
* Get selection from operator
*
200   HELP = "[C] = Change, [X] = File, [D] = Delete, [EX] = Exit, [cr] = Ignore."
      CALL GEN.IN(0,22,'Enter Selection Number ##','',Q,0,2,'','',1,-2,22,1,QI,HELP,0,23)
      QI = OCONV(QI, 'MCU')
      BEGIN CASE
        CASE QI = ''
          CRT SCREEN
          RELEASE
          GOTO 100
        CASE QI = 'C'
          GOSUB 1000
          GOTO 200
        CASE QI = 'EX'
          RETURN
        CASE QI = 'X'
          WRITE INV.REC ON LI.INV, INV.ID
          CRT SCREEN
          GOTO 100
        CASE QI = 'D'
          HELP = 'Enter Y to delete or N not to delete.'
          CALL GEN.IN(0,22,'Are you sure? N','',Q,0,1,'','',1,-1,22,1,QI,HELP,0,23)
          QI = OCONV(QI, 'MCU')
          IF QI = 'Y' THEN
            DELETE LI.INV, INV.ID
            CRT SCREEN
            GOTO 100
          END ELSE
            GOTO 200
          END
        CASE 1
          GOTO 200
      END CASE
*
***********************************************************************
* Change sku name
***********************************************************************
*
1000  HELP = "Enter name. [space] = Clear."
      MASK = INV.REC<1>:STR('-', 30-LEN(INV.REC<1>))
      CALL GEN.IN(16,5,MASK,'',Q,0,30,'','',2,-30,5,0,QI,HELP,0,23)
      IF Q#'' THEN INV.REC<1> = Q
      IF Q = ' ' THEN INV.REC<1> = ''
      CRT @(16, 5):INV.REC<1>'L#30'
*
      RETURN
*
***********************************************************************
*
   END
