***********************************************************************
*
*
* PROGRAM TO DISPLAY LATEST FUEL SURCHARGE
*
*
* AUTHOR: David Friend
*
*                           EDIT HISTORY
*                           ------------
*    DATE  : NAME : DESCRIPTION
*  ---------------------------------------------------
*  05/09/06  JCW01  CHANGES TO @SELECTED, CLEARSELECT, AND STMT<X>
***********************************************************************
*
*
$INCLUDE PROG.ADMIN INC_OPTIONS
*
* Initialize variables
*
      CALL GET.USER(USER.NAME)
      PROMPT''
      PROG.NAME='FB.FSC.2'
      PROG.DESC='Latest Fuel Surcharge'
      PACK.NAME='Freight Billing'
      ORIG.CARRIER.ID=''
       UPL.VIN = 'FBBP' ;  UPL.VIN<2> = 'FB.FSC.2' ; UPL.VOUT = ''                    ;* NPR_UPL 04/23/2010
       CALL UPD.PROGRAM.LOG(UPL.VIN,UPL.VOUT)                                                           ;* NPR_UPL 04/23/2010
      METHOD=''
      METHOD<1>='GROSS'
      METHOD<2>='NET'
*
* Open files
*
      OPEN '','CLIENTS' TO F.CLIENTS ELSE
        CALL OPEN.ABORT('CLIENTS',PROG.NAME)
      END
      OPEN '','CARRIERS' TO F.CARRIERS ELSE
        CALL OPEN.ABORT('CARRIERS',PROG.NAME)
      END
      OPEN '','FB.FSC' TO F.FSC ELSE
        CALL OPEN.ABORT('FB.FSC',PROG.NAME)
      END
*
* Display screen heading
*
30    CALL AFS.SCR.HEAD(CO.ID,FILE.ID,'',PROG.NAME,PROG.DESC,PACK.NAME,CO.NAME,TIME.DATE,1)

*
* Get carrier number to process or all
*
ENTER.CARRIER:***
      HELP='Enter the carrier number to reportor <RETRUN> for All carriers.'
      CALL GEN.IN(0,3,'Enter carrier number.  #####','',Q,0,8,'','',1,-5,3,0,QI,HELP,0,23)
      QI=OCONV(QI,'MCU')
      ALL.ONE=''
      BEGIN CASE
        CASE QI='X'!QI='EX'
          GOTO EXIT.PROGRAM
        CASE QI=''
          ALL.ONE='ALL'
        CASE NOT(QI MATCHES "0N")
          VIN=0 ; * Starting Chars In
          VOUT=''
          CALL SOUNDEX.DISPLAY.NEW(VIN,VOUT,QI,'CARRIERS','SDX.CARRIER,NAME','1,2,3,4',ITEM.LIST)
          CALL AFS.SCR.REFRESH(PROG.NAME,PROG.DESC,PACK.NAME,CO.NAME,TIME.DATE,1)
          BEGIN CASE
             CASE ITEM.LIST = ''
                GOTO ENTER.CARRIER                    ;* START OVER
             CASE DCOUNT(ITEM.LIST<1>,@VM) GT 1
                GOTO ENTER.CARRIER                    ;* START OVER
             CASE 1
                IF NUM(ITEM.LIST<1,1>) THEN DATA ITEM.LIST<1,1>
                GOTO ENTER.CARRIER                    ;* START OVER
          END CASE
        CASE NUM(QI)
        CASE 1
           GO ENTER.CARRIER                           ;* START OVER
      END CASE
      QI=QI'R%5'
      IF ALL.ONE#'ALL' THEN
        READ CARRIER.REC FROM F.CARRIERS,QI ELSE
          CALL ELINE('Carrier ':QI:' is not on file.')
          GOTO ENTER.CARRIER
        END
        TEMP.NAME=CARRIER.REC<1>
      END ELSE
        QI='ALL'
        TEMP.NAME='All carriers.'
      END
      CRT@(22,3):QI:' - ':TEMP.NAME
      IF QI#'ALL' THEN CARRIER.ID=QI ELSE CARRIER.ID=''
      ORIG.CARRIER.ID=QI'R%5'
*
*  Verify selection
*
ENTER.VERIFICATION:***
      HELP="X=Change. EX=Exit. RETURN=Continue."
      CALL GEN.IN(0,22,'Enter selection please. --','',Q,0,2,'','',0,-2,22,1,QI,HELP,0,23)
      QI=OCONV(QI,'MCU')
      BEGIN CASE
         CASE QI='X'
            CRT @(0,22):@(-3):
            GO ENTER.CARRIER
         CASE QI='EX'
            GOTO EXIT.PROGRAM
         CASE QI=''
            NULL
         CASE 1
            CALL ELINE('Invalid entry. Must be X, EX, or RETURN.')
            GOTO ENTER.VERIFICATION
      END CASE
*
* Start processing
*
      STMT='SSELECT FB.FSC'
      IF ORIG.CARRIER.ID#"00ALL" THEN
        STMT := ' WITH CARRIER# = "':ORIG.CARRIER.ID:'"'
      END
      STMT:=' BY CARRIER BY CLIENT'
      EXECUTE STMT RTNLIST FSC.LIST CAPTURING OUTPUT
      NUM.BILLS = @SELECTED
*     IF NOT( @SELECTED ) THEN ; * JCW01
      IF @SELECTED < 1 THEN    ; * JCW01
        CALL ELINE('ERROR - No carrier information on file.')
        GOTO EXIT.PROGRAM
      END
*
* Print report
*
* Set-up printer
*
      WIDTH=132
      CTR=INT(WIDTH/2)
**************************************************************
      VIN=''
      VIN<3>='R'
      CALL LASER.CODE(VIN,VOUT)
      IF VOUT<1>#'' THEN 
        CALL ELINE(VOUT<1>)
        STOP
      END
      IF VOUT<3>#'' THEN EXECUTE VOUT<3>
      PRINTER ON
      IF VOUT<2>#'' THEN PRINT VOUT<2>:
*
      L.TAB=CTR-(LEN(PROG.DESC)/2)
      L.TAB=L.TAB-LEN(PROG.NAME)
      TEMP=PROG.NAME:SPACE(L.TAB):PROG.DESC
      R.TAB=WIDTH-LEN(TEMP)
      R.TAB=R.TAB-LEN(TIMEDATE())
      HEAD1=PROG.NAME:SPACE(L.TAB):PROG.DESC:SPACE(R.TAB):TIMEDATE():"'L'"
*
      TEMP=CO.NAME
*     R.TAB=(WIDTH-LEN(TEMP))-9
*     HEAD2=TEMP:SPACE(R.TAB):"PAGE 'PL'"
      R.TAB=(WIDTH-LEN(TEMP))-20
      HEAD2=TEMP:SPACE(R.TAB):USER.NAME'L(#10 )':"PAGE 'PL'"
      HEAD3="'L'"
*
      HEAD4=""
      HEAD5=STR('-',WIDTH):"'L'"
*
      HEAD6=''
      HEAD6:=SPACE(2):'Carrier''L#32'
      HEAD6:=SPACE(2):'Client Override''L#32'
      HEAD6:=SPACE(2):'Active ''L#8'
      HEAD6:=SPACE(2):'Begin Date''L#12'
      HEAD6:=SPACE(2):'End Date''L#10'
      HEAD6:=SPACE(2):'FSC %''L#7'
      HEAD6:=SPACE(2):'Calc Method''L#11'
      HEAD6:="'L'"
*
      HEAD7=STR('-',WIDTH):"'L'"
      HED=HEAD1:HEAD2:HEAD3:HEAD4:HEAD5:HEAD6:HEAD7
      HEADING HED
      OLD.CARRIER.NUM='*****'
      LOOP
        DONE=0
        READNEXT FSC.ID FROM FSC.LIST ELSE DONE=1
      UNTIL DONE=1 DO
        READ FSC.REC FROM F.FSC,FSC.ID THEN
          GOSUB BUILD.DET.LINE
        END
      REPEAT
      PRINTER OFF
      PRINTER CLOSE

*
EXIT.PROGRAM:*
*
      STOP


BUILD.DET.LINE:***
      CARRIER.NUM=FIELD(FSC.ID,'*',1)
      IF OLD.CARRIER.NUM#CARRIER.NUM THEN
         CARRIER.TEXT=CARRIER.NUM:' '
         READ CARRIER.REC FROM F.CARRIERS,CARRIER.NUM THEN
            CARRIER.TEXT:=CARRIER.REC<1>
         END ELSE
            CARRIER.TEXT:=" ** Not On File **"
         END
         OLD.CARRIER.NUM=CARRIER.NUM
      END ELSE
        CARRIER.TEXT=SPACE(33)
      END
      LINE=CARRIER.TEXT'L#33':SPACE(1)
      CLIENT.ID=FIELD(FSC.ID,'*',2)
      IF CLIENT.ID#'' THEN
        READV CLIENT.NAME FROM F.CLIENTS,CLIENT.ID,2 THEN
          CLIENT.TEXT=CLIENT.ID:" ":CLIENT.NAME
        END ELSE
          LINE:=CLIENT.ID:" ":" ** Not On File **"
        END
      END ELSE
        CLIENT.TEXT=STR('-',33)
      END
      LINE:=CLIENT.TEXT'L#33':SPACE(3)
      NUM.OF.ITEMS = DCOUNT(FSC.REC<1>,@VM)
      LINE:=FSC.REC<1,NUM.OF.ITEMS>'L#10':SPACE(2)
      LINE:=FSC.REC<2,NUM.OF.ITEMS>'D2/':SPACE(4)
      IF FSC.REC<3,NUM.OF.ITEMS>='' THEN
        LINE:=' <OPEN> '
      END ELSE
        LINE:=FSC.REC<3,NUM.OF.ITEMS>'D2/'
      END
      LINE:=SPACE(2):FSC.REC<4,NUM.OF.ITEMS>'R26#7':SPACE(7)
      IF FSC.REC<5,NUM.OF.ITEMS>='1' OR FSC.REC<5,NUM.OF.ITEMS>='2' THEN
        LINE:=METHOD<FSC.REC<5,NUM.OF.ITEMS>>'L#12'
      END ELSE
        LINE:='Unkown Method'
      END
      PRINT LINE
      RETURN
