******************************************************************************
* PROGRAM NAME : AFS.EBILL.UPDATE
* AUTHOR       : JMK
* DATE         : 03/31/08
* PURPOSE      : TO UPDATE THE UPS ELECTRONIC BILLING FILE WITH AFS COMM. AMTS
******************************************************************************
* MAINTENANCE  :
******************************************************************************
*
* INITIALIZE VARIABLES
*
      EQU TB TO CHAR(9)
      PROG.NAME='AFS.EBILL.UPDATE'
      TYPE='CSV'
      CLIENT.ID='01825'
      PLAN.ID='00000679FY'
      EMAIL.PATH='E:\ARDENT\PUBLIC\EMAILDATA\'
      CALL GET.USER(USER)
      CALL CHANNEL(CH.NUM)
*
* OPEN FILES
*
      OPEN '','FB.TABLES' TO F.TABLES ELSE
         CALL OPEN.ABORT('FB.TABLES',PROG.NAME)
      END
      OPEN '','EMAILDATA' TO F.EMAILDATA ELSE
         CALL OPEN.ABORT('EMAILDATA',PROG.NAME)
      END
      OPEN '','FB.BILLS.HIST,':CLIENT.ID TO F.BILLS ELSE
         CALL OPEN.ABORT('FB.BILLS.HIST,':CLIENT.ID,PROG.NAME)
      END
      OPEN '','FB.UNPROCESS,':CLIENT.ID TO F.UNPROCESS ELSE
         CALL OPEN.ABORT('FB.UNPROCESS,':CLIENT.ID,PROG.NAME)
      END
      OPEN '','BILLDATA.EBILL.HIST' TO F.BILLDATA.HIST ELSE
         CALL OPEN.ABORT('BILLDATA.EBILL.HIST',PROG.NAME)
      END
      OPEN '','BILLDATA.MKERBOW' TO F.BILLDATA.MKERBOW ELSE
         CALL OPEN.ABORT('BILLDATA.MKERBOW',PROG.NAME)
      END
      OPEN '','AR-BRIDGE' TO F.ARBRIDGE ELSE
         CALL OPEN.ABORT('AR-BRDIGE',PROG.NAME)
      END
      OPEN '','AR.BRIDGE.LAST' TO F.ARBRIDGE.LAST ELSE
         CALL OPEN.ABORT('AR.BRIDGE.LAST',PROG.NAME)
      END
      READ DUPSBILL.CODES FROM F.TABLES,'UPSBILL.CODES' ELSE DUPSBILL.CODES=''
      READ PROCESS.DATES FROM F.UNPROCESS,'PROCESS.DATES' THEN
         BEG.DATE=PROCESS.DATES<1>
         END.DATE=PROCESS.DATES<2>
      END
*
* START BY GATHERING THE FB COMMISSION INFORMATION
*
      COMM.ARRAY=''
      STMT='SELECT FB.UNPROCESS,01825 WITH FILE.NAME = "FB.BILLS,':CLIENT.ID:'" ITEM.NAME'
      EXECUTE STMT CAPTURING RESULTS
      EOF=0
      CNT=0
      LOOP
         READNEXT PRO.ID ELSE EOF=1
      UNTIL EOF DO
         CNT+=1
         READ PRO.REC FROM F.BILLS,PRO.ID THEN
            CARRIER.ID=FIELD(PRO.ID,'*',1)
            PRO.NO=FIELD(PRO.ID,'*',2)
            IOT=FIELD(PRO.NO,'-',3)
            SEQ=FIELD(PRO.ID,'*',3)
            IF SEQ#"0" THEN GO NEXT.BILL
            SHIPPER.NO=PRO.NO[1,3]:PRO.NO[5,3]
            SHIPPER.NO=SHIPPER.NO'R%10'
            UPS.DATE=PRO.NO[8,6]
            ACTUAL.CHG=PRO.REC<8>
            DISC.COMM.AMT=PRO.REC<42>
            OVCHG.COMM.AMT=PRO.REC<43>
            CONS.COMM.AMT=PRO.REC<44>
            MGMT.SVC.FEE=PRO.REC<165>
            AFS.FEE.AMT=DISC.COMM.AMT+OVCHG.COMM.AMT+CONS.COMM.AMT+MGMT.SVC.FEE
            LOCATE SHIPPER.NO IN COMM.ARRAY<1> BY 'AR' SETTING SPOS ELSE
               INS SHIPPER.NO BEFORE COMM.ARRAY<1,SPOS>
               FOR ATT=2 TO 17
                  INS 0 BEFORE COMM.ARRAY<ATT,SPOS>
               NEXT ATT
            END
            BEGIN CASE
               CASE IOT='I'
                  COMM.ARRAY<2,SPOS>+=ACTUAL.CHG
                  COMM.ARRAY<3,SPOS>+=AFS.FEE.AMT
               CASE IOT='T'
                  COMM.ARRAY<6,SPOS>+=ACTUAL.CHG
                  COMM.ARRAY<7,SPOS>+=AFS.FEE.AMT
               CASE IOT='O'
                  COMM.ARRAY<10,SPOS>+=ACTUAL.CHG
                  COMM.ARRAY<11,SPOS>+=AFS.FEE.AMT
            END CASE
NEXT.BILL: *
         END
      REPEAT
*
* GET THE NON-SHIPMENT FEES AND APPLY TO THE UPDATE INVOICE AMOUNTS
*
      READ AR.REC FROM F.ARBRIDGE,CLIENT.ID ELSE
         READ AR.REC FROM F.ARBRIDGE.LAST,CLIENT.ID:'*':END.DATE ELSE AR.REC=''
      END
      IF AR.REC='' THEN
         READ AR.REC FROM F.ARBRIDGE,CLIENT.ID:'-TEST' ELSE AR.REC=''
      END
      FEES.TTL=0
      NVAL=DCOUNT(AR.REC<1>,@VM)
      FOR VAL=1 TO NVAL
         SCODE=AR.REC<1,VAL>
         IF INDEX(SCODE,'-',1) THEN
            SCODE=FIELD(SCODE,'-',1)
         END
         SCODE.AMT=AR.REC<3,VAL>
         BEGIN CASE
            CASE SCODE='1'
            CASE SCODE='4'
            CASE SCODE='6'
            CASE SCODE='10'
            CASE SCODE='11'
            CASE SCODE='17'
            CASE SCODE='18'
            CASE SCODE='21'
            CASE SCODE='22'
            CASE SCODE>='25' AND SCODE<='37'
            CASE SCODE>='50' AND SCODE<='63'
            CASE SCODE>='140' AND SCODE<='149'
            CASE SCODE='1001'
            CASE SCODE='1004'
            CASE SCODE='1006'
            CASE SCODE='1010'
            CASE SCODE='1011'
            CASE SCODE='1017'
            CASE SCODE='1018'
            CASE SCODE='1021'
            CASE SCODE='1022'
            CASE SCODE>='1025' AND SCODE<='1037'
            CASE SCODE>='1050' AND SCODE<='1063'
            CASE SCODE>='1140' AND SCODE<='1149'
            CASE SCODE='3001'
            CASE SCODE='3004'
            CASE SCODE='3006'
            CASE SCODE='3010'
            CASE SCODE='3011'
            CASE SCODE='3017'
            CASE SCODE='3018'
            CASE SCODE='3021'
            CASE SCODE='3022'
            CASE SCODE>='3025' AND SCODE<='3037'
            CASE SCODE>='3050' AND SCODE<='3063'
            CASE SCODE>='3140' AND SCODE<='3149'
            CASE 1
               FEES.TTL+=SCODE.AMT
         END CASE
      NEXT VAL
      UPS.TTL=SUM(COMM.ARRAY<2>)+SUM(COMM.ARRAY<6>)+SUM(COMM.ARRAY<10>)
      FEES.APPLY=0
      NVAL=DCOUNT(COMM.ARRAY<1>,@VM)
      FOR VAL=1 TO NVAL
         SHIPPER.NO=COMM.ARRAY<1,VAL>
         IAMT.SHIPPER=COMM.ARRAY<2,VAL>
         TAMT.SHIPPER=COMM.ARRAY<6,VAL>
         OAMT.SHIPPER=COMM.ARRAY<10,VAL>
         UPS.SHIPPER=IAMT.SHIPPER+TAMT.SHIPPER+OAMT.SHIPPER
         FEES.SHIPPER=(FEES.TTL*UPS.SHIPPER/UPS.TTL)'R0'
         FEES.APPLY+=FEES.SHIPPER
         IF VAL=NVAL THEN
            IF FEES.APPLY#FEES.TTL THEN
               FEES.ADD.APPLY=FEES.TTL-FEES.APPLY
               FEES.SHIPPER+=FEES.ADD.APPLY
            END
         END
         APPLY.SHIPPER=0
         IFEES.SHIPPER=(FEES.SHIPPER*IAMT.SHIPPER/UPS.SHIPPER)'R0'
         APPLY.SHIPPER+=IFEES.SHIPPER
         TFEES.SHIPPER=(FEES.SHIPPER*TAMT.SHIPPER/UPS.SHIPPER)'R0'
         APPLY.SHIPPER+=TFEES.SHIPPER
         OFEES.SHIPPER=(FEES.SHIPPER*OAMT.SHIPPER/UPS.SHIPPER)'R0'
         APPLY.SHIPPER+=OFEES.SHIPPER
         IF APPLY.SHIPPER#FEES.SHIPPER THEN
            ADD.APPLY.SHIPPER=FEES.SHIPPER-APPLY.SHIPPER
            OFEES.SHIPPER+=ADD.APPLY.SHIPPER
         END
         COMM.ARRAY<3,VAL>+=IFEES.SHIPPER
         COMM.ARRAY<7,VAL>+=TFEES.SHIPPER
         COMM.ARRAY<11,VAL>+=OFEES.SHIPPER
         COMM.ARRAY<20,VAL>=FEES.SHIPPER
      NEXT VAL
      UPS.TTL=SUM(COMM.ARRAY<2>)+SUM(COMM.ARRAY<6>)+SUM(COMM.ARRAY<10>)
      SHP.TTL=SUM(COMM.ARRAY<20>)
      AFS.TTL=SUM(COMM.ARRAY<3>)+SUM(COMM.ARRAY<7>)+SUM(COMM.ARRAY<11>)
*
* NOW GET THE UPS EBILL RECORD FOR THIS WEEK.
*
      UPS.EBILL.ID=PLAN.ID:'_':UPS.DATE[1,2]:'_':UPS.DATE[3,2]:'_20':UPS.DATE[5,2]:'.XLS'
      OUT.EBILL.ID=PLAN.ID:'_':UPS.DATE[1,2]:'_':UPS.DATE[3,2]:'_20':UPS.DATE[5,2]:'.csv'
      READ UPS.EBILL.REC FROM F.BILLDATA.HIST,UPS.EBILL.ID THEN
         EXP.REC=''
         NATT=DCOUNT(UPS.EBILL.REC,@AM)
         FOR ATT=1 TO NATT
            EBILL.LINE=CHANGE(UPS.EBILL.REC<ATT>,TB,@VM)
            SHIPPER.NO=EBILL.LINE<1,3>
            INV.AMT=EBILL.LINE<1,6>
            TRACK.NO=EBILL.LINE<1,7>
            BEGIN CASE
               CASE EBILL.LINE<1,22>='1' OR EBILL.LINE<1,22>='4'
                  IOT='O'
               CASE EBILL.LINE<1,22>='2'
                  IOT='I'
               CASE EBILL.LINE<1,22>='3'
                  IOT='T'
               CASE EBILL.LINE<1,22>=''
                  LOCATE EBILL.LINE<1,20> IN DUPSBILL.CODES<1> BY 'AL' SETTING XX.POS THEN
                     IOT=DUPSBILL.CODES<3,XX.POS>
                  END
            END CASE
            UPS.AMT=ICONV(EBILL.LINE<1,37>,'MD2')
            LOCATE SHIPPER.NO IN COMM.ARRAY<1> SETTING SPOS THEN
               IN.CHG=COMM.ARRAY<2,SPOS>
               IN.FEE=COMM.ARRAY<3,SPOS>
               IN.ALLOC=COMM.ARRAY<5,SPOS>
               TP.CHG=COMM.ARRAY<6,SPOS>
               TP.FEE=COMM.ARRAY<7,SPOS>
               TP.ALLOC=COMM.ARRAY<9,SPOS>
               OUT.CHG=COMM.ARRAY<10,SPOS>
               OUT.FEE=COMM.ARRAY<11,SPOS>
               OUT.ALLOC=COMM.ARRAY<13,SPOS>
               SPLIT.ALLOC=COMM.ARRAY<15,SPOS>
               BEGIN CASE
                  CASE IOT='I'
                     COMM.ARRAY<4,SPOS>+=UPS.AMT
                  CASE IOT='T'
                     COMM.ARRAY<8,SPOS>+=UPS.AMT
                  CASE IOT='O'
                     COMM.ARRAY<12,SPOS>+=UPS.AMT
                  CASE IOT='S'
                     COMM.ARRAY<16,SPOS>+=UPS.AMT
               END CASE
            END
         NEXT ATT
         NVAL=DCOUNT(COMM.ARRAY<1>,@VM)
         FOR VAL=1 TO NVAL
            IF COMM.ARRAY<16,VAL>+0#0 THEN
               IF COMM.ARRAY<2,VAL>#COMM.ARRAY<4,VAL> THEN
                  SPLIT.AMT=COMM.ARRAY<4,VAL>-COMM.ARRAY<2,VAL>
                  IF SPLIT.AMT>=0 THEN
                     COMM.ARRAY<14,VAL>+=SPLIT.AMT
                     COMM.ARRAY<2,VAL>-=SPLIT.AMT
                  END ELSE
                     COMM.ARRAY<14,VAL>-=SPLIT.AMT
                     COMM.ARRAY<2,VAL>+=SPLIT.AMT
                  END
               END
               IF COMM.ARRAY<6,VAL>#COMM.ARRAY<8,VAL> THEN
                  SPLIT.AMT=COMM.ARRAY<8,VAL>-COMM.ARRAY<6,VAL>
                  IF SPLIT.AMT>=0 THEN
                     COMM.ARRAY<14,VAL>+=SPLIT.AMT
                     COMM.ARRAY<6,VAL>-=SPLIT.AMT
                  END ELSE
                     COMM.ARRAY<14,VAL>-=SPLIT.AMT
                     COMM.ARRAY<6,VAL>+=SPLIT.AMT
                  END
               END
               IF COMM.ARRAY<10,VAL>#COMM.ARRAY<12,VAL> THEN
                  SPLIT.AMT=COMM.ARRAY<12,VAL>-COMM.ARRAY<10,VAL>
                  IF SPLIT.AMT>=0 THEN
                     COMM.ARRAY<14,VAL>+=SPLIT.AMT
                     COMM.ARRAY<10,VAL>-=SPLIT.AMT
                  END ELSE
                     COMM.ARRAY<14,VAL>-=SPLIT.AMT
                     COMM.ARRAY<10,VAL>+=SPLIT.AMT
                  END
               END
            END
         NEXT VAL
         OUT.EBILL.REC=''
         FOR ATT=1 TO NATT
            EBILL.LINE=CHANGE(UPS.EBILL.REC<ATT>,TB,@VM)
            NXT.EBILL.LINE=CHANGE(UPS.EBILL.REC<ATT+1>,TB,@VM)
            SHIPPER.NO=EBILL.LINE<1,3>
            NXT.SHIPPER.NO=NXT.EBILL.LINE<1,3>
            INV.AMT=EBILL.LINE<1,6>
            TRACK.NO=EBILL.LINE<1,7>
            BEGIN CASE
               CASE EBILL.LINE<1,22>='1' OR EBILL.LINE<1,22>='4'
                  IOT='O'
               CASE EBILL.LINE<1,22>='2'
                  IOT='I'
               CASE EBILL.LINE<1,22>='3'
                  IOT='T'
               CASE EBILL.LINE<1,22>=''
                  LOCATE EBILL.LINE<1,20> IN DUPSBILL.CODES<1> BY 'AL' SETTING XX.POS THEN
                     IOT=DUPSBILL.CODES<3,XX.POS>
                  END
            END CASE
            UPS.AMT=ICONV(EBILL.LINE<1,37>,'MD2')
            LOCATE SHIPPER.NO IN COMM.ARRAY<1> SETTING SPOS THEN
               ADJ.IN.AMT=COMM.ARRAY<2,SPOS>+COMM.ARRAY<3,SPOS>
               ADJ.TP.AMT=COMM.ARRAY<6,SPOS>+COMM.ARRAY<7,SPOS>
               ADJ.OUT.AMT=COMM.ARRAY<10,SPOS>+COMM.ARRAY<11,SPOS>
               ADJ.SPLT.AMT=COMM.ARRAY<14,SPOS>+COMM.ARRAY<15,SPOS>
               ADJ.INV.AMT=ADJ.IN.AMT+ADJ.TP.AMT+ADJ.OUT.AMT+ADJ.SPLT.AMT
               IF ADJ.INV.AMT'R26'#INV.AMT THEN
                  EBILL.LINE<1,6>=ADJ.INV.AMT'R26'
                  COMM.ARRAY<18,SPOS>=ADJ.INV.AMT
                  BEGIN CASE
                     CASE IOT='I'
                        ORG.AMT=COMM.ARRAY<2,SPOS>
                        ADJ.UPS.AMT=(ADJ.IN.AMT*UPS.AMT/ORG.AMT)'R0'
                        IF ADJ.IN.AMT-COMM.ARRAY<5,SPOS><ADJ.UPS.AMT THEN
                           ADJ.UPS.AMT=ADJ.IN.AMT-COMM.ARRAY<5,SPOS>
                        END
                        IF NXT.SHIPPER.NO#SHIPPER.NO THEN
                           APPLY.INV.AMT=COMM.ARRAY<19,SPOS>+ADJ.UPS.AMT
                           ADJ.UPS.AMT+=COMM.ARRAY<18,SPOS>-APPLY.INV.AMT
                        END
                        COMM.ARRAY<5,SPOS>+=ADJ.UPS.AMT
                        COMM.ARRAY<19,SPOS>+=ADJ.UPS.AMT
                        EBILL.LINE<1,37>=ADJ.UPS.AMT'R26'
                     CASE IOT='T'
                        ORG.AMT=COMM.ARRAY<6,SPOS>
                        ADJ.UPS.AMT=(ADJ.TP.AMT*UPS.AMT/ORG.AMT)'R0'
                        IF ADJ.TP.AMT-COMM.ARRAY<9,SPOS><ADJ.UPS.AMT THEN
                           ADJ.UPS.AMT=ADJ.TP.AMT-COMM.ARRAY<9,SPOS>
                        END
                        IF NXT.SHIPPER.NO#SHIPPER.NO THEN
                           APPLY.INV.AMT=COMM.ARRAY<19,SPOS>+ADJ.UPS.AMT
                           ADJ.UPS.AMT+=COMM.ARRAY<18,SPOS>-APPLY.INV.AMT
                        END
                        COMM.ARRAY<9,SPOS>+=ADJ.UPS.AMT
                        COMM.ARRAY<19,SPOS>+=ADJ.UPS.AMT
                        EBILL.LINE<1,37>=ADJ.UPS.AMT'R26'
                     CASE IOT='O'
                        ORG.AMT=COMM.ARRAY<10,SPOS>
                        ADJ.UPS.AMT=(ADJ.OUT.AMT*UPS.AMT/ORG.AMT)'R0'
                        IF ADJ.OUT.AMT-COMM.ARRAY<13,SPOS><ADJ.UPS.AMT THEN
                           ADJ.UPS.AMT=ADJ.OUT.AMT-COMM.ARRAY<13,SPOS>
                        END
                        IF NXT.SHIPPER.NO#SHIPPER.NO THEN
                           APPLY.INV.AMT=COMM.ARRAY<19,SPOS>+ADJ.UPS.AMT
                           ADJ.UPS.AMT+=COMM.ARRAY<18,SPOS>-APPLY.INV.AMT
                        END
                        COMM.ARRAY<13,SPOS>+=ADJ.UPS.AMT
                        COMM.ARRAY<19,SPOS>+=ADJ.UPS.AMT
                        EBILL.LINE<1,37>=ADJ.UPS.AMT'R26'
                     CASE IOT='S'
                        ORG.AMT=COMM.ARRAY<14,SPOS>
                        ADJ.UPS.AMT=(ADJ.SPLT.AMT*UPS.AMT/ORG.AMT)'R0'
                        IF ADJ.SPLT.AMT-COMM.ARRAY<17,SPOS><ADJ.UPS.AMT THEN
                           ADJ.UPS.AMT=ADJ.SPLT.AMT-COMM.ARRAY<17,SPOS>
                        END
                        IF NXT.SHIPPER.NO#SHIPPER.NO THEN
                           APPLY.INV.AMT=COMM.ARRAY<19,SPOS>+ADJ.UPS.AMT
                           ADJ.UPS.AMT+=COMM.ARRAY<18,SPOS>-APPLY.INV.AMT
                        END
                        COMM.ARRAY<17,SPOS>+=ADJ.UPS.AMT
                        COMM.ARRAY<19,SPOS>+=ADJ.UPS.AMT
                        EBILL.LINE<1,37>=ADJ.UPS.AMT'R26'
                  END CASE
               END
            END
            IN.LINE=EBILL.LINE
            OUT.LINE=''
            CALL CONVERT.STRING(IN.LINE,OUT.LINE,TYPE)
            OUT.EBILL.REC<ATT>=OUT.LINE
         NEXT ATT
         OUT.EBILL.HDR=''
         OUT.EBILL.HDR<1,1>='1'
         OUT.EBILL.HDR<1,2>=PLAN.ID
         OUT.EBILL.HDR<1,6>='0.00'
         OUT.EBILL.HDR<1,12>='0'
         OUT.EBILL.HDR<1,20>='IDD'
         OUT.EBILL.HDR<1,21>='INVOICE DATE'
         OUT.EBILL.HDR<1,23>=UPS.DATE[1,4]:'20':UPS.DATE[5,2]
         OUT.EBILL.HDR<1,37>='0.00'
         OUT.EBILL.HDR<1,38>='0.00'
         OUT.EBILL.HDR<1,63>=''
         IN.LINE=OUT.EBILL.HDR
         OUT.LINE=''
         CALL CONVERT.STRING(IN.LINE,OUT.LINE,TYPE)
         OUT.EBILL.HDR=OUT.LINE
         INS OUT.EBILL.HDR BEFORE OUT.EBILL.REC<1>
         WRITE OUT.EBILL.REC ON F.EMAILDATA,OUT.EBILL.ID
         EMAIL.OUT=''
         EMAIL.OUT<1>='mkerbow@afs.net'
         EMAIL.OUT<4>='Reports@afs.net'  ; * From Email address
         EMAIL.OUT<5,1>='01825 - Cavender Stores LTD* Update UPS Ebill'    ; * Subject of Email

* Email body
         EMAIL.OUT<6,-1>='AFS, LLC.                                                  Phone: 318-798-2111'
*DF         EMAIL.OUT<6,-1>='330 Marshall Street, Suite 400                               FAX: 318-797-3628'
         EMAIL.OUT<6,-1>='670 Emberwood Drive                                          FAX: 318-797-3628'
*DF         EMAIL.OUT<6,-1>='Shreveport, LA 71101                                               www.afs.net'      ; * JMK74
         EMAIL.OUT<6,-1>='Shreveport, LA 71106                                               www.afs.net'      ; * JMK74
         EMAIL.OUT<6,-1>=' '
         EMAIL.OUT<6,-1>=' '
         EMAIL.OUT<6,-1>='This file includes the updated UPS Ebill information.'
         EMAIL.OUT<6,-1>='------------------------------------------------------------------------------'
         EMAIL.OUT<7>='1'
         EMAIL.OUT<11,-1>=EMAIL.PATH:OUT.EBILL.ID

         VIN=EMAIL.OUT
         VOUT=''
         CALL SENDMAIL(VIN,VOUT)         ; * This is the regular client email

      END
