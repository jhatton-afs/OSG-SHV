***********************************************************************
* Analyze bills acted upon by auditors
* Copyright (c) AFS Logistics, LLC., 2002
*
* AUTHOR : Owen Holman
* Date   : January 2002
***********************************************************************
!
* Include the ODBC definitions
!
$INCLUDE UNIVERSE.INCLUDE ODBC.H
      COMMON PROGPARAMS(24)
      EQU PROGFROM TO PROGPARAMS(1)
      EQU USER TO PROGPARAMS(2)
      EQU PROCDATE TO PROGPARAMS(3)
      EQU CHNUM TO PROGPARAMS(4)

      form = "T##########"
      dash = "----------"
      Expect = ""
      NULL=""
      TOT.NUM.BILLS=0

      CALL GET.USER(USER)
* Begin GRB01
      VIN1=""
      VIN1<1> = "UPD.SQL.FBDUP.PX"
      VIN1<2> = USER
      VIN1<3> = SYSTEM(18)
      VIN1<4> = "S"
      CALL PROGTRACK(VIN1,VOUT1)
* End GRB01
      GET (ARG.,1) OBEG.DATE THEN
         BEG.DATE=ICONV(OBEG.DATE,'D')
      END ELSE
         BEG.DATE=DATE()
      END
      GET (ARG.,2) OEND.DATE THEN
         END.DATE=ICONV(OEND.DATE,'D')
      END ELSE
         END.DATE=DATE()
      END
      GET (ARG.,3) CLIENT.LIST.NAME ELSE CLIENT.LIST.NAME=''
      GET (ARG.,4) OPT.LIST ELSE OPT.LIST=''
      GET (ARG.,5) START.TIME ELSE START.TIME=''
      IF START.TIME#'' THEN
         STMT="SLEEP ":START.TIME
         EXECUTE STMT CAPTURING TIME.RESULTS
      END
      IF CLIENT.LIST.NAME#'' THEN
         STMT='GET-LIST ':CLIENT.LIST.NAME
      END ELSE
         STMT='SSELECT CLIENTS WITH @ID # "9]" AND WITH STATUS # "I"'
      END
      EXECUTE STMT RTNLIST CLIENT.LIST CAPTURING RESULTS
      NUM.CLIENTS=@SELECTED

      OPEN '','CLIENTS' TO F.CLIENT ELSE ABORT 201,'CLIENTS'
      OPEN '','FB.TABLES' TO F.TABLES ELSE ABORT 201,'FB.TABLES'
      READ LI.CODE.REC FROM F.TABLES,'LI.CODES' ELSE LI.CODE.REC=''
      CRT @(0,0):@(-1):@(0,3):'UPD.SQL.FBDUP.PX - Processing Clients from ':BEG.DATE'D2/':' to ':END.DATE'D2/':'...'

      CLIENT.EOF=0
      CLIENT.CNT=0
      CLIENT.PER=0
      LOOP
         READNEXT CLIENT.ID FROM CLIENT.LIST ELSE CLIENT.EOF=1
      UNTIL CLIENT.EOF DO
         CLIENT.CNT+=1
         CLIENT.PCT=ICONV(CLIENT.CNT/NUM.CLIENTS*100,'MR2')
         READ CLIENT.REC FROM F.CLIENT,CLIENT.ID THEN
            CRT @(0,5):@(-4):CLIENT.CNT'R#4':' - ':CLIENT.PCT'R26#6':'% ':CLIENT.ID:' - ':CLIENT.REC<2>
****            CALL PROG.BAR(8,CLIENT.CNT,NUM.CLIENTS,CLIENT.PER)
            OPEN '','FB.BILLS.HIST,':CLIENT.ID TO F.BILL THEN

               DIV.FOUND=0

               OPEN '','DIVISION.CODE,':CLIENT.ID TO F.DIV THEN DIV.FOUND=1

               STMT = 'SELECT FB.PROC.DATE.XREF,':CLIENT.ID
               STMT:=' WITH DATE GE "':OCONV(BEG.DATE,'D2/'):'"'
               STMT:=' AND WITH DATE LE "':OCONV(END.DATE,'D2/'):'"'
               EXECUTE STMT RTNLIST DT.LIST CAPTURING OUTPUT
               IF @SELECTED < 1 THEN GOTO SKIP.CLIENT
               STMT = 'QSELECT FB.PROC.DATE.XREF,':CLIENT.ID
               EXECUTE STMT PASSLIST DT.LIST RTNLIST NDX.LIST CAPTURING OUTPUT
               IF @SELECTED < 1 THEN GOTO SKIP.CLIENT
               STMT='SELECT FB.BILLS.HIST,':CLIENT.ID
               STMT:=' WITH VERSION # "0"'
               STMT:=' AND WITH 39 # "R"'
               STMT:=' AND WITH 37 GE "':OCONV(BEG.DATE,'D2/'):'"'
               STMT:=' AND WITH 37 LE "':OCONV(END.DATE,'D2/'):'"'
*              STMT:=' SAVING UNIQUE @ID'
               EXECUTE STMT PASSLIST NDX.LIST RTNLIST BILL.LIST CAPTURING OUTPUT
               NUM.BILLS=@SELECTED
               TOT.NUM.BILLS+=NUM.BILLS
               CRT @(0,10):@(-4):' #Bills - ':NUM.BILLS'R#12':' - ':TOT.NUM.BILLS'R#15'
               BILL.EOF=0
               BILL.CNT=0
               BILL.PER=0
               LOOP
                  READNEXT BILL.ID FROM BILL.LIST ELSE BILL.EOF=1
               UNTIL BILL.EOF DO
                  BILL.CNT+=1
                  BILL.PCT=ICONV(BILL.CNT/NUM.BILLS*100,'MR2')
                  CALL PROG.BAR(13,BILL.CNT,NUM.BILLS,BILL.PER)
*                 IF BILL.ID='00585*1316938*0' THEN DEBUG
*                 IF BILL.ID='00924*736038*0' THEN DEBUG
                  READ BILL.REC FROM F.BILL,BILL.ID THEN
* DEFINE FIELDS TO UPDATE FROM CLIENT.REC HERE
                     CARRIER.ID=FIELD(BILL.ID,'*',1)
                     PRO=FIELD(BILL.ID,'*',2)
                     DIV.REC=''
                     IF DIV.FOUND THEN
                        DIV.ID=BILL.REC<9>
                        READ DIV.REC FROM F.DIV,DIV.ID ELSE DIV.REC=''
                     END
                     SQLVIN=''
                     SQLVIN<1>=USER
                     SQLVIN<2>=CLIENT.ID
                     SQLVIN<3>=BILL.ID
                     SQLVOUT=''
                     CALL UPD.SQL.FBDUP.SUB(SQLVIN,SQLVOUT,CLIENT.REC,DIV.REC,BILL.REC)
                  END
SKIP.BILL:        *
               REPEAT
               WKENDLOAD=0
               IF CLIENT.LIST.NAME='CLIENTFBDUP1' THEN WKENDLOAD=1
               IF CLIENT.LIST.NAME='CLIENTFBDUP2' THEN WKENDLOAD=1
               IF CLIENT.LIST.NAME='CLIENTFBDUP3' THEN WKENDLOAD=1
               IF CLIENT.LIST.NAME='CLIENTFBDUP4' THEN WKENDLOAD=1
               IF WKENDLOAD THEN
                  MVIN=''
*                 MVIN<1,-1>='RKosik@afs.net'
*                 MVIN<2,-1>='PDalton@afs.net'
                  MVIN<1,-1>='nmeddaugh@afs.net'
                  MVIN<4>='AFS_UPD.SQL.FBDUP.SUB'
                  MVIN<5>='SQL Shipment Update by Client'
                  TEXT='On ':DATE()'D2/':' ':OCONV(TIME(),'MTHS'):', ':USER:' updated '
                  TEXT<1,-1>='Client ':CLIENT.ID:' ':CLIENT.REC<2>:' completed the SQL update for the processing period from ':OCONV(BEG.DATE,'D4/'):' to ':OCONV(END.DATE,'D4/'):' with ':NUM.BILLS:' shipments loaded.'
                  TEXT<1,-1>=' '
                  MVIN<6>=TEXT
                  MVIN<7>=1
                  CALL SENDMAIL(MVIN,MVOUT)
               END
            END
         END
SKIP.CLIENT:*
      REPEAT


THE.END:***
*     End of program
* Begin GRB01
      VIN1=''
      VIN1<1> = "UPD.SQL.FBDUP.PX"
      VIN1<2> = USER
      VIN1<3> = SYSTEM(18)
      VIN1<4> = "E"
      CALL PROGTRACK(VIN1,VOUT1)
* End GRB01
      STOP
