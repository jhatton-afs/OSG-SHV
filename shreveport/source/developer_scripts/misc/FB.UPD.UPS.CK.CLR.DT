****************************************************************************************************
* PROGRAM: FB.UPD.UPS.CK.CLR.DT
* PROGRAM TO Update UPS related Schedule files with check clear date (Run from IMPORT.ML)
*
* FREIGHT BILLING PACKAGE
*
* AUTHOR : NCG Pratt
* DATE   : April 30, 2010
*
PROG.NAME = "FB.UPD.UPS.CK.CLR.DT"

PROMPT ''
PROG.NAME='FB.UPD.UPS.CK.CLR.DT'
PROG.DESC='Clear UPS.SCHED records out to History'
PACK.NAME='FREIGHT BILLING'
CALL GET.USER(USER)
UPL.VIN = 'FBBP' ;  UPL.VIN<2> = 'FB.UPD.UPS.CK.CLR.DT' ; UPL.VOUT = ''                    ;* NPR_UPL 05/18/2010
CALL UPD.PROGRAM.LOG(UPL.VIN,UPL.VOUT)                                                     ;* NPR_UPL 05/18/2010
*

CALL CHANNEL(CH.NUM)

CRT "USER ":USER
CRT
*** CRT "CH.NUM ":CH.NUM
*** CRT "RUN ":OCONV(DATE(),"D2/") : "  AT  ":OCONV(TIME(),"MTS")
ST.TIME = TIME()
CRT "STARTED AT ":OCONV(ST.TIME,"MTS")
CRT

OPEN '','UPS.SCHED' TO F.UPS.SCHED ELSE
   CRT "Cant open file UPS.SCHED in ":PROG.NAME
   STOP
END

OPEN '','UPS.SCHED.HIST' TO F.UPS.SCHED.HIST ELSE
   CRT "Cant open file UPS.SCHED.HIST in ":PROG.NAME
   STOP
END

OPEN '','FBWORK.UPS.CKC' TO F.WORK.UPS.CKC ELSE
   EXECUTE 'CREATE-FILE FBWORK.UPS.CKC 1 103' CAPTURING OUTPUT
   OPEN '','FBWORK.UPS.CKC' TO F.WORK.UPS.CKC ELSE
      CRT "Cant open file FBWORK.UPS.CKC in ":PROG.NAME
      STOP
   END
END

OPEN '','FBWORK.UPS.CKC2' TO F.WORK.UPS.CKC2 ELSE
   EXECUTE 'CREATE-FILE FBWORK.UPS.CKC2 1 103' CAPTURING OUTPUT
   OPEN '','FBWORK.UPS.CKC2' TO F.WORK.UPS.CKC2 ELSE
      CRT "Cant open file FBWORK.UPS.CKC2 in ":PROG.NAME
      STOP
   END
END

OPEN '','FB.CHECKS.HDR' TO F.CHECKS.HDR ELSE
   CRT "Cant open file FB.CHECKS.HDR in ":PROG.NAME
   STOP
END

OPEN '','FB.CHECKS.DTL' TO F.CHECKS.DTL ELSE
   CRT "Cant open file FB.CHECKS.DTL in ":PROG.NAME
   STOP
END

CLEARFILE F.WORK.UPS.CKC2

CLIENT.LIST = ""
CKCL.LIST = ''

CARRIER.ID  = "00041"   ;* UPS

*** CRT @(-1)
*
EXECUTE \SSELECT FBWORK.UPS.CKC\ CAPTURING OUTPUT

CHECKS.TO.CLEAR = @SELECTED

CRT ; CRT OUTPUT ; CRT

EOF = "N"
CNT = 0

LOOP
   READNEXT ID.HDR ELSE EOF = "Y"
   CNT += 1
UNTIL EOF = "Y" DO
   READ HDR.REC FROM F.CHECKS.HDR, ID.HDR THEN
      CRT "Check to clear = ":ID.HDR
      IF HDR.REC<3> = CARRIER.ID THEN
         IF HDR.REC<21> NE "" THEN CLEAR.DATE = HDR.REC<21> ELSE CLEAR.DATE = HDR.REC<5>

*** CRT @(10,08):" Done " : CNT : " of " : CHECKS.TO.CLEAR

         IF CLEAR.DATE NE "" THEN
            STATUS = HDR.REC<7>[1,1]
            IF STATUS NE "V" THEN
               GOSUB DO.DETAIL.RECS
               CKCL.LIST<-1> = ID.HDR
            END
         END
      END
   END
REPEAT

CLEARFILE F.WORK.UPS.CKC

CRT "DONE Check Detail records"

GOSUB DO.CLIENTS

CRT "ENDED AT ":OCONV(TIME(),"MTS")

MVIN=''
******MVIN<1> ='npratt@afs.net'
MVIN<2> =''
MVIN<3>=''
MVIN<4>=''
MVIN<5>= "FB.UPD.UPS.CK.CLR.DT >>> Clear UPS.SCHED records out to History - Process Complete"
MVIN<6>= "FB.UPD.UPS.CK.CLR.DT >>> Clear UPS.SCHED records out to History - Process Complete"
MVIN<6,-1>= " "
MVIN<6,-1>= "Started at " : OCONV(ST.TIME,"MTS")
MVIN<6,-1>= " "
MVIN<6,-1>= "Ended at   " : OCONV(TIME(),"MTS")
MVIN<6,-1>= " "

N.CKS = DCOUNT(CKCL.LIST,@AM)
FOR CK = 1 TO N.CKS
   MVIN<6,-1>= "UPS.SCHED items moved to history for bills paid with check " : CKCL.LIST<CK>
NEXT CK

MVIN<7>=1
CALL SENDMAIL(MVIN,MVOUT)

STOP

DO.DETAIL.RECS :
*
DET.CNT = HDR.REC<25>
FOR DET.NO = 1 TO DET.CNT
   ID.DTL = ID.HDR:"*":DET.NO

***   IF MOD(DET.NO,100) = 0 THEN CRT @(10,10):ID.HDR "L#12":"  -- Done ":DET.NO "R#5" : " of ":DET.CNT

   READ DTL.REC FROM F.CHECKS.DTL, ID.DTL THEN
      PRO.NO = DTL.REC<1>
      CLIENT = DTL.REC<3>

      ID.UPS.SCHED = CLIENT:"*":PRO.NO
      READ TST.REC FROM F.UPS.SCHED, ID.UPS.SCHED THEN
         READ WORK.REC FROM F.WORK.UPS.CKC2,CLIENT ELSE WORK.REC = ""
         WORK.REC<-1> = ID.UPS.SCHED
         WRITE WORK.REC ON F.WORK.UPS.CKC2, CLIENT
         LOCATE CLIENT IN CLIENT.LIST SETTING POS ELSE
            CLIENT.LIST<-1> = CLIENT
         END
      END
*** CRT CLIENT : "  Detail record ":ID.DTL
   END ELSE
*** CRT ; CRT CLIENT : "  Detail record ":ID.DTL :"  Not FOUND "
   END
NEXT DET.NO
*
RETURN

DO.CLIENTS :
*
N.CLS = DCOUNT(CLIENT.LIST,@AM)
FOR CL = 1 TO N.CLS
   WORK.CLIENT = CLIENT.LIST<CL>
   CRT "Updating UPS.SCHED records for client ":WORK.CLIENT
   GOSUB UPD.SCHED.RECS
NEXT CL

RETURN

UPD.SCHED.RECS :

READ ID.LIST FROM F.WORK.UPS.CKC2, WORK.CLIENT ELSE RETURN

N.SR = DCOUNT(ID.LIST,@AM)
FOR IDR = 1 TO N.SR
   IF CLEAR.DATE NE "" THEN
      ID.BILL = ID.LIST<IDR>
      ACTIVITY.CODE  = 7
      ACT.TRANS.DATE = CLEAR.DATE
      ACT.DUE.DATE   = ""
      UPDATE.USER    = USER
      UPDATE.DATE    = CLEAR.DATE
      UPDATE.TIME    = TIME()

      READ TST.REC FROM F.UPS.SCHED, ID.BILL THEN
***         CRT WORK.CLIENT : " UPS.SCHED.ID ":ID.LIST<IDR> :" - CHECK CLEAR DATE =   " : OCONV(CLEAR.DATE,"D2/")
         GOSUB UPD.UPS.SCHED
      END
   END ELSE
***      CRT ; CRT WORK.CLIENT : "  ":ID.LIST<IDR> :"  Not cleared " ; CRT
   END   
NEXT IDR

RETURN

UPD.UPS.SCHED :

UPS.VIN = '' ; UPS.VOUT = ''

UPS.VIN<1> = ID.BILL
UPS.VIN<2> = ACTIVITY.CODE
UPS.VIN<3> = ACT.TRANS.DATE
UPS.VIN<4> = ACT.DUE.DATE     ;* No longer used
UPS.VIN<5> = UPDATE.USER
UPS.VIN<6> = UPDATE.DATE
UPS.VIN<7> = UPDATE.TIME

CALL FB.UPD.UPS.SCHED(UPS.VIN,UPS.VOUT,F.UPS.SCHED,F.UPS.SCHED.HIST)

RETURN
