**********************************************************************
* Program : CHK.CLIENT.TERM.DATE
* Desc: Select clients with termination date LE to DATE() and make inactive
* Author  : TAG
* Date    : 03/09/2017
*
* Copyright (c) 2016 by AFS Logistics, LLC. - All Rights Reserved


* Last updated by afs (AFSLOGISTICS\tgorton) at 08:27:07 on 03/14/2017.



* Comments
**********************************************************************
* 030917 - TAG - SysAid 30369 Program Created
**********************************************************************
      COMMON PROGPARAMS(24)
      EQU PROGFROM TO PROGPARAMS(1)
      EQU USER TO PROGPARAMS(2)
      EQU PROCDATE TO PROGPARAMS(3)
      EQU CHNUM TO PROGPARAMS(4)
* Initialize general variables
      TODAY.DATE = OCONV(DATE(),'D2/')
      PROG.NAME = "CHK.CLIENT.TERM.DATE"
*
***      CALL CHANNEL(CH.NUM)
      CALL GET.USER.NEW(USER,CH.NUM)
      VIN1=""
      VIN1<1> = PROG.NAME
      VIN1<2> = USER
      VIN1<3> = SYSTEM(18)
      VIN1<4> = "S"
      CALL PROGTRACK(VIN1,VOUT1)
*

* Select clients with termination date LE to DATE()
      SEL.STMT = 'SELECT CLIENTS WITH TERM.DATE LE ':DQUOTE(TODAY.DATE)
      SEL.STMT := ' AND WITH TERM.DATE NE ""'
      SEL.STMT := ' AND WITH STATUS # "I"'
      EXECUTE SEL.STMT CAPTURING OUTPUT RETURNING ERRNUM
      READLIST TERM.CLIENTS ELSE TERM.CLIENTS = ""
      IF TERM.CLIENTS = '' THEN GO END.PGM
* Open Files
      OPEN '','CLIENTS' TO F.CLIENTS ELSE CALL OPEN.ABBORT('CLIENTS',PROG.NAME)
      OPEN '','AR.SLSMAN.1000.1' TO F.SLSMAN ELSE CALL OPEN.ABORT('AR.SLSMAN.1000.1',PROG.NAME)
*Main Programming Loop
      NUM.TERM.CLIENTS=DCOUNT(TERM.CLIENTS,@AM)
      FOR CTR = 1 TO NUM.TERM.CLIENTS
         CLIENT.ID = TERM.CLIENTS<CTR>
         READU CLIENT.REC FROM F.CLIENTS, CLIENT.ID THEN
            CLIENT.NAME= CLIENT.REC<2>
            TERM.DATE = CLIENT.REC<312>
            AST.FOUND = INDEX(CLIENT.NAME,'*',1)
            IF AST.FOUND AND ((DATE()-TERM.DATE) LE 7) THEN CONTINUE
            CLIENT.REC<30> = 'I'
            WRITE CLIENT.REC ON F.CLIENTS,CLIENT.ID
            RELEASE F.CLIENTS,CLIENT.ID
            GOSUB SEND.EMAIL
         END
      NEXT CTR
      GO END.PGM
*****************
SEND.EMAIL:
*****************
      EMAIL.OUT=''
      EMAIL.OUT<1,-1>='inactiveclients@afs.net'    ; * TO: Email addresses
      EMAIL.OUT<4>='***ALERT***@afs.net'           ; * From Email address
*
      EMAIL.OUT<5,1>=CLIENT.REC<2>:'-':CLIENT.ID:' Status set to Inactive'
      LAST.PROC.DATE = CLIENT.REC<82>
      INIT.SALESMAN.ID = CLIENT.REC<114>
      INIT.SALESMAN.STR = "Not On File"
      READV INIT.SLSMAN.NAME FROM F.SLSMAN,INIT.SALESMAN.ID,1 THEN
         INIT.SALESMAN.STR = INIT.SALESMAN.ID:" - ":OCONV(INIT.SLSMAN.NAME,"MCT")
      END
*
      SRVC.SALESMAN.ID=CLIENT.REC<115>
      SRVC.SALESMAN.STR = "Not on File"
      READV SRVC.SLSMAN.NAME FROM F.SLSMAN, SRVC.SALESMAN.ID,1 THEN
         SRVC.SALESMAN.STR = SRVC.SALESMAN.ID:" - ":OCONV(SRVC.SLSMAN.NAME,"MCT")
      END
*

      EMAIL.OUT<6,-1>='Based on the termination date entered, the status of client ':CLIENT.ID:' ':CLIENT.REC<2>:' was changed to Inactive'
      EMAIL.OUT<6,-1>='at ':OCONV(TIME(),'MTHS'):' on ':DATE()'D4/':' by overnight processing.'
      EMAIL.OUT<6,-1>=' '
      EMAIL.OUT<6,-1>='Initiating Salesman : ' : INIT.SALESMAN.STR
      EMAIL.OUT<6,-1>=' '
      EMAIL.OUT<6,-1>='Servicing  Salesman : ' : SRVC.SALESMAN.STR
      EMAIL.OUT<6,-1>=' '
      IF LAST.PROC.DATE NE "" THEN
         EMAIL.OUT<6,-1>='Last process Date   :  ': OCONV(LAST.PROC.DATE,"D4/")
      END ELSE
         EMAIL.OUT<6,-1>='Last process Date   :  Unknown'
      END
*
      EMAIL.OUT<7>=1                     ; * Auto format (add carriage returns to end of text)
      VOUT=''
      CALL SENDMAIL(EMAIL.OUT,VOUT)
*
      RETURN
****************
END.PGM:
****************
      VIN1=''
      VIN1<1> = PROG.NAME
      VIN1<2> = USER
      VIN1<3> = SYSTEM(18)
      VIN1<4> = "E"
      CALL PROGTRACK(VIN1,VOUT1)
      STOP
