$OPTIONS EXEC.EQ.PERF
$OPTIONS INFO.ABORT
*
*
      SUBROUTINE FB.2.1.DISC(DISC.PERC,DISC.DET,NET,QI)
$OPTIONS HEADER.EJECT
ERRMSG=''
* SUBROUTINE TO ENTER DISCOUNTS
*
* FREIGHT BILLING PACKAGE
*
* AUTHOR: BRIAN M. QUEEN
* DATE: JANUARY 19, 1993
*
* Initialize variables
*
      PROMPT''
      IF QI='I' THEN
        MODE='EDIT'
        INQUIRE=1
      END ELSE
        MODE='INPUT'
        INQUIRE=0
      END
      EDIT.FLAG=0
      TYPE='Regular'
      LN=0
      X=DCOUNT(DISC.DET,CHAR(254))
      IF X GT 0 THEN MODE='EDIT'
      GOSUB 8050; * Calculate totals
*
* Display distribution headings
*
      CALL FB.CLEAR.WINDOW
      CRT @(36,6):"---------------- Discounts ----------------"
      CRT @(36,7):"Lin         Gross   Discount            Net"
      CRT @(36,8):"Tot"
*
* Start input
*
      PAGE=1
      GOSUB 9000; * Display screen
*
* Enter discount line number.
*
100   IF MODE='INPUT' THEN
        LN=LN+1
        QI=LN
        IF LN GT STOP THEN
          PAGE=PAGE+1
          GOSUB 9000; * Display screen
        END
      END ELSE
        HELP="Enter Line Num. ''=End. 'B'=Back. 'F'=Forward. 'X'=Back. 'EX'=Exit."
        CALL GEN.IN(36,18,'Enter line number. ###','',Q,0,3,'','',0,-3,18,0,QI,HELP,0,23)
      END
      QI=OCONV(QI,'MCU')
      BEGIN CASE
        CASE QI='B'
          PAGE=PAGE-1
          IF PAGE=0 THEN PAGE=1
          GOSUB 9000; * Display screen
        CASE QI='F'
          PAGE=PAGE+1
          GOSUB 9000; * Display screen
        CASE QI=''
          IF EDIT.FLAG THEN
            GOSUB 8000; * Enter discount % and/or net
            IF QI='X' THEN
              LN=LN-2
              GOTO 100
            END
          END
          RETURN
        CASE QI='X'
          CALL FB.CLEAR.WINDOW
          RETURN
        CASE QI='EX'
          STOP
        CASE QI=0
          GOSUB 8000
          GOTO 100
        CASE NUM(QI)
          IF INQUIRE THEN
            CALL ELINE('Can not access detail in inquiry!')
          END ELSE
            SEL=QI
1000        IF SEL LT START ! SEL GT STOP THEN
              CALL ELINE('Invalid entry.')
              RETURN
            END
            GOSUB 9100; * Calculate line position
*
* Enter line amount
*
1010        HELP="Enter gross. ''=Accept. 'D'=Delete. 'E'=Change Mode. 'X'=Back. 'EX'=Exit."
            MASK=DISC.DET<SEL,1>'R26,#13'
            CALL GEN.IN(40,LINE,MASK,'AMT2',Q,0,13,'','',2,-13,LINE,0,QI,HELP,0,23)
            QI=OCONV(QI,'MCU')
            BEGIN CASE
              CASE QI='EX'
                STOP
              CASE QI='X'
                IF LN=1 THEN
                  CALL FB.CLEAR.WINDOW
                  RETURN
                END
                IF DISC.DET<SEL,1> NE '' THEN
                  CRT @(40,LINE):DISC.DET<SEL,1>'R26,#13':' ':DISC.DET<SEL,2>:@(-4):
                END ELSE
                  CRT @(40,LINE):@(-4):
                END
                LN=LN-2
                GOTO 100
              CASE QI='D'
                DISC.DET=DELETE(DISC.DET,SEL)
                GOSUB 9000; * Display screen
                IF MODE='EDIT' THEN EDIT.FLAG=1
                GOTO 100
              CASE QI='E'
                MODE='EDIT'
                CRT @(40,LINE):@(-4):
                RETURN
              CASE QI=''
                IF DISC.DET<SEL,1>='' THEN
                  LN=LN-1
                  GOSUB 8000; * Enter discount % and/or net
                  MODE='EDIT'
                  GOTO 100
                END
*               IF DISC.DET<SEL,2>[1,1]='R' THEN D.GROSS=D.GROSS-DISC.DET<SEL,1>
*               IF DISC.DET<SEL,2>[1,1]='M' THEN M.GROSS=M.GROSS-DISC.DET<SEL,1>
*               GROSS=GROSS-DISC.DET<SEL,1>
                Q=DISC.DET<SEL,1>
              CASE NUM(Q)
*               IF DISC.DET<SEL,2>[1,1]='R' THEN D.GROSS=D.GROSS-DISC.DET<SEL,1>
*               IF DISC.DET<SEL,2>[1,1]='M' THEN M.GROSS=M.GROSS-DISC.DET<SEL,1>
*               GROSS=GROSS-DISC.DET<SEL,1>
                IF MODE='EDIT' THEN EDIT.FLAG=1
              CASE 1
                CALL ELINE('Invalid entry')
                GOTO 1010
            END CASE
            DISC.DET<SEL,1>=Q
            CRT @(40,LINE):DISC.DET<SEL,1>'R26,#13'
*
* Enter type of charge
*
1015        HELP="Enter charge type. [0 or R]=Reg. [1 or M]=Misc. [cr]=Accept. [X]=Back to amt."
            IF DISC.DET<SEL,2> NE '' THEN
              MASK=DISC.DET<SEL,2>'L#13'
            END ELSE
              MASK=TYPE'L#13'
            END
            CALL GEN.IN(54,LINE,MASK,'',Q,0,13,'','',2,-13,LINE,0,QI,HELP,0,23)
            QI=OCONV(QI,'MCU')
            BEGIN CASE
              CASE QI='R' ! QI=0
                TYPE='Regular'
                IF MODE='EDIT' THEN EDIT.FLAG=1
              CASE QI='M' ! QI=1
                TYPE='Miscellaneous'
                IF MODE='EDIT' THEN EDIT.FLAG=1
              CASE QI='X'
                CRT @(54,LINE):DISC.DET<SEL,2>'L#13'
                GOTO 1010
              CASE QI='EX'
                STOP
              CASE QI=''
                NULL
              CASE 1
                CALL ELINE('Invalid entry.')
                GOTO 1015
            END CASE
            DISC.DET<SEL,2>=TYPE
*           IF DISC.DET<SEL,2>[1,1]='R' THEN D.GROSS=D.GROSS+DISC.DET<SEL,1>
*           IF DISC.DET<SEL,2>[1,1]='M' THEN M.GROSS=M.GROSS+DISC.DET<SEL,1>
*           GROSS=GROSS+DISC.DET<SEL,1>
            CRT @(54,LINE):DISC.DET<SEL,2>'L#13'
            GOSUB 9050
          END
      END CASE
      GOTO 100
*-----------------------------------------------------------------------
*
* INTERNAL SUBROUTINES
*
*-----------------------------------------------------------------------
*
* Enter discount percentage.
*
8000  HELP="Enter Discount %. ''=Accept. 'X'=Back to amounts."
      MASK=DISC.PERC'R26#10'
      CALL GEN.IN(54,8,MASK,'AMT2',Q,0,10,'','',2,-10,8,0,QI,HELP,0,23)
      QI=OCONV(QI,'MCU')
      BEGIN CASE
        CASE QI='X'
          CRT @(54,8):DISC.DET<2,SEL>'R26,#10'
          RETURN
        CASE QI='EX'
          STOP
        CASE QI=''
          CRT @(54,8):DISC.PERC'R26,#10'
          GOTO 8010
        CASE 1
          DISC.PERC=Q
      END CASE
      IF DISC.PERC GT 10000 THEN
        CALL ELINE('Discount percentage is greater than 100%')
        GOTO 8000
      END
      CRT @(54,8):DISC.PERC'R26,#10'
*
* Enter net amount and recalculate discount percentage
*
8010  HELP="Enter Net Amount. ''=Accept. 'X'=Back to discount amount."
      DISC.AMT=D.GROSS*(DISC.PERC/10000)
      DISC.AMT=OCONV(DISC.AMT,'MR0')
      NET=D.GROSS-DISC.AMT+M.GROSS
      MASK=NET'R26,#11'
      CALL GEN.IN(68,8,MASK,'AMT2',Q,0,11,'','',2,-11,8,0,QI,HELP,0,23)
      QI=OCONV(QI,'MCU')
      BEGIN CASE
        CASE QI='X'
          GOTO 8000
        CASE QI=''
          Q=NET
      END CASE
      NET=Q
      DISC.AMT=D.GROSS-NET+M.GROSS
      IF D.GROSS NE 0 THEN DISC.PERC=(DISC.AMT*10000/D.GROSS) ELSE DISC.PERC=0
      DISC.PERC=OCONV(DISC.PERC,'MR0')
      CRT @(54,8):DISC.PERC'R26,#10'
      CRT @(68,8):NET'R26,#11'
      MODE='EDIT'
      RETURN
*
* Calculate totals
*
8050  GROSS=0
      D.GROSS=0
      M.GROSS=0
      X=DCOUNT(DISC.DET,CHAR(254))
      IF X GT 0 THEN
        FOR I=1 TO X
          IF DISC.DET<I,2>='Regular' THEN
            D.GROSS=D.GROSS+DISC.DET<I,1>
          END ELSE
            M.GROSS=M.GROSS+DISC.DET<I,1>
          END
          GROSS=GROSS+DISC.DET<I,1>
        NEXT I
      END
      RETURN
*
* Print page
*
9000  START=(PAGE*9)-8
      STOP=START+8
      FOR SEL=START TO STOP
        GOSUB 9100; * Calculate line position
        IF DISC.DET<SEL,1>='' THEN
          CRT @(36,LINE):SEL'L#3':@(-4):
        END ELSE
          CRT @(36,LINE):SEL'L#3':' ':DISC.DET<SEL,1>'R26,#13':' ':DISC.DET<SEL,2>:@(-4):
        END
      NEXT SEL
*
9050  GOSUB 8050; * Calculate totals
      DISC.AMT=D.GROSS*(DISC.PERC/10000)
      DISC.AMT=OCONV(DISC.AMT,'MR0')
      NET=D.GROSS-DISC.AMT+M.GROSS
      CRT @(43,8):GROSS'R26,#10':DISC.PERC'R26,#11':NET'R26,#15'
      RETURN
*
* Calculate line position
*
9100  X=REM(SEL,9)
      IF X=0 THEN X=9
      LINE=8+X
      RETURN
