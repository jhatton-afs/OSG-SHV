$OPTIONS HEADER.EJECT
ERRMSG=''
*
$OPTIONS EXEC.EQ.PERF
$OPTIONS INFO.ABORT
*
* PROGRAM FB.1.13
* PROGRAM TO MODIFY CLASSIFICATION CODES
*
* FREIGHT PAYMENT PACKAGE
*
* AUTHOR: BRIAN M. QUEEN
* DATE: AUGUST 29, 1992
*
* Initialize variables
*
      PROMPT''
      PROG.NAME='FB.1.13'
      PROG.DESC='EDIT CLASSIFICATIONS'
      PACK.NAME='FREIGHT PAYMENT'
      CALL SCR.HEAD(CO.ID,FILE.ID,'',PROG.NAME,PROG.DESC,PACK.NAME,'AUTOMATED FREIGHT SYSTEMS, INC.',TIME.DATE,1)
       UPL.VIN = 'FBBP' ;  UPL.VIN<2> = 'FB.1.13' ; UPL.VOUT = ''                    ;* NPR_UPL 04/23/2010
       CALL UPD.PROGRAM.LOG(UPL.VIN,UPL.VOUT)                                                           ;* NPR_UPL 04/23/2010
*
* Open files
*
      OPEN '','FB.CLASS' TO F.FILE ELSE
        EXECUTE 'CREATE-FILE FB.CLASS 1 7' PASSLIST CAPTURING OUTPUT
        OPEN '','FB.CLASS' TO F.FILE ELSE CALL OPEN.ABORT("FB.CLASS",PROG.NAME)
      END
*
* Select codes and build display array
*
      DISP.REC=""
      SELECT F.FILE
      LOOP
        READNEXT ID ELSE ID=""
      UNTIL ID="" DO
        IF DISP.REC="" THEN
          DISP.REC<1>=ID
        END ELSE
          LOCATE(ID,DISP.REC,1;POS;"AR") ELSE
            DISP.REC=INSERT(DISP.REC,1,POS,0,ID)
          END
        END
      REPEAT
*
* First page of codes
*
      PAGE=1
      GOSUB 9000
*
* Enter selection code
*
100   HELP="Enter line number. [B]=Back. [T]=Top, [cr]=Next page. [U]=Update. [X]=Abort."
      CALL GEN.IN(0,22,"Enter line number please. ###",'',Q,0,3,'','',1,-3,22,0,QI,HELP,0,23)
      QI=OCONV(QI,'MCU') 
      BEGIN CASE
        CASE QI=''
          PAGE=PAGE+1
          GOSUB 9000
        CASE QI='B'
          PAGE=PAGE-1
          IF PAGE=0 THEN PAGE=1
          GOSUB 9000
        CASE QI='T'
          PAGE=1
          GOSUB 9000
        CASE QI='EX' ! QI='X'
          STOP
        CASE NUM(QI)
          SEL=QI
          IF SEL LT START ! SEL GT STOP THEN
            CALL ELINE('Invalid selection.')
            GOTO 100
          END
          GOSUB 8000
        CASE QI='U'
          X=DCOUNT(DISP.REC<1>,CHAR(253))
          FOR I=X TO 1 STEP -1
            IF DISP.REC<1,I>='' THEN
              DISP.REC=DELETE(DISP.REC,1,I)
            END
          NEXT I
*
          Z=0
          LOOP
            Z=Z+1
            CODE=DISP.REC<1,Z>
          UNTIL CODE="" DO
            ITEM=''
            ITEM<1>=DISP.REC<1,Z>
            WRITE ITEM ON F.FILE,CODE
          REPEAT
          STOP
      END CASE
      GOTO 100
*------------------------------------------------------------------------
*
* INTERNAL SUBROUTINES
*
*------------------------------------------------------------------------
*
* Edit code
*
8000  ROW=3+REM(SEL,18)
      IF ROW=3 THEN ROW=21
      CODE=DISP.REC<1,SEL>
      DESC=DISP.REC<2,SEL>
8010  HELP="Enter new code. [cr]=Accept. [D]=Delete. [X]=Abort."
      CALL GEN.IN(5,ROW,CODE'R#5','',Q,0,5,'','',2,-5,ROW,0,QI,HELP,0,23)
      QI=OCONV(QI,'MCU')
      IF QI='X' THEN
        CRT @(5,ROW):CODE'R#5'
        RETURN
      END
      IF QI='' THEN
        Q=CODE
      END
      IF QI='D' THEN
        DELETE F.FILE,CODE
        DISP.REC=DELETE(DISP.REC,1,SEL)
        GOSUB 9000
        RETURN
      END
      CODE=Q
      CRT @(5,ROW):CODE'R#5'
      DISP.REC<1,SEL>=CODE
      RETURN
*
* Display array
*
9000  CRT @(0,3):@(-3):"  #":@(5,3):"Class"
      START=(PAGE*18)-17
      IF START GT 108 THEN
        START=1
        PAGE=1
      END
      STOP=START+17
      FOR I=START TO STOP STEP 1
        ROW=3+REM(I,18)
        IF ROW=3 THEN ROW=21
        CODE=DISP.REC<1,I>
        DESC=DISP.REC<2,I>
        PRINT @(0,ROW):I"R#3":'  ':CODE"R#5"
      NEXT I
      RETURN
