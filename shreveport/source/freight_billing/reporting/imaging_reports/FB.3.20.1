***********************************************************************
*
*
* PROGRAM TO PROCESS THE PRECENTAGES OF REVENUE EACH CARRIER REPRESENTS
*
*
* AUTHOR: Jim T. Row & Owen Holman
*
*                           EDIT HISTORY
*                           ------------
*    DATE  : NAME : DESCRIPTION
*  ---------------------------------------------------
* 05/11/95 : JIMR : Project started ...
* 05/12/95 : JimR : Working on formatted output
* 05/15/95 : JimR : Ironing out a few minor bugs.. Adj formatting..etc.
* 05/16/95 : JimR : Adding logistics column, expanding worksheet width
*                   and some more coool stuf.
* 05/16/95 : JimR : Finished up the program with major help from Owen.
*                   An cummulitive percentage was also added by Owen
*                   Still needs a final file name & description.
* 05/16/95 : JimR : Adjusting the total line...
* 11/19/96 : Cody Henderson : Allowing a selectable carrier...
* 04/20/06 : JCW01  ADD FB.PROC.DATE.XREF FOR PERFORMANCE
* 05/08/06 : JCW02  CHANGES TO '@SELECTED', 'CLEARSELECT', AND 'STMT<X>'
* 031208 - JMK01 - T080799 - Remove duplicates in FB History Reporting
*
***********************************************************************
*
*
$INCLUDE PROG.ADMIN INC_OPTIONS
*
* Initialize variables
*
      COPIES=1
      CARRIERS=0
      ONE.CARRIER=''
*      USER=OCONV(SYSTEM(19),'MCU')
      CALL GET.USER(USER.NAME)
      SMALL.USER=OCONV(USER.NAME,'MCL')
      DEBBIE=0
      IF USER.NAME='DEBBIE' THEN DEBBIE=1
*      IF USER.NAME='OWEN' THEN DEBBIE=1
*
      PROMPT''
      CALL CHANNEL(CH.NUM)
      STATUS=''
      PROG.NAME='FB.3.20.1'
      PROG.DESC='Carrier Bills And Revenue Analysis'
      PACK.NAME='Freight Billing'
      NO.DIV.FLAG=0
       UPL.VIN = 'FBBP' ;  UPL.VIN<2> = 'FB.3.20.1' ; UPL.VOUT = ''                    ;* NPR_UPL 04/23/2010
       CALL UPD.PROGRAM.LOG(UPL.VIN,UPL.VOUT)                                                           ;* NPR_UPL 04/23/2010
      ERRMSG=''
      CALL FB.FORM(F.NUM)
      BUILD.DF=1
      DA.NAME=''
      DARRAY = '' ; * Spreadsheet array
      DARRAY<1,1>="% of Revenue"
      DARRAY<1,2>="Client Name"
      DARRAY<1,3>="Num Bills"
      DARRAY<1,4>="% of Bills"
      DARRAY<1,5>="Cumml %"
      DARRAY<1,6>="Amount Paid"
      DARRAY<1,7>="OvrChg Amount"
      DARRAY<1,8>="% OvrChg"
      DARRAY<1,9>="Logist Amount"
      DARRAY<1,10>="% Logist"
      DARRAY<1,11>="BD Amount"
      DARRAY<1,12>="BD Bills"
      DARRAY<1,13>="AD Amount"
      DARRAY<1,14>="AD Bills"
      DARRAY=CHANGE(DARRAY,@VM,CHAR(9))
*
* Open files
*
      OPEN '','FBWORK':CH.NUM TO F.WORK ELSE
        EXECUTE 'CREATE-FILE FBWORK':CH.NUM:' 1 1001' PASSLIST CAPTURING OUTPUT
        OPEN '','FBWORK':CH.NUM TO F.WORK ELSE
          CALL OPEN.ABORT('FBWORK':CH.NUM,PROG.NAME)
        END
      END
      OPEN '','VOC' TO F.VOC ELSE
         CALL ELINE('UNABLE TO OPEN VOC FILE, Data File Not Possible')
         BUILD.DF=0
         STOP
      END
      READ BILLDATA.REC FROM F.VOC,"BILLDATA" ELSE
         CALL ELINE('UNABLE TO OPEN "VOC BILLDATA", Data File Not Possible')
         BUILD.DF=0
         STOP
      END
      BILLDATA.REC<2>:='\':SMALL.USER
      WRITE BILLDATA.REC ON F.VOC,"BILLDATA.":USER.NAME ELSE
         CALL ELINE("YOUR USER NAME '":USER.NAME:"' IS NOT IN THE BILLDATA FOLDER - PLEASE SEE OWEN/TAMI/DAVID")
         CALL ELINE("Data File Not Possible")
         BUILD.DF=0
      END
      OPEN '','BILLDATA.':USER.NAME TO BILLDATA ELSE
        CALL ELINE('UNABLE TO OPEN "BILLDATA."':USER.NAME:', Data File Not Possible')
        BUILD.DF=0
      END
      OPEN '','CLIENTS' TO F.CLIENTS ELSE CALL OPEN.ABORT("CLIENTS",PROG.NAME)
      OPEN '','TABLES' TO F.TABLES ELSE CALL OPEN.ABORT("TABLES",PROG.NAME)
      READ DPRINTERS FROM F.TABLES,'PRINTERS' ELSE DPRINTERS=''
      OPEN 'DICT','FBWORK':CH.NUM TO F.DICT.WORK ELSE
        CALL OPEN.ABORT('DICT FBWORK':CH.NUM,PROG.NAME)
      END
      OPEN 'FB.BILLS.CRX' TO F.CRX ELSE
        CALL OPEN.ABORT('FB.BILLS.CRX',PROG.NAME)
      END
      OPEN '','CARRIERS' TO F.CARRIERS ELSE
        CALL OPEN.ABORT('CARRIERS',PROG.NAME)
      END
*
* Create work file dictionary items
*
      TEMP=''
      TEMP<1>='S'
      TEMP<2>=3
      TEMP<3>='Carr $'
      TEMP<7>='MR2'
      TEMP<8>=''
      TEMP<9>='R'
      TEMP<10>=10
      WRITE TEMP ON F.DICT.WORK,'CAR.DOL'
      TEMP<3>=''
      TEMP<2>=1 ; WRITE TEMP ON F.DICT.WORK,1
      TEMP<2>=2 ; WRITE TEMP ON F.DICT.WORK,2
      TEMP<2>=5 ; WRITE TEMP ON F.DICT.WORK,5
      TEMP<2>=7 ; WRITE TEMP ON F.DICT.WORK,7
*
      TEMP<2>=4
      TEMP<3>='Bills'
      TEMP<7>=''
      TEMP<9>='R'
      TEMP<10>=6
      WRITE TEMP ON F.DICT.WORK,'BILLS'
*
      TEMP=''
      TEMP<1>='S'
      TEMP<2>=0
      TEMP<8>='G1*1'
      TEMP<9>='R'
      TEMP<10>=5
      WRITE TEMP ON F.DICT.WORK,'CLIENT#'
*
      TEMP=''
      TEMP<1>='S'
      TEMP<2>=0
      TEMP<8>='G0*1'
      TEMP<9>='R'
      TEMP<10>=5
      WRITE TEMP ON F.DICT.WORK,'CARRIER#'
*
      TEMP=''
      TEMP<1>='S'
      TEMP<2>=0
      TEMP<8>='G0*1':@VM:'TCARRIERS;X;;1'
      TEMP<9>='L'
      TEMP<10>=30
      WRITE TEMP ON F.DICT.WORK,'CARRIER'
*
*
*  WORK FILE CONSISTS OF ITEMS WITH THE FOLLOWING DETAIL
*  Variable Attribute Description.....................................
*               ID
*                1    Overcharge amount (Discount Var + Overchg Var)
*                2    Logistics Markup - Consulting Variance
*                3    Amout Paid    - Adjusted bill amout
*                4    Bill Count    - Number of bills for client
*                5    BalDueAmt     - Balance Due Amount (PAID)
*                6    BalDueCnt     - Number of Balance dues
*                7    AddDueAmt     - Additional Due Amount (PAID)
*                8    AddDueCnt     - Number of Additional dues
*                9    Most Recent Bill Date
*
*
* Display screen heading
*
30    CALL AFS.SCR.HEAD(CO.ID,FILE.ID,'',PROG.NAME,PROG.DESC,PACK.NAME,CO.NAME,TIME.DATE,1)
*
* GET CARRIER NUMBER OR ALL CARRIERS
*
50    HELP='Enter carrier number.'
      CRT @(0,3):'Enter carrier number. ':
      IF ONE.CARRIER#'' THEN MASK=ONE.CARRIER'R%5' ELSE MASK='#####'
      CALL GEN.IN(35,3,MASK,'',Q,0,5,'','',2,35,3,0,QI,HELP,0,23)
      QI=OCONV(QI,'MCU')
      BEGIN CASE
        CASE QI='X'
          GOTO 9999
        CASE QI='EX'
          GOTO 9999
        CASE QI=''
*         CRT @(35,5):'ALL''R#5':
          CALL ELINE('You must enter a carrier number.')
          GOTO 50
        CASE NUM(QI)
          QI=QI'R%5'
          READV CARRIER.NAME FROM F.CARRIERS,QI,1 ELSE
            CALL ELINE('Carrier number not on file. Try again!')
            GOTO 50
          END
        CASE 1
          CALL ELINE('INVALID ENTRY. PLEASE TRY AGAIN.')
          GOTO 50
      END CASE
      ONE.CARRIER=QI
*     IF QI='' ELSE
        CRT @(35,3):ONE.CARRIER'R%5':' ':CARRIER.NAME
        DA.NAME='Carrier ':ONE.CARRIER'R%5':' ':CARRIER.NAME
*     END
*
* SELECT PRINTER
*
      MASK=''
      PRT.DESC=''
      LOCATE F.NUM IN DPRINTERS<2> SETTING POS THEN
        MASK=DPRINTERS<1,POS>
        PRT.DESC=DPRINTERS<4,POS>
      END
75   HELP="Enter the printer number, [F]-Build Spreadsheet file, [X]-Back, [EX]it"
      CALL GEN.IN(0,5,'Enter printer number: ':MASK:' - ':PRT.DESC,'',Q,0,2,'','',2,22,5,0,QI,HELP,0,23)
      QI=OCONV(QI,'MCU')
      BEGIN CASE
        CASE QI=''
          IF MASK='' THEN
            CALL ELINE('You must select a printer.')
            GOTO 75
          END ELSE
            QI=MASK
          END
        CASE QI='X'
          GOTO 50
        CASE QI='EX'
          GOTO 9999
      END CASE
      IF QI='F' THEN
        IF BUILD.DF=0 THEN
          CALL ELINE('You are not set up in the Directory BILLDATA. Contact Programming!')
          GO 75
        END
        MASK=QI
        QUE='F'
        PRT.DESC="Spreadsheet File"
      END ELSE
        LOCATE QI IN DPRINTERS<1> SETTING POS ELSE
          CALL ELINE('Error. You must enter number a valid printer.')
          GOTO 75
        END
        IF DPRINTERS<5,POS>='N' THEN
          CALL ELINE('Error! The printer you selected cannot print this report. Choose Another!')
          GOTO 75
        END
        MASK=QI
        PRT.DESC=DPRINTERS<4,POS>
        QUE=DPRINTERS<2,POS>
      END
      CRT @(22,5):QI:' - ':PRT.DESC:@(-3):
*
* Get current period beginning and ending dates
*
100   HELP='Enter the data processed beginning date.'
      CALL GEN.IN(0,7,'Data processed beginning date.  ':DATE()'D2/','DATE',BEG.DATE,0,8,'','',1,-8,7,0,QI,HELP,0,23)
      QI=OCONV(QI,'MCU')
      IF QI='X' THEN GOTO 75
      IF QI='EX' THEN GOTO 9999
      IF QI='' THEN BEG.DATE=DATE()
      CRT @(32,7):BEG.DATE'D2/'
*
* Get current period ending date
*
200   HELP='Enter the data processed ending date.'
      CALL GEN.IN(0,9,'Data processed ending date.     ':DATE()'D2/','DATE',END.DATE,0,8,'','',1,-8,9,0,QI,HELP,0,23)
      IF QI='X' THEN GOTO 100
      IF QI='EX' THEN GOTO 9999
      IF QI='' THEN END.DATE=DATE()
      IF END.DATE LT BEG.DATE THEN
        CALL ELINE('Invalid ending date.')
        GOTO 200
      END
*
*  Verify date entry range
*
      CRT @(32,9):END.DATE'D2/'
300:  HELP="X=Change. EX=Exit. RETURN=Continue."
      CALL GEN.IN(0,22,'Enter selection please. --','',Q,0,2,'','',0,-2,22,1,QI,HELP,0,23)
      QI=OCONV(QI,'MCU')
      BEGIN CASE
         CASE QI='X'
            CRT @(0,22):@(-3):
            GO 200
         CASE QI='EX'
            GOTO 9999
         CASE QI=''
            NULL
         CASE 1
            CALL ELINE('Invalid entry. Must be X, EX, or RETURN.')
            GOTO 300
      END CASE
*
* Start processing
*
      IF DEBBIE THEN
        CALL ELINE("Will only Select ONE Bill per carrier.. Debbie Only")
      END
400:  TEXT="Processing Date Range - Please Wait ... May Take Several Hours."
      CALL CENTER(TEXT,80)
      CRT @(0,13):@(-3):TEXT
*
* Clear work file
*
*DF     GOTO JIM

      CLEARFILE F.WORK
*
* Select data for current client
*

***************************************************************
*
* SELECT CLIENT FILE
*
      TEXT='Selecting Clients.'
      CALL CENTER(TEXT,80)
      CRT @(0,10):TEXT
     

      EXECUTE "SELECT VOC 'FB.BILLS.HIST' *A7" RTNLIST CLIENT.LIST CAPTURING OUTPUT
      ITEMS = @SELECTED
      IF @SELECTED < 1 THEN ; * JCW02
        CALL ELINE('ERROR - No clients on file.')
        GOTO 9999
      END
      COUNTER.1=0
      PER=0
      LOOP
        READNEXT CLIENT.ID FROM CLIENT.LIST ELSE CLIENT.ID='DONE'
      UNTIL CLIENT.ID='DONE' DO
        COUNTER.1=COUNTER.1+1
        IF CLIENT.ID='FB.BILLS.HIST' OR CLIENT.ID[1,2]='99' THEN GO SKIP.CLIENT
        READV CLIENT.NAME FROM F.CLIENTS,CLIENT.ID,2 ELSE
          CLIENT.NAME='*** Not On File ***'
        END
          CRT @(0,17):CLIENT.ID:" ":CLIENT.NAME:" - Now selecting freight bills":@(-4):
        CALL PROG.BAR(20,COUNTER.1,ITEMS,PER.1)
        STMT='SSELECT FB.PROC.DATE.XREF,':CLIENT.ID:' WITH DATE >= "':OCONV(BEG.DATE,'D2/'):'" AND WITH DATE <= "':OCONV(END.DATE,'D2/'):'"'          ; * JCW 01
        EXECUTE STMT RTNLIST CAPTURING RESULTS  ; * JCW 01
        IF @SELECTED > 0 THEN  ; * JCW01
          STMT = 'QSELECT FB.PROC.DATE.XREF,':CLIENT.ID  ; * JCW01
          EXECUTE STMT PASSLIST RTNLIST CAPTURING RESULTS  ; * JCW01
        END ELSE  ; * JCW01
          GOTO SKIP.CLIENT  ; * JCW01
        END  ; * JCW01
        OPEN '','FB.BILLS.HIST,':CLIENT.ID TO F.BILLS THEN
          STMT='SELECT FB.BILLS.HIST,':CLIENT.ID
          STMT:=' WITH VERSION = "0"'
          IF ONE.CARRIER#'' THEN STMT:=' AND WITH CARRIER# = "':ONE.CARRIER:'"'
*         STMT:=' SAVING UNIQUE @ID'     ; * JMK01
          EXECUTE STMT PASSLIST RTNLIST BILLS.LIST CAPTURING OUTPUT
          NUM.BILLS = @SELECTED
          CRT @(0,17):CLIENT.ID:" ":CLIENT.NAME:" - Now processing ":NUM.BILLS:" Freight Bills":@(-4):
          COUNTER.2=0
          LOOP
            DONE=0
            READNEXT PRO.ID FROM BILLS.LIST ELSE DONE=1
          IF PRO.ID#'' THEN
            IF LEN(FIELD(PRO.ID,"*",1))#5 THEN
              CALL ELINE('FOR CLIENT ':CLIENT.ID:' PRO ':PRO.ID:' is fried!')
            END
          END
          UNTIL DONE=1 DO
            COUNTER.2=COUNTER.2+1
*           CRT @(0,17):CLIENT.ID:' ':PRO.ID:@(-4):
            IF NUM.BILLS>0 THEN CALL PROG.BAR(22,COUNTER.2,NUM.BILLS,PER.2)
            READ DBILL FROM F.BILLS,PRO.ID THEN
*
              WORK.ID=FIELD(PRO.ID,'*',1):'*':CLIENT.ID
              READ WORK.REC FROM F.WORK,WORK.ID ELSE WORK.REC=''
*
              CAR.WORK.ID=FIELD(PRO.ID,'*',1):'*TOTAL'
              READ CAR.WORK.REC FROM F.WORK,CAR.WORK.ID ELSE CAR.WORK.REC=''
*
              TOT.WORK.ID='TOTAL'
              READ TOT.WORK.REC FROM F.WORK,TOT.WORK.ID ELSE
                TOT.WORK.REC=''
              END
*
* Update carrier total record in work file
*
              PRO=FIELD(PRO.ID,'*',2)
              START=LEN(PRO)-1
              BEGIN CASE
                *
                * Balance Due
                *
                CASE PRO[START,2]='BD'
                  ATT=5 ; VALUE=DBILL<8> ; GOSUB 8000 ; * Amount pd/due
                  ATT=6 ; VALUE=1 ; GOSUB 8000 ; * Inc bill count
                *
                * Amount Due (paid)
                *
                CASE PRO[START,2]='AD'
                  ATT=7 ; VALUE=DBILL<8> ; GOSUB 8000 ; * Amount pd/due
                  ATT=8 ; VALUE=1 ; GOSUB 8000 ; * Inc bill count
                *
                * Amount Billed & Paid
                *
                CASE 1
                  ATT=1 ; VALUE=DBILL<10>+DBILL<11> ; GOSUB 8000 ; * Discount+Overcharge
                  ATT=2 ; VALUE=DBILL<12> ; GOSUB 8000 ; * Logistics
                  ATT=3 ; VALUE=DBILL<8> ; GOSUB 8000 ; * Amount paid
                  ATT=4 ; VALUE=1 ; GOSUB 8000 ; * Inc Bill Count
              END CASE

*
* Update totals
*
              IF DBILL<37>#'' THEN BILL.DATE=DBILL<37> ELSE BILL.DATE=DBILL<1>
              IF WORK.REC<9> < BILL.DATE THEN WORK.REC<9>=BILL.DATE
              WRITE WORK.REC ON F.WORK,WORK.ID
              WRITE TOT.WORK.REC ON F.WORK,TOT.WORK.ID
              WRITE CAR.WORK.REC ON F.WORK,CAR.WORK.ID
            END
SKIP.BILL:***
          REPEAT
        END
SKIP.CLIENT:***
      REPEAT


JIM:

*
* Print report
*
* Set-up printer
*
      WIDTH=150
      EXECUTE "TERM ,,,,,,150,80"
      CTR=INT(WIDTH/2)
**************************************************************
ESC=CHAR(27)
*WIDTH=175
*CTR=77
*CALL FB.LASER.SET('S',WIDTH,64,8)
LASER.STR=''
LASER.STR:= ESC:'E'
LASER.STR:= ESC:'&l0O' ; * Portrait
LASER.STR:= ESC:'&l8D'
LASER.STR:= ESC:'&l88F' ; ** 88 Lines on a Page
LASER.STR:= ESC:'(8U'
LASER.STR:= ESC:'(s0p'
LASER.STR:= '19h'
LASER.STR:= '0s'
LASER.STR:= '0b'
LASER.STR:= '4102T'
**************************************************************
*      EXECUTE 'SP-ASSIGN F25,':COPIES ; * Default to Lexmark

      PRINTER ON
*OH       LASER = ''
*OH       LASER<1 >= 0                           ;* Portrait
*OH       LASER<2 >= 6                           ;* Lines per inch
*OH       LASER<3 >= 61                          ;* Page length
*OH       LASER<4 >= 16.7                        ;* Characters per inch
*OH       LASER<5 >= 0
*OH       LASER<6 >= 0
*OH       LASER<7 >= 1
*OH       LASER<8 >= 0
*OH       MARGIN = 0
*OH       PRINT ESC:"E"
*OH       PRINT ESC:"&l":LASER<1>:"O":           ;* Set portrait
*OH       PRINT ESC:"&l":LASER<2>:"D":           ;* Lines per inch
*OH       PRINT ESC:"&l":LASER<3>:"F":           ;* Page length
*OH       PRINT ESC:"(s":LASER<4>:"h":           ;* Set pitch
*OH       PRINT LASER<5>:"p":                    ;* Proportional printing
*OH       PRINT LASER<6>:"s":                    ;* Italics off
*OH       PRINT "0T":                            ;* Line Printer Font
*OH       PRINT ESC:"&l":LASER<7>:"H"            ;* Bin selection
*OH       PRINT ESC:"&a":MARGIN:"L"
*OH       PRINT ESC:'"g':LASER<8>:'F'                      ;* Paper output
      PRINT LASER.STR ; * Configure Laser Printer
*
** Get next division code
*
*
*
      L.TAB=CTR-(LEN(PROG.DESC)/2)
      L.TAB=L.TAB-LEN(PROG.NAME)
      TEMP=PROG.NAME:SPACE(L.TAB):PROG.DESC
      R.TAB=WIDTH-LEN(TEMP)
      R.TAB=R.TAB-LEN(TIMEDATE())
      HEAD1=PROG.NAME:SPACE(L.TAB):PROG.DESC:SPACE(R.TAB):TIMEDATE():"'L'"
*
      TITLE='Bills processed from ':BEG.DATE'D2/':' to ':END.DATE'D2/'
      L.TAB=CTR-(LEN(TITLE)/2)
      L.TAB=L.TAB-LEN(CO.NAME)
      TEMP=CO.NAME:SPACE(L.TAB):TITLE
*     R.TAB=(WIDTH-LEN(TEMP))-9
*     HEAD2=TEMP:SPACE(R.TAB):"PAGE 'PL'"
      R.TAB=(WIDTH-LEN(TEMP))-20
      HEAD2=TEMP:SPACE(R.TAB):USER.NAME'L(#10 )':"PAGE 'PL'"
      IF ONE.CARRIER#'' THEN
        READV CARRIER.NAME FROM F.CARRIERS,ONE.CARRIER,1 ELSE CARRIER.NAME='Not on File'
        TEXT='For Carrier ':ONE.CARRIER:' - ':CARRIER.NAME
        L.TAB=CTR-(LEN(TEXT)/2)
        HEAD3=SPACE(L.TAB):TEXT:"'L'"
      END ELSE
        HEAD3="'L'"
      END
*
      HEAD4=""
      HEAD5=STR('-',WIDTH):"'L'"
*
      HEAD6='Line''L#4'
      HEAD7=' ''L#4'
*
      HEAD6:=SPACE(1):'  % of''L#8'
      HEAD7:=SPACE(1):' Revenue''L#8'
*
      HEAD6:=SPACE(1):'--Client Name''L#30'
      HEAD7:=SPACE(1):'Carrier Name''L#30'
*
      HEAD6:=SPACE(1):'  Num ''L#9'
      HEAD7:=SPACE(1):' Bills ''L#9'
*
      HEAD6:=SPACE(1):' % Of''L#6'
      HEAD7:=SPACE(1):' Bills''L#6'
*
      HEAD6:=SPACE(1):'Cumml''L#6'
      HEAD7:=SPACE(1):'  %''L#6'
*
      HEAD6:=SPACE(2):'  Amount''R#10'
      HEAD7:=SPACE(2):'   Paid ''R#10'
*
      HEAD6:=SPACE(2):'  OvrChg''R#9'
      HEAD7:=SPACE(2):'  Amount''R#9'
*
      HEAD6:=SPACE(2):'  %''L#6'
      HEAD7:=SPACE(2):'OvrChg''L#6'
*
      HEAD6:=SPACE(2):'  Logist''R#9'
      HEAD7:=SPACE(2):'  Amount''R#9'
*
      HEAD6:=SPACE(2):'  %''L#6'
      HEAD7:=SPACE(2):'Logist''L#6'
*
      HEAD6:=SPACE(2):'  BD  ''R#7'
      HEAD7:=SPACE(2):'Amount''R#7'
*
      HEAD6:=SPACE(2):' BD''L#5'
      HEAD7:=SPACE(2):'Bills''L#5'
*
      HEAD6:=SPACE(2):'  AD  ''R#7'
      HEAD7:=SPACE(2):'Amount''R#7'
*
      HEAD6:=SPACE(2):' AD  ''L#5'
      HEAD7:=SPACE(2):'Bills''L#5'

      HEAD6:="'L'"
      HEAD7:="'L'"
*
      HEAD8=STR('-',WIDTH):"'L'"
*
      HED=HEAD1:HEAD2:HEAD3:HEAD4:HEAD5:HEAD6:HEAD7:HEAD8
      HEADING HED
*     PAGE 0
*
** Select data
*
      DA.TITLE='All Frieght Bills for '
      DA.TITLE:=DA.NAME
      DA.TITLE:=' processed from ':BEG.DATE'D2/':' to ':END.DATE'D2/'
      DARRAY=DA.TITLE:@AM:@AM:DARRAY
      COUNTER=0
      SUB4=0
      CARRIER.BILLS=0
      PREC='SSELECT FBWORK':CH.NUM
      PREC:=' BY CARRIER'
      PREC:=' BY CARRIER#'
      PREC:=' BY-DSND BILLS'
      PREC:=' BY-DSND CAR.DOL'
      PREC:=' BY-DSND 1'
      PREC:=' BY-DSND 2'
      PREC:=' BY-DSND 5'
      PREC:=' BY-DSND 7'
      EXECUTE PREC PASSLIST CAPTURING OUTPUT
      READ TOTAL.REC FROM F.WORK,'TOTAL' ELSE
        TOTAL.REC=''
      END
*
      DONE = 0
      OLD.CARRIER=''
      LOOP
        READNEXT ID ELSE DONE = 1
      UNTIL DONE = 1 DO
        CARRIER.ID=FIELD(ID,'*',1)
        IF CARRIER.ID='TOTAL' ELSE
          IF CARRIER.ID#OLD.CARRIER THEN
            READV NAME.TEXT FROM F.CARRIERS, OLD.CARRIER,1 ELSE
              NAME.TEXT='** Not on file **'
            END
            READ REC FROM F.WORK,OLD.CARRIER:'*TOTAL' ELSE
              READ REC FROM F.WORK,CARRIER.ID:'*TOTAL' ELSE
                REC=''
              END
            END
            SUBTOTAL.REC=REC
            IF OLD.CARRIER#'' THEN
              IF SYSTEM(4) < 2 THEN PAGE
              PRINT STR('-',WIDTH)
*             COUNTER=''
              CARRIER.BILLS+=REC<4>
              SUBTOTAL.REC=TOTAL.REC
              SUB4=CARRIER.BILLS
              TOT.BILLS=TOTAL.REC<4>
              GOSUB BUILD.DET.LINE
              IF QUE='F' ELSE
                 PRINT LINE
                 PRINT
              END
              SUB4=0
            END
            OLD.CARRIER=CARRIER.ID
          END
          CLIENT.ID=FIELD(ID,'*',2)
          IF CLIENT.ID='TOTAL' ELSE
            READV NAME.TEXT FROM F.CLIENTS, CLIENT.ID,2 ELSE
              NAME.TEXT='** Not on file **'
            END
            NAME.TEXT='--':NAME.TEXT
            READ SUBTOTAL.REC FROM F.WORK,CARRIER.ID:'*TOTAL' ELSE
              SUBTOTAL.REC=''
            END
            READ REC FROM F.WORK,ID THEN
              SUB4+=REC<4>
              TOT.BILLS=SUBTOTAL.REC<4>
              GOSUB BUILD.DET.LINE
              IF QUE='F' ELSE PRINT LINE
            END
          END
        END
      REPEAT
      IF QUE='F' THEN
        WRITE DARRAY ON BILLDATA,'FB-3-20-1-':ONE.CARRIER:'.XLT' ELSE
          CALL ELINE('Close your FB-3-20-1-':ONE.CARRIER:'.XLT Spreadsheet and press enter')
          WRITE DARRAY ON BILLDATA,'FB-3-20-1-':ONE.CARRIER:'.XLT' ELSE
            CALL ELINE('Unable to Write Spreadsheet Data to file!')
          END
        END
        CRT @(0,13):@(-3):
        CRT @(0,17):@(-3):"The file \\Afs\billdata\":SMALL.USER:"\Fb-3-20-1-":ONE.CARRIER:".xlt was written."
        CRT @(0,18):'1) Double click the "My Computer" Icon on your desktop'
        CRT @(0,19):\2) Double Click the "Billdata on 'Afs' (U:)" Drive Icon\
        CRT @(0,20):\3) Double Click the \:USER.NAME:' Folder'
        CRT @(0,21):\4) Double Click the Fb-3-20-1-\:ONE.CARRIER:\.xlt Excel Sheet\
        CRT @(0,23):"Press [Enter] to continue ......":
        INPUT NUL
        GOTO 9999
      END
*
      READV NAME.TEXT FROM F.CARRIERS, OLD.CARRIER,1 ELSE
        NAME.TEXT='** Not on file **'
      END
      READ REC FROM F.WORK,OLD.CARRIER:'*TOTAL' ELSE
        REC=''
      END
      IF SYSTEM(4) < 2 THEN PAGE
      PRINT STR('-',WIDTH)
*     COUNTER=''
      CARRIER.BILLS+=REC<4>
      SUBTOTAL.REC=TOTAL.REC
      SUB4=CARRIER.BILLS
      TOT.BILLS=TOTAL.REC<4>
      GOSUB BUILD.DET.LINE
      PRINT LINE
      PRINT
      SUB4=CARRIER.BILLS
*
** Print total line
*
      IF SYSTEM(4) < 4 THEN PAGE
      PRINT STR('=',WIDTH)
      REC=TOTAL.REC
      NAME.TEXT='TOTAL'
*     COUNTER=''
      TOT.BILLS=TOTAL.REC<4>
      SUBTOTAL.REC=TOTAL.REC
      GOSUB BUILD.DET.LINE
      PRINT LINE

*
** END OF APPLICATION'
*
      EXECUTE "TERM ,,,,,,,60"
      GOTO 9999


8000 * Update Arrays
      WORK.REC<ATT>        +=VALUE
      CAR.WORK.REC<ATT>    +=VALUE
      TOT.WORK.REC<ATT>    +=VALUE
      RETURN

BUILD.DET.LINE:***
      COUNTER=COUNTER+1
      LINE=''
      DA.LINE=''
      *
      * LINE COUNTER
      *
      LINE:=COUNTER'R#4':' '
      *
      * PRECENTAGE OF REVENUE
      *
      IF REC<3>+0=0 THEN VAR=0 ELSE
        VAR=(REC<3>*100/SUBTOTAL.REC<3>)
      END
      LINE:=VAR'R44,#8':' '
      DA.LINE<1,1>=OCONV(VAR,'MR40')
      *
      * CARRIER NAME
      *
      LINE:=NAME.TEXT'L#30':' '
      TEMP.NAME.TEXT=NAME.TEXT
REMOVE.LEADING.DASHES:***
      IF TEMP.NAME.TEXT[1,1]='-' THEN
         TEMP.NAME.TEXT=TEMP.NAME.TEXT[2,999]
         GOTO REMOVE.LEADING.DASHES
      END
      DA.LINE<1,2>=TEMP.NAME.TEXT
      *
      * BILL COUNT
      *
      LINE:=REC<4>'R04,#6':'    '
      DA.LINE<1,3>=OCONV(REC<4>,'MR00,')
      *
      * PRECENTAGE OF GROSS BILLS
      *
      IF TOTAL.REC<4>+0=0 THEN VAR=0 ELSE
        VAR=(REC<4>*100/SUBTOTAL.REC<4>)
      END
      LINE:=VAR'R24,#6':' '
      DA.LINE<1,4>=OCONV(VAR,'MR20')
      *
      * Accuml %
      *
      IF TOTAL.REC<4>+0=0 THEN VAR=0 ELSE
        VAR=(SUB4*100/TOT.BILLS)
      END
      LINE:=VAR'R24,#6':'  '
      DA.LINE<1,5>=OCONV(VAR,'MR20')
      *
      * AMMOUNT BILLED
      *
      LINE:=REC<3>'R06,#10':'  '
      DA.LINE<1,6>=OCONV(REC<3>,'MR2,')
      *
      * AMOUNT OVER CHARGED
      *
      LINE:=-REC<1>'R06,#9':'  '
      DA.LINE<1,7>=OCONV(-REC<1>,'MR2,')
      *
      * PERCENTAGE OF OVERCHARGE
      *
      IF REC<3>+0=0 THEN VAR=0 ELSE
       VAR=(-REC<1>)*100/REC<3>
      END
      LINE:=VAR'R24,#6':'  '
      DA.LINE<1,8>=OCONV(VAR,'MR20')
      *
      * LOGISTICS MARKUP AMOUNT
      *
      LINE:=-REC<2>'R06,#9':'  '
      DA.LINE<1,9>=OCONV(-REC<2>,'MR2,')
      *
      * PERCENTAGE OF LOGISTICS
      *
      IF REC<3>+0=0 THEN VAR=0 ELSE
       VAR=(-REC<2>)*100/REC<3>
      END
      LINE:=VAR'R24,#6':'  '
      DA.LINE<1,10>=OCONV(VAR,'MR20')
      *
      * BALANCE DUE AMOUNT
      *
      LINE:=REC<5>'R06,#7':'  '
      DA.LINE<1,11>=OCONV(REC<5>,'MR2,')
      *
      * BALANCE DUE COUNT
      *
      LINE:=REC<6>'R04,#5':'  '
      DA.LINE<1,12>=OCONV(REC<6>,'MR00,')
      *
      * ADDITIONAL DUE
      *
      LINE:=REC<7>'R06,#7':'  '
      DA.LINE<1,13>=OCONV(REC<7>,'MR2,')
      *
      * ADDITIONAL DUE COUNT
      *
      LINE:=REC<8>'R04,#5':'  '
      DA.LINE<1,14>=OCONV(REC<8>,'MR00,')
      DARRAY<-1>=CHANGE(DA.LINE,@VM,CHAR(9))
      RETURN
9999:* EXIT PROGRAM
      STOP
