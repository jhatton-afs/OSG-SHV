      SUBROUTINE FB.CC.NOTES(IN.NOTE,VIN,VOUT)
* PROGRAM TO ENTER FB NOTE LINES
      UPL.VIN = 'FBBP' ; UPL.VIN<2> = 'FB.CC.NOTES' ; UPL.VOUT = ''    ; * NPR_UPL 04/23/2010
      CALL UPD.PROGRAM.LOG(UPL.VIN,UPL.VOUT)       ; * NPR_UPL 04/23/2010
*
* FREIGHT PAYMENT PACKAGE
*
* AUTHOR: NCG PRATT
* DATE: 11-21-08
*
$OPTIONS EXEC.EQ.PERF
$OPTIONS INFO.ABORT
$OPTIONS HEADER.EJECT
      ERRMSG=''
*******************************************************************************
* 102811 - JMK01 - SysAid#238 - Blank notes for client 1977
*******************************************************************************
*
* Initialize variables
*
      OPEN '','FB.NOTES' TO F.NOTES ELSE
         CALL OPEN.ABORT("FB.NOTES",PROG.NAME)
      END
*
      OPEN '','BCTRL' TO F.BCTRL ELSE
         CALL OPEN.ABORT("BCTRL",PROG.NAME)
      END
*
      CALL GET.USER(USER)
*
      FB.NOTE = ""
      DISPLAY.MODE=0
*
      ID.NOTES = VIN<1>
      USERS.ARRAY = ""                   ; * Additional Users who added notes to Bill MV
      DATE.ARRAY = ""                    ; * Date note was added to Bill MV
      TIME.ARRAY = ""                    ; * Time note was added to Bill MV
      NOTES.ARRAY = ""                   ; * Notes Added SV within MV
*
      PROMPT ''
      CALL CHANNEL(CH.NUM)
*
* Get company file information
*
      PROG.NAME='FB.CC.NOTES'
      PROG.DESC='FB NOTES ENTRY'
      PACK.NAME='FREIGHT PAYMENT'
*     CALL AFS.SCR.HEAD(CO.ID,FILE.ID,'',PROG.NAME,PROG.DESC,PACK.NAME,CO.NAME,TIME.DATE,1)
*
      NOTE.DISP = IN.NOTE
*
      CLR.DISPLAY = ""
      FOR LN = 4 TO 21
         CLR.DISPLAY := @(0,LN):@(-3)
      NEXT LN
      CRT CLR.DISPLAY
      CRT @(0,4):"Pro No : ":FIELD(ID.NOTES,"*",3):SPACE(10):"N O T E S"
*
      IF NOTE.DISP NE "" THEN
         FOR I=6 TO 21 STEP 1
            CRT @(0,I):I-5'R#2':') ':NOTE.DISP<1,I-5>'L#75'
         NEXT I
         CALL ELINE("")
      END
*
      ADDTL.NOTES = ""
*
      READ ADDTL.NOTES FROM F.NOTES, ID.NOTES THEN
         USERS.ARRAY = ADDTL.NOTES<1>    ; * Additional Users who added notes to Bill MV
         DATE.ARRAY = ADDTL.NOTES<2>     ; * Date note was added to Bill MV
         TIME.ARRAY = ADDTL.NOTES<3>     ; * Time note was added to Bill MV
         NOTES.ARRAY = ADDTL.NOTES<4>    ; * Notes Added SV within MV
         GOSUB DISPLAY.ADDITIONAL.NOTES
      END
*
50:
*
      NOTE.CNT = DCOUNT(NOTES.ARRAY,@VM)
      NEXT.NOTE = NOTE.CNT + 1
*
      HELP = "Enter Note # to change, [U]pdate, [A]dd Note, [EX]=Exit"
      CALL GEN.IN(0,22,'Enter Selection ','',ACK,0,2,'','',1,28,22,0,QI,HELP,0,23)
      QI = OCONV(QI,'MCU')
*
      BEGIN CASE
         CASE NUM(QI) AND QI NE ""
            IF QI GT 0 OR QI LE NEXT.NOTE THEN
               NOTES = NOTES.ARRAY<1,QI>
               W.USER = USERS.ARRAY<1,QI>
               IF USER = W.USER OR NOTES = '' THEN
                  CONVERT @SVM TO @VM IN NOTES
                  CRT CLR.DISPLAY
                  CALL FB.2.1.NOTES(NOTES,"",0)
                  CONVERT @VM TO @SVM IN NOTES
                  USERS.ARRAY<1,QI> = USER         ; * Additional Users who added notes to Bill MV
                  DATE.ARRAY<1,QI> = DATE()        ; * Date note was added to Bill MV
                  TIME.ARRAY<1,QI> = TIME()        ; * Time note was added to Bill MV
                  NOTES.ARRAY<1,QI> = NOTES        ; * Notes Added SV within MV
                  NOTES.ARRAY<1,QI> = NOTES
                  GOSUB DISPLAY.ADDITIONAL.NOTES
                  GOTO 50
               END ELSE
                  CALL ELINE("Only notes entered by you may be changed")
                  GOTO 50
               END
            END ELSE
               CALL ELINE("Enter a Number between 1 and ":NEXT.NOTE)
               GOTO 50
            END
         CASE QI = "A"
            NOTES = ""
            CRT CLR.DISPLAY
            CALL FB.2.1.NOTES(NOTES,QI,0)
            IF TRIM(NOTES) NE "" AND NOTES NE "EX" THEN
               CONVERT @VM TO @SVM IN NOTES
               USERS.ARRAY<1,NEXT.NOTE> = USER     ; * Additional Users who added notes to Bill MV
               DATE.ARRAY<1,NEXT.NOTE> = DATE()    ; * Date note was added to Bill MV
               TIME.ARRAY<1,NEXT.NOTE> = TIME()    ; * Time note was added to Bill MV
               NOTES.ARRAY<1,NEXT.NOTE> = NOTES    ; * Notes Added SV within MV
               GOSUB DISPLAY.ADDITIONAL.NOTES
               GOTO 50
            END
         CASE QI = "U"
            GOSUB UPDATE.NOTES
         CASE QI = 'EX' OR QI = ''
            GOTO 9900
         CASE 1
            CALL ELINE("Invalid Selection")
            GOTO 50
      END CASE
*
9900: RETURN
*
DISPLAY.ADDITIONAL.NOTES:* Display and allow entry of Additional notes
*
      DROW = 6
      LINES.LEFT = 15
      PAGE.CNT = 1
*
      CRT CLR.DISPLAY
      CRT @(0,4):"Pro No : ":FIELD(ID.NOTES,"*",3):SPACE(10):"N O T E S"
*
      N.LINES = DCOUNT(NOTES.ARRAY,@VM)
      FOR II = 1 TO N.LINES
         W.USER = TRIM(USERS.ARRAY<1,II>)
         W.DATE = TRIM(DATE.ARRAY<1,II>)
         W.TIME = TRIM(TIME.ARRAY<1,II>)
         W.NOTE = TRIM(NOTES.ARRAY<1,II>)
         N.NOTES = DCOUNT(W.NOTE,@SVM)
*
         READV W.NAME FROM F.BCTRL, W.USER, 13 ELSE W.NAME = "??"
         H.STR = "Entered ":OCONV(W.DATE,"D4/"):" by ":W.NAME
*         HLINE = II "R#2":" Entered by ":W.NAME:" (":W.USER:") on ":OCONV(W.DATE,"D2/"):" at ":OCONV(W.TIME,"MTS")
         LN.STR = 74 - LEN(H.STR)
         HLINE = II "R#3":") ":STR("_",LN.STR): " " : H.STR
         LINES.NEEDED = (N.NOTES+3)
         IF LINES.LEFT LT LINES.NEEDED THEN
            CALL ELINE("")
            CRT CLR.DISPLAY
            DROW = 5
            LINES.LEFT = 15
            PAGE.CNT += 1
            CRT @(70,3):"Page ":PAGE.CNT
         END
         CRT @(0,DROW):@(-4):HLINE
         DROW += 1
         LINES.LEFT = LINES.LEFT - 1
*
         CRT @(5,DROW):@(-4):SPACE(74)
         DROW += 1
         LINES.LEFT = LINES.LEFT - 1
*
         FOR JJ = 1 TO N.NOTES
            CRT @(5,DROW):@(-4):W.NOTE<1,1,JJ>[1,74]
            DROW += 1
            LINES.LEFT = LINES.LEFT - 1
         NEXT JJ
         CRT @(5,DROW):@(-4):SPACE(74)
         DROW += 1
         LINES.LEFT = LINES.LEFT - 1
      NEXT II
*
      CALL ELINE("")
*
      RETURN
*
UPDATE.NOTES:* Update MNotes
*
      NEW.ARRAY = ""
      NEW.ARRAY<1> = USERS.ARRAY<1>      ; * Additional Users who added notes to Bill MV
      NEW.ARRAY<2> = DATE.ARRAY<1>       ; * Date note was added to Bill MV
      NEW.ARRAY<3> = TIME.ARRAY<1>       ; * Time note was added to Bill MV
      NEW.ARRAY<4> = NOTES.ARRAY<1>      ; * Notes Added SV within MV
*
      VOUT<1> = 1
      IF NEW.ARRAY<4>='' THEN            ; * JMK01
         DELETE F.NOTES, ID.NOTES        ; * JMK01
      END ELSE                           ; * JMK01
         WRITE NEW.ARRAY ON F.NOTES, ID.NOTES
      END                                ; * JMK01
*
      RETURN
