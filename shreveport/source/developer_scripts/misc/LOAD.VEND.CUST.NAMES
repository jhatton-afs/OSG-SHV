      PROG.NAME = 'LOAD.VEND.CUST.NAMES'
      OPEN 'FB.TABLES' TO F.TABLES ELSE
        CALL OPEN.ABORT('FB.TABLES', PROG.NAME)
      END
      VCN.ID = ''
      VCN.REC = ''
      CLIENT.ID = FIELD(@SENTENCE,' ',2)
      IF CLIENT.ID = '' THEN
        STMT = 'SELECT VOC "VEND.CUST.NAMES" *A7'
      END ELSE
        STMT = 'SELECT CLIENTS ':CLIENT.ID
      END
      CRT STMT
CALL ELINE('PRESS BREAK')
      EXECUTE STMT RTNLIST LIST1 CAPTURING MSG
      IF @SELECTED < 1 THEN
        CRT 'NO ITEMS SELECTED; PROGRAM STOPPED.'
        STOP
      END
      STMT = 'SELECT CLIENTS'
      CRT STMT
      EXECUTE STMT PASSLIST LIST1 RTNLIST CLIENT.LIST CAPTURING MSG
      DONE1 = 0
      LOOP UNTIL DONE1 DO
        READNEXT CLIENT.ID FROM CLIENT.LIST THEN
          CRT CLIENT.ID
          STMT = 'CLEAR-FILE VEND.CUST.NAMES,':CLIENT.ID
          EXECUTE STMT CAPTURING MSG
          PREV.DESC = 'X9X9X9X9'
          VCN.CTR = -1
          OPEN 'VEND.CUST,':CLIENT.ID TO F.VC ELSE
            CALL OPEN.ABORT('VEND.CUST,':CLIENT.ID, PROG.NAME)
          END
          OPEN 'VEND.CUST.NAMES,':CLIENT.ID TO F.VCN ELSE
            CALL OPEN.ABORT('VEND.CUST.NAMES,':CLIENT.ID, PROG.NAME)
          END
          STMT = 'SSELECT VEND.CUST,':CLIENT.ID:' BY 1'
          EXECUTE STMT RTNLIST VC.LIST CAPTURING MSG
          IF @SELECTED > 0 THEN
            DONE2 = 0
            LOOP UNTIL DONE2 DO
              READNEXT VC.ID FROM VC.LIST THEN
                READ VC.REC FROM F.VC, VC.ID THEN
                  DESC = VC.REC<1>
                  IF PREV.DESC # DESC THEN
                    IF PREV.DESC # 'X9X9X9X9' THEN
                      WRITE VCN.REC TO F.VCN, VCN.ID
                    END
                    PREV.DESC = DESC
                    VCN.CTR += 1
                    VCN.ID = VCN.CTR'R%5'
                    VCN.REC = ''
                    VCN.REC<1> = DESC
                  END
                  VC.REC<42> = VCN.ID
                  VCN.REC<2,-1> = VC.ID
                  WRITE VC.REC ON F.VC, VC.ID
                END
              END ELSE
                DONE2 = 1
                WRITE VCN.REC TO F.VCN, VCN.ID
              END
            REPEAT
          END
          TABLE.REC = ''
          TABLE.REC<1> = VCN.CTR + 1
          TABLE.ID = 'VCN*':CLIENT.ID
          WRITE TABLE.REC TO F.TABLES, TABLE.ID
        END ELSE
          DONE1 = 1
        END
        *** WRITE NEXT ID RECORD
      REPEAT

