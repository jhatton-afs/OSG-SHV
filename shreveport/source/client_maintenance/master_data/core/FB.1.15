*************************************************************************
* ROUTINE TO ENTER CLIENT/CARRIER STARTUP DATES FOR LOGISTICS
*
* FREIGHT BILLING PACKAGE
*
* AUTHOR: TAMI SEAGO
* DATE: SEPTEMBER 21, 1999
*
* THIS PROGRAM IS A PROPRIETARY PRODUCT OF AUTOMATED FREIGHT SYSTEMS
* AND MAY NOT BE USED, COPIED OR MODIFIED WITHOUT THE EXPRESS
* WRITTEN CONSENT OF SAME.
*
********************************************************************
*
* MODIFICATIONS:
* 072312 - RLM01 - Sysaid #3073 - Replace former employees' email addresses with those of current employees
*
************************************************************************
* Initialize variables
************************************************************************
$OPTIONS HEADER.EJECT
      ERRMSG=''
*
$OPTIONS EXEC.EQ.PERF
$OPTIONS INFO.ABORT
      PROMPT ''
      PROG.NAME="FB.1.15"
      PROG.DESC="Client/Carrier Startup Date"
      PACK.NAME="Freight Payment"
      UPL.VIN = 'FBBP' ; UPL.VIN<2> = 'FB.1.15' ; UPL.VOUT = ''        ; * NPR_UPL 04/23/2010
      CALL UPD.PROGRAM.LOG(UPL.VIN,UPL.VOUT)       ; * NPR_UPL 04/23/2010
* Open files
      OPEN '','CLIENTS' TO F.CLIENTS ELSE CALL OPEN.ABORT("CLIENTS",PROG.NAME)
      OPEN '','STARTUP.DATE' TO F.SU.DATE ELSE CALL OPEN.ABORT("STARTUP.DATE",PROG.NAME)
      OPEN '','CARRIERS' TO F.CARRIERS ELSE CALL OPEN.ABORT("CARRIERS",PROG.NAME)
      OPEN '','OPERATOR' TO F.OP ELSE CALL OPEN.ABORT("OPERATOR",PROG.NAME)      ; * GRB01	
      CALL GET.USER(USER)

************************************************************************
* Enter client number.
************************************************************************
100:  CALL AFS.SCR.HEAD(CO.ID,FILE.ID,'',PROG.NAME,PROG.DESC,PACK.NAME,CO.NAME,TIME.DATE,1)
      HELP="Enter client number. Word of name for search. X=Exit. EX=Exit."
      CALL GEN.IN(0,3,'Enter Client Number: #####','',Q,0,20,'','',2,-5,3,0,QI,HELP,0,23)
      QI=OCONV(QI,'MCU')
      BEGIN CASE
         CASE QI='' ! QI='EX' ! QI='X'
            STOP
         CASE NOT(QI MATCHES "0N")
            CALL SOUNDEX.DISPLAY(QI,'BCUST','SDX.CLIENTS,NAME',2,ITEM.LIST)
            CALL AFS.SCR.REFRESH(PROG.NAME,PROG.DESC,PACK.NAME,CO.NAME,TIME.DATE,1)
            BEGIN CASE
               CASE ITEM.LIST=''
                  GOTO 100
               CASE DCOUNT(ITEM.LIST<1>,@VM) GT 1
                  GOTO 100
               CASE ITEM.LIST NE ''
                  IF NUM(ITEM.LIST<1,1>) THEN DATA ITEM.LIST<1,1>
                  GOTO 100
            END CASE
         CASE NUM(QI)
            Q=QI'R%5'
         CASE 1
            GOTO 100
      END CASE
      CLIENT.ID=Q
      READ CLIENT.REC FROM F.CLIENTS,Q ELSE
         CALL ELINE('ERROR - Client ':CLIENT.ID:' not on file.')
         GOTO 100
      END
      CLIENT.NAME=CLIENT.REC<2>
      CRT @(21,3):CLIENT.ID:' ':CLIENT.NAME

************************************************************************
* Enter carrier number.
************************************************************************
200:  HELP="Enter carrier number. Word of name for search. [L]ist current [X]Back [EX]it."
      CALL GEN.IN(0,5,'Enter Carrier Number: #####','',Q,0,20,'','',2,-5,5,0,QI,HELP,0,23)
      QI=OCONV(QI,'MCU')
      BEGIN CASE
         CASE QI='EX'
            STOP
         CASE QI='X'
            CRT @(0,5):@(-3):
            GOTO 100
         CASE QI = 'L'
            EXECUTE 'SORT STARTUP.DATE CARRIER.NAME START.DATE WITH CLIENT =':CLIENT.ID:' BY CARRIER.NAME'
            INPUT NULL
            CALL AFS.SCR.REFRESH(PROG.NAME,PROG.DESC,PACK.NAME,CO.NAME,TIME.DATE,1)
            CRT @(0,3):'Enter Client Number: '
            CRT @(21,3):CLIENT.ID:' ':CLIENT.NAME
            GO 200
         CASE NOT(QI MATCHES "0N")
            CALL SOUNDEX.DISPLAY(QI,'BCUST','SDX.CARRIER,NAME',2,ITEM.LIST)
            CALL AFS.SCR.REFRESH(PROG.NAME,PROG.DESC,PACK.NAME,CO.NAME,TIME.DATE,1)
            BEGIN CASE
               CASE ITEM.LIST=''
                  GOTO 200
               CASE DCOUNT(ITEM.LIST<1>,@VM) GT 1
                  GOTO 200
               CASE ITEM.LIST NE ''
                  IF NUM(ITEM.LIST<1,1>) THEN DATA ITEM.LIST<1,1>
                  GOTO 200
            END CASE
         CASE NUM(QI)
            Q=QI'R%5'
         CASE 1
            GOTO 200
      END CASE
      CARRIER.ID=Q
      READ CARRIER.REC FROM F.CARRIERS,Q ELSE
         CALL ELINE('ERROR - Carrier ':CARRIER.ID:' not on file.')
         GOTO 200
      END
      CARRIER.NAME=CARRIER.REC<1>
      CRT @(22,5):CARRIER.ID:' ':CARRIER.NAME:@(-4)


************************************************************************
* Check for existing startup date
************************************************************************
      STARTUP.ID = CLIENT.ID'R%5':'*':CARRIER.ID'R%5'
      READ START.REC FROM F.SU.DATE,STARTUP.ID ELSE START.REC = ''
      START.DATE = START.REC<1>


************************************************************************
* Enter client/carrier startup date.
************************************************************************

300:  CRT @(0,7): 'Enter the start date for overcharges on this client and carrier.'
      IF START.DATE = '' THEN
         LN = '##/##/##'
      END ELSE
         LN = START.DATE'D2/'
      END
      HELP="Enter startup date for client/carrier. [X]Back [EX]it"
      CALL GEN.IN(0,8,LN,'DATE',Q,0,8,'','',0,-8,8,0,QI,HELP,0,23)
      QI=OCONV(QI,'MCU')
      BEGIN CASE
         CASE QI='X'
            CRT @(0,8):@(-3):
            GOTO 200
         CASE QI='EX'
            STOP
         CASE NUM(Q)
            READV CL.START.DATE FROM F.CLIENTS,CLIENT.ID,27 ELSE START.DATE = DATE()
            IF Q LT CL.START.DATE THEN
               CRT @(0,8):Q'D2/'
               CALL ELINE('CAN NOT HAVE A START DATE BEFORE CLIENTS START DATE')
               GO 300
            END
         CASE 1
            CALL ELINE('ERROR - Invalid Date.')
            GO 300
      END CASE
      START.DATE=Q
      CRT @(0,8):START.DATE'D2/'


************************************************************************
* Verify selection criteria is correct.
************************************************************************
400:  HELP="X=Change. EX=Exit. RETURN=Continue."
      CALL GEN.IN(0,22,'Enter selection please. --','',Q,0,2,'','',0,-2,22,0,QI,HELP,0,23)
      QI=OCONV(QI,'MCU')
      BEGIN CASE
         CASE QI='X'
            CRT @(0,22):@(-3):
            GOTO 300
         CASE QI='EX'
            STOP
         CASE QI=''
            NULL
         CASE 1
            CALL ELINE('Invalid entry. Must be X, EX, or RETURN.')
            GOTO 400
      END CASE
      READV OP.FLG FROM F.OP,USER,29 ELSE
         OP.FLG = "N"
      END
      IF OP.FLG # "Y" THEN
         ****   BEGIN CASE

         ****       CASE USER='DAVID' ; NULL
         ****         CASE USER='MARY' ; NULL
         ****    CASE USER = 'WPARDUE'; *GRB01
         ****     CASE 1
         EMAIL.OUT=''
         EMAIL.OUT<1,1>='BRose@afs.net'  ; * RLM01
         EMAIL.OUT<1,2>='BFranklin@afs.net'        ; * RLM01
         EMAIL.OUT<1,3>='SMatthews@afs.net'
         EMAIL.OUT<1,4>='SMatthews@afs.net'
         EMAIL.OUT<1,6>='MMcKay@afs.net'           ; * CC: Email addresses

         IF USER='BFRANKLIN' THEN EMAIL.OUT<1>='BFranklin@afs.net'     ; * RLM01
         EMAIL.OUT<3>=''                 ; * BCC: Email addresses
         EMAIL.OUT<4>='FB.1.15@afs.net'  ; * From Email address
         EMAIL.OUT<5>=USER:' attempted to extend an Overcharge start date'
         EMAIL.OUT<6>=USER:' attempted to extend the Overcharge start date for the Following:'
         EMAIL.OUT<6,-1>="Client: ":CLIENT.ID:"-":CLIENT.NAME
         EMAIL.OUT<6,-1>="Carrier: ":CARRIER.ID:"-":CARRIER.NAME
         EMAIL.OUT<6,-1>="Attempted Extension Date: ":START.DATE'D4/'
         IF START.REC<1>#'' THEN
            EMAIL.OUT<6,-1>="Current Overcharge start Date is: ":START.REC<1>'D4/'
         END
         EMAIL.OUT<6,-1>="The Attempt was detected and the Update was Denied,"
         EMAIL.OUT<6,-1>="No changes were allowed"
         CALL SENDMAIL(EMAIL.OUT,VOUT)
         CALL ELINE('You are not authorized to modify start dates!')
         GO 300
      END
      ****   END CASE

************************************************************************
* Save start Date
************************************************************************
      IF START.DATE # START.REC<1> THEN
         START.REC<5,-1> = START.REC<1>
         START.REC<1> = START.DATE
         START.REC<2,-1> = USER
         START.REC<3,-1> = DATE()
         START.REC<4,-1> = TIME()
         WRITE START.REC ON F.SU.DATE,STARTUP.ID
      END
      GO 100
