      ****************************************
      * Program name : UPD.QV.CLIENT.INFO
      * Author       : Mike Kerbow
      * Date written : Dec 28, 2013
      * Library      : FBBP
      * COPYRIGHT (C) 1998 - AFS LOGISTICS LLC - ALL RIGHTS RESERVED.
      *
      *
* Update QV folder with client information
      * Last updated by afs (gbino) at 08:03:35 on 03/29/2016.
      *
      * Start modification history
      * Mod nn  mm/dd/yy, init, req #, description
      * 022114 - JMK01 - SysAid#8688 - FP Clients Automated Export
      * End modification history
      *
      ****************************************
      COMMON PROGPARAMS(24)
      EQU PROGFROM TO PROGPARAMS(1)
      EQU USER TO PROGPARAMS(2)
      EQU PROCDATE TO PROGPARAMS(3)
      EQU CH.NUM TO PROGPARAMS(4)
*
* INITIALIZE VARIABLES
*
      PROG.NAME='UPD.QV.CLIENT.INFO'
      TB=CHAR(9)
      EXPORT.REC=''
      CALL GET.USER(USER)
* Begin GRB01
      VIN1=""
      VIN1<1> = PROG.NAME
      VIN1<2> = USER
      VIN1<3> = SYSTEM(18)
      VIN1<4> = "S"
      CALL PROGTRACK(VIN1,VOUT1)
* End GRB01
*
* OPEN FILES
*
      OPEN '','BCTRL' TO F.BCTRL ELSE
         CALL OPEN.ABORT('BCTRL',PROG.NAME)
      END
      OPEN '','CLIENTS' TO F.CLIENTS ELSE
         CALL OPEN.ABORT('CLIENTS',PROG.NAME)
      END
      OPEN '','BILLDATA.QV.CLIENTS' TO F.QV ELSE
         CALL OPEN.ABORT('BILLDATA.QV.CLIENTS',PROG.NAME)
      END
      OPEN '','AR.SLSMAN.1000.1' TO F.SLSMAN ELSE
         CALL OPEN.ABORT('AR.SLSMAN.1000.1',PROG.NAME)
      END
      OPEN '','FB.COMPANY' TO F.FP.COMPANY ELSE    ; * JMK01
         CALL OPEN.ABORT("FB.COMPANY",PROG.NAME)   ; * JMK01
      END                                ; * JMK01
      OPEN '','AR.CUST.1000.1' TO F.CUST ELSE      ; * JMK01
         CALL OPEN.ABORT('AR.CUST.1000.1',PROG.NAME)         ; * JMK01
      END                                ; * JMK01
*
* START EXPORT CYCLE
*
      CLIENTS.STMT='SSELECT CLIENTS'
      EXECUTE CLIENTS.STMT RTNLIST CLIENTS.LIST CAPTURING CLIENTS.RESULTS
      CLIENTS.TCNT=@SELECTED
      CLIENTS.CNT=0
      CLIENTS.EOF=0
      CLIENTS.PER=''
      LOOP
         READNEXT CLIENTS.ID FROM CLIENTS.LIST ELSE CLIENTS.EOF=1
      UNTIL CLIENTS.EOF DO
         CLIENTS.CNT+=1
         CALL PROG.BAR(22,CLIENTS.CNT,CLIENTS.TCNT,CLIENTS.PER)
         QV.SEND=1
         IF CLIENTS.ID[1,2]='99' THEN QV.SEND=0
         IF QV.SEND THEN
            READ CLIENTS.REC FROM F.CLIENTS,CLIENTS.ID THEN
               CUST.ID=CLIENTS.ID'R%12'  ; * JMK01
               READ CUST.REC FROM F.CUST,CUST.ID ELSE CUST.REC=''      ; * JMK01
               CLIENTS.NAME=CLIENTS.REC<2>
               CLIENTS.CITY=CLIENTS.REC<4>         ; * JMK01
               CLIENTS.STATE=CLIENTS.REC<5>        ; * JMK01
               CLIENTS.ACCT.OWNER=CLIENTS.REC<302>
               IF CLIENTS.ACCT.OWNER='' THEN
                  CLIENTS.ACCT.OWNER=CLIENTS.REC<114>
               END
               READ ACCT.OWNER.REC FROM F.SLSMAN,CLIENTS.ACCT.OWNER ELSE ACCT.OWNER.REC=''
               CLIENTS.ACCT.OWNER.NAME=ACCT.OWNER.REC<1>
               EST.BY=CLIENTS.REC<114>   ; * JMK01
               READV EST.BY.NAME FROM F.SLSMAN,EST.BY,1 ELSE EST.BY.NAME = ''    ; * JMK01
               SVC.BY=CLIENTS.REC<115>   ; * JMK01
               READV SVC.BY.NAME FROM F.SLSMAN,SVC.BY,1 ELSE SVC.BY.NAME = ''    ; * JMK01
               READV REG.MGR.ID FROM F.SLSMAN,SVC.BY,10 ELSE REG.MGR.ID=""       ; * JMK01
               READV REG.MGR.NAME FROM F.SLSMAN,REG.MGR.ID,1 ELSE REG.MGR.NAME=""          ; * JMK01
               FP.CO = CLIENTS.REC<92> 'R%5'       ; * JMK01
               READ FP.CO.REC FROM F.FP.COMPANY,FP.CO ELSE FP.CO.REC=''          ; * JMK01
               FP.CO.NAME=FP.CO.REC<15>  ; * JMK01
               CLIENTS.CORP.NAME=CLIENTS.REC<246>  ; * JMK01
               IF CLIENTS.CORP.NAME='' THEN CLIENTS.CORP.NAME=CUST.REC<1>        ; * JMK01
               CLIENTS.STATUS=CLIENTS.REC<30>      ; * JMK01
               CLIENTS.START.DATE=''     ; * JMK01
               IF CLIENTS.REC<27>#'' THEN          ; * JMK01
                  CLIENTS.START.DATE=OCONV(CLIENTS.REC<27>,'D4/')      ; * JMK01
               END                       ; * JMK01
               CLIENTS.RANK=CLIENTS.REC<240>       ; * JMK01

               EXPORT.LINE=''
               EXPORT.LINE<1, 1>=CLIENTS.ID
               EXPORT.LINE<1, 2>=CLIENTS.NAME
               EXPORT.LINE<1, 3>=CLIENTS.ACCT.OWNER
               EXPORT.LINE<1, 4>=CLIENTS.ACCT.OWNER.NAME
               EXPORT.LINE<1, 5>=EST.BY  ; * JMK01
               EXPORT.LINE<1, 6>=EST.BY.NAME       ; * JMK01
               EXPORT.LINE<1, 7>=SVC.BY  ; * JMK01
               EXPORT.LINE<1, 8>=SVC.BY.NAME       ; * JMK01
               EXPORT.LINE<1, 9>=REG.MGR.ID        ; * JMK01
               EXPORT.LINE<1,10>=REG.MGR.NAME      ; * JMK01
               EXPORT.LINE<1,11>=CLIENTS.CITY      ; * JMK01
               EXPORT.LINE<1,12>=CLIENTS.STATE     ; * JMK01
               EXPORT.LINE<1,13>=FP.CO.NAME        ; * JMK01
               EXPORT.LINE<1,14>=CLIENTS.CORP.NAME           ; * JMK01
               EXPORT.LINE<1,15>=CLIENTS.STATUS    ; * JMK01
               EXPORT.LINE<1,16>=CLIENTS.START.DATE          ; * JMK01
               EXPORT.LINE<1,17>=CLIENTS.RANK      ; * JMK01
               EXPORT.LINE=CHANGE(EXPORT.LINE,@VM,TB)
               EXPORT.REC<-1>=EXPORT.LINE
            END
         END
      REPEAT
      IF EXPORT.REC#'' THEN
         EXPORT.HDR=''
         EXPORT.HDR<1, 1>='Client#'
         EXPORT.HDR<1, 2>='Name'
         EXPORT.HDR<1, 3>='Account Owner'
         EXPORT.HDR<1, 4>='Account Owner Name'
         EXPORT.HDR<1, 5>='Established By'         ; * JMK01
         EXPORT.HDR<1, 6>='Established By Name'    ; * JMK01
         EXPORT.HDR<1, 7>='Serviced By'  ; * JMK01
         EXPORT.HDR<1, 8>='Serviced By Name'       ; * JMK01
         EXPORT.HDR<1, 9>='Regional Manager'       ; * JMK01
         EXPORT.HDR<1,10>='Regional Manager Name'  ; * JMK01
         EXPORT.HDR<1,11>='City'         ; * JMK01
         EXPORT.HDR<1,12>='State'        ; * JMK01
         EXPORT.HDR<1,13>='Company'      ; * JMK01
         EXPORT.HDR<1,14>='Corporate Name'         ; * JMK01
         EXPORT.HDR<1,15>='Status'       ; * JMK01
         EXPORT.HDR<1,16>='Start Date'   ; * JMK01
         EXPORT.HDR<1,17>='AFS Ranking'  ; * JMK01
         EXPORT.HDR=CHANGE(EXPORT.HDR,@VM,TB)
         INS EXPORT.HDR BEFORE EXPORT.REC<1>
         WRITE EXPORT.REC ON F.QV,'FP_CLIENTS.XLS'

         EMAIL.OUT=''
         EMAIL.OUT<1,-1>='RCamper@afs.net'
         EMAIL.OUT<3,-1>='MKerbow@afs.net'
         EMAIL.OUT<5,1>= 'QlikView Client Information Update on ':OCONV(DATE(),'D4/'):' at ':OCONV(TIME(),'MTHS')
         READV USER.NAME FROM F.BCTRL, USER,13 ELSE USER.NAME = ""
         IF USER.NAME NE "" THEN USER.STRING = USER:" - ":USER.NAME ELSE USER.STRING = USER
         EMAIL.OUT<6>=USER.STRING:' triggered an update to the Account Owner information for QlikView.'
         EMAIL.OUT<7>=1                  ; * Auto format (add carriage returns to end of text)
         VOUT=''
         CALL SENDMAIL(EMAIL.OUT,VOUT)
      END
9999: * END OF PROGRAM
* Begin GRB01
      VIN1=''
      VIN1<1> = PROG.NAME
      VIN1<2> = USER
      VIN1<3> = SYSTEM(18)
      VIN1<4> = "E"
      CALL PROGTRACK(VIN1,VOUT1)
* End GRB01
   END
