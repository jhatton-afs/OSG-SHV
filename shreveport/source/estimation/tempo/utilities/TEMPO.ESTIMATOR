****************************************************
* Program name : TEMPO.ESTIMATOR
* Author       : Nicole Meddaugh
* Date written : 09-28-2021
* Library      : FBBP
* COPYRIGHT (C) 2016 - AFS LOGISTICS LLC - ALL RIGHTS RESERVED.
*
* Last updated by afs (AFSLOGISTICS\nmeddaugh) at 15:43:04 on 09/28/2021.
*
* Start modification history
* 09/28/21 NKH00 ACCU-1308 New Program
* End modification history
*
*******************************************************
*
*++++++++++++++TO CREATE COMPARISON FILES +++++++++++++
*
* DELETE BILLDATA EST-S AND EST-T FOR COMPARE
*
* THE WRITE AT THE BOTTOM OF THIS PROGRAM WILL CREATE
* THE FILES IN BILLDATA TO USE FOR THE COMPARE
*
* UNCOMMENT CODE IN INET.FB.ESTIMATOR.V3 AND INET.FB.ESTIMATOR.S
* TO WRITE TO NKM.TEMP - THIS IS LOCATED ALMOST AT THE
* END OF THE PROGRAM.
*
*++++++++++++++++++++++++++++++++++++++++++++++++++++++
*
      OPEN '','INET.FBESTIMATOR.ACTIVITY' TO F.ACTIVITY ELSE
         CALL OPEN.ABORT("INET.FBESTIMATOR.ACTIVITY","TEMPO.ESTIMATOR")
      END

      OPEN '','CLIENTS' TO F.CLIENTS ELSE
         CALL OPEN.ABORT("CLIENTS","TEMPO.ESTIMATOR")
      END
      
      OPEN 'NKM.TEMP' TO NKM.TEMP ELSE STOP 201, 'NKM.TEMP'      

      CALL GET.USER(USER)

* Get Input

      GOSUB CHECK.FILES
      GOSUB GET.TEST
      GOSUB GET.CLIENT
      GOSUB MAIN.LOGIC
      
      STOP
      
CHECK.FILES:
      EXECUTE 'CLEAR-FILE NKM.TEMP'
      NKM.KEY = 'SEQ'
      NKM.REC<1> = 1
      WRITE NKM.REC ON NKM.TEMP, NKM.KEY
      NKM.REC = ''

      EXECUTE 'CLS'

      CRT 'Did you delete EST-S/EST-T from Billdata? ' ; INPUT ANS
      IF ANS = '' THEN STOP
      IF ANS # 'Y' THEN 
         CALL ELINE('You must delete the output file from Billdata')
         STOP
      END
      
      SAMPLE = 'A'
      LOOP
      UNTIL NUM(SAMPLE)
         CRT 'Enter Sample Size (Number of Activity Records to use)' ; INPUT SAMPLE
         IF SAMPLE = '' THEN STOP
         IF NOT(NUM(SAMPLE)) THEN
            CALL ELINE('Sample Size must be numeric')
         END
      REPEAT
      
      RETURN

GET.TEST:
      ANS = ''
      LOOP
      UNTIL ANS = 'T' OR ANS = 'S'
         CRT 'Are you running data for [T]empo or [S]hreveport: ' ; INPUT ANS
         IF ANS = '' THEN STOP
         IF ANS # 'T' AND ANS # 'S' THEN 
            CALL ELINE('You must select either T or S')
         END
      REPEAT
      
      RETURN

GET.CLIENT:
      GOOD.CLIENT = 0
      LOOP
      UNTIL GOOD.CLIENT      
         CRT 'Please enter the client ID: ' ; INPUT CLIENT.ID
         IF CLIENT.ID = '' THEN STOP
         CLIENT.ID=CLIENT.ID'R%5'      
         READ CLIENT.REC FROM F.CLIENTS, CLIENT.ID THEN
            GOOD.CLIENT = 1
         END ELSE 
            CALL ELINE(CLIENT.ID:' is not a vlid client')
         END
      REPEAT
      
      RETURN

MAIN.LOGIC:

      IF ANS = 'T' THEN
         RECORD.NAME = 'EST-T'
      END ELSE
         RECORD.NAME = 'EST-S'
      END

      OPENSEQ 'BILLDATA.':USER, RECORD.NAME TO OUTSEQ.FILE ELSE
         CREATE OUTSEQ.FILE ELSE
         END
      END

*      CMD = 'SELECT INET.FBESTIMATOR.ACTIVITY WITH DT > "09-01-17" AND WITH USER.ID > "" SAMPLE 1000'   ;* Note do not sample 10000 - This slows down production!!!!!
*CMD = 'SELECT INET.FBESTIMATOR.ACTIVITY CISCO**18184*25084.787'
      CMD = 'SELECT INET.FBESTIMATOR.ACTIVITY WITH CLIENT = "':CLIENT.ID:'" SAMPLE ':SAMPLE
      EXECUTE CMD

      START.TIME = TIME()
      EOF = 0
      LOOP
         READNEXT ID ELSE EOF = 1
      UNTIL EOF
         READ A.REC FROM F.ACTIVITY, ID THEN
            SHIP.DATE = OCONV(A.REC<4>,'D')
            DATA A.REC<2>,A.REC<3>,SHIP.DATE,A.REC<5>,A.REC<6>,A.REC<7>,A.REC<8,1>,A.REC<9,1>,'','','','',''
            IF ANS = 'T' THEN
               EXECUTE 'INET.FB.ESTIMATOR.V3'
            END ELSE
               EXECUTE 'INET.FB.ESTIMATOR.S'
            END
         END
      REPEAT
      END.TIME = TIME()

      DISPLAY 'START = ':OCONV(START.TIME,'MTS')
      DISPLAY 'END = ':OCONV(END.TIME,'MTS')

      TIME.DIF = END.TIME - START.TIME
      DISPLAY 'DIFFERENCE = ':TIME.DIF
*
      CMD = 'SSELECT NKM.TEMP'
      EXECUTE CMD
*
      ROW = ''
      EOF=0
      LOOP
         READNEXT ID ELSE EOF = 1
      UNTIL EOF
         READ NKM.REC FROM NKM.TEMP, ID THEN
            IF ID # 'SEQ' THEN
               FOR XX = 1 TO 53
                  ROW<1,XX> = NKM.REC<XX>
               NEXT XX
               WRITESEQ ROW TO OUTSEQ.FILE ELSE NULL
            END
         END
      REPEAT

      RETURN
*
