$OPTIONS HEADER.EJECT
      ERRMSG=''
*
$OPTIONS EXEC.EQ.PERF
$OPTIONS INFO.ABORT
*
* PROGRAM UPS.DUPS
* PROGRAM TO CREATE OR MODIFY CLIENT MASTER
*
* FREIGHT BILLING PACKAGE
*
* AUTHOR: BRIAN M. QUEEN
* DATE: NOVEMBER 13, 1990

* PICK COMPILER INFORMATION
* ~~~~~~~~~~~~~~~~~~~~~~~~~
*  ~PICK-FILE: FBBP
*  ~PICK-RECORD: UPS.DUPS
*  ~ PICK-FILE: BP
*  ~ PICK-RECORD: JIM
*  ~COMPILE-REC: YES
*  ~AUTO-ANSWER: NO
*  ~RUN-REC: NO
*
* Initialize variables
*
      PROMPT''
      PER.1=0
      ADD.FLAG=0
      PROG.NAME='UPS.DUPS'
      PROG.DESC='UPS DUPLICATES'
      PACK.NAME='FREIGHT BILLING'
      TODAY=DATE()
       UPL.VIN = 'FBBP' ;  UPL.VIN<2> = 'UPS.DUPS' ; UPL.VOUT = ''                    ;* NPR_UPL 04/23/2010
       CALL UPD.PROGRAM.LOG(UPL.VIN,UPL.VOUT)                                                           ;* NPR_UPL 04/23/2010
      OTODAY=OCONV(TODAY,'D2/')
      ODATE=OTODAY[7,4]:OTODAY[1,2]:OTODAY[4,2]
      EQU TB TO CHAR(9)
      CALL GET.USER(USER)
      CALL CHANNEL(CH.NUM)
      CALL AFS.SCR.HEAD(CO.ID,FILE.ID,PERIOD,PROG.NAME,PROG.DESC,PACK.NAME,CO.NAME,TIME.DATE,1)
      HELP="Enter user folder. EX=Exit"
      CALL GEN.IN(0,22,'Enter Folder Name ##########','',Q,0,20,'','',20,-10,22,1,QI,HELP,0,23)
      QI=OCONV(QI,'MCU')
      IF QI='EX' THEN GOTO THE.END
      IF QI='' THEN GOTO THE.END
      FOLDER=Q
      OPEN '','VOC' TO F.VOC ELSE
         CALL ELINE('Unable to open VOC file - Call IS Department.')
         GOTO THE.END
      END
      READ BILLDATA.REC FROM F.VOC,'BILLDATA' ELSE
         CALL ELINE('Unable to open VOC BILLDATA - Call IS Department.')
         GOTO THE.END
      END
      BILLDATA.REC<2>:='\':USER
      USR.FNAME='BILLDATA.':USER
      WRITE BILLDATA.REC ON F.VOC,USR.FNAME ELSE
         CALL ELINE('Your user name ':USER:' is not in the BILLDATA folder - Call IS Department.')
         GOTO THE.END
      END
      BILLDATA.REC<2>:='\':FOLDER
      SRC.FNAME='BILLDATA.':USER:'.':FOLDER
      WRITE BILLDATA.REC ON F.VOC,SRC.FNAME ELSE
         CALL ELINE('Your folder name ':FOLDER:' is not in the USER folder - Call IS Department.')
         GOTO THE.END
      END
      OPEN '',SRC.FNAME TO SRC.FILE ELSE
         CALL OPEN.ABORT(SRC.FNAME,'UPS.DUPS')
         GOTO THE.END
      END
      WK.FNAME='FBWORK':CH.NUM
      OPEN '',WK.FNAME TO WK.FILE ELSE
         CALL OPEN.ABORT(WK.FNAME,'UPS.DUPS')
         GOTO THE.END
      END
      OPEN '','BILLDATA.EBILL.HIST' TO HDR.FILE ELSE
         CALL OPEN.ABORT('BILLDATA.EBILL.HIST','UPS.DUPS')
         GOTO THE.END
      END
      CMND='SELECT ':SRC.FNAME:' # "UPS.DUPS_]"'
      CRT @(0,10):@(-3):'Processing the Source Files...'
      EXECUTE CMND
      SRC.ITMS=@SELECTED
      EOF=0
      CNT=0
      LOOP
         READNEXT SRC.ID ELSE EOF=1
      UNTIL EOF DO
         CNT+=1
         CALL PROG.BAR(20,CNT,SRC.ITMS,PER.1)
         READ SRC.REC FROM SRC.FILE,SRC.ID THEN
            SRC.REC=CHANGE(SRC.REC,TB,@VM)
            NATT=DCOUNT(SRC.REC,@AM)
            FOR ATT=1 TO NATT
               SRC.LINE=SRC.REC<ATT>
               TRACK.NO=SRC.LINE<1,7>
               SVC.CD=SRC.LINE<1,20>
               WK.ID='UPS*':TRACK.NO:'*':SVC.CD
               READU WK.REC FROM WK.FILE,WK.ID ELSE WK.REC=''
               WK.REC<-1>=SRC.LINE
               WRITE WK.REC ON WK.FILE,WK.ID
               RELEASE WK.FILE,WK.ID
            NEXT ATT
         END
      REPEAT
      CMND='SELECT ':WK.FNAME:' = "UPS*]"'
      CRT @(0,12):@(-3):'Processing the Work Files...'
      EXECUTE CMND
      WK.ITMS=@SELECTED
      TGT.REC=''
      TGT.ID='UPS.DUPS_':ODATE:'.XLS'
      EOF=0
      CNT=0
      LOOP
         READNEXT WK.ID ELSE EOF=1
      UNTIL EOF DO
         CNT+=1
         CALL PROG.BAR(20,CNT,WK.ITMS,PER.1)
         READ WK.REC FROM WK.FILE,WK.ID THEN
            NATT=DCOUNT(WK.REC,@AM)
            IF NATT>1 THEN
               TGT.REC<-1>=WK.REC
            END
            DELETE WK.FILE,WK.ID
         END
      REPEAT
      IF TGT.REC#'' THEN
         READ TGT.HDR FROM HDR.FILE,'EBILLHEADER.TXT' ELSE TGT.HDR=''
         INS TGT.HDR BEFORE TGT.REC<1>
         TGT.REC=CHANGE(TGT.REC,@VM,TB)
         WRITE TGT.REC ON SRC.FILE,TGT.ID
         CALL ELINE("Your compiled Spreadsheet is ":TGT.ID:" in folder ":FOLDER:".")
      END
THE.END:*
   END
