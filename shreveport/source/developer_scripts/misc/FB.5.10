* PROGRAM: FB.5.10
* PROGRAM TO PROCESS OUTSTANDING CHECKS REPORT
*
* PACKAGE: FREIGHT BILLING
*
* AUTHOR: BRIAN M. QUEEN
* DATE: MAY 1, 1992
*
************************************************************************
*
* 110104 - JMK01 - ADD BANK INFO TO REPORT HEADING
* Last updated by AFS (valerie) at 14:25:45 on 06/01/1994.
*   UPDATED FOR UNIVERSE AND CHANGED REPORT TO PRINT A CHECK AS
*   OUTSTANDING IF VOID DATE IS GREATER THAN REPORT DATE.
*
* 050806 - JCW01 - CHANGES TO '@SELECTED', 'CLEARSELECT', AND 'STMT<X>'
* 061906 - JMK02 - ADD DOWNLOAD OPTIONS FOR REPORT
* 090806 - JCW02 - USE NEW FB.CHECKS FILES
* 050508 - JMK03 - T081193 - Merrill Lynch Bank Recon ; problem with cleared ACH
* 082708 - NPR01 - T081911 - Added ACH Batch number and Date to download file.
* 091808 - NPR02 - T082076 - Added ACH Total to printed report
************************************************************************
$INCLUDE PROG.ADMIN INC_OPTIONS
*
* Initialize variables
*
      PROMPT''
      PROG.NAME='FB.5.10'
      PROG.DESC='Outstanding Checks Report'
      PACK.NAME='Freight Billing'
      GRAND.TOT=''
      ACH.TOT=''                         ; * JMK03
      CALL GET.USER(USER)                ; * JMK02
*
* Open files
*
*     OPEN '','FB.CHECKS' TO F.CHECKS ELSE          ; * JCW02
*        CALL OPEN.ABORT("FB.CHECKS",PROG.NAME)     ; * JCW02
*     END                                           ; * JCW02
      OPEN '','FB.CHECKS.HDR' TO F.CHECKS ELSE      ; * JCW02
         CALL OPEN.ABORT("FB.CHECKS.HDR",PROG.NAME) ; * JCW02
      END                                           ; * JCW02
      OPEN '','CARRIERS' TO F.CARRIER ELSE
         CALL OPEN.ABORT("CARRIERS",PROG.NAME)
      END
      OPEN '','FB.BANKS' TO F.BANKS ELSE
         CALL OPEN.ABORT("FB.BANKS",PROG.NAME)
      END
      OPEN '','VOC' TO F.VOC ELSE        ; * JMK02
         CALL ELINE('Unable to open VOC file - Call IS Department.')   ; * JMK02
         STOP                            ; * JMK02
      END                                ; * JMK02
      READ BILLDATA.REC FROM F.VOC,'BILLDATA' ELSE           ; * JMK02
         CALL ELINE('Unable to open VOC BILLDATA - Call IS Department.')         ; * JMK02
         STOP                            ; * JMK02
      END                                ; * JMK02
      BILLDATA.REC<2>:='\':USER          ; * JMK02
      WRITE BILLDATA.REC ON F.VOC,'BILLDATA.':USER ELSE      ; * JMK02
         CALL ELINE('Your user name ':USER:' is not in the BILLDATA folder - Call IS Department.')   ; * JMK02
         STOP                            ; * JMK02
      END                                ; * JMK02
      OPEN '','BILLDATA.':USER TO F.BILLDATA ELSE  ; * JMK02
         CALL ELINE('Unable to open BILLDATA.':USER:'.')     ; * JMK02
         STOP                            ; * JMK02
      END                                ; * JMK02
*
* Display screen heading
*
50:   CALL AFS.SCR.HEAD('','','',PROG.NAME,PROG.DESC,PACK.NAME,CO.NAME,TIME.DATE,1)
*
* Get ending date
*
100:  HELP="Enter ending date. ''=Accept. 'X'=Exit."
      CALL GEN.IN(0,3,'Enter ending date........':DATE()'D2/','DATE',END.DATE,0,8,'','',1,-8,3,0,QI,HELP,0,23)
      QI=OCONV(QI,'MCU')
      IF QI='X' ! QI='EX' THEN STOP
      CRT @(25,3):END.DATE'D2/'
*
* Get bank account
*
200:  HELP="Enter bank ID. F=Freight Payment. B=Brokerage. V=Vouchers. X=Back, or EX=Exit"
      CRT @(0,5):@(-4)
      CALL GEN.IN(0,5,'Enter bank selection.....- ','',Q,0,2,'','',1,-2,5,0,QI,HELP,0,23)
      QI=OCONV(QI,'MCU')
      BEGIN CASE
         CASE QI='X'
            CRT @(0,5):@(-4)
            GOTO 100
         CASE QI='EX'
            STOP
         CASE ALPHA(QI)
            READV BANK.DESC FROM F.BANKS,"BANK*":QI,1 ELSE
               CALL ELINE("Invalid bank - ":QI)
               GOTO 200
            END
            BANK=QI
         CASE 1
            GOTO 200
      END CASE
      CRT @(25,5):BANK'L#2':BANK.DESC'L#20'
*
* Get display options
*
300:* HELP="S=Screen. P=Printer. X=Back, or EX=Exit"         ; * JMK02
      HELP="S=Screen. P=Printer. D=Download, X=Back, or EX=Exit"       ; * JMK02
      SELECTION=''
      CALL GEN.IN(0,7,'Enter display selection. P ','',Q,0,2,'','',1,-2,7,0,QI,HELP,0,23)
      QI=OCONV(QI,'MCU')
      BEGIN CASE
         CASE QI='X'
            CRT @(0,7):@(-4)
            GOTO 200
         CASE QI='EX'
            STOP
         CASE QI='P' ! QI=''
            SELECTION='P'
         CASE QI='S'
            SELECTION='S'
         CASE QI='D'                     ; * JMK02
            SELECTION='D'                ; * JMK02
         CASE 1
            GOTO 300
      END CASE
*
* Display processing message
*
      TEXT="Processing Outstanding Checks Report - Please Wait."
      CALL CENTER(TEXT,80)
      CRT @(0,12):TEXT
      IF SELECTION='S' THEN EXECUTE '132'
      IF SELECTION='D' THEN EXP.REC=''   ; * JMK02
*
* Select checks with errors
*
*     PREC='SSELECT FB.CHECKS WITH 6 = "" OR 6 = "0" AND WITH 2 LE "':END.DATE'D2/':'" AND WITH BANK.ID = "':BANK:'"'
      PREC='SELECT FB.CHECKS.HDR WITH 6 = "" OR 6 = "0"'  ; * JCW02
      EXECUTE PREC RTNLIST CAPTURING OUTPUT               ; * JCW02
      IF @SELECTED < 1 THEN              ; * JCW01
         CALL ELINE("No outstanding checks on file.")
         EXECUTE '80'
         GOTO 50
      END
      PREC = 'SSELECT FB.CHECKS.HDR WITH 2 LE "':END.DATE'D2/':'" AND WITH BANK.ID = "':BANK:'"' ; * JCW02
*     EXECUTE PREC PASSLIST CAPTURING OUTPUT          ; * JCW02
      EXECUTE PREC PASSLIST RTNLIST CAPTURING OUTPUT  ; * JCW02
      NUM.ITMS = @SELECTED
      IF @SELECTED < 1 THEN              ; * JCW01
         CALL ELINE("No outstanding checks on file.")
         EXECUTE '80'
         GOTO 50
      END
      COUNTER=0
*
* Create report heading
*
      ODATE=END.DATE'D2/'
      HDG1=''                            ; * JMK01
      HDG2=''                            ; * JMK01
      BEGIN CASE                         ; * JMK01
         CASE BANK='F' OR BANK='V' OR BANK='B'     ; * JMK01
            HDG1='AFS Logistics, LLC'    ; * JMK01
         CASE 1                          ; * JMK01
            HDG1=BANK.DESC               ; * JMK01
            HDG2='A Division of AFS Logistics, LLC'          ; * JMK01
      END CASE                           ; * JMK01
      IF SELECTION='D' THEN              ; * JMK02
         EXP.REC<1>=HDG1:' ':HDG2:' ':PROG.DESC:' ':ODATE:' '          ; * JMK02
      END                                ; * JMK02
*     CALL AFS.PAGE.HEAD(PROG.NAME,'AFS Logistics, LLC','','Outstanding Checks Report',ODATE,132,HED) ; * JMK01
      CALL AFS.PAGE.HEAD(PROG.NAME,HDG1,HDG2,PROG.DESC,ODATE,132,HED)  ; * JMK01
      HED := " 'L'Bank................ Check# Date.... Carrier Name..................    Check Amount"
*      IF SELECTION='P' THEN PRINTER ON
      IF SELECTION='P' THEN
         VIN=''
         VIN<3>='R6'
         CALL LASER.CODE(VIN,VOUT)
         IF VOUT<1>#'' THEN
            CALL ELINE(VOUT<1>)
            STOP
         END
         IF VOUT<3>#'' THEN EXECUTE VOUT<3>
         PRINTER ON
         IF VOUT<2>#'' THEN PRINT VOUT<2>:
      END
      IF SELECTION='D' THEN              ; * JMK02
         EXP.REC<3,1>='Bank'             ; * JMK02
         EXP.REC<3,2>='Check#'           ; * JMK02
         EXP.REC<3,3>='Check Date'       ; * JMK02
         EXP.REC<3,4>='Carrier Name'     ; * JMK02
         EXP.REC<3,5>='Check Amount'     ; * JMK02
         EXP.REC<3,6>='ACH Batch'        ; * NPR01
         EXP.REC<3,7>='ACH Date'         ; * NPR01
      END ELSE                           ; * JMK02
         HEADING HED
      END                                ; * JMK02
*
      LOOP
         READNEXT ID ELSE ID=''
      UNTIL ID='' DO
         COUNTER=COUNTER+1
         IF SELECTION NE 'S' THEN
            CALL PROG.BAR(22,COUNTER,NUM.ITMS,PER)
         END
         READ CHECK.REC FROM F.CHECKS,ID THEN
            CHECK.NUM=CHECK.REC<1>'R%6'
            CHECK.DATE=CHECK.REC<2>
            CARRIER=CHECK.REC<3>
            READV CARRIER.NAME FROM F.CARRIER,CARRIER,1 ELSE
               CARRIER.NAME='~~~ Unknown ~~~'
            END
            BEGIN CASE
               CASE CHECK.REC<4> NE 0 AND CHECK.REC<6> = ''
                  CHECK.AMT=CHECK.REC<4>'R0'
                  GRAND.TOT=GRAND.TOT+CHECK.AMT
                  ACH.BATCH=CHECK.REC<27>          ; * JMK03
                  ACH.DATE = OCONV(FIELD(CHECK.REC<26>,"*",1),"D2/")  ; * NPR01
                  LOCATE ACH.BATCH IN ACH.TOT<1> BY 'AR' SETTING ACHPOS ELSE     ; * JMK03
                     INS ACH.BATCH BEFORE ACH.TOT<1,ACHPOS>  ; * JMK03
                     INS 0 BEFORE ACH.TOT<2,ACHPOS>          ; * JMK03
                  END                    ; * JMK03
                  ACH.TOT<2,ACHPOS>+=CHECK.AMT     ; * JMK03
                  IF SELECTION='D' THEN  ; * JMK02
                     EXP.LINE=''
                     EXP.LINE<1,1>=BANK.DESC       ; * JMK02
                     EXP.LINE<1,2>=CHECK.NUM       ; * JMK02
                     EXP.LINE<1,3>=CHECK.DATE'D2/'           ; * JMK02
                     EXP.LINE<1,4>=CARRIER.NAME    ; * JMK02
                     EXP.LINE<1,5>=CHECK.AMT'R26,'           ; * JMK02
                     EXP.LINE<1,6>=ACH.BATCH       ; * NPR01
                     EXP.LINE<1,7>=ACH.DATE        ; * NPR01
                     EXP.REC<-1>=EXP.LINE          ; * JMK02
                  END ELSE               ; * JMK02
                     PRINT BANK.DESC'L#20':' ':CHECK.NUM:' ':CHECK.DATE'D2/':
                     PRINT ' ':CARRIER.NAME'L#30':CHECK.AMT'R26,#16'
                  END                    ; * JMK02
               CASE CHECK.REC<9> > END.DATE AND CHECK.REC<10>
                  CHECK.AMT=CHECK.REC<10>'R0'
                  GRAND.TOT=GRAND.TOT+CHECK.AMT
                  ACH.BATCH=CHECK.REC<27>          ; * JMK03
                  ACH.DATE = OCONV(FIELD(CHECK.REC<26>,"*",1),"D2/")  ; * NPR01
                  LOCATE ACH.BATCH IN ACH.TOT<1> BY 'AR' SETTING ACHPOS ELSE     ; * JMK03
                     INS ACH.BATCH BEFORE ACH.TOT<1,ACHPOS>  ; * JMK03
                     INS 0 BEFORE ACH.TOT<2,ACHPOS>          ; * JMK03
                  END                    ; * JMK03
                  ACH.TOT<2,ACHPOS>+=CHECK.AMT     ; * JMK03
                  IF SELECTION='D' THEN  ; * JMK02
                     EXP.LINE=''
                     EXP.LINE<1,1>=BANK.DESC       ; * JMK02
                     EXP.LINE<1,2>=CHECK.NUM       ; * JMK02
                     EXP.LINE<1,3>=CHECK.DATE'D2/'           ; * JMK02
                     EXP.LINE<1,4>=CARRIER.NAME    ; * JMK02
                     EXP.LINE<1,5>=CHECK.AMT'R26,'           ; * JMK02
                     EXP.LINE<1,6>=ACH.BATCH       ; * NPR01
                     EXP.LINE<1,7>=ACH.DATE        ; * NPR01
                     EXP.REC<-1>=EXP.LINE          ; * JMK02
                  END ELSE               ; * JMK02
                     PRINT BANK.DESC'L#20':' ':CHECK.NUM:' ':CHECK.DATE'D2/':
                     PRINT ' ':CARRIER.NAME'L#30':CHECK.AMT'R26,#16'
                  END                    ; * JMK02
               CASE 1
                  NULL
            END CASE
         END
      REPEAT
*
      IF SELECTION='D' THEN              ; * JMK02
         EXP.ID='OUT':BANK:'-':OCONV(END.DATE,'DYMD[4,2,2]':@VM:'MCN'):'.XLS'    ; * JMK02
         EXP.REC=CHANGE(EXP.REC,@VM,CHAR(9))       ; * JMK02
         WRITE EXP.REC ON F.BILLDATA,EXP.ID        ; * JMK02
         CALL ELINE('Your file is called ':EXP.ID:', located on your "U:\" drive')         ; * JMK02
      END ELSE                           ; * JMK02
         PRINT SPACE(68):STR('-',15)
         PRINT
         PRINT '   Grand total':SPACE(53):GRAND.TOT'R26,#16'
         PRINT SPACE(68):STR('=',15)
         PRINT ' '                         ; * JMK03
         PRINT STR("-",83)                 ; * NPR02
         ACH.TOTAL = 0                     ; * NPR02
         CHECK.TOTAL = 0                   ; * NPR02
         NVAL=DCOUNT(ACH.TOT<1>,@VM)       ; * JMK03
         FOR VAL=1 TO NVAL                 ; * JMK03
            ACH.NO=ACH.TOT<1,VAL>          ; * JMK03
            IF ACH.NO='' THEN              ; * JMK03
               ACH.NO='CHECKS'             ; * JMK03
               CHECK.TOTAL += ACH.TOT<2,VAL> ; * NPR02
            END ELSE                       ; * JMK03
               ACH.NO='ACH # ':ACH.NO      ; * JMK03
               ACH.TOTAL += ACH.TOT<2,VAL> ; * NPR02
               PRINT ACH.NO'L#20':' ': ACH.TOT<2,VAL> 'R26,#16' ; * JMK03
            END                            ; * JMK03
*NPR02            PRINT ACH.NO'L#20':' ': ACH.TOT<2,VAL> 'R26,#16' ; * JMK03
         NEXT VAL                        ; * JMK03
*
         IF ACH.TOTAL NE "0" THEN PRINT SPACE(21):STR('-',16)                             ; * NPR02
*
         IF ACH.TOTAL NE "0" THEN                                ; * NPR02
           PRINT
           PRINT "ACH Total" 'L#20':' ': ACH.TOTAL 'R26,#16'     ; * NPR02
         END                                                     ; * NPR02
*
         PRINT 
         IF CHECK.TOTAL NE "0" THEN                              ; * NPR02
           PRINT
           PRINT "Check Total" 'L#20':' ': CHECK.TOTAL 'R26,#16' ; * NPR02
         END                                                     ; * NPR02
*
         PRINT
         PRINT SPACE(21):STR('=',16)                             ; * NPR02
         PRINT "Grand Total" 'L#20':' ':GRAND.TOT'R26,#16'
         PRINT SPACE(21):STR('=',16)  
      END                                ; * JMK02
*
      PRINTER OFF
      PRINTER CLOSE
      IF SELECTION='S' THEN
         CALL ELINE('')
         EXECUTE '80'
      END
      STOP
