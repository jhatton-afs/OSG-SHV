      SUBROUTINE CALC.TL.FSC(VIN,VOUT,F.NFSC,F.TABLES,F.TL.FSC,F.PCIN,F.PCOUT,F.MILEAGE,F.ZIPS,F.CAN.ZIPS)
      UPL.VIN = 'FBBP' ; UPL.VIN<2> = 'CALC.TL.FSC' ; UPL.VOUT = ''    ; * NPR_UPL 04/22/2010
***      CALL GET.USER.NEW(USER,CH.NUM)
***      UPL.VIN<3> = USER
***      UPL.VIN<4> = CH.NUM
***      CALL UPD.PROGRAM.LOG(UPL.VIN,UPL.VOUT)       ; * NPR_UPL 04/22/2010
      BILL.DATE=VIN<1>
      WT=VIN<2>
      MILES=VIN<3>
      CC=VIN<5>
      FRT.AMT=VIN<6>
      ORIG.ZIP=VIN<7>
      DEST.ZIP=VIN<8>
      ORIG.ZIP=CHANGE(ORIG.ZIP,"'","")
      DEST.ZIP=CHANGE(DEST.ZIP,"'","")
      CLIENT.ID=VIN<9>
      CARRIER.ID=VIN<10>
      READ ORIG.ZIP.REC FROM F.ZIPS,ORIG.ZIP ELSE
         READ ORIG.ZIP.REC FROM F.CAN.ZIPS,ORIG.ZIP ELSE ORIG.ZIP.REC=''
      END
      READ DEST.ZIP.REC FROM F.ZIPS,DEST.ZIP ELSE
         READ DEST.ZIP.REC FROM F.CAN.ZIPS,DEST.ZIP ELSE DEST.ZIP.REC=''
      END
      ORIG.CITY=ORIG.ZIP.REC<1>
      ORIG.ST=ORIG.ZIP.REC<2>
      DEST.CITY=DEST.ZIP.REC<1>
      DEST.ST=DEST.ZIP.REC<2>
      ZIP.ARR=''
      ZIP.ARR<1,1>=ORIG.ZIP
      ZIP.ARR<1,2>=DEST.ZIP
      PCVIN=''
      PCVOUT=''
      PCVIN<1>='BOTH'
***      PCVIN<2>=CH.NUM
      PCVIN<3>=ZIP.ARR<1>
      PCVIN<4>=1
      PCVIN<5>='CALC.TL.FSC'
      PCVIN<6>='TL FSC ESTIMATING'
      PCVIN<8>=CLIENT.ID                 ; * JMK01
      PCVIN<7>=1
      PCVIN<9>=CARRIER.ID                ; * JMK01
      CALL PCMILER.BETA(PCVIN,PCVOUT,F.PCIN,F.PCOUT,F.MILEAGE)
      PCM.MILES=PCVOUT<4>
      IF MILES='' THEN MILES=PCM.MILES
      EST.FSC.ID=CARRIER.ID:'*':BILL.DATE:'*EST'
      FSC.ARRAY=''
      READ EST.FSC.REC FROM F.TL.FSC,EST.FSC.ID ELSE
         EST.FSC.REC=''
         READ CARRIER.LIST FROM F.TABLES,'FB.TL.FSC.LIST' THEN
            NVAL=DCOUNT(CARRIER.LIST,@VM)
            FOR VAL=1 TO NVAL
               LIST.CARRIER.ID=CARRIER.LIST<1,VAL>
               ACT.FSC.ID=LIST.CARRIER.ID:'*':BILL.DATE:'*ACT'
               READ ACT.FSC.REC FROM F.TL.FSC,ACT.FSC.ID ELSE
                  ACT.FSC.REC=''
                  READ BENCH.DFSC FROM F.NFSC,"B*":LIST.CARRIER.ID ELSE BENCH.DFSC=''
                  IF BENCH.DFSC<30>#'ENABLE' THEN
                     BENCH.DFSC=''       ; ** No Effect
                     CONTR.DFSC=''
                  END
                  FSC.VIN=''
                  FSC.VOUT=''
                  FSC.VIN<1>=BILL.DATE
                  FSC.VIN<2>=WT
                  FSC.VIN<3>=MILES
                  FSC.VIN<5>=CC
                  FSC.VIN<6>=FRT.AMT
                  FSC.VIN<7>=ORIG.ST
                  FSC.VIN<8>=DEST.ST
                  CALL CALC.FSC(FSC.VIN,FSC.VOUT,BENCH.DFSC,F.TABLES)
                  ACT.FSC.REC=''
                  FOR ATT=1 TO 3
                     ACT.FSC.REC<ATT>=FSC.VIN<ATT>
                     ACT.FSC.REC<3+ATT>=FSC.VIN<5+ATT>
                  NEXT ATT
                  ACT.FSC.REC<7>=FSC.VOUT
                  IF ACT.FSC.REC#'' THEN
                     WRITE ACT.FSC.REC ON F.TL.FSC,ACT.FSC.ID
                  END
               END
               IF EST.FSC.REC<2>='' THEN
                  FOR ATT=1 TO 3
                     EST.FSC.REC<1+ATT>=VIN<ATT>
                     EST.FSC.REC<4+ATT>=VIN<5+ATT>
                  NEXT ATT
               END
               NATT=DCOUNT(ACT.FSC.REC,@AM)
               LOCATE LIST.CARRIER.ID IN EST.FSC.REC<8> BY 'AR' SETTING LPOS ELSE
                  INS LIST.CARRIER.ID BEFORE EST.FSC.REC<8,LPOS>
                  FOR ATT=7 TO NATT
                     INS ACT.FSC.REC<ATT> BEFORE EST.FSC.REC<2+ATT,LPOS>
                  NEXT ATT
               END
               ACT.FSC.AMT=ACT.FSC.REC<9>+0
               IF ACT.FSC.AMT THEN
                  LOCATE ACT.FSC.AMT IN FSC.ARRAY<1> BY 'AR' SETTING FPOS THEN NULL ELSE NULL
                  INS ACT.FSC.AMT BEFORE FSC.ARRAY<1,FPOS>
               END
            NEXT VAL
            NVAL=DCOUNT(FSC.ARRAY<1>,@VM)
            IF NVAL > 4 THEN
               NEW.FSC.ARRAY=''
               FOR VAL=1 TO 4
                  NEW.FSC.ARRAY<1,VAL>=FSC.ARRAY<1,VAL>
               NEXT VAL
               FSC.ARRAY=NEW.FSC.ARRAY
               NVAL=4
            END
            IF NVAL=0 THEN
               FSC.PERC=0
            END ELSE
               FSC.PERC=SUM(FSC.ARRAY<1>)/NVAL
            END
            EST.FSC.REC<1>=INT(FSC.PERC)
            IF EST.FSC.REC<1>+0#'0' THEN
               WRITE EST.FSC.REC ON F.TL.FSC,EST.FSC.ID
            END
         END
      END
      FSC.PERC=EST.FSC.REC<1>
      OUT.FRT.AMT=((FRT.AMT/(FSC.PERC+100000))*100000)'R0'
      OUT.FSC.AMT=FRT.AMT-OUT.FRT.AMT
      VOUT<1>=FSC.PERC
      VOUT<2>=OUT.FSC.AMT
      VOUT<3>=OUT.FRT.AMT
      RETURN
   END
