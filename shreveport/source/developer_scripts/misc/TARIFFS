**************************************************************
* Program name : TARIFFS
* Author       : Cody Henderson
* Date written : October 7, 1996
* Library      : FBBP
$* COPYRIGHT (C) 1998 - AFS LOGISTICS LLC - ALL RIGHTS RESERVED.
*
* FREIGHT AUDIT PACKAGE - Cross reference tariffs with effective dates
*
* Last updated by afs (AFSLOGISTICS\nmeddaugh) at 10:47:40 on 04/30/2018.
*
* Start modification history
* 021506 - JMK01 - EXPAND TARIFF FIELD FOR RWS
* 082713 - JMK02 - ADD PRINT OPTION FOR OUTPUT
* 082117 - NKH01 - Replaced Tom Gorton with Nicole Meddaugh
* 043018 - NKH02 - Modified for Rateware web server version 2 (RWXL.V2)
* End modification history
**************************************************************
*
$OPTIONS HEADER.EJECT
$OPTIONS EXEC.EQ.PERF
$OPTIONS INFO.ABORT

      * Initialize variables
      ERRMSG=''
      PROG.NAME='TARIFFS'
      PROG.DESC='PROGRAMMER TARIFF TABLE'
      PACK.NAME='FREIGHT AUDITING'
      CALL AFS.SCR.HEAD(CO.ID,FILE.ID,'',PROG.NAME,PROG.DESC,PACK.NAME,CO.NAME,TIME.DATE,1)
      UPL.VIN = 'FBBP' ; UPL.VIN<2> = 'TARIFFS' ; UPL.VOUT = ''        ; * NPR_UPL 04/23/2010
      CALL UPD.PROGRAM.LOG(UPL.VIN,UPL.VOUT)       ; * NPR_UPL 04/23/2010
      PROMPT''
      ADD.FLAG=0
      FIND.LOCKED.NAME=''

      * Open files.
      OPEN '','FB.TABLES' TO F.FB.TABLES ELSE CALL OPEN.ABORT("FB.TABLES",PROG.NAME)

      * Get tariff table
      READU TARIFF.REC FROM F.FB.TABLES,'TARIFFS' LOCKED
         FIND.LOCKED.NAME = 'FB.TABLES,TARIFFS'
         GOSUB FIND.LOCKED
         CALL ELINE(FL.USER:' has ':FIND.LOCKED.NAME:' locked. Press enter to exit program.')
         GOTO 9999
      END ELSE
         TARIFF.REC=''
      END

      * First page of codes.
      PAGE=1
      GOSUB 9000

      * Check for proper user
      OK=0
*      USER=OCONV(SYSTEM(19),'MCU')
      CALL GET.USER(USER)
      IF USER='DAVID' THEN OK=1
      IF USER='GBINO' THEN OK=1          ; * JMK01
      IF USER='NMEDDAUGH' THEN OK=1      ; * NKH01
      IF USER = 'WPARDUE' THEN OK = 1

      IF OK=1 ELSE
         CALL ELINE('Unauthorized user. ACCESS DENIED. Call Mike or David for help.')
         GOTO 9999
      END

**************************************************************
100:  * Enter selection code
**************************************************************
*     HELP="Enter line number. 'B'=Back. 'T'=Top, ''=Next page. 'E'=End. 'EX'=Exit."       ; * JMK02
      HELP="Enter line number. [B]ack, [T]op, [P]rint, ''=Next page, [E]nd, [EX]it."       ; * JMK02
      CALL GEN.IN(0,22,"Enter line number please. ###",'',Q,0,3,'','',1,-3,22,0,QI,HELP,0,23)
      QI=OCONV(QI,'MCU')
      BEGIN CASE
         CASE QI=''
            IF TARIFF.REC<1,PAGE*18>='' THEN
               CALL ELINE('Error! You are on the last page!')
               GO 100
            END
            PAGE=PAGE+1
            GOSUB 9000
         CASE QI='B'
            PAGE=PAGE-1
            IF PAGE=0 THEN PAGE=1
            GOSUB 9000
         CASE QI='T'
            PAGE=1
            GOSUB 9000
         CASE QI='P'                     ; * JMK02
            GOSUB 9100                   ; * JMK02
            GOSUB 9000                   ; * JMK02
         CASE QI='EX'
            GOTO 9999
         CASE NUM(QI)
            SEL=QI
            IF SEL LT START ! SEL GT STOP THEN
               CALL ELINE('Invalid selection.')
               GOTO 100
            END
            IF SEL = 1 ELSE
               IF TARIFF.REC<1,SEL-1> = '' THEN
                  CALL ELINE('Invalid entry. Use next available line.')
                  GOTO 100
               END
            END
            GOSUB 8000
         CASE QI='E'
            X=DCOUNT(TARIFF.REC<1>,CHAR(253))
            FOR I=X TO 1 STEP -1
               IF TARIFF.REC<1,I>='' THEN
                  TARIFF.REC=DELETE(TARIFF.REC,1,I)
                  TARIFF.REC=DELETE(TARIFF.REC,2,I)
                  TARIFF.REC=DELETE(TARIFF.REC,3,I)
               END
            NEXT I
            VIN=''
            VIN<1>=1
            VIN<2>='L'
            AIN=TARIFF.REC
            CALL SORT.ARRAY(VIN,VOUT,AIN,AOUT)
            TARIFF.REC=AOUT
            WRITE TARIFF.REC ON F.FB.TABLES,'TARIFFS'
            GOTO 9999
      END CASE
      GOTO 100

**************************************************************
8000: * Edit tariffs
**************************************************************
      ROW=3+REM(SEL,18)
      IF ROW=3 THEN ROW=21
      TARIFF=TARIFF.REC<1,SEL>
      DESC=TARIFF.REC<2,SEL>
      RATING.PROG=TARIFF.REC<3,SEL>

**************************************************************
8015:
**************************************************************
      HELP="Enter the ACTUAL tariff name. ''=Accept. 'D'=Delete. 'EX'=Exit."
      IF TARIFF='' THEN MASK=STR('-',14) ELSE MASK=TARIFF'L#14'
      CALL GEN.IN(5,ROW,MASK,'',Q,0,14,'','',2,5,ROW,0,QI,HELP,0,23)
      IF QI='' THEN QI=TARIFF
      QI=OCONV(QI,'MCU')
      BEGIN CASE
         CASE QI='D'
            TARIFF.REC=DELETE(TARIFF.REC,1,SEL)
            TARIFF.REC=DELETE(TARIFF.REC,2,SEL)
            TARIFF.REC=DELETE(TARIFF.REC,3,SEL)
            GOSUB 9000
            GOTO 100
         CASE QI='EX'
            GOTO 9999
         CASE QI=''
            CALL ELINE('Invalid entry. Data required in this field.')
            GOTO 8015
         CASE INDEX(QI,' ',1)
            CALL ELINE('Invalid entry. No spaces allowed.')
            GOTO 8015
            RETURN
      END CASE
      TARIFF=QI
      CRT @(5,ROW):TARIFF'L#14'

**************************************************************
8020:
**************************************************************
      HELP="Enter tariff Rating Program. ''=Accept. [EX]-Exit."
      IF RATING.PROG='' THEN MASK=STR('-',18) ELSE MASK=RATING.PROG'L#18'
      CALL GEN.IN(20,ROW,MASK,'',Q,0,18,'','',2,20,ROW,0,QI,HELP,0,23)
      QI=OCONV(QI,'MCU')
      IF QI='' THEN QI=RATING.PROG
      BEGIN CASE
         CASE QI='X'
            CRT @(20,ROW):RATING.PROG'L#18'
            GOTO 8015
         CASE QI='EX'
            GOTO 9999
         CASE QI=''
            CALL ELINE('Invalid entry. Data required in this field.')
            GOTO 8020
         CASE INDEX(QI,' ',1)
            CALL ELINE('Invalid entry. No spaces allowed.')
            GOTO 8020
      END CASE
      RATING.PROG=QI
      CRT @(20,ROW):RATING.PROG'L#18'

**************************************************************
8030:
**************************************************************
      HELP="Enter tariff Description. ''=Accept. [EX]-Exit."
      IF DESC='' THEN MASK=STR('-',40) ELSE MASK=DESC'L#40'
      CALL GEN.IN(39,ROW,MASK,'',Q,0,40,'','',2,39,ROW,0,QI,HELP,0,23)
      QI=OCONV(QI,'MCU')
      IF QI='' THEN Q=DESC
      BEGIN CASE
         CASE QI='X'
            CRT @(39,ROW):DESC'L#40'
            GOTO 8020
         CASE QI='EX'
            GOTO 9999
      END CASE
      DESC=Q
      CRT @(39,ROW):DESC'L#40'

      TARIFF.REC<1,SEL>=TARIFF
      TARIFF.REC<2,SEL>=DESC
      TARIFF.REC<3,SEL>=RATING.PROG
      RETURN

**************************************************************
9000: * Display tariff array
**************************************************************
      CRT @(00,3):@(-3):"Line":
      CRT @(05,3):"Tariff Name":
      CRT @(20,3):"Rating Program":
      CRT @(39,3):"Description or Comments":
      START=(PAGE*18)-17
      IF START GT 90 THEN                ; * NKH02
         START=1
         PAGE=1
      END
      STOP=START+17
      FOR I=START TO STOP STEP 1
         ROW=3+REM(I,18)
         IF ROW=3 THEN ROW=21
         TARIFF=TARIFF.REC<1,I>
         DESC=TARIFF.REC<2,I>
         RATING.PROG=TARIFF.REC<3,I>
*       PRINT @(0,ROW):I"R#4":' ':TARIFF"L#8":'   ':DATE'D2/':@(25,ROW):DESC[1,40]:
         PRINT @(00,ROW):I"R#4":
         PRINT @(05,ROW):TARIFF:
         PRINT @(20,ROW):RATING.PROG:
         PRINT @(39,ROW):DESC:
      NEXT I
      RETURN

**************************************************************
9100: * Print tariff array
**************************************************************
      PRINTER ON
      PRINT "Line"'R#4 ':"Tariff"'L#10 ':"Rating Program"'L#15 ':"Description"'L#30'
      NVAL=DCOUNT(TARIFF.REC<1>,@VM)
      FOR VAL=1 TO NVAL
         TARIFF=TARIFF.REC<1,VAL>
         TARIFF.DESC=TARIFF.REC<2,VAL>
         RATING.PROG=TARIFF.REC<3,VAL>
         PRINT VAL'R#4 ':TARIFF'L#10 ':RATING.PROG'L#15 ':TARIFF.DESC'L#30'
      NEXT VAL
      PRINTER OFF
      PRINTER CLOSE
      RETURN

**************************************************************
FIND.LOCKED:* Find locked subroutine
**************************************************************
      STMT = 'LIST.READU EVERY'
      EXECUTE STMT CAPTURING OUTPUT
      FL.LEN = DCOUNT(OUTPUT,@AM)
      FL.USER = 'Someone'
      FOUND = 0

      FOR X = 1 TO FL.LEN UNTIL FOUND
         T.USER = OCONV(TRIM(OUTPUT<X>[44,9]),'MCU')
         T.FILE = TRIM(OUTPUT<X>[53,17])
         IF T.FILE = FIND.LOCKED.NAME THEN
            FL.USER = T.USER
            FOUND = 1
         END
      NEXT

*  CALL ELINE(FL.USER:" has the file ":FIND.LOCKED.NAME:" locked. Find them and tell them to get out!")

      RETURN

**************************************************************
9999:* Exit program
**************************************************************
      STOP
