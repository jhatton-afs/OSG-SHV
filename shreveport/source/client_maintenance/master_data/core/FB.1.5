$OPTIONS HEADER.EJECT
$OPTIONS EXEC.EQ.PERF
$OPTIONS INFO.ABORT
ERRMSG=''
* PROGRAM FB.1.5
* PROGRAM TO MODIFY LOGISTIC VARIANCE CODES
*
* FREIGHT BILLING PACKAGE
*
* AUTHOR: BRIAN M. QUEEN
* DATE: Auugust 31, 1993
*
* Initialize variables
*
      PROG.NAME='FB.1.5'
      PROG.DESC='EDIT LOGISTIC VARIANCE CODES'
      PACK.NAME='FREIGHT BILLING'
      CALL AFS.SCR.HEAD(CO.ID,FILE.ID,'',PROG.NAME,PROG.DESC,PACK.NAME,CO.NAME,TIME.DATE,1)
       UPL.VIN = 'FBBP' ;  UPL.VIN<2> = 'FB.1.5' ; UPL.VOUT = ''                    ;* NPR_UPL 04/23/2010
       CALL UPD.PROGRAM.LOG(UPL.VIN,UPL.VOUT)                                                           ;* NPR_UPL 04/23/2010
      ADD.FLG=0
*
* Open client file
*
      OPEN '','CLIENTS' TO F.CLIENT ELSE CALL OPEN.ABORT ('BCUST',PROG.NAME)
*
* Enter client number
*
50    CRT @(0,2):@(-3)
      HELP='Enter the client number. [name]=Search.'
      CALL GEN.IN(0,2,'Enter Client Number #####','',Q,0,20,'','',0,-5,2,0,QI,HELP,0,23)
      QI=OCONV(QI,'MCU')
      BEGIN CASE
        CASE QI=''
          STOP
        CASE NUM(QI)
          Q=QI'R%5'
        CASE 1
          CALL SOUNDEX.DISPLAY(QI,'BCUST','SDX.CLIENTS,NAME',2,ITEM.LIST)
          CALL AFS.SCR.REFRESH(PROG.NAME,PROG.DESC,PACK.NAME,CO.NAME,TIME.DATE,1)
          IF ITEM.LIST='' ! DCOUNT(ITEM.LIST<1>,@VM) > 1 THEN GOTO 50
          IF NOT(NUM(ITEM.LIST<1,1>)) THEN GOTO 50
          Q=ITEM.LIST<1,1>'R%5'
      END CASE
      CLIENT.ID=Q'R%5'
*
* Read client item
*
      READ CLIENT.REC FROM F.CLIENT,CLIENT.ID ELSE
        CALL ELINE('Client number ':CLIENT.ID:' not on file.')
        GOTO 50
      END
      CLIENT.NAME=CLIENT.REC<2>
      CRT @(20,2):CLIENT.ID:' ':CLIENT.NAME
      OPEN '','VARIANCE.CODES,':CLIENT.ID TO F.CODES ELSE CALL OPEN.ABORT("VARIANCE.CODES,":CLIENT.ID,PROG.NAME)
*
* Select variance codes and build display array
*
      VC.REC=""
      SELECT F.CODES
      LOOP
        READNEXT ID ELSE ID="**"
      UNTIL ID="**" DO
        IF VC.REC="" THEN
          VC.REC<1>=ID
        END ELSE
          LOCATE(ID,VC.REC,1;POS;"AR") ELSE VC.REC=INSERT(VC.REC,1,POS,0,ID)
        END
      REPEAT
*
* First page of vendor/customer codes
*
      PAGE=1
      GOSUB 9000
*
* Enter selection code
*
100   HELP="Line number. [B]=Back. [T]=Top, [cr]=Next page. [E]=End. [EX]=Exit. [L]=Last."
      CALL GEN.IN(0,22,"Enter line number please. #####",'',Q,0,5,'','',1,-5,22,0,QI,HELP,0,23)
      QI=OCONV(QI,'MCU')
      BEGIN CASE
        CASE QI=''
          PAGE=PAGE+1
          GOSUB 9000
        CASE QI='B'
          PAGE=PAGE-1
          IF PAGE=0 THEN PAGE=1
          GOSUB 9000
        CASE QI EQ 'L'
          MX = DCOUNT(VC.REC<1>,CHAR(253))
          MX = INT(MX/18)
          PAGE = MX
          GOSUB 9000
        CASE QI='T'
          PAGE=1
          GOSUB 9000
        CASE QI='EX'
          STOP
        CASE NUM(QI)
          SEL=QI
          IF SEL LT START ! SEL GT STOP THEN
            CALL ELINE('Invalid selection.')
            GOTO 100
          END
          GOSUB 8000
        CASE QI='E'
          CRT @(0,22):@(-3)
          HELP="Enter Y to print listing"
          CALL GEN.IN(0,22,'Do you want a listing? N','',Q,0,1,'','',1,-1,22,1,QI,HELP,0,23)
          QI=OCONV(QI,'MCU')
          BEGIN CASE
            CASE QI='' ! QI='N'
              STOP
            CASE QI='Y'
              CALL PAGE.HEAD('FB.1.5',CLIENT.NAME,'','Logisitic Variance File Listing',DATE(),132,HED)
              PREC='SORT VARIANCE.CODES,':CLIENT.ID:' 1 HEADING "':HED:'" (P'
              EXECUTE PREC
              STOP
          END CASE
      END CASE
      GOTO 100
*------------------------------------------------------------------------
*
* INTERNAL SUBROUTINES
*
*------------------------------------------------------------------------
*
* Edit variance code
*
8000  ROW=3+REM(SEL,18)
      IF ROW=3 THEN ROW=21
      CODE=VC.REC<1,SEL>
      IF CODE NE '' THEN READV DESC FROM F.CODES,CODE,1 ELSE DESC='~~~ Not on file ~~~'
8010  HELP="Enter new variance code. [cr]=Accept. [D]=Delete. [X]=Exit."
      CALL GEN.IN(6,ROW,CODE'L#20','',Q,0,20,'','',2,-20,ROW,0,QI,HELP,0,23)
      QI=OCONV(QI,'MCU')
      BEGIN CASE
        CASE QI=''
          Q=CODE
        CASE QI='X'
          CRT @(6,ROW):CODE'L#20'
          RETURN
        CASE QI='D'
          READV DESC FROM F.CODES,CODE,1 ELSE DESC=''
          DELETE F.CODES,CODE
          VC.REC=DELETE(VC.REC,1,SEL)
          GOSUB 9000
          RETURN
      END CASE
      CODE=Q
      CRT @(6,ROW):CODE'L#20'
*
      HELP="Enter new variance code description. [cr]=Accept. [X]=Exit."
      CALL GEN.IN(27,ROW,DESC'L#20','',Q,0,20,'','',2,-20,ROW,0,QI,HELP,0,23)
      QI=OCONV(QI,'MCU')
      BEGIN CASE
        CASE QI='X'
          CRT @(27,ROW):DESC'L#20'
          GOTO 8010
        CASE QI=''
          Q=DESC
      END CASE
      DESC=Q
      CRT @(27,ROW):DESC'L#20'
      VC.REC<1,SEL>=CODE
      WRITEV DESC ON F.CODES,CODE,1
      RETURN
*
* Display variance code array
*
9000  CRT @(0,3):@(-3):"   #":@(6,3):"Variance Code":@(27,3):"Description"
      START=(PAGE*18)-17
      IF START GT 9990 THEN
        START=1
        PAGE=1
      END
      STOP=START+17
      FOR I=START TO STOP STEP 1
        ROW=3+REM(I,18)
        IF ROW=3 THEN ROW=21
        VC=VC.REC<1,I>
        IF VC NE '' THEN
          READV DESC FROM F.CODES,VC,1 ELSE DESC='~~~ Not on file ~~~'
        END ELSE
          DESC=''
        END
        PRINT @(0,ROW):I"R#4":'  ':VC"L#20":' ':DESC"L#35"
      NEXT I
      RETURN
