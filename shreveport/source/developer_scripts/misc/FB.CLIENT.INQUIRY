$OPTIONS HEADER.EJECT
      ERRMSG=''
*
$OPTIONS EXEC.EQ.PERF
$OPTIONS INFO.ABORT
*
* PROGRAM ???
* PROGRAM TO DISPLAY SOME CLIENT MASTER INFO.
*
* FREIGHT BILLING PACKAGE
*
* AUTHOR: NPR
* DATE: NOVEMBER 27, 2007
*
      PROMPT''
      PROG.NAME='FB.CLIENT.INQUIRY'
      PROG.DESC='CLIENT INQUIRY'
      PACK.NAME='FREIGHT BILLING'
*
       UPL.VIN = 'FBBP' ;  UPL.VIN<2> = 'FB.CLIENT.INQUIRY' ; UPL.VOUT = ''                    ;* NPR_UPL 04/23/2010
       CALL UPD.PROGRAM.LOG(UPL.VIN,UPL.VOUT)                                                           ;* NPR_UPL 04/23/2010
      OPEN '','BCUST' TO F.CLIENT ELSE CALL OPEN.ABORT("BCUST",PROG.NAME)
      OPEN '','AR.SLSMAN.1000.1' TO F.SLSMAN ELSE
         CALL OPEN.ABORT('AR.SLSMAN.1000.1',PROG.NAME)
      END
*
      CALL GET.USER(USER)
      SUPER.USER=0
      VIN=USER
      CALL SUPER.USER(VIN,VOUT)
      SUPER.USER=VOUT<1>
*
*
* Display page heading
*
      CALL AFS.SCR.HEAD(CO.ID,FILE.ID,PERIOD,PROG.NAME,PROG.DESC,PACK.NAME,CO.NAME,TIME.DATE,1)
*
* Display menu and client data
*
100:  CRT @(0,2):@(-3)
*
      HELP="Enter client number or name to search. [EX]=Exit"
*
      CALL GEN.IN(0,3,'Enter Client Number : #####','',Q,0,20,'','',2,-5,3,0,QI,HELP,0,23)
      QI=OCONV(QI,'MCU')
      BEGIN CASE
         CASE QI='X' OR QI='EX' OR QI=''
*            STOP
            CHAIN "G"
         CASE ALPHA(QI)
            CALL SOUNDEX.DISPLAY(QI,'BCUST','SDX.CLIENTS,NAME',2,ITEM.LIST)
            CALL AFS.SCR.REFRESH(PROG.NAME,PROG.DESC,PACK.NAME,CO.NAME,TIME.DATE,1)
            IF ITEM.LIST='' ! DCOUNT(ITEM.LIST<1>,@VM) > 1 THEN GOTO 100
            IF NOT(NUM(ITEM.LIST<1,1>)) THEN GOTO 100
            Q=ITEM.LIST<1,1>'R%5'
         CASE NUM(QI)
            Q=QI'R%5'
         CASE 1
            GOTO 100
      END CASE
*
* Read client item
*
      READ CLIENT.REC FROM F.CLIENT,Q ELSE
         CALL ELINE("Cannot find client ":Q:" in the CLIENT file.")
         GOTO 100
      END
*
      LEAD.SLS.ID=CLIENT.REC<114>
      IF LEAD.SLS.ID='' THEN
         LEAD.SALESMAN='No Lead/Originating Salesman on File'
      END ELSE
         READ SALESMAN.REC FROM F.SLSMAN,LEAD.SLS.ID THEN
            LEAD.SALESMAN=SALESMAN.REC<1>:' - (':LEAD.SLS.ID:')'
         END ELSE
            LEAD.SALESMAN='Name Not Found - (':LEAD.SLS.ID:')'
         END
      END

*
      SERVICE.SLS.ID=CLIENT.REC<115>
      IF SERVICE.SLS.ID='' THEN
         SERVICE.SALESMAN='No Servicing Salesman on File'
      END ELSE
         READ SALESMAN.REC FROM F.SLSMAN,SERVICE.SLS.ID THEN
            SERVICE.SALESMAN=SALESMAN.REC<1>:' - (':SERVICE.SLS.ID:')'
         END ELSE
            SERVICE.SALESMAN='Name Not Found - (':SERVICE.SLS.ID:')'
         END
      END
*
      AUDITOR=CLIENT.REC<71>
      COMPANY=CLIENT.REC<92> * 1
*
      CLIENT.NUMBER.TEXT='Client Number ':Q
      CALL CENTER(CLIENT.NUMBER.TEXT,80)
      CRT @(0,2):CLIENT.NUMBER.TEXT
      CRT @(0,3):@(-4):
      GOSUB DISPLAY.INFO
*
*      CALL ELINE("RETURN to Continue")
CALL ELINE("")
      GOTO 100
*
*
DISPLAY.INFO :
*
      CRT @(0, 3):'Name......':CLIENT.REC<2>'L#30'
      CRT @(0, 4):'Address 1.':CLIENT.REC<3,1>'L#30'
      CRT @(0, 5):'Address 2.':CLIENT.REC<3,2>'L#30'
      CRT @(0, 6):'Zip.......':CLIENT.REC<6>'L#10':' State ':CLIENT.REC<5>'L#2'
      CRT @(0, 7):'City......':CLIENT.REC<4>'L#30'
      CRT @(0, 8):'Fiscal yr.':CLIENT.REC<29>
      CRT @(0, 9):'Contact...':CLIENT.REC<46>'L#30'
      CRT @(0,10):'Invoice...':CLIENT.REC<44>'L#30'
      CRT @(0,11):'Phone.....':'(':CLIENT.REC<31>[1,3]:')':CLIENT.REC<31>[4,3]:'-':CLIENT.REC<31>[7,4]
      CRT @(0,12):'Fax.......':

      LFAX=LEN(CLIENT.REC<45>)
      BEGIN CASE
         CASE LFAX=10
            PRINT '(':CLIENT.REC<45>[1,3]:')':CLIENT.REC<45>[4,3]:'-':CLIENT.REC<45>[7,4]
         CASE LFAX=7
            PRINT CLIENT.REC<45>[1,3]:'-':CLIENT.REC<45>[4,7]
         CASE 1
            PRINT
      END CASE
*
      CRT @(0,14):'Lead Salesman ....'
      CRT @(20,14):LEAD.SALESMAN
      CRT @(0,15):'Service Salesman..'
      CRT @(20,15):SERVICE.SALESMAN
      CRT @(0,17):'Auditor        ...'
      CRT @(20,17):AUDITOR
      CRT @(0,18):'Pre-audit start...  ':CLIENT.REC<27>'D2/'
      CRT @(0,19):'Post-audit start..  ':CLIENT.REC<28>'D2/'
*
      CRT @(45,3):' Pay frequency........':CLIENT.REC<38>'R#2':' days'

      IF CLIENT.REC<30>='A' THEN STAT.DESC='..Active' ELSE STAT.DESC='Inactive'
      CRT @(45,4):' Status..............':STAT.DESC'R#8'

      CRT @(45,5):'<S>tay-out/<P>re-deposit...':CLIENT.REC<41>
      CRT @(45,6):'Reporting Options..........':CLIENT.REC<98>'L#1':' ':CLIENT.REC<97>'L#1'
      CRT @(45,7):'Variance default...........':CLIENT.REC<23>
      CRT @(45,8):'Invoice customer...........':CLIENT.REC<42>
      CRT @(45,9):'Company Code...............':CLIENT.REC<92> * 1
*
      EMAIL.ADD='No'                     ; * JMK01
      IF CLIENT.REC<125>#'' THEN EMAIL.ADD='Yes'
      IF CLIENT.REC<126>#'' THEN EMAIL.ADD='Yes'
      IF CLIENT.REC<127>#'' THEN EMAIL.ADD='Yes'
      IF CLIENT.REC<153>#'' THEN EMAIL.ADD='Yes'
      IF CLIENT.REC<154>#'' THEN EMAIL.ADD='Yes'
      IF CLIENT.REC<155>#'' THEN EMAIL.ADD='Yes'
      IF CLIENT.REC<222>#'' THEN EMAIL.ADD='Yes'
      IF CLIENT.REC<223>#'' THEN EMAIL.ADD='Yes'
      IF CLIENT.REC<224>#'' THEN EMAIL.ADD='Yes'
      IF CLIENT.REC<179>#'' THEN EMAIL.ADD='Trb'
*
*      CRT @(45,10):'Email address..............':EMAIL.ADD          ; * JMK01
*
RETURN
*
END
