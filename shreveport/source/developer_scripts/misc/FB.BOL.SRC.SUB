      SUBROUTINE FB.BOL.SRC.SUB(BOL.NUMS, F.BOL.CRX, VOUT)
*     This subroutine is for determining if a BOL number has related
*        documentation and whether the BOL was thru the AFS-BOL system
*        or was a Client Provided BOL number.
*     One or more BOL numbers can be passed in as multi-values. The
*        program will start at the first BOL number and search until
*        it either finds a BOL.CRX record with FIELD 52 not null or
*        reaches the end of the list. As soon as it finds a BOL.CRX
*        record with field 52 not null it will determine the SOURCE
*        and will stop searching.
      VOUT=''
      BOL.SRC=''
      BOL.FOUND=0
      BOL.CNT=DCOUNT(BOL.NUMS,@VM)
      BC=0
      LOOP
         BC+=1
      UNTIL BC > BOL.CNT OR BOL.FOUND DO
         BOL.NUM=BOL.NUMS<1,BC>
         READV BOL.PROVIDED FROM F.BOL.CRX, BOL.NUM, 52 THEN
            IF BOL.PROVIDED # '' THEN
            
               IF BOL.PROVIDED[1, 7]='AFS_BOL' OR BOL.PROVIDED[1,12]='PULSETMS_BOL' THEN
                  BOL.SRC=1
                  BOL.FOUND=1
               END ELSE
                  BOL.SRC=2
                  BOL.FOUND=1
               END
            END
         END
      REPEAT

      VOUT<1>=BOL.SRC
      RETURN

