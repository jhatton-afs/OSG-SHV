* FREIGHT BILLING PACKAGE
*---- PROGRAM: CHECK.FOR.DUPS.IN.VCZ
* AUTHOR: JAY WEBERT
* DATE: APRIL 11, 2007
* DESCRIPTION: COUNT DUPLICATE POINTERS IN VEND.CUST.ZIPS RECORDS
*
* THIS PROGRAM IS A PROPRIETARY PRODUCT OF AUTOMATED FREIGHT SYSTEMS
* AND MAY NOT BE USED, COPIED OR MODIFIED WITHOUT THE EXPRESS
* WRITTEN CONSENT OF SAME.
*-----------------------------------------------------------------------
      PROG.NAME = 'CHECK.FOR.DUPS.IN.VCZ'
      *** SELECT CLIENT(S) TO PROCESS; ONE OR ALL
      CLIENT.ID = ''
      CLIENT.ID = FIELD(@SENTENCE,' ',2)
      IF CLIENT.ID = '' THEN
        STMT = 'SELECT VOC "VEND.CUST.ZIPS" *A7'
        EXECUTE STMT RTNLIST CAPTURING MSG
        IF @SELECTED < 1 THEN
          CRT 'NO CLIENTS SELECTED'
          STOP
        END
        STMT = 'SELECT CLIENTS'
        EXECUTE STMT PASSLIST RTNLIST CLIENT.LIST CAPTURING MSG
        IF @SELECTED < 1 THEN
          CRT 'NO CLIENT RECORDS SELECTED'
          STOP
        END
      END ELSE
        CLIENT.ID = CLIENT.ID'R%5'
        OPEN 'CLIENTS' TO F.CLIENTS THEN
          READ TEST FROM F.CLIENTS, CLIENT.ID THEN
            STMT = 'SELECT CLIENTS ':CLIENT.ID
            EXECUTE STMT RTNLIST CLIENT.LIST CAPTURING MSG
            IF @SELECTED < 1 THEN 
              CRT 'NO CLIENT RECORDS SELECTED'
              STOP
            END
          END ELSE
            CRT 'CLIENT RECORD ':CLIENT.ID:' DOES NOT EXIST'
            STOP
          END
        END ELSE
          CRT 'COULD NOT OPEN CLIENTS FILE'
          STOP
        END
      END
      *** CHECK FOR VEND.CUST.ZIPS RECORDS WITH DUPLICATE POINTERS
      DONE = 0
      LOOP UNTIL DONE DO
        READNEXT CLIENT.ID FROM CLIENT.LIST THEN
          CRT CLIENT.ID
          OPEN 'VEND.CUST.ZIPS,':CLIENT.ID TO F.VCZ ELSE
            CRT 'COULD NOT OPEN VEND.CUST.ZIPS,':CLIENT.ID
            GOTO NEXT.CLIENT
          END
          STMT = 'SELECT VEND.CUST.ZIPS,':CLIENT.ID
          EXECUTE STMT RTNLIST VCZ.LIST CAPTURING MSG
          IF @SELECTED < 1 THEN GOTO NEXT.CLIENT
          REC.CNT = @SELECTED
          DUP.CNT = 0
          DONE1 = 0
          LOOP UNTIL DONE1 DO
            READNEXT VCZ.ID FROM VCZ.LIST THEN
              READV VC.IDS FROM F.VCZ, VCZ.ID, 1 THEN
                ID.CNT = DCOUNT(VC.IDS,CHAR(253))
                FOR I = 1 TO (ID.CNT-1)
                  VC.ID = VC.IDS<1,I>
                  LOCATE VC.ID IN VC.IDS<1>, (I+1) SETTING POS THEN
                    DUP.CNT += 1
                    EXIT
                  END
                NEXT I
              END
            END ELSE
              IF DUP.CNT > 0 THEN
                PERC = (DUP.CNT/REC.CNT)*100
                CRT DUP.CNT:' out of ':REC.CNT:' records have duplicate VEND.CUST IDs; ':PERC:'%'
              END
              DONE1 = 1
            END
          REPEAT
        END ELSE
          DONE = 1
        END
NEXT.CLIENT:
      REPEAT
      STOP



