***********************************************************************
* Analyze bills acted upon by auditors
* Copyright (c) AFS Logistics, LLC., 2002
*
* AUTHOR : Owen Holman
* Date   : January 2002
***********************************************************************
* 022712 - JMK01 - SysAid#1841 - Set up AFS Brokerage in AFSi V2
***********************************************************************
!
* Include the ODBC definitions
!
      $INCLUDE UNIVERSE.INCLUDE ODBC.H

      form = "T##########"
      dash = "----------"
      Expect = ""
      NULL=""
      TOT.NUM.BILLS=0

      CALL GET.USER(USER)
      BEGIN CASE
         CASE USER='MKERBOW'
         CASE USER='DAVID'
         CASE USER='OWEN'
         CASE USER='PBASS'
         CASE USER='SV.UVAUTO'
         CASE USER='UVTASKS'
         CASE 1
            CRT "YOU ARE NOT AUTHORIZED TO USE THIS PROGRAM"
            STOP
      END CASE

      GET (ARG.,1) OBEG.DATE THEN
         BEG.DATE=ICONV(OBEG.DATE,'D')
      END ELSE
         BEG.DATE=DATE()
      END
      GET (ARG.,2) OEND.DATE THEN
         END.DATE=ICONV(OEND.DATE,'D')
      END ELSE
         END.DATE=DATE()
      END
      GET (ARG.,3) CLIENT.LIST.NAME ELSE CLIENT.LIST.NAME=''
      GET (ARG.,4) START.TIME ELSE START.TIME=''

      IF START.TIME#'' THEN
         CRT "Waiting until ":START.TIME
         TIME.STMT="SLEEP ":START.TIME
         EXECUTE TIME.STMT CAPTURING TIME.RESULTS
      END
      IF CLIENT.LIST.NAME#'' AND CLIENT.LIST.NAME#'ALL' THEN
         CLIENT.STMT='GET-LIST ':CLIENT.LIST.NAME
      END ELSE
         CLIENT.STMT='SSELECT CLIENTS WITH @ID # "9]"'
      END
      EXECUTE CLIENT.STMT RTNLIST CLIENT.LIST CAPTURING CLIENT.RESULTS
      NUM.CLIENTS=@SELECTED

      OPEN '','CLIENTS' TO F.CLIENT ELSE ABORT 201,'CLIENTS'
      OPEN '','FB.TABLES' TO F.TABLES ELSE ABORT 201,'FB.TABLES'
      OPEN '','FB.STATUS.UPDATE' TO F.STATUS.UPDATE ELSE ABORT 201,'FB.STATUS.UPDATE'

      READ LI.CODE.REC FROM F.TABLES,'LI.CODES' ELSE LI.CODE.REC=''
      CRT @(0,0):@(-1):@(0,3):'UPD.QSL.FBSTATUS.UPDATE.PX - Processing Clients from ':BEG.DATE'D2/':' to ':END.DATE'D2/':'...'
DEBUG
      CLIENT.EOF=0
      CLIENT.CNT=0
      CLIENT.PER=0
      LOOP
         READNEXT CLIENT.ID FROM CLIENT.LIST ELSE CLIENT.EOF=1
      UNTIL CLIENT.EOF DO
         CLIENT.CNT+=1
         CLIENT.PCT=ICONV(CLIENT.CNT/NUM.CLIENTS*100,'MR2')
         READ CLIENT.REC FROM F.CLIENT,CLIENT.ID THEN
            CRT @(0,5):@(-4):CLIENT.CNT'R#4':' - ':CLIENT.PCT'R26#6':'% ':CLIENT.ID:' - ':CLIENT.REC<2>
            CALL PROG.BAR(8,CLIENT.CNT,NUM.CLIENTS,CLIENT.PER)
            OPEN '','FB.BILLS.HIST,':CLIENT.ID TO F.BILL THEN

               BILL.STMT='SELECT FB.BILLS.HIST,':CLIENT.ID
               BILL.STMT:=' WITH VERSION = "0"'
               BILL.STMT:=' AND WITH 15 GE "':OCONV(BEG.DATE,'D2/'):'"'
               BILL.STMT:=' AND WITH 15 LE "':OCONV(END.DATE,'D2/'):'"'

               BILL.STMT:=' OR'

               BILL.STMT:=' WITH VERSION = "0"'
               BILL.STMT:=' AND WITH 30 GE "':OCONV(BEG.DATE,'D2/'):'"'
               BILL.STMT:=' AND WITH 30 LE "':OCONV(END.DATE,'D2/'):'"'

               BILL.STMT:=' OR'

               BILL.STMT:=' WITH VERSION = "0"'
               BILL.STMT:=' AND WITH 37 GE "':OCONV(BEG.DATE,'D2/'):'"'
               BILL.STMT:=' AND WITH 37 LE "':OCONV(END.DATE,'D2/'):'"'

               BILL.STMT:=' OR'

               BILL.STMT:=' WITH VERSION = "0"'
               BILL.STMT:=' AND WITH 133 GE "':OCONV(BEG.DATE,'D2/'):'"'
               BILL.STMT:=' AND WITH 133 LE "':OCONV(END.DATE,'D2/'):'"'

               BILL.STMT:=' OR'

               BILL.STMT:=' WITH VERSION = "0"'
               BILL.STMT:=' AND WITH 246 GE "':OCONV(BEG.DATE,'D2/'):'"'
               BILL.STMT:=' AND WITH 246 LE "':OCONV(END.DATE,'D2/'):'"'

               EXECUTE BILL.STMT RTNLIST BILL.LIST CAPTURING BILL.RESULTS

               NUM.BILLS=@SELECTED
               TOT.NUM.BILLS+=NUM.BILLS
               CRT @(0,10):@(-3):' #Bills - ':NUM.BILLS'R#12':' - ':TOT.NUM.BILLS'R#15'
               BILL.EOF=0
               BILL.CNT=0
               BILL.PER=0
               LOOP
                  READNEXT BILL.ID FROM BILL.LIST ELSE BILL.EOF=1
               UNTIL BILL.EOF DO
                  BILL.CNT+=1
                  CALL PROG.BAR(13,BILL.CNT,NUM.BILLS,BILL.PER)
                  READ BILL.REC FROM F.BILL,BILL.ID THEN
                     ENTRY.DT=BILL.REC<30>
                     PROC.DT=BILL.REC<37>
                     FUND.DT=BILL.REC<133>
                     CHECK.DT=BILL.REC<15>
                     RECV.DT=BILL.REC<246>
                     STATUS.UPDATE=0
                     IF ENTRY.DT GE BEG.DATE AND ENTRY.DT LE END.DATE THEN STATUS.UPDATE=1
                     IF ENTRY.DT GE BEG.DATE AND ENTRY.DT LE END.DATE THEN STATUS.UPDATE=1
                     IF FUND.DT GE BEG.DATE AND FUND.DT LE END.DATE THEN STATUS.UPDATE=1
                     IF CHECK.DT GE BEG.DATE AND CHECK.DT LE END.DATE THEN STATUS.UPDATE=1
                     IF RECV.DT GE BEG.DATE AND RECV.DT LE END.DATE THEN STATUS.UPDATE=1
                     NVAL=DCOUNT(BILL.REC<86>,@VM)
                     FOR VAL=1 TO NVAL
                        IF BILL.REC<86,VAL> GE BEG.DATE AND BILL.REC<86,VAL> LE END.DATE THEN STATUS.UPDATE=1
                     NEXT VAL
                     IF STATUS.UPDATE THEN
                        CALL UPD.FB.STATUS.UPDATE(CLIENT.ID,BILL.ID,F.STATUS.UPDATE) ; * JMK248
                     END
                  END
SKIP.BILL: *
               REPEAT
            END
         END
SKIP.CLIENT: *
      REPEAT


THE.END: ***
*     End of program
      STOP
