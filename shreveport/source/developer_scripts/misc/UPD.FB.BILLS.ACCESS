      SUBROUTINE UPD.FB.BILLS.ACCESS(VIN,VOUT,CLIENT.REC,CARRIER.REC,BILL.REC,F.ACCESS,F.TABLES)
********************************************************************
* Program name :  UPD.FB.BILLS.ACCESS
* Author       :  Unknown
* Date written :  Unknown
* Library      :  FBBP
$* COPYRIGHT (C) 2016 - AFS LOGISTICS LLC - ALL RIGHTS RESERVED.
*
* Update to FB.BILLS.ACCESS file
*
* Last updated by afs (AFSLOGISTICS\nmeddaugh) at 10:00:19 on 01/09/2020.
*
* Start modification history
* 070810 - NPR01 - T100399 - Added option to display why someone touched a freight bill
* 010920 - NKH01 - ACCU-733 Change 'VIEW' to action
* End modification history
**********************************************************************
      UPL.VIN = 'FBBP' ; UPL.VIN<2> = 'UPD.FB.BILLS.ACCESS' ; UPL.VOUT = ''      ; * NPR_UPL 04/23/2010
      CALL GET.USER.NEW(USER,CH.NUM)
      UPL.VIN<3> = USER
      UPL.VIN<4> = CH.NUM
      CALL UPD.PROGRAM.LOG(UPL.VIN,UPL.VOUT)       ; * NPR_UPL 04/23/2010
      CLIENT.ID=VIN<1>                   ; * 5 DIGIT AFS CLIENT NUMBER
      PRO.ID=VIN<2>                      ; * AFS FB.BILLS.HIST RECORD ID CARRIER*PRO*SEQ
      CARRIER.ID=FIELD(PRO.ID,'*',1)     ; * 5 DIGIT AFS CARRIER NUMBER
      USER=VIN<3>                        ; * LOGIN USER
      TYPE=VIN<4>                        ; * LOGIN USER DEPARTMENT OR "IN" FOR INTERNET
      ACC.DATE=VIN<5>                    ; * DATE OF INQUIRY
      ACC.TIME=VIN<6>                    ; * TIME OF INQUIRY
      NOTIFY=VIN<7>                      ; * NOTIFY IF "FP" ACCESS MORE THAN 3 TIMES (1 TO NOTIFY, 0 TO BUILD FILE)
      PROG.NAME=VIN<8>                   ; * PROGRAM NAME OF TOP LEVEL CALLING PROGRAM
      PROG.DESC=VIN<9>                   ; * PROGRAM DESC OF TOP LEVEL CALLING PROGRAM
      PACK.NAME=VIN<10>                  ; * PACKAGE NAME OF TOP LEVEL CALLING PROGRAM
      CO.NAME=VIN<11>                    ; * COMPANY NAME FROM TOP LEVEL CALLING PROGRAM
      TOUCH.REASON=VIN<12>               ; * Reason the bill was accessed                            ;*NPR01
      DASH=STR('-',40)
      READ PARCEL.LIST FROM F.TABLES,'FB.ACCESS.NOTIFY*PARCEL' ELSE PARCEL.LIST=''
      READ LTL.LIST FROM F.TABLES,'FB.ACCESS.NOTIFY*LTL' ELSE LTL.LIST=''
      READ INTL.LIST FROM F.TABLES,'FB.ACCESS.NOTIFY*INTL' ELSE INTL.LIST=''
*
      ACCESS.ID=PRO.ID
      READ ACCESS.REC FROM F.ACCESS,ACCESS.ID ELSE ACCESS.REC=''
      ACCESS.ORG=ACCESS.REC
*
      ACC.CHK=ACC.DATE:'*':TYPE
      LOCATE ACC.CHK IN ACCESS.REC<1> BY 'AL' SETTING APOS ELSE
         INS ACC.CHK BEFORE ACCESS.REC<1,APOS>
         INS '' BEFORE ACCESS.REC<2,APOS>
         INS '' BEFORE ACCESS.REC<3,APOS>
         INS '' BEFORE ACCESS.REC<4,APOS>          ; * NPR01
      END
      LOCATE ACC.TIME IN ACCESS.REC<3,APOS> BY 'AR' SETTING TPOS ELSE
         * Begin NKH01
         CHECK.POS = TPOS-1
         IF ACCESS.REC<4,APOS,CHECK.POS>='VIEW' AND ACCESS.REC<2,APOS,CHECK.POS> = USER THEN
            ACCESS.REC<2,APOS,CHECK.POS> = USER
            ACCESS.REC<3,APOS,CHECK.POS> = ACC.TIME
            ACCESS.REC<4,APOS,CHECK.POS> = TOUCH.REASON
         END ELSE
         * End NKH01      
            INS USER BEFORE ACCESS.REC<2,APOS,TPOS>
            INS ACC.TIME BEFORE ACCESS.REC<3,APOS,TPOS>
            INS TOUCH.REASON BEFORE ACCESS.REC<4,APOS,TPOS>     ; * NPR01
         END   ; * NKH01
      END
      IF ACCESS.REC#ACCESS.ORG THEN
         WRITE ACCESS.REC ON F.ACCESS,ACCESS.ID
      END
      IF NOTIFY THEN
         ACC.CNT=0
         ACCESS.LOG=''
         NVAL=DCOUNT(ACCESS.REC<1>,@VM)
         FOR VAL=1 TO NVAL
            LOG.DATE=FIELD(ACCESS.REC<1,VAL>,'*',1)
            LOG.TYPE=FIELD(ACCESS.REC<1,VAL>,'*',2)
            ACC.CNT+=1                   ; * JMK01
            NSVAL=DCOUNT(ACCESS.REC<2,VAL>,@SVM)
            FOR SVAL=1 TO NSVAL
               LOG.USER=ACCESS.REC<2,VAL,SVAL>
               IF LOG.USER='' AND LOG.TYPE='IN' THEN
                  ACCESS.REC<2,VAL,SVAL>='SV.CABS'
                  LOG.USER=ACCESS.REC<2,VAL,SVAL>
                  WRITE ACCESS.REC ON F.ACCESS,ACCESS.ID
               END
               LOG.TIME=ACCESS.REC<3,VAL,SVAL>
               LOG.ID=LOG.DATE:'*':LOG.TIME
               LOCATE LOG.ID IN ACCESS.LOG<1> BY 'AL' SETTING LPOS THEN NULL ELSE NULL
               INS LOG.ID BEFORE ACCESS.LOG<1,LPOS>
               INS LOG.USER BEFORE ACCESS.LOG<2,LPOS>
               INS LOG.TYPE BEFORE ACCESS.LOG<3,LPOS>
               INS TOUCH.REASON BEFORE ACCESS.LOG<4,LPOS>    ; * NPR01
            NEXT SVAL
         NEXT VAL
         IF ACC.CNT>5 THEN
            FBVIN=''
            FBVIN<1>=PROG.NAME
            FBVIN<2>=PROG.DESC
            FBVIN<3>=PACK.NAME
            FBVIN<4>=CO.NAME
            FBVIN<5>=OCONV(TIME(),'MTH'):' ':OCONV(DATE(),'D2/')
            FBVIN<6>=CLIENT.ID
            FBVIN<7>=CARRIER.ID
            FBVIN<8>=PRO.ID
            FBVOUT=''
            CALL GET.FB.INFO(FBVIN,FBVOUT,CLIENT.REC,CARRIER.REC,BILL.REC,F.TABLES)
            NTEXT=FBVOUT
            NTEXT<1,-1>=' '
            NTEXT<1,-1>='The access log for this freight bill is as follows:'
            NTEXT<1,-1>='User ID''L#9 ':'Name''L#20 ':'Source''L#24 ':'Date''R#10   ':'Time''R#10' : ' Reason'           ; * NPR01
            NTEXT<1,-1>=DASH'L#9 ':DASH'L#20 ':DASH'L#24 ':DASH'R#10   ':DASH'R#10'
            NVAL=DCOUNT(ACCESS.LOG,@VM)
            FOR VAL=1 TO NVAL
               LOG.DATE=FIELD(ACCESS.LOG<1,VAL>,'*',1)
               LOG.TIME=FIELD(ACCESS.LOG<1,VAL>,'*',2)
               LOG.USER=ACCESS.LOG<2,VAL>
               LOG.TYPE=ACCESS.LOG<3,VAL>
               LOG.REAS=ACCESS.LOG<4,VAL>          ; * NPR01
               IF LOG.TYPE='IN' THEN
                  LOG.NAME='From WebSite'
               END ELSE
                  LOG.NAME=OCONV(LOG.USER,'TBCTRL;X;;13')
               END
               BEGIN CASE
                  CASE LOG.TYPE='AC' ; LOG.SRC='Accounting'
                  CASE LOG.TYPE='AU' ; LOG.SRC='Audit'
                  CASE LOG.TYPE='FP' ; LOG.SRC='Freight Payment'
                  CASE LOG.TYPE='IL' ; LOG.SRC='International Logistics'
                  CASE LOG.TYPE='IN' ; LOG.SRC='Internet'
                  CASE LOG.TYPE='IS' ; LOG.SRC='Information Systems'
                  CASE LOG.TYPE='LG' ; LOG.SRC='Logistics'
                  CASE LOG.TYPE='LT' ; LOG.SRC='LTL Logistics'
                  CASE LOG.TYPE='SA' ; LOG.SRC='Sales'
                  CASE LOG.TYPE='TP' ; LOG.SRC='Transportation'
                  CASE LOG.TYPE='TL' ; LOG.SRC='Transportation Logistics'
                  CASE 1 ; LOG.SRC=LOG.TYPE
               END CASE
               IF LOG.USER#'' THEN
                  NTEXT<1,-1>=LOG.USER'L#9 ':LOG.NAME'L#20 ':LOG.SRC'L#24 ':OCONV(LOG.DATE,'D4/')'R#10   ':OCONV(LOG.TIME,'MTHS')'R#10' : ' ':LOG.REAS           ; *NPR01
               END
            NEXT VAL
            MVIN=''
            CARRIER.FOUND=0
            LOCATE CARRIER.ID IN PARCEL.LIST<1> SETTING PPOS THEN
               CARRIER.FOUND=1
            END
            LOCATE CARRIER.ID IN LTL.LIST<1> SETTING LPOS THEN
               CARRIER.FOUND=1
            END
            LOCATE CARRIER.ID IN INTL.LIST<1> SETTING IPOS THEN
               CARRIER.FOUND=1
            END
            IF CARRIER.FOUND=0 THEN
            END
            MVIN<4>='AFS'
            MVIN<5>='Freight Bill Activity Alert for ':PRO.ID
            IF CARRIER.FOUND = 0 THEN
               MVIN<6> = "** CARRIER NOT ASSIGNED TO ANY ALERT LISTS **"
               MVIN<7> = NTEXT
            END ELSE
               MVIN<6>=NTEXT
            END
            IF MVIN<1>#'' THEN
               CALL SENDMAIL(MVIN,MVOUT)
            END
         END
      END
*
      RETURN
   END
