****************************************************
* Program name : UPD.FB.APPROVAL
* Author       : Greg Bino
* Date written : 06-29-2016
* Library      : FBBP
* COPYRIGHT (C) 1998 - AFS LOGISTICS LLC - ALL RIGHTS RESERVED.
*
* Update operator table
*
* Last updated by AFS (nmeddaugh) at 07:57:18 on 01/16/2020.
*
* Start modification history
* 011620 - NKH01qT - ACCU-733 Updated FB.BILLS.ACCESS 
* End modification history
*
*******************************************************
* 092011 - JMK01 - VERIFY APPROVAL DATE FOR ANY TYPE OF REJECT ACTION
* 120211 - JMK02 - SysAid#724 - Add Lee to Daily Update of Freight Bills for Wabtec
* 011212 - JMK03 - SysAid#71 - Wabtec 1977-Vitran Express 748 FB for approval
* 012412 - JMK04 - SysAid#1332 - Daily Update of Freight Bills From Web Review Service
* 071714 - DRF01 - SysAid 10744 - Added LaJuanyka to this report.
* 030915 - SWB01 - SysAid#13523 - Modifications for Wabtec Master Billing Report
*************************************************************************
*Notes:
*
*Be aware that REVIEW.STATUS.CODE 970 is used in LOAD.FB.REVIEW and
*FB.2.1.NW as an indicator that the bill is being pushed to the web.
*If you change or add any REVIEW.STATUS.CODES, be aware that it might
*affect FB.2.1.NW, LOAD.FB.REVIEW and BUILD.DF.REVIEW.SUB,
*all of which have logic based on codes 970 - 975.
*
*************************************************************************
      PROG.NAME='UPD.FB.APPROVAL'
      PROG.DESC='WEB APPROVAL PUSH'      ; * JMK03
      PACK.NAME='FREIGHT BILLING'        ; * JMK03
      CO.NAME='AFS Logistics, LLC'                 ; * JMK03
      TB=CHAR(9)
      CALL GET.USER(USER)
      * Begin GRB01
      VIN1=""
      VIN1<1> = "UPD.FB.APPROVAL"
      VIN1<2> = USER
      VIN1<3> = SYSTEM(18)
      VIN1<4> = "S"
      CALL PROGTRACK(VIN1,VOUT1)
* End GRB01
      BLOCK.TXT='PUSH TO SQL'
      OPEN '','FBR01977' TO F.FBR01977 ELSE
         CALL OPEN.ABORT('FBR01977',PROG.NAME)
      END
      OPEN '','FBR01977.HISTORY' TO F.FBR01977.HIST ELSE
         CALL OPEN.ABORT('FBR01977.HISTORY',PROG.NAME)
      END
      OPEN '','CARRIERS' TO F.CARRIERS ELSE
         CALL OPEN.ABORT('CARRIERS',PROG.NAME)
      END
      OPEN '','CLIENTS' TO F.CLIENTS ELSE
         CALL OPEN.ABORT('CLIENTS',PROG.NAME)
      END
      OPEN '','CLIENT.SCAC' TO F.CSCAC ELSE
         CALL OPEN.ABORT('CLIENT.SCAC',PROG.NAME)
      END
      OPEN '','FB.REVIEW' TO F.REVIEW ELSE
         CALL OPEN.ABORT('FB.REVIEW',PROG.NAME)
      END
      OPEN '','EMAILDATA' TO F.EMAILDATA ELSE
         CALL OPEN.ABORT('EMAILDATA',PROG.NAME)
      END
      OPEN '','FB.TABLES' TO F.TABLES ELSE
         CALL OPEN.ABORT('FB.TABLES',PROG.NAME)
      END
      OPEN '','BCTRL' TO F.BCTRL ELSE    ; * JMK03
         CALL OPEN.ABORT('BCTRL',PROG.NAME)        ; *JMK03
      END                                ; * JMK03

      READ BCTRL.REC FROM F.BCTRL,USER ELSE BCTRL.REC=''     ; * JMK03
      IF USER='SV.UVAUTO' THEN           ; * JMK03
         BCTRL.REC=''                    ; * JMK03
         BCTRL.REC<13>='UV Automated Process'      ; * JMK03
         BCTRL.REC<14>=''                ; * JMK03
         BCTRL.REC<15>='IS'              ; * JMK03
      END                                ; * JMK03
      USER.NAME=BCTRL.REC<13>            ; * JMK03
      IF BCTRL.REC<14>='A' THEN          ; * JMK03
         USER.TYPE='A'                   ; * JMK03
      END ELSE                           ; * JMK03
         USER.TYPE=''                    ; * JMK03
      END                                ; * JMK03
      USER.DPT=BCTRL.REC<15>             ; * JMK03

      STMT='SSELECT FBR01977'
      EXECUTE STMT RTNLIST FILE.LIST CAPTURING RESULTS
      TCNT=@SELECTED

      INT.TODAY=DATE()                   ; * SWB01
      APRV.ARRAY=''
      SUBJECT=''
      BODY.TEXT=''
      REJECT.ARRAY=''
*     REJECT.ARRAY<1,1>='Client#'              ; * SWB01
*     REJECT.ARRAY<1,2>='Freight Bill Pro'     ; * SWB01
*     REJECT.ARRAY<1,3>='Reject Reason'        ; * SWB01
*     REJECT.ARRAY<1,4>='Authorized Person'    ; * SWB01

      REJECT.ARRAY<1,1>='Client#'        ; * SWB01
      REJECT.ARRAY<1,2>='Review Status'  ; * SWB01
      REJECT.ARRAY<1,3>='Division Name'  ; * SWB01
      REJECT.ARRAY<1,4>='Division #'     ; * SWB01
      REJECT.ARRAY<1,5>='Carrier Number'           ; * SWB01
      REJECT.ARRAY<1,6>='Carrier Name'   ; * SWB01
      REJECT.ARRAY<1,7>='Bill Date'      ; * SWB01
      REJECT.ARRAY<1,8>='Pro Number'     ; * SWB01
      REJECT.ARRAY<1,9>='Bill Amt'       ; * SWB01
      REJECT.ARRAY<1,10>='Entered Date'  ; * SWB01
      REJECT.ARRAY<1,11>='Keyed Date'    ; * SWB01
      REJECT.ARRAY<1,12>='Rejected Date'           ; * SWB01
      REJECT.ARRAY<1,13>='Reject Reason'           ; * SWB01
      REJECT.ARRAY<1,14>='Authorized Person'       ; * SWB01
      REJECT.ARRAY<1,15>='Days Pending'  ; * SWB01

      EOF=0
      CNT=0
      CNT.APPROVE=0
      CNT.REJECT.NEVER=0
      CNT.REJECT.EVENT=0
      CNT.NO.REVIEW=0
      PER=''
      LOOP
         READNEXT FILE.ID FROM FILE.LIST ELSE EOF=1
      UNTIL EOF DO
         CNT+=1
         CALL PROG.BAR(20,CNT,TCNT,LIST.PER)
         IF OCONV(FILE.ID,'MCU')='HISTORY' THEN GO SKIP.FILE
         CLIENT.ID=FIELD(FILE.ID,'_',1)
         BLOCK.ID='NO-PROCESS*':CLIENT.ID
         FILE.DATE=FIELD(FILE.ID,'_',2)
         READ CLIENT.REC FROM F.CLIENTS,CLIENT.ID ELSE GO SKIP.FILE
         OPEN '','FB.BILLS,':CLIENT.ID TO F.BILLS ELSE
            CALL OPEN.ABORT('FB.BILLS,':CLIENT.ID,PROG.NAME)
         END
         OPEN '','FB.BILLS.HIST,':CLIENT.ID TO F.BILLS.HIST ELSE
            CALL OPEN.ABORT('FB.BILLS.HIST,':CLIENT.ID,PROG.NAME)
         END
         OPEN '','EDI.BILLS.HIST,':CLIENT.ID TO F.EDIH ELSE
            CALL OPEN.ABORT('EDI.BILLS.HIST,':CLIENT.ID,PROG.NAME)
         END
         OPEN '','DIVISION.CODES,':CLIENT.ID TO F.DIV ELSE
            CALL OPEN.ABORT('DIVISION.CODES,':CLIENT.ID,PROG.NAME)
         END
         OPEN '','VEND.CUST,':CLIENT.ID TO F.VC ELSE
            CALL OPEN.ABORT('VEND.CUST,':CLIENT.ID,PROG.NAME)
         END
         OPEN '','FB.BILLS.ACCESS,':CLIENT.ID TO F.ACCESS ELSE
            CALL OPEN.ABORT('FB.BILLS.ACCESS,':CLIENT.ID,PROG.NAME)
         END
         FWR.FOUND=0                     ; * SWB01
         OPEN '','FB.WEB.REVIEW,':CLIENT.ID TO F.WEB.REVIEW THEN FWR.FOUND=1     ; * SWB01
         READ FILE.REC FROM F.FBR01977,FILE.ID THEN
            FILE.ORG=FILE.REC
            FILE.REC=CHANGE(FILE.REC,TB,@VM)
            NATT=DCOUNT(FILE.REC,@AM)
            IF NATT=1 THEN GO SKIP.FILE
            FOR ATT=1 TO NATT
               CALL PROG.BAR(22,ATT,NATT,FILE.PER)
               IF ATT>1 THEN
                  APP.LINE=FILE.REC<ATT>
                  BILL.ID=TRIM(APP.LINE<1,1>)
                  PRO.NUM=FIELD(BILL.ID, '*', 2)   ; * SWB01
                  CARRIER.ID=FIELD(BILL.ID,'*',1)
                  READ CARRIER.REC FROM F.CARRIERS,CARRIER.ID ELSE CARRIER.REC=''
                  CARRIER.NAME=CARRIER.REC<1>      ; * SWB01
                  REVIEW.STATUS.CODE=TRIM(APP.LINE<1,2>)
                  APP.REASON=TRIM(APP.LINE<1,3>)
                  EXPENSE.CODE=TRIM(APP.LINE<1,4>)
                  APP.DATE=TRIM(APP.LINE<1,5>)
                  APP.DATE=ICONV(APP.DATE[5,2]:'-':APP.DATE[7,2]:'-':APP.DATE[1,4],'D')
                  APP.USER=OCONV(TRIM(APP.LINE<1,6>),'MCU')
                  READ BILL.REC FROM F.BILLS,BILL.ID THEN
                     BILL.ORG=BILL.REC
                     DIV.ID=BILL.REC<9>
                     READ DIV.REC FROM F.DIV,DIV.ID ELSE DIV.REC=''
                     DIV.NAME=DIV.REC<11>
                     LOCATE DIV.ID IN APRV.ARRAY<1> BY 'AR' SETTING DIVPOS ELSE
                        INS DIV.ID BEFORE APRV.ARRAY<1,DIVPOS>
                        INS DIV.NAME BEFORE APRV.ARRAY<2,DIVPOS>
                        INS '' BEFORE APRV.ARRAY<3,DIVPOS>   ; * APPROVAL DATE
                        INS '' BEFORE APRV.ARRAY<4,DIVPOS>   ; * 971
                        INS '' BEFORE APRV.ARRAY<5,DIVPOS>   ; * 972
                        INS '' BEFORE APRV.ARRAY<6,DIVPOS>   ; * 973
                        INS '' BEFORE APRV.ARRAY<7,DIVPOS>   ; * 974
                        INS '' BEFORE APRV.ARRAY<8,DIVPOS>   ; * EXPENSE CODE UPDATE
                     END
                     LOCATE APP.DATE IN APRV.ARRAY<3,DIVPOS> BY 'AR' SETTING DTPOS ELSE
                        INS APP.DATE BEFORE APRV.ARRAY<3,DIVPOS,DTPOS>
                        INS 0 BEFORE APRV.ARRAY<4,DIVPOS,DTPOS>
                        INS 0 BEFORE APRV.ARRAY<5,DIVPOS,DTPOS>
                        INS 0 BEFORE APRV.ARRAY<6,DIVPOS,DTPOS>
                        INS 0 BEFORE APRV.ARRAY<7,DIVPOS,DTPOS>
                        INS 0 BEFORE APRV.ARRAY<8,DIVPOS,DTPOS>
                     END
                     APRV.ARRAY<9>+=1
                     IF EXPENSE.CODE#'' THEN
                        BILL.REC<5>=EXPENSE.CODE
                        BILL.REC<31>=EXPENSE.CODE
                        APRV.ARRAY<8,DIVPOS,DTPOS>+=1
                     END
                     BEGIN CASE
                        CASE REVIEW.STATUS.CODE='971'        ; * APPROVED
                           CNT.APPROVE+=1
                           BILL.REC<37>=''
                           BILL.REC<39>='O'
                           BILL.REC<66>=''
                           BILL.REC<241>=APP.DATE
                           APRV.ARRAY<4,DIVPOS,DTPOS>+=1
*                       CASE REVIEW.STATUS.CODE='972'        ; * REJECT NEVER PAY ; * JMK01
                        CASE REVIEW.STATUS.CODE='972' AND APP.DATE # "" AND BILL.REC<39>="P"         ; * REJECT NEVER PAY ; * JMK01
                           CNT.REJECT.NEVER+=1
                           BILL.REC<37>=''
                           BILL.REC<39>='R'
                           BILL.REC<61>=CHANGE(APP.REASON,'|',' ')
                           NVAL=DCOUNT(BILL.REC<61>,@VM)
                           FOR VAL=1 TO NVAL
                              BILL.REC<62,VAL,1>=APP.USER
                              BILL.REC<62,VAL,2>=APP.DATE
                           NEXT VAL
                           BILL.REC<66>='N'
*                          BILL.REC<241>=APP.DATE
                           APRV.ARRAY<5,DIVPOS,DTPOS>+=1
                           REJECT.LINE=''
*                          REJECT.LINE<1,1>=CLIENT.ID       ; * SWB01
*                          REJECT.LINE<1,2>=BILL.ID         ; * SWB01
*                          REJECT.LINE<1,3>=APP.REASON      ; * SWB01
*                          REJECT.LINE<1,4>=APP.USER        ; * SWB01
                           REJECT.LINE<1,1>=CLIENT.ID        ; * SWB01
                           REJECT.LINE<1,2>='REJ NP'         ; * SWB01
                           REJECT.LINE<1,3>=DIV.NAME         ; * SWB01
                           REJECT.LINE<1,4>=DIV.ID           ; * SWB01
                           REJECT.LINE<1,5>=CARRIER.ID       ; * SWB01
                           REJECT.LINE<1,6>=CARRIER.NAME     ; * SWB01
                           REJECT.LINE<1,7>=OCONV(BILL.REC<1>,'D4/')   ; * SWB01
                           REJECT.LINE<1,8>=PRO.NUM          ; * SWB01
                           REJECT.LINE<1,9>=OCONV(BILL.REC<8>,'MD2Z')  ; * SWB01
                           REJECT.LINE<1,10>=OCONV(BILL.REC<30>,'D4/')           ; * SWB01
                           REJECT.LINE<1,11>=OCONV(BILL.REC<247>,'D4/')          ; * SWB01
                           REJECT.LINE<1,12>=OCONV(APP.DATE,'D4/')     ; * SWB01
                           REJECT.LINE<1,13>=APP.REASON      ; * SWB01
                           REJECT.LINE<1,14>=APP.USER        ; * SWB01
                           REJECT.ARRAY<-1>=REJECT.LINE
*                       CASE REVIEW.STATUS.CODE='973'        ; * REJECT EVENTUAL PAY ; * JMK01
                        CASE REVIEW.STATUS.CODE='973' AND APP.DATE # "" AND BILL.REC<39>="P"         ; * REJECT EVENTUAL PAY ; * JMK01
                           CNT.REJECT.EVENT+=1
                           BILL.REC<37>=''
                           BILL.REC<39>='R'
                           BILL.REC<61>=CHANGE(APP.REASON,'|',' ')
                           NVAL=DCOUNT(BILL.REC<61>,@VM)
                           FOR VAL=1 TO NVAL
                              BILL.REC<62,VAL,1>=APP.USER
                              BILL.REC<62,VAL,2>=APP.DATE
                           NEXT VAL
                           BILL.REC<66>='E'
*                          BILL.REC<241>=APP.DATE
                           APRV.ARRAY<6,DIVPOS,DTPOS>+=1
                           REJECT.LINE=''
*                          REJECT.LINE<1,1>=CLIENT.ID       ; * SWB01
*                          REJECT.LINE<1,2>=BILL.ID         ; * SWB01
*                          REJECT.LINE<1,3>=APP.REASON      ; * SWB01
*                          REJECT.LINE<1,4>=APP.USER        ; * SWB01
                           REJECT.LINE<1,1>=CLIENT.ID        ; * SWB01
                           REJECT.LINE<1,2>='EP'   ; * SWB01
                           REJECT.LINE<1,3>=DIV.NAME         ; * SWB01
                           REJECT.LINE<1,4>=DIV.ID           ; * SWB01
                           REJECT.LINE<1,5>=CARRIER.ID       ; * SWB01
                           REJECT.LINE<1,6>=CARRIER.NAME     ; * SWB01
                           REJECT.LINE<1,7>=OCONV(BILL.REC<1>,'D4/')   ; * SWB01
                           REJECT.LINE<1,8>=PRO.NUM          ; * SWB01
                           REJECT.LINE<1,9>=OCONV(BILL.REC<8>,'MD2Z')  ; * SWB01
                           REJECT.LINE<1,10>=OCONV(BILL.REC<30>,'D4/')           ; * SWB01
                           REJECT.LINE<1,11>=OCONV(BILL.REC<247>,'D4/')          ; * SWB01
                           REJECT.LINE<1,12>=OCONV(APP.DATE,'D4/')     ; * SWB01
                           REJECT.LINE<1,13>=APP.REASON      ; * SWB01
                           REJECT.LINE<1,14>=APP.USER        ; * SWB01
                           REJECT.ARRAY<-1>=REJECT.LINE
                        CASE REVIEW.STATUS.CODE='974'        ; * NOT REVIEWED
                           CNT.NO.REVIEW+=1
                           BILL.REC<37>=''
                           BILL.REC<39>='P'
                           APRV.ARRAY<7,DIVPOS,DTPOS>+=1
                     END CASE
                     BODY.TEXT=''
                     BODY.TEXT<1,-1>=APRV.ARRAY<9>+0'R#5':' freight bills have been updated from the web review service as follows:'
                     BODY.TEXT<1,-1>=' '
                     NVAL=DCOUNT(APRV.ARRAY<1>,@VM)
                     FOR VAL=1 TO NVAL
                        BODY.TEXT<1,-1>=APRV.ARRAY<1,VAL>:' - ':APRV.ARRAY<2,VAL>
                        NSVAL=DCOUNT(APRV.ARRAY<3,VAL>,@SVM)
                        FOR SVAL=1 TO NSVAL
                           IF APRV.ARRAY<4,VAL,SVAL>+0#0 THEN
                              BODY.TEXT<1,-1>=APRV.ARRAY<4,VAL,SVAL>'R#5':' bills approved on ':APRV.ARRAY<3,VAL,SVAL>'D4/'
                           END
                           IF APRV.ARRAY<5,VAL,SVAL>+0#0 THEN
                              BODY.TEXT<1,-1>=APRV.ARRAY<5,VAL,SVAL>'R#5':' bills rejected to never pay on ':APRV.ARRAY<3,VAL,SVAL>'D4/'
                           END
                           IF APRV.ARRAY<6,VAL,SVAL>+0#0 THEN
                              BODY.TEXT<1,-1>=APRV.ARRAY<6,VAL,SVAL>'R#5':' bills rejected to eventually pay on ':APRV.ARRAY<3,VAL,SVAL>'D4/'
                           END
                           IF APRV.ARRAY<7,VAL,SVAL>+0#0 THEN
                              BODY.TEXT<1,-1>=APRV.ARRAY<7,VAL,SVAL>'R#5':' bills not reviewed on ':APRV.ARRAY<3,VAL,SVAL>'D4/'
                           END
                           IF APRV.ARRAY<8,VAL,SVAL>+0#0 THEN
                              BODY.TEXT<1,-1>=APRV.ARRAY<8,VAL,SVAL>'R#10':' expense codes updated on ':APRV.ARRAY<3,VAL,SVAL>'D4/'
                           END
                        NEXT SVAL
                        BODY.TEXT<1,-1>=' '
                     NEXT VAL
                     IF BILL.REC#BILL.ORG THEN
                        NVAL=DCOUNT(BILL.REC<84>,@VM)
                        IF APP.DATE#'' THEN        ; * JMK01
                           IF BILL.REC<84,NVAL>#APP.USER AND BILL.REC<86,NVAL>#APP.DATE THEN
                              BILL.REC<84,-1>=APP.USER
                              BILL.REC<85,-1>=72000          ; * 8:00 PM
                              BILL.REC<86,-1>=APP.DATE
                           END
                        END              ; * JMK01
                        WRITE BILL.REC ON F.BILLS,BILL.ID
                        WRITE BILL.REC ON F.BILLS.HIST,BILL.ID

                        AVIN=''          ; * JMK03
                        AVOUT=''         ; * JMK03
                        AVIN<1>=CLIENT.ID          ; * JMK03
                        AVIN<2>=BILL.ID  ; * JMK03
                        AVIN<3>=USER     ; * JMK03
                        AVIN<4>=USER.DPT           ; * JMK03
                        AVIN<5>=DATE()   ; * JMK03
                        AVIN<6>=TIME()   ; * JMK03
                        AVIN<7>='1'      ; * JMK03
                        AVIN<8>=PROG.NAME          ; * JMK03
                        AVIN<9>=PROG.DESC          ; * JMK03
                        AVIN<10>=PACK.NAME         ; * JMK03
                        AVIN<11>=CO.NAME           ; * JMK03
                        ACCESS.REASON=REVIEW.STATUS.CODE     ; * JMK03
                        W.REASON = ACCESS.REASON   ; * NKH01
*                        IF USER.TYPE='' THEN W.REASON = ACCESS.REASON ELSE W.REASON = ACCESS.REASON:"_":USER.TYPE        ; * JMK03  ; * NKH01
                        AVIN<12> = W.REASON        ; * JMK03
                        CALL UPD.FB.BILLS.ACCESS(AVIN,AVOUT,CLIENT.REC,CARRIER.REC,BILL.ORG,F.ACCESS,F.TABLES)           ; * JMK03

                        IF FWR.FOUND THEN          ; * SWB01
                           FWR.REC.CNT=0           ; * SWB01
                           FWR.REC.FOUND=1         ; * SWB01
                           LOOP          ; * SWB01
                              FWR.REC.CNT += 1     ; * SWB01
                              READU FWR.REC FROM F.WEB.REVIEW,BILL.ID:'_':FWR.REC.CNT THEN           ; * SWB01
                                 RELEASE F.WEB.REVIEW,BILL.ID:'_':FWR.REC.CNT    ; * SWB01
                              END ELSE   ; * SWB01
                                 FWR.REC.FOUND=0   ; * SWB01
                                 FWR.REC=''        ; * SWB01
                              END        ; * SWB01
                           UNTIL NOT(FWR.REC.FOUND) DO       ; * SWB01
                           REPEAT        ; * SWB01
                           FWR.REC<1>=APP.DATE     ; * SWB01
                           FWR.REC<2>=REVIEW.STATUS.CODE     ; * SWB01
                           FWR.REC<3>=DIV.ID       ; * SWB01
                           FWR.REC<4>=BILL.ORG<30>           ; * SWB01
                           FWR.REC<5>=APP.DATE     ; * SWB01
                           FWR.REC<6>=APP.USER         ; * SWB01
                           FWR.REC<7>=APP.REASON   ; * SWB01
                           FWR.REC<8>=BILL.ORG<1>  ; * SWB01
                           FWR.REC<9>=BILL.ORG<247>          ; * SWB01
                           FWR.REC<10>='PFW'       ; * SWB01
                           WRITE FWR.REC ON F.WEB.REVIEW,BILL.ID:'_':FWR.REC.CNT           ; * SWB01
                        END              ; * SWB01
                     END
                  END
               END
            NEXT ATT
         END
         WRITE FILE.ORG ON F.FBR01977.HIST,FILE.ID
         DELETE F.FBR01977,FILE.ID
         IF SUBJECT='' THEN
            SUBJECT=OCONV(TRIM('DAILY UPDATE OF FREIGHT BILLS FROM WEB REVIEW SERVICE'),'MCT')
         END
         IF CNT.APPROVE>0 OR CNT.NO.REVIEW>0 THEN
            READ TEST FROM F.TABLES,BLOCK.ID ELSE
               WRITE BLOCK.TXT ON F.TABLES,BLOCK.ID
               IF SUBJECT='' THEN
                  SUBJECT=OCONV(TRIM('WEEKLY UPDATE OF FREIGHT BILLS FROM WEB REVIEW SERVICE'),'MCT')
               END
            END
         END
SKIP.FILE:*
      REPEAT
      IF REJECT.ARRAY#'' THEN
         REJECT.ARRAY=CHANGE(REJECT.ARRAY,@VM,TB)
         REJECT.FILE.ID='WABTEC_REJECTS_':FILE.DATE:'.XLS'
         WRITE REJECT.ARRAY ON F.EMAILDATA,REJECT.FILE.ID
      END
      IF BODY.TEXT#'' THEN
         EMAIL.OUT=''

         EMAIL.OUT<1,-1>='wabtecwebrejectedbills@afs.net'    ; * SWB01; * JMK@@@
         EMAIL.OUT<2,-1>='DFriend@afs.net'
         EMAIL.OUT<4>='UPD.FB.APPROVAL@afs.net'
         EMAIL.OUT<5>=SUBJECT
         EMAIL.OUT<6>=BODY.TEXT
         EMAIL.OUT<7>=1
         EMAIL.OUT<11>='E:\ARDENT\PUBLIC\EMAILDATA\':REJECT.FILE.ID
         CALL SENDMAIL(EMAIL.OUT,RESULT)
      END
*************************************************************************      
LOGIT:
*************************************************************************      
       * Begin GRB01
      VIN1=""
      VIN1<1> = "UPD.FB.APPROVAL"
      VIN1<2> = USER
      VIN1<3> = SYSTEM(18)
      VIN1<4> = "E"
      CALL PROGTRACK(VIN1,VOUT1)
* End GRB01
      CHAIN "OFF"
   END
