      SUBROUTINE RERATE.ACC.SUB.02427(RVIN,RVOUT)
*****************************************************************************
* Program name : RERATE.ACC.SUB.02427
* Author       : Owen Holman
* Date written : 06-15-2001
* Library      : FBBP
* COPYRIGHT (C) 2016 - AFS LOGISTICS LLC - ALL RIGHTS RESERVED.
*
* Springs Window Fashion Monthly Accrual Report
*
* Last updated by AFS (nmeddaugh) at 07:57:18 on 05/09/2021.
*
* Start modification history
* 020916 - DRF01 - SysAid 22638 gave Melinda (MFEASTER) access to this program
* 012517 - GRB01 - Added operator table
* 040918 - NKH01 - Modified to call Carrier Connect XL
* 092018 - NKH02 - Ticket ACCU-44 Use the substitute client for FB.AUDIT.ACC file
* 050721 - NKH03 - ACCU-1155 Check for FAIL from Carrier Connect before Rating
* End modification history
*
******************************************************************************
 CALL GET.USER.NEW(USER,CH.NUM)
      UPL.VIN = 'FBBP' ; UPL.VIN<2> = 'RERATE.ACC.SUB.02427' ; UPL.VOUT = ''     ; * NPR_UPL 04/23/2010
       UPL.VIN<2> = USER
      UPL.VIN<3> = CH.NUM
      CALL UPD.PROGRAM.LOG(UPL.VIN,UPL.VOUT)       ; * NPR_UPL 04/23/2010
      CRT @(0,20):@(-3):
      TEXT='Reviewing Rates...'
      CALL CENTER(TEXT,80)
      CRT @(0,20):TEXT
      DATA.START=2                       ; * Line number that 1st line of data is on
      BILLDATA.USER=RVIN<1>              ; * Name of BILLDATA FILE THAT DATA IS IN
      ITEM.NAME=RVIN<2>                  ; * Name of "File" in BILLDATA
      CLIENT.OPT='02427'
      CARRIER.OPT='S'
      RR.EFF.DATE=DATE()

      CLIENT.AUDIT=1
      ERROR=''
      PROG.NAME='RERATE.ACC.SUB.02427'
      PROG.DESC="(Re) Rate freight on shipments using OLA setups"
      PACK.NAME="Freight Billing"
      CONTR.AMT=''
      BENCH.AMT=''
      CARRIER.EST.ARRAY=''
      ERROR.ARRAY=''
      VALID.CLASSES=50:@VM:55:@VM:60:@VM:65:@VM:70:@VM:77:@VM:85:@VM:92:@VM:100:@VM:110:@VM:125:@VM:150:@VM:175:@VM:200:@VM:250:@VM:300:@VM:400:@VM:500
      FIRST.TIME=1      ; * NKH01

*
* Open general files
*
      OPEN 'BILLDATA.':BILLDATA.USER TO F.BILLDATA ELSE
         CALL OPEN.ABORT("BILLDATA.":BILLDATA.USER,PROG.NAME)
      END
      OPEN '','CLIENTS' TO F.CLIENTS ELSE
         ERROR = "CANNOT OPEN FILE CLIENTS"
         GO 9000
      END
      OPEN '','FB.TABLES' TO F.TABLES ELSE
         ERROR = "CANNOT OPEN FILE F.TABLES"
         GO 9000
      END
      OPEN '','CARRIERS' TO F.CARRIERS ELSE
         ERROR = "CANNOT OPEN FILE CARRIERS"
         GO 9000
      END

      OPEN '','FB.BC.FSC' TO F.NFSC ELSE
         ERROR = "CANNOT OPEN FILE FB.BC.FSC"
         GO 9000
      END

      OPEN '','FB.TABLES' TO F.TABLES ELSE
         ERROR = "CANNOT OPEN FILE FB.TABLES"
         GO 9000
      END

      OPEN '','SCAC.XREF' TO F.SCAC.XREF ELSE
         ERROR = "CANNOT OPEN FILE SCAC.XREF"
         GO 9000
      END

*---- Open mileage input and output files

      OPEN '','PCIN' TO F.PCIN ELSE
         ERROR = "CANNOT OPEN FILE PCIN"
         GO 9000
      END
      OPEN '','PCOUT' TO F.PCOUT ELSE
         ERROR = "CANNOT OPEN FILE  "
         GO 9000
      END
      OPEN '','ZIPS' TO ZIPS ELSE                 ; * NKH01
         CALL OPEN.ABORT('ZIPS','RERATE.SS.SUB')  ; * NKH01
      END                                         ; * NKH01                  
      OPEN '','ZIPS.CODES' TO F.ZIPS ELSE
         ERROR = "CANNOT OPEN ZIPS FILE "
         GO 9000
      END
      OPEN '','CAN.ZIP.CODES' TO F.CAN.ZIPS ELSE
         ERROR = "CANNOT OPEN CAN.ZIP.CODES FILE "
         GO 9000
      END
      OPEN '','FTP.02427.ACC' TO F.FTP ELSE
*OPEN '','BILLDATA.':BILLDATA.USER TO F.FTP ELSE   ; * For testing
         ERROR = "CANNOT OPEN FTP.02427.ACC FILE "
         GO 9000
      END
      OPEN '','FTP.02427.ACC.HIST' TO F.FTP.HIST ELSE
*OPEN '','BILLDATA.':BILLDATA.USER TO F.FTP.HIST ELSE ; * For Testing
         ERROR = "CANNOT OPEN FTP.02427.ACC.HIST FILE "
         GO 9000
      END
      OPEN '','FTP.02427.ACC.SEND' TO F.FTP.SEND ELSE
         ERROR = "CANNOT OPEN FTP.02427.ACC.SEND FILE "
         GO 9000
      END
*
* Read Source Data
*
      READ DA FROM F.FTP,ITEM.NAME ELSE
         ERROR = 'FTP.02427.ACC ':ITEM.NAME:' is not on file.. Aborting!'
         GO 9000
      END
      WRITE DA ON F.FTP.HIST,ITEM.NAME
      NROWS=DCOUNT(DA,@AM)
      ITEM.SFX=OCONV(FIELD(ITEM.NAME,'.',2),'MCU')
      IF ITEM.SFX = "CSV" THEN
         DA.OUT=''
         DA.IN=DA
         FOR ROW=1 TO NROWS
            ROW.IN=''
            ROW.OUT=DA.IN<ROW>
            CALL CONVERT.STRING(ROW.IN,ROW.OUT,'CSV')
            DA.OUT<ROW>=ROW.IN
         NEXT ROW
         DA=DA.OUT
      END

      TAB=CHAR(9)
      DA=CHANGE(DA,TAB,@VM)
      NEW.DA=''
      NCOLS=DCOUNT(DA<1>,@VM)
      HDG.LINE=DA<1>
      LOCATE 'ShipDate' IN HDG.LINE<1> SETTING DATEPOS ELSE DATEPOS=13
      LOCATE 'SCACCode' IN HDG.LINE<1> SETTING SCACPOS ELSE SCACPOS=18
      LOCATE 'FromZip' IN HDG.LINE<1> SETTING OPOS ELSE OPOS=20
      LOCATE 'ToZip' IN HDG.LINE<1> SETTING DPOS ELSE DPOS=21
      LOCATE 'Weight' IN HDG.LINE<1> SETTING WPOS ELSE WPOS=22
      LOCATE 'Charge' IN HDG.LINE<1> SETTING CHGPOS ELSE CHGPOS=23
      LOCATE 'Error' IN HDG.LINE<1> SETTING ERRPOS ELSE ERRPOS=24
      HDG.LINE<1,CHGPOS>='Charge'
      HDG.LINE<1,ERRPOS>='Error'
      NEW.DA<1>=HDG.LINE


      CARPOS=''
*
* Process the data
*
      BRK.CARRIER=''
      PRV.CLIENT.ID=''
      BRK.PRO=''
      ROW.PER=''
      FOR ROW = 1 TO NROWS
         CALL PROG.BAR(22,ROW,NROWS,ROW.PER)
         IF ROW < DATA.START THEN
            NEW.DA<ROW>=DA<ROW>
            IF ROW='1' THEN
               NEW.DA<ROW>=HDG.LINE
            END
            GO SKIP.LINE
         END
         LINE=DA<ROW>
         CLIENT.ID=CLIENT.OPT
         IF CLIENT.ID#PRV.CLIENT.ID THEN
*
* Open client's files.
*
            AUDIT.CLIENT=1
            AUDIT.CLIENT.ID = CLIENT.ID
            OPEN '','FB.AUDIT,':CLIENT.ID TO F.AUDIT THEN
               READV SUB.CLIENT FROM F.AUDIT,'SUBSTITUTE',1 THEN
                  AUDIT.CLIENT.ID = SUB.CLIENT
                  OPEN '','FB.AUDIT,':AUDIT.CLIENT.ID TO F.AUDIT ELSE CLIENT.AUDIT=0
               END ELSE
                  AUDIT.CLIENT.ID = CLIENT.ID
               END
            END ELSE CLIENT.AUDIT=0
            IF CLIENT.AUDIT=0 THEN
               ERROR = "Client ":CLIENT.ID:" is not setup for freight estimates"
               NEW.DA<-1>=LINE
            END
            OPEN '','FB.AUDIT.TABLES,':AUDIT.CLIENT.ID TO F.AUDIT.TABLES ELSE
               ERROR = "Cannot open file FB.AUDIT.TABLES,":AUDIT.CLIENT.ID
               NEW.DA<-1>=LINE
            END
            OPEN '','FB.AUDIT.ZIPS,':AUDIT.CLIENT.ID TO F.AUDIT.ZIPS ELSE
               ERROR = "Cannot open FB.AUDIT.ZIPS,":AUDIT.CLIENT.ID:" No On-Line Audit Allowed!"
               NEW.DA<-1>=LINE
            END
            OPEN '','FB.BILLS.HIST,':AUDIT.CLIENT.ID TO F.BILLS ELSE
               ERROR = "Cannot open FB.BILLS.HIST,":AUDIT.CLIENT.ID:" No On-Line Audit Allowed!"
               NEW.DA<-1>=LINE
            END
            PRV.CLIENT.ID=CLIENT.ID
         END

         ERROR=''
         SCAC=TRIM(LINE<1,SCACPOS>)
         SHIP.DATE=ICONV(TRIM(LINE<1,DATEPOS>),'D')
         ORIG.ZIP=TRIM(LINE<1,OPOS>)
         DEST.ZIP=TRIM(LINE<1,DPOS>)
         WEIGHT=TRIM(LINE<1,WPOS>)
         CLASS='70'
         BEGIN CASE
            CASE SCAC='AIIH'
               READV CARRIER.ID FROM F.SCAC.XREF,'AITW',1 ELSE CARRIER.ID='13591'
            CASE 1
               READV CARRIER.ID FROM F.SCAC.XREF,SCAC,1 ELSE CARRIER.ID=''
         END CASE
         ORIG.ZIP=CHANGE(FIELD(LINE<1,OPOS>,'-',1),' ','')
         CONVERT " " TO "" IN ORIG.ZIP
         IF NUM(ORIG.ZIP) AND ORIG.ZIP#'' THEN
            IF LEN(ORIG.ZIP)>5 THEN
               ORIG.ZIP=ORIG.ZIP[1,5]
            END ELSE
               ORIG.ZIP=ORIG.ZIP'R%5'
            END
         END
         DEST.ZIP=CHANGE(FIELD(LINE<1,DPOS>,'-',1),' ','')
         CONVERT " " TO "" IN DEST.ZIP
         IF NUM(DEST.ZIP) AND DEST.ZIP#'' THEN
            IF LEN(DEST.ZIP)>5 THEN
               DEST.ZIP=DEST.ZIP[1,5]
            END ELSE
               DEST.ZIP=DEST.ZIP'R%5'
            END
         END
* Classes, Weights, & Codes
         PRV.CLASS=''
         CLASSES='70'
         WEIGHTS=WEIGHT
         CLS.CNT=0
         MISC.CHG.CODES=''
         MISC.CHG.AMTS=''
         A.VOUT=''
         FSC.CVOUT=''
         FSC.BVOUT=''
         MODE='EFF'
         IF SHIP.DATE<ICONV('08/04/14','D') THEN
            ORATE.DATE='08/04/14'
         END ELSE
            ORATE.DATE=OCONV(SHIP.DATE,'D2/')
         END
         VIN.ARRAY=''
         IF CARRIER.OPT='A' THEN
            CARRIER.ID='99990'
            CLIENT.ID='02427'
            IF CLIENT.ID#PRV.CLIENT.ID THEN
*
* Open client's files.
*
               AUDIT.CLIENT=1
               AUDIT.CLIENT.ID = CLIENT.ID
               OPEN '','FB.AUDIT,':CLIENT.ID TO F.AUDIT THEN
                  READV SUB.CLIENT FROM F.AUDIT,'SUBSTITUTE',1 THEN
                     AUDIT.CLIENT.ID = SUB.CLIENT
                     OPEN '','FB.AUDIT,':AUDIT.CLIENT.ID TO F.AUDIT ELSE CLIENT.AUDIT=0
                  END ELSE
                     AUDIT.CLIENT.ID = CLIENT.ID
                  END
               END ELSE CLIENT.AUDIT=0
               IF CLIENT.AUDIT=0 THEN
                  ERROR = "Client ":CLIENT.ID:" is not setup for freight estimates"
                  NEW.DA<-1>=LINE
               END
               OPEN '','FB.AUDIT.TABLES,':AUDIT.CLIENT.ID TO F.AUDIT.TABLES ELSE
                  ERROR = "Cannot open file FB.AUDIT.TABLES,":AUDIT.CLIENT.ID
                  NEW.DA<-1>=LINE
               END
               OPEN '','FB.AUDIT.ZIPS,':AUDIT.CLIENT.ID TO F.AUDIT.ZIPS ELSE
                  ERROR = "Cannot open FB.AUDIT.ZIPS,":AUDIT.CLIENT.ID:" No On-Line Audit Allowed!"
                  NEW.DA<-1>=LINE
               END
               OPEN '','FB.BILLS.HIST,':AUDIT.CLIENT.ID TO F.BILLS ELSE
                  ERROR = "Cannot open FB.BILLS.HIST,":AUDIT.CLIENT.ID:" No On-Line Audit Allowed!"
                  NEW.DA<-1>=LINE
               END
               PRV.CLIENT.ID=CLIENT.ID
            END

         END
         BENCH.AMT=''
         CONTR.AMT=''
         BENCH.FSC.AMT=''
         CONTR.FSC.AMT=''
         QUOTE.AMT=''
         EFF.BENCH.AMT=''
         EFF.CONTR.AMT=''
         EFF.BENCH.FRT.AMT=''
         EFF.BENCH.FSC.AMT=''
         EFF.CONTR.FRT.AMT=''
         EFF.CONTR.FSC.AMT=''
         EFF.CONS.FRT.AMT=''
         EFF.CONS.FSC.AMT=''
         EFF.CARRIER.ID=''
         EFF.CARRIER.NAME=''
         CARRIER.EST.ARRAY=''
         GOSUB RATE.SHIPMENT
IF USER='DAVID' AND CARRIER.ID='02653' THEN 
CALL ELINE('PRESS BREAK')
END
         IF CARRIER.OPT='L' THEN
            BENCH.AMT=CARRIER.EST.ARRAY<8,1>
            CONTR.AMT=CARRIER.EST.ARRAY<10,1>
            BENCH.FSC.AMT=CARRIER.EST.ARRAY<9,1>
            CONTR.FSC.AMT=CARRIER.EST.ARRAY<11,1>
            QUOTE.AMT=FIELD(CARRIER.EST.ARRAY<1,1>,'_',1)
            LOG.COMM=CARRIER.EST.ARRAY<14,1>
            QUOTE.AMT=CONTR.AMT+LOG.COMM
         END
         EFF.BENCH.AMT=BENCH.AMT
         EFF.CONTR.AMT=CONTR.AMT
         EFF.BENCH.FRT.AMT=BENCH.AMT-BENCH.FSC.AMT
         EFF.BENCH.FSC.AMT=BENCH.FSC.AMT
         EFF.CONTR.FRT.AMT=CONTR.AMT-CONTR.FSC.AMT
         EFF.CONTR.FSC.AMT=CONTR.FSC.AMT
         EFF.CONS.FRT.AMT=EFF.BENCH.FRT.AMT-EFF.CONTR.FRT.AMT
         EFF.CONS.FSC.AMT=EFF.BENCH.FSC.AMT-EFF.CONTR.FSC.AMT
         EFF.CARRIER.ID=CARRIER.EST.ARRAY<2,1>
         EFF.CARRIER.NAME=CARRIER.EST.ARRAY<3,1>
         IF EFF.BENCH.AMT+0=0 AND EFF.CONTR.AMT+0=0 THEN
            EFF.CARRIER.ID='None'
            EFF.CARRIER.NAME='No Approved Carriers for this Route'
         END
         IF CLIENT.OPT='02427' THEN
            LINE<1,CHGPOS>=QUOTE.AMT'R26'
         END
         IF ERROR#'' THEN
            LINE<1,ERRPOS>=ERROR
         END

         NEW.DA<-1>=LINE
SKIP.LINE:****
      NEXT ROW
      IF ITEM.SFX = "CSV" THEN
         DA.OUT=NEW.DA
         DA.IN=''
         FOR ROW=1 TO NROWS
            ROW.IN=DA.OUT<ROW>
            ROW.OUT=''
            CALL CONVERT.STRING(ROW.IN,ROW.OUT,'CSV')
            DA.IN<ROW>=ROW.OUT
         NEXT ROW
         NEW.DA=DA.IN
      END
      NEW.DA=CHANGE(NEW.DA,@VM,TAB)
*DF      WRITE NEW.DA ON F.FTP,'RR-':ITEM.NAME
      WRITE NEW.DA ON F.FTP.SEND,'RR-':ITEM.NAME
      WRITE NEW.DA ON F.FTP.HIST,'RR-':ITEM.NAME
      RVOUT<1>='RR-':ITEM.NAME
      ERROR=''
      RETURN
      STOP
***************************************************      
RATE.SHIPMENT:***
*************************************************** 
      RATE.DATE=ICONV(ORATE.DATE,'D')
      IF RATE.DATE='' THEN
         ERROR='Invaid Ship Date of ':ORATE.DATE
         RETURN
      END
      IF RATE.DATE > DATE() THEN
         ERROR='Ship date cannot be in the future. Use today as the date.'
         RETURN
      END
      NUM.WEIGHTS = DCOUNT(WEIGHTS,@VM)
      TOT.WEIGHT = 0
      TOT.WEIGHT = SUM(WEIGHTS)
      IF TOT.WEIGHT GT 15000 THEN
         ERROR = "Total weight exceeds program limits. Please contact AFS"
         RETURN
      END
      NVAL1=DCOUNT(CLASSES<1>,@VM)
      NVAL=NVAL1
      NVAL2=DCOUNT(WEIGHTS<1>,@VM)
      IF NVAL2>NVAL THEN NVAL=NVAL2
      FOR VAL=1 TO NVAL
         CLS=CLASSES<1,VAL>
         IF CLS='' THEN
            ERROR = 'Weight #':VAL:' does not have a class code assigned to it.'
            RETURN
         END
         LOCATE CLS IN VALID.CLASSES<1> SETTING POS ELSE
            ERROR = CLS:' is an invalid class.'
            RETURN
         END
         WGT=WEIGHTS<1,VAL>
         IF WGT+0=0 THEN
            ERROR = 'Class #':VAL:' does not have a valid weight assigned to it.'
            RETURN
         END
      NEXT VAL
*
* READ TABLES THAT MAY BE NEEDED
*
      READ DTARIFF.DATE.XREF FROM F.TABLES,'TARIFF.DATE.XREF' ELSE DTARIFF.DATE.XREF=''
      READ DTARIFFS FROM F.TABLES,'TARIFFS' ELSE DTARIFFS=''
      READ LI.CODES FROM F.TABLES,'LI.CODES' ELSE LI.CODES=''
      READ DCLIENTS FROM F.CLIENTS,CLIENT.ID ELSE
         ERROR = "Client ":CLIENT.ID:" not on file."
         RETURN
      END
      CL.LOG.FSC=DCLIENTS<111>
      LOGISTICS=DCLIENTS<93>='Y'
      CARR.ONLY.FSC=DCLIENTS<189>
      MAX.LTL=15000
      IF DCLIENTS<150>#'' THEN MAX.LTL=DCLIENTS<150>
      IN.QUOTE.PERC = DCLIENTS<136>
      OUT.QUOTE.PERC = DCLIENTS<137>
      TP.QUOTE.PERC = DCLIENTS<138>
      IOT='O'
      BEGIN CASE
         CASE IOT = "I"
            IOT.DESC = "Inbound"
            SAVINGS.QUOTE.PERC=IN.QUOTE.PERC
         CASE IOT = "O"
            IOT.DESC = "Outbound"
            SAVINGS.QUOTE.PERC=OUT.QUOTE.PERC
         CASE IOT = "T"
            IOT.DESC = "Third Party"
            SAVINGS.QUOTE.PERC=TP.QUOTE.PERC
      END CASE
      IF SAVINGS.QUOTE.PERC = 'NO' THEN
         ERROR="Cannot Rate! Quote percent has not been set up."
         RETURN
      END


*
* Select Carrier(s) (Even if only one) to Loop thru
*
      STMT= 'SELECT FB.AUDIT,':AUDIT.CLIENT.ID
      STMT:=' WITH 1 = "LTLALL"'
      IF CARRIER.ID # 'ALL' THEN
         STMT:=" AND WITH CARRIER# '":CARRIER.ID:"'"
      END ELSE
         STMT:=' AND WITH 11 = "ENABLE"'
         STMT:=' AND WITH 13 = "Y"'      ; * Core Carrier Flag = "Y"
      END
      EXECUTE STMT RTNLIST CHECK.LIST CAPTURING OUTPUT
      IF @SELECTED < 1 THEN
         IF CARRIER.ID # 'ALL' THEN
            ERROR='Carrier ':CARRIER.ID:' is not set up for freight estimates'
         END ELSE
            ERROR='There are no core carriers set up for freight estimates'
         END
         QUOTE.AMT=''
         RETURN
      END
*
* Setup AUDIT.VIN
*
      READ O.ZIP.REC FROM F.ZIPS,ORIG.ZIP ELSE
         READ O.ZIP.REC FROM F.CAN.ZIPS,ORIG.ZIP ELSE
            ERROR=ORIG.ZIP:' ZIP NOT ON FILE, PLEASE TRY ANOTHER ZIP'
            QUOTE.AMT=''
            RETURN
         END
      END
      ORIG.CITY = O.ZIP.REC<1>
      ORIG.ST = O.ZIP.REC<2>

      READ D.ZIP.REC FROM F.ZIPS,DEST.ZIP ELSE
         READ D.ZIP.REC FROM F.CAN.ZIPS,DEST.ZIP ELSE
            ERROR=DEST.ZIP:' ZIP NOT ON FILE, PLEASE TRY ANOTHER ZIP'
            QUOTE.AMT=''
            RETURN
         END
      END
      DEST.CITY = D.ZIP.REC<1>
      DEST.ST = D.ZIP.REC<2>

      A.VIN=''
      A.VIN<3>=RATE.DATE
      A.VIN<11>=AUDIT.CLIENT.ID
      A.VIN<12>=IOT
      A.VIN<13,1>=ORIG.ZIP
      A.VIN<13,2>=ORIG.CITY
      A.VIN<13,3>=ORIG.ST
      A.VIN<14,1>=DEST.ZIP
      A.VIN<14,2>=DEST.CITY
      A.VIN<14,3>=DEST.ST
      A.VIN<16>=USER

*     A.VIN<17>=1                        ; *Misroute Flag to Ignore Misc Charge Code Errors
      A.VIN<18>=CLASSES
      A.VIN<19>=WEIGHTS
      A.VIN<21>=MISC.CHG.CODES
      A.VIN<22>=MISC.CHG.AMTS
      A.VIN<23>=MISC.CHG.AMTS
      A.VIN<24>=LOGISTICS
      A.VIN<25>=''
      A.VIN<35>=CL.LOG.FSC='N'           ; * Flag to NOT Calculate Log FSC
      A.VIN<46>=MAX.LTL                  ; * JMK02
      A.VIN<48>=DCLIENTS<188>            ; * JMK03 Forced Benchmark Minimum Flag Y/N
      A.VIN<49>=CARR.ONLY.FSC            ; * JMK05
*
**** Begin "Loop" of all Carriers *****
*
      CARRIER.EST.ARRAY=''
*<1>=Amount (MV Lowest to Highest)
*<2>=Carrier #
*<3>=Carrier Name
*<4>=Carrier Address
*<5>=Carrier City
*<6>=Carrier State
*<7>=Error (If Any)
*<8>=Flag
GET.NEXT.CARRIER:
      READNEXT CHECK.CARRIER.ID FROM CHECK.LIST ELSE
         RETURN
      END
      ERROR=''
      GOSUB GET.CARRIER.INFO
      IF ERROR#'' AND CARRIER.ID # 'ALL' THEN
         ERROR='Carrier ':CARRIER.ID:' is not set up for freight estimates'
         RETURN
      END
**********************************************************************
      A.VIN<10>= CHECK.CARRIER.ID        ; * Add "Current" Carrier to AUDIT.VIN
      A.VIN<50>= CHECK.CARRIER.SCAC      ; * JMK26
      A.VIN<51>= CHECK.CARRIER.TYPE      ; * JMK26
      VIN=A.VIN
      A.VOUT=''
      TEST.VIN=CHANGE(A.VIN,@AM,@VM)

      * Begin NKH01
      GOSUB GET.CC.INFO      
      
      ORIG.DIR.FLG = ''
      DEST.DIR.FLG = ''
      COMB.DIR.FLG = ''

      CC.STATUS = 'FAIL'                 ; * NKH03
      SCAC.COUNT = DCOUNT(CCVOUT<1>,@VM)
      FOR SC = 1 TO SCAC.COUNT      
         LOCATE(CHECK.CARRIER.SCAC,CCVOUT,1,SC;S.POS;'AL') THEN 
            * From TransitMethod
            CC.STATUS = 'PASS'           ; * NKH03            
            TRANSIT.TIME = CCVOUT<29,SC>            
            IF TRANSIT.TIME = 0 THEN TRANSIT.TIME = 1
            ORIG.DIR.FLG = CCVOUT<31,SC>[1,1]
            IF ORIG.DIR.FLG = 'C' OR ORIG.DIR.FLG = 'P' THEN ORIG.DIR.FLG = 'I'  
            DEST.DIR.FLG = CCVOUT<30,SC>[1,1]
            IF DEST.DIR.FLG = 'C' OR DEST.DIR.FLG = 'P' THEN DEST.DIR.FLG = 'I'  
            COMB.DIR.FLG = CCVOUT<32,SC>[1,1] 
            IF COMB.DIR.FLG = 'C' OR COMB.DIR.FLG = 'P' THEN COMB.DIR.FLG = 'I'  
         END
      NEXT SC                  
      
      VIN<56>=ORIG.DIR.FLG
      VIN<57>=DEST.DIR.FLG
      VIN<58>=COMB.DIR.FLG
      VIN<59>=AUDIT.CLIENT.ID   ; * NKH02                      
      * End NKH01  

      IF CC.STATUS = 'PASS' THEN         ; * NKH03
         CALL FB.AUTO.V2(VIN,A.VOUT,D.AUDIT,CONTR.DFSC,BENCH.DFSC,DTARIFFS,DTARIFF.DATE.XREF,LI.CODES,F.AUDIT.TABLES,F.AUDIT.ZIPS,F.PCIN,F.PCOUT,F.TABLES)  ; * NKH01
      END ELSE                           ; * NKH03
         GO GET.NEXT.CARRIER             ; * NKH03
      END                                ; * NKH03         
      WHY=A.VOUT<24>                     ; * Save for Debugging purposes
      A.VOUT<24>=''                      ; * Kill Why So you can look at A.VOUT with Debugger
      CONTR.AMT = A.VOUT<8>
      BENCH.AMT = A.VOUT<9>
      OLA.ERROR = A.VOUT<1>
      CONTR.FSC.AMT=A.VOUT<46,7>
      BEGIN CASE
         CASE A.VIN<49>='Y'
            BENCH.FSC.AMT=A.VOUT<47,7>
         CASE A.VIN<49>='C'
            BENCH.FSC.AMT=A.VOUT<47,7>
         CASE A.VIN<49>='O'
            BENCH.FSC.AMT=A.VOUT<83,7>
         CASE 1
            BENCH.FSC.AMT=A.VOUT<48,7>
      END CASE
      TRANSIT.TIME=A.VOUT<49>            ; * JMK09
      ORIG.DIRECT.FLAG=A.VOUT<50>        ; * JMK09
      IF ORIG.DIRECT.FLAG='I' THEN TRANSIT.TIME+=1           ; * JMK24
      DEST.DIRECT.FLAG=A.VOUT<51>        ; * JMK09
      IF DEST.DIRECT.FLAG='I' THEN TRANSIT.TIME+=1           ; * JMK24
      COMB.DIRECT.FLAG=A.VOUT<52>        ; * JMK09
      AUDIT=A.VOUT<2>
      IF OLA.ERROR # "" THEN
         IF CARRIER.ID # 'ALL' AND CONTR.AMT+0=0 THEN
            ERROR=OLA.ERROR
            RETURN
         END
      END
      IF AUDIT # "Y" AND CARRIER.ID="ALL" THEN GO GET.NEXT.CARRIER
      IF AUDIT = "MA" THEN OLA.ERROR = "Not an approved carrier for this shipment!"
      IF CONTR.AMT+0=0 THEN GO GET.NEXT.CARRIER
      IF LOGISTICS AND BENCH.AMT+0=0 THEN GO GET.NEXT.CARRIER
*
*** Add Quote Percentage Logic Here ***
*
*** Locate and set percentages for quotes and commissions
      IF LOGISTICS THEN
         LOCATE RATE.DATE IN DCLIENTS<84> BY 'AR' SETTING POS ELSE
            POS=POS-1
         END
         COMM.PERC = DCLIENTS<85,POS>
         SAVINGS = BENCH.AMT-CONTR.AMT
         LOG.COMM = SAVINGS*COMM.PERC/10000
         CL.SAVINGS = SAVINGS - LOG.COMM
         SAVINGS.QUOTE.AMT = CL.SAVINGS*(1-SAVINGS.QUOTE.PERC/10000)
         QUOTE.AMT=OCONV(CONTR.AMT + LOG.COMM + SAVINGS.QUOTE.AMT,'MR0')
      END ELSE
         QUOTE.AMT=CONTR.AMT
      END
      QUOTE.AMT=CONTR.AMT+LOG.COMM

      LOCATE (QUOTE.AMT:'_':BENCH.AMT) IN CARRIER.EST.ARRAY<1> BY 'AR' SETTING POS ELSE NULL
      INS (QUOTE.AMT:'_':BENCH.AMT) BEFORE CARRIER.EST.ARRAY<1,POS>
      INS CHECK.CARRIER.ID BEFORE CARRIER.EST.ARRAY<2,POS>
      INS CHECK.CARRIER.NAME BEFORE CARRIER.EST.ARRAY<3,POS>
      INS CHECK.CARRIER.STREET BEFORE CARRIER.EST.ARRAY<4,POS>
      INS CHECK.CARRIER.CITY BEFORE CARRIER.EST.ARRAY<5,POS>
      INS CHECK.CARRIER.STATE BEFORE CARRIER.EST.ARRAY<6,POS>
      INS OLA.ERROR BEFORE CARRIER.EST.ARRAY<7,POS>
      INS BENCH.AMT BEFORE CARRIER.EST.ARRAY<8,POS>
      INS BENCH.FSC.AMT BEFORE CARRIER.EST.ARRAY<9,POS>
      INS CONTR.AMT BEFORE CARRIER.EST.ARRAY<10,POS>
      INS CONTR.FSC.AMT BEFORE CARRIER.EST.ARRAY<11,POS>
      INS TRANSIT.TIME BEFORE CARRIER.EST.ARRAY<12,POS>
      INS COMB.DIRECT.FLAG BEFORE CARRIER.EST.ARRAY<13,POS>
      INS LOG.COMM BEFORE CARRIER.EST.ARRAY<14,POS>
      GO GET.NEXT.CARRIER
GET.CARRIER.INFO:***
      READ CHECK.CARRIER.REC FROM F.CARRIERS,CHECK.CARRIER.ID ELSE
         ERROR = "Carrier ":CHECK.CARRIER.ID:" not on file."
         RETURN
      END
      CHECK.CARRIER.NAME = CHECK.CARRIER.REC<1>
      CHECK.CARRIER.STREET = TRIM(CHANGE(CHECK.CARRIER.REC<2>,@VM,' '))
      CHECK.CARRIER.CITY = CHECK.CARRIER.REC<3>
      CHECK.CARRIER.STATE = CHECK.CARRIER.REC<4>
      CHECK.CARRIER.SCAC = CHECK.CARRIER.REC<12>   ; *Added By ROB GAUDET 9/23/2003
      CHECK.CARRIER.STATUS = CHECK.CARRIER.REC<89>           ; * JMK14
      IF CHECK.CARRIER.STATUS = "I" THEN           ; * JMK14
         ERROR = "CARRIER IS NOT ACTIVE - CARRIER# ":CHECK.CARRIER.ID  ; * JMK14
         RETURN                          ; * JMK14
      END                                ; * JMK14
      CHECK.CARRIER.TYPE = 'A'           ; * JMK09
      IF CHECK.CARRIER.REC<44>='6' THEN  ; * JMK09
         CHECK.CARRIER.TYPE='B'          ; * JMK09
      END                                ; * JMK09
      CHECK.CARRIER.TEXT=TRIM(CHECK.CARRIER.ID:' ':CHECK.CARRIER.NAME:' ':CHECK.CARRIER.STREET:' ':CHECK.CARRIER.CITY)
      READ D.AUDIT FROM F.AUDIT, CHECK.CARRIER.ID ELSE
         ERROR = "CARRIER NOT SETUP FOR AUTO AUDIT - CARRIER#":CHECK.CARRIER.ID
         RETURN
      END
      IF D.AUDIT<11>#'ENABLE' THEN
         ERROR='Not Considered in On-Line Audit - Carrier Disabled'
         RETURN
      END
      READ BENCH.DFSC FROM F.NFSC,'B*':CHECK.CARRIER.ID:'*':AUDIT.CLIENT.ID ELSE           ; *NPR01
         READ BENCH.DFSC FROM F.NFSC,'B*':CHECK.CARRIER.ID ELSE BENCH.DFSC=''    ; *NPR01
      END                                ; *NPR01

      IF BENCH.DFSC<30>='DISABLE' THEN
         BENCH.DFSC=''                   ; ** No Effect
         CONTR.DFSC=''
      END ELSE
         READ CONTR.DFSC FROM F.NFSC,'C*':CHECK.CARRIER.ID:'*':AUDIT.CLIENT.ID ELSE
            READ CONTR.DFSC FROM F.NFSC,'C*':CHECK.CARRIER.ID ELSE CONTR.DFSC=BENCH.DFSC
         END

         IF CONTR.DFSC<30>='DISABLE' THEN CONTR.DFSC=''
      END
      RETURN

* Begin NKH01
*********************************************************************
GET.CC.INFO:
*********************************************************************
      IF CARRIER.ID > ''  AND CARRIER.ID # 'ALL' THEN
         READV CARRIER.SCAC FROM F.CARRIERS, CARRIER.ID, 12 ELSE CARRIER.SCAC = ''
      END ELSE
         CARRIER.SCAC = 'ALL'
      END

      READV ORIG.COUNTRY FROM ZIPS, ORIG.ZIP, 3 ELSE 
         IF NUM(ORIG.ZIP[1,1]) THEN
            ORIG.COUNTRY = 'US'
         END ELSE
            ORIG.COUNTRY = 'CAN'
         END
      END
      BEGIN CASE
         CASE ORIG.COUNTRY = 'US'
            ORIG.COUNTRY = 'USA'
         CASE ORIG.COUNTRY = 'CA'
            ORIG.COUNTRY = 'CAN'
         CASE ORIG.COUNTRY = 'MX'
            ORIG.COUNTRY = 'MEX'
      END CASE
      READV DEST.COUNTRY FROM ZIPS, DEST.ZIP, 3 ELSE 
         IF NUM(DEST.ZIP[1,1]) THEN
            DEST.COUNTRY = 'US'
         END ELSE
            DEST.COUNTRY = 'CAN'
         END
      END
      BEGIN CASE
         CASE DEST.COUNTRY = 'US'
            DEST.COUNTRY = 'USA'
         CASE DEST.COUNTRY = 'CA'
            DEST.COUNTRY = 'CAN'
         CASE DEST.COUNTRY = 'MX'
            DEST.COUNTRY = 'MEX'
      END CASE               
         
      CCVIN = ''  
      CCVIN<1> = CARRIER.SCAC
      CCVIN<2> = DEST.COUNTRY
      CCVIN<3> = DEST.ZIP
      CCVIN<4> = ORIG.COUNTRY
      CCVIN<5> = ORIG.ZIP
      CCVIN<6> = CLIENT.ID

      IF FIRST.TIME THEN
         CCVOUT = ''
         CALL CCXL.V2(CCVIN,CCVOUT)
         SAVE.CCVIN = CCVIN
         FIRST.TIME = 0
      END ELSE
         IF SAVE.CCVIN # CCVIN THEN
            CCVOUT = ''
            CALL CCXL.V2(CCVIN,CCVOUT)
            SAVE.CCVIN = CCVIN
         END
      END

      RETURN
* End NKH01      
      
*************************************************** 
9000: ****
*************************************************** 
      CALL ELINE(ERROR)                  ; * NPR01
      IF ERROR#'' THEN                   ; * NPR01
         RVOUT<1>=""                     ; * NPR01
         RVOUT<2>=ERROR                  ; * NPR01
      END                                ; * NPR01
      RETURN                             ; * NPR01

      STOP
