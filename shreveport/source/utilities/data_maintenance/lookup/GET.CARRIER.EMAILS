*************************************************************************
* Program : GET.CARRIER.EMAILS
* Author  : MIKE KERBOW
* Date    : MAY 2006
*
* Copyright (c) 2006 by AFS Logistics, LLC. - All Rights Reserved
*************************************************************************
*******************************************
      CALL CHANNEL(CH.NUM)
      CALL GET.USER(USER)
      PROG.NAME='GET.CARRIER.EMAILS'
      PROG.DESC='Select Carrier Email Addresses'
      PACK.NAME='Freight Payment'
      MASK.CHAR='-'
       UPL.VIN = 'FBBP' ;  UPL.VIN<2> = 'GET.CARRIER.EMAILS' ; UPL.VOUT = ''                    ;* NPR_UPL 04/23/2010
       CALL UPD.PROGRAM.LOG(UPL.VIN,UPL.VOUT)                                                           ;* NPR_UPL 04/23/2010
      TAB=CHAR(9)
      ESC=CHAR(27)

* Write screen header
      CALL AFS.SCR.HEAD(CO.ID,FILE.ID,'',PROG.NAME,PROG.DESC,PACK.NAME,CO.NAME,TIME.DATE,1)
      CRT @(0,2):STR('=',79)

*
* OPEN FILES
*
      OPEN '','CARRIERS' TO F.CARRIERS ELSE
         CALL OPEN.ABORT('CARRIERS',PROG.NAME)
      END
      OPEN '','FBWORK':CH.NUM TO F.WORK ELSE
         EXECUTE 'CREATE-FILE FBWORK':CH.NUM:' 1 1001' PASSLIST CAPTURING OUTPUT
         OPEN '','FBWORK':CH.NUM TO F.WORK ELSE
            CALL OPEN.ABORT('FBWORK':CH.NUM,PROG.NAME)
         END
      END
      OPEN '','VOC' TO F.VOC ELSE
         CALL ELINE('Unable to open VOC file - Call IS Department.')
         STOP
      END
      READ BILLDATA.REC FROM F.VOC,'BILLDATA' ELSE
         CALL ELINE('Unable to open "VOC BILLDATA" - Call IS Department.')
         STOP
      END
      BILLDATA.REC<2>:='\':USER
      WRITE BILLDATA.REC ON F.VOC,'BILLDATA.':USER ELSE
         CALL ELINE('Your user name "':USER:'" is not in the BILLDATA folder - call IS Department.')
         STOP
      END
      OPEN '','BILLDATA.':USER TO F.BILLDATA ELSE
         CALL ELINE('Unable to open "BILLDATA"':USER:' - call IS Department.')
         STOP
      END
*
* Restrict program usage
      VALID.USER=1
      BEGIN CASE
         CASE USER='OWEN'
         CASE USER='MYRA'
         CASE USER='DAVID'
         CASE USER='MKERBOW'
         CASE 1
            VALID.USER=0
      END CASE

      IF NOT(VALID.USER) THEN
         TEMP='**  This is a restricted access application  **'
         CALL CENTER(TEMP,80)
         CRT @(0,12):TEMP
         GOTO THE.END
      END

100:***
      LN='Are you ready to search for email addresses? '
      MASK='N-'
      HELP='[Y]=Yes [N]=No [EX]=Exit'
      CALL GEN.IN(0,5,LN:MASK,'',Q,0,2,'','',2,-2,5,1,QI,HELP,0,23)
      QI=OCONV(QI,'MCU')
      BEGIN CASE
         CASE QI='EX' OR QI='N' OR QI=''
            GOTO THE.END
         CASE QI='Y'
            NULL
         CASE 1
            GOTO 100
      END CASE

      CLEARFILE F.WORK
      EMAIL.LIST=''
      EMAIL.TEST=''
*
*
* SELECT CARRIER FILE
*
      TEXT='Selecting Carriers.'
      CALL CENTER(TEXT,80)
      CRT @(0,10):TEXT
*
      READ CARRIER.LIST FROM F.BILLDATA,'CARRIER-LIST.TXT' ELSE CARRIER.LIST=''
******************************************************************
* Select of First List
******************************************************************
      CARRIER.ITEMS = DCOUNT(CARRIER.LIST,@AM)
      IF CARRIER.ITEMS < 1 THEN
         CALL ELINE('ERROR - No carriers on file.')
         STOP
      END
      ATT.LIST=''
      ATT.LIST<1,-1>=81
      ATT.LIST<1,-1>=83
      ATT.LIST<1,-1>=86
      ATT.LIST<1,-1>=88
      ATT.LIST<1,-1>=91
      ATT.LIST<1,-1>=93
      ATT.NVAL=DCOUNT(ATT.LIST<1>,@VM)
      PER=0
      FOR CARRIER.CNT=1 TO CARRIER.ITEMS
         CARRIER.ID=CARRIER.LIST<CARRIER.CNT>
         READ CARRIER.REC FROM F.CARRIERS,CARRIER.ID ELSE GO SKIP.CARRIER
         CARRIER.NAME=CARRIER.REC<1>
         CRT @(0,17):CARRIER.ID:" ":CARRIER.NAME:" - Now processing carrier email addresses":@(-4):
*
* Process Email Addresses
*
         FOR ATT.VAL=1 TO ATT.NVAL
            ATT=ATT.LIST<1,ATT.VAL>
            NVAL=DCOUNT(CARRIER.REC<ATT>,@VM)
            FOR VAL=1 TO NVAL
               EMAIL.ADD=CARRIER.REC<ATT,VAL>
               GOSUB ADD.EMAIL.ADD
            NEXT VAL
         NEXT ATT.VAL
SKIP.CARRIER:***
      NEXT CARRIER.CNT
*
      WRITE EMAIL.LIST ON F.BILLDATA,'CARRIER-EMAILS.TXT'
      CALL ELINE('This has been written to your "U:" drive as CARRIER-EMAILS.TXT')
THE.END:*
      STOP
ADD.EMAIL.ADD:***
      IF INDEX(EMAIL.ADD,'@',1) THEN
         EMAIL.ADD.TEST=OCONV(EMAIL.ADD,'MCU')
         DOMAIN=FIELD(EMAIL.ADD.TEST,'@',2)
         IF DOMAIN#'AFSLOGISTICS.COM' AND DOMAIN#'AFS.NET' THEN
            LOCATE EMAIL.ADD.TEST IN EMAIL.TEST SETTING POS ELSE
               EMAIL.TEST<-1>=EMAIL.ADD.TEST
               EMAIL.LIST<-1>=EMAIL.ADD
            END
         END
      END
      RETURN
   END
