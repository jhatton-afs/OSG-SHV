********************************************************************************
*  Program : FB.9.13.3 (List of Overrides)
*  Author  : J Michael Kerbow - May 2004
********************************************************************************
*
*      COPYRIGHT (C) 1995 - 2004 by AFS LOGISTICS, LLC. - ALL RIGHTS RESERVED
*
*  This program is the sole property of AFS Logistics, LLC (AUTOMATED FREIGHT
*  SYSTEMS) and may not be used, copied, or modified without the express
*  written consent of Brian Barker of AFS Logistics, LLC.
********************************************************************************
* 052604 - JMK01 - ADD AUDITOR NAME TO DOWNLOAD
*
$INCLUDE PROG.ADMIN INC_OPTIONS
*
* Initialize variables
*
*
      PROMPT''
      CALL CHANNEL(CH.NUM)
      CALL GET.USER(USER.NAME)
      PROG.NAME='FB.9.13.3'
      PROG.DESC="Download Newway Overrides"
      PACK.NAME="Freight Billing"
      TAB=CHAR(9)
       UPL.VIN = 'FBBP' ;  UPL.VIN<2> = 'FB.9.13.3' ; UPL.VOUT = ''                    ;* NPR_UPL 04/23/2010
       CALL UPD.PROGRAM.LOG(UPL.VIN,UPL.VOUT)                                                           ;* NPR_UPL 04/23/2010
      ESC=CHAR(27)
      MASK.CHAR='-'
      WIDTH=132
      CLIENT=''
      ERRMSG=''
      STATUS=''
      OPEN '','CLIENTS' TO F.CLIENTS ELSE
         CALL OPEN.ABORT("CLIENTS",PROG.NAME)
      END
      OPEN '','CARRIERS' TO F.CARRIERS ELSE
         CALL OPEN.ABORT("CARRIERS",PROG.NAME)
      END
      OPEN '','FB.OVERRIDE' TO F.OVERRIDE ELSE
         CALL OPEN.ABORT("FB.OVERRIDE",PROG.NAME)
      END
      OPEN '','FB.TABLES' TO F.TABLES ELSE
         CALL OPEN.ABORT('FB.TABLES',PROG.NAME)
      END
      OPEN '','VOC' TO F.VOC ELSE        ; * JMK01
         CALL OPEN.ABORT('VOC',PROG.NAME)          ; * JMK01
      END                                ; * JMK01
      OPEN '','BILLDATA.':USER.NAME TO F.BILLDATA ELSE       ; * JMK01
         CALL OPEN.ABORT('BILLDATA.':USER.NAME,PROG.NAME)    ; * JMK01
      END                                ; * JMK01
      READ OVERRIDE.FIELDS FROM F.TABLES,'OVERRIDE.FIELDS' ELSE OVERRIDE.FIELDS=''
      READ OVERRIDE.OPERATORS FROM F.TABLES,'OVERRIDE.OPERATORS' ELSE OVERRIDE.OPERATORS=''

      CALL AFS.SCR.HEAD(CO.ID,FILE.ID,'',PROG.NAME,PROG.DESC,PACK.NAME,CO.NAME,TIME.DATE,1)
      CRT @(0,2):STR('=',79)

* Enter client number
ENTER.CLIENT:***
      CRT @(0,3):'Enter Client Number :'
      MASK=CLIENT:STR(MASK.CHAR,5-LEN(CLIENT)):@(-4)
      HELP='Enter client name/number or ALL. [ALL] [EX]it'
      CALL GEN.IN(22,3,MASK,'',Q,0,20,'','',2,22,3,1,QI,HELP,0,23)
      QI=OCONV(QI,'MCU')
      IF CLIENT AND QI='' THEN QI=CLIENT
      BEGIN CASE
         CASE QI='EX'
            GOTO THE.END
         CASE QI='ALL'
            NULL
         CASE NOT(QI MATCHES "0N")
            CALL SOUNDEX.DISPLAY(QI,'BCUST','SDX.CLIENTS,NAME',2,ITEM.LIST)
            CALL AFS.SCR.REFRESH(PROG.NAME,PROG.DESC,PACK.NAME,CO.NAME,TIME.DATE,1)
            CRT @(0,2):STR('=',79)
            BEGIN CASE
               CASE ITEM.LIST=''
                  GOTO ENTER.CLIENT
               CASE DCOUNT(ITEM.LIST<1>,@VM) GT 1
                  GOTO ENTER.CLIENT
               CASE ITEM.LIST NE ''
                  IF NUM(ITEM.LIST<1,1>) THEN DATA ITEM.LIST<1,1>
                  GOTO ENTER.CLIENT
            END CASE
      END CASE
      IF QI#'ALL' THEN
         QI=QI'R%5'
         READ CLIENT.REC FROM F.CLIENTS,QI ELSE
            CALL ELINE('Client ':QI:' not on file. Try again.')
            GOTO ENTER.CLIENT
         END
         CLIENT.NAME=CLIENT.REC<2>
         CLIENT.AUDITOR=CLIENT.REC<71>   ; * JMK01
         IF CLIENT.AUDITOR='' THEN       ; * JMK01
            CLIENT.AUDITOR='Unassigned'  ; * JMK01
         END                             ; * JMK01
      END ELSE
         CLIENT.NAME='All Clients'
         CLIENT.AUDITOR='N/A'            ; * JMK01
      END
      CLIENT=QI
      CRT @(22,3):CLIENT:' ':CLIENT.NAME:@(-4)

* Verify correct
VERIFY.SELECTION:***
      CRT @(0,22):'Select :'
      MASK='C':MASK.CHAR
      HELP='Verify client. [C]ontinue [X]=Back/Change [EX]it'
      CALL GEN.IN(9,22,MASK,'',Q,0,2,'','',0,9,22,1,QI,HELP,0,23)
      QI=OCONV(QI,'MCU')
      IF QI='' THEN QI='C'
      BEGIN CASE
         CASE QI='EX'
            GOTO THE.END
         CASE QI='X'
            CRT @(0,22):@(-3)
            GOTO ENTER.CLIENT
         CASE QI='C'
            NULL
         CASE 1
            CALL ELINE('Invalid entry. Try again.')
            GOTO VERIFY.SELECTION
      END CASE
      CRT @(0,4):@(-3)

* Prepare to download
      DA=''
      DA.ID=CLIENT:'-OVERRIDES.XLS'      ; * JMK01
      TITLE='Override List'
      CTR=INT(WIDTH/2)
      L.TAB=CTR-(LEN(TITLE)/2)
      L.TAB=L.TAB-LEN(PROG.NAME)
      TEMP=PROG.NAME:SPACE(L.TAB):TITLE
      R.TAB=WIDTH-LEN(TEMP)
      R.TAB=R.TAB-LEN(TIMEDATE())
      DA<1>=PROG.NAME:SPACE(L.TAB):TITLE:SPACE(R.TAB):TIMEDATE()
      TITLE=''
      L.TAB=CTR-(LEN(TITLE)/2)
      L.TAB=L.TAB-LEN(CO.NAME)
      TEMP=CO.NAME:SPACE(L.TAB):TITLE
      R.TAB=(WIDTH-LEN(TEMP))-20
      DA<2>=TEMP:SPACE(R.TAB):USER.NAME'L(#10 )'
      DA<4,1>='Customer'
      DA<4,-1>='Customer Name'
      DA<4,-1>='Auditor Name'
      DA<4,-1>='Carrier'
      DA<4,-1>='Carrier Name'
      DA<4,-1>='Group'
      DA<4,-1>='SubGroup'
      DA<4,-1>='Begin Date'
      DA<4,-1>='End Date'
      DA<4,-1>='Auditor'
      DA<4,-1>='Field'
      DA<4,-1>='Operator'
      DA<4,-1>='Information'

      IF CLIENT#'ALL' THEN
         CLIENT.ID=CLIENT
         CLIENT.ITEMS=1
*        CLIENT.LINE=CLIENT'R#5':@VM:CLIENT.NAME ; * JMK01
         CLIENT.LINE=CLIENT'R#5':@VM:CLIENT.NAME:@VM:CLIENT.AUDITOR    ; * JMK01
         GOSUB DNLD.OVERRIDE
      END ELSE
         STMT="SELECT VOC 'FB.BILLS.HIST' *A7"
         EXECUTE STMT RTNLIST CLIENT.LIST CAPTURING OUTPUT
         CLIENT.ITEMS=@SELECTED
         IF CLIENT.ITEMS=0 THEN
            CALL ELINE('ERROR - No clients on file.')
            GOTO THE.END
         END
         DONE.1=0
         COUNTER.1=0
         PER.1=0
         LOOP
            READNEXT CLIENT.ID FROM CLIENT.LIST ELSE DONE.1=1
         UNTIL DONE.1=1 DO
            COUNTER.1=COUNTER.1+1
            READ CLIENT.REC FROM F.CLIENTS,CLIENT.ID THEN
               CLIENT.NAME=CLIENT.REC<2>
               CLIENT.AUDITOR=CLIENT.REC<71>
            END ELSE
               CLIENT.NAME='** Not On File **'
               CLIENT.AUDITOR='Unassigned'
            END
            CRT @(0,8):CLIENT.ID:' ':CLIENT.NAME:' - Now selecting overrides':@(-4)
            CALL PROG.BAR(12,COUNTER.1,CLIENT.ITEMS,PER.1)
*           CLIENT.LINE=CLIENT.ID'R#5':@VM:CLIENT.NAME ; * JMK01
            CLIENT.LINE=CLIENT.ID'R#5':@VM:CLIENT.NAME:@VM:CLIENT.AUDITOR        ; * JMK01
            GOSUB DNLD.OVERRIDE

SKIP.CLIENT:***
         REPEAT
      END                                ; * IF CLIENT#'ALL'

      PRINTER OFF
      GOTO THE.END

********************************************************************************

DNLD.OVERRIDE:***
      STMT='SSELECT FB.OVERRIDE WITH CLIENT# = ':CLIENT.ID:' BY CLIENT BY CARRIER'
      EXECUTE STMT RTNLIST OVERRIDE.LIST CAPTURING OUTPUT
      OVERRIDE.ITEMS=@SELECTED
      IF OVERRIDE.ITEMS=0 THEN GOTO SKIP.OVERRIDE
      CRT @(0,17):CLIENT:' ':CLIENT.NAME:' - Now processing ':OVERRIDE.ITEMS:' client/carrier override(s)':@(-4):
      DONE.2=0
      COUNTER.2=0
      LINE=CLIENT.LINE
      LOOP
         READNEXT OVERRIDE.ID FROM OVERRIDE.LIST ELSE DONE.2=1
      UNTIL DONE.2=1 DO
         COUNTER.2+=1
         CRT @(0,20):COUNTER.2'R%5':'  ':OVERRIDE.ID

         CARRIER.ID=FIELD(OVERRIDE.ID,'*',2)
         IF CARRIER.ID='ALL' THEN
            LINE<1,-1>='ALL'
            LINE<1,-1>='All Carriers'
         END ELSE
            READV CARRIER.NAME FROM F.CARRIERS,CARRIER.ID,1 ELSE CARRIER.NAME='** Not On File **'
            LINE<1,-1>=CARRIER.ID'R#5'
            LINE<1,-1>=CARRIER.NAME
         END
         READ OVERRIDE.REC FROM F.OVERRIDE,OVERRIDE.ID THEN
            SAVE.LINE=LINE
            NUM.ITEMS=DCOUNT(OVERRIDE.REC<1>,@VM)
            FOR X=1 TO NUM.ITEMS
               BEG.DATE=OVERRIDE.REC<1,X>
               END.DATE=OVERRIDE.REC<2,X>
               FIELD=OVERRIDE.REC<3,X>
               LOCATE FIELD IN OVERRIDE.FIELDS<1> SETTING POS THEN
                  TYPE=OVERRIDE.FIELDS<4,POS>
                  IF TYPE='' THEN TYPE = 'MCU'
               END
               OPERATOR=OVERRIDE.REC<4,X>
               INFO=OCONV(OVERRIDE.REC<5,X>,TYPE)
               GROUP=OVERRIDE.REC<6,X>
               AUDIT=OVERRIDE.REC<7,X>
               SUBGROUP=OVERRIDE.REC<14,X>
               LINE<1,-1>=GROUP'L#1':'        '
               LINE<1,-1>=SUBGROUP'R%2':'       '
               LINE<1,-1>=BEG.DATE'D2/':'    '
               IF END.DATE='' THEN
                  LINE<1,-1>='{OPEN}'
               END ELSE
                  LINE<1,-1>=END.DATE'D2/'
               END
               LINE<1,-1>=AUDIT'L#3'
               LINE<1,-1>=FIELD'L#6'
               LINE<1,-1>=OPERATOR'L#2'
               LINE<1,-1>=INFO'L#20'
               DA<-1>=LINE
               LINE=SAVE.LINE
            NEXT X
         END ELSE
            LINE<1,-1>='Read Failure of FB.OVERRIDE file'
            DA<-1>=LINE
         END                             ; * READ OVERRIDE.REC FROM F.OVERRIDE,OVERRIDE.ID
         LINE=CLIENT.LINE
SKIP.OVERRIDE:***
      REPEAT
      RETURN

********************************************************************************

THE.END:***
      DA=CHANGE(DA,@VM,TAB)              ; * JMK01
      WRITE DA ON F.BILLDATA,DA.ID       ; * JMK01
      CALL ELINE('Override table downloaded to ':DA.ID:' on your user drive.')   ; * JMK01
      CRT @(0,22):
      STOP                               ; * End program
