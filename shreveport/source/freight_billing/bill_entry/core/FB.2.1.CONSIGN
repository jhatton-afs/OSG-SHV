*******************************************************
* Program name : FB.2.1.CONSIGN
* Author       : BRIAN M. QUEEN
* Date written : JANUARY 16, 1992
* Library      : FBBP
* COPYRIGHT (C) 2016 - AFS LOGISTICS LLC - ALL RIGHTS RESERVED.
*
* SUBROUTINE TO ENTER VENDOR/CUSTOMER
*
* Last updated by afs (AFSLOGISTICS\nmeddaugh) at 09:22:51 on 03/22/2019.
*
* Start modification history
* 012407 - JCW01 - USE NEW VEND.CUST.NAMES FILE
* 032907 - JCW02 - UPDATE VEND.CUST.XREF AND VEND.CUST.ZIPS FILES
* 032219 - NKH01 - ACCU-316 Modified CREATE-FILE file sizes
* End modification history
*
*******************************************************
$OPTIONS EXEC.EQ.PERF
$OPTIONS INFO.ABORT
*
      SUBROUTINE FB.2.1.CONSIGN(IN.OUT,CONSIGN.DESC,F.VEND.CUST,F.VC.XREF,F.VC.ZIPS,CLIENT.ID,SEL,VOUT)
$OPTIONS HEADER.EJECT
ERRMSG=''
*
*------------------------------------------------------------
* Initialize variables
*
      PROMPT''
*
* Enter code
*
      IF IN.OUT EQ 'I' THEN
        HELP="Enter Vendor#, Name for search, [L] to list, [L A] List all starting with A"
        DESC.HELP="Enter the vendor's name."
      END ELSE
        HELP="Enter Customer#, Name for search, [L] to list, [L A] List all starting with A"
        DESC.HELP="Enter the customer's name."
      END
*
1000  MASK=CONSIGN.DESC:STR('-',30-LEN(CONSIGN.DESC))
      IF CONSIGN.DESC='' THEN REQ=1 ELSE REQ=0
      CALL GEN.IN(5,13,MASK,'',Q,REQ,30,'','',2,-30,13,1,QI,HELP,0,23)
      SEL=OCONV(QI,'MCU')
      BEGIN CASE
        CASE SEL='' ! SEL='X' ! SEL='EX'
          IF SEL='' & CONSIGN.DESC='' THEN SEL='X'
          RETURN
        CASE NUM(SEL)
          CONSIGN.CODE=SEL'R%5'
          READ DVEND.CUST FROM F.VEND.CUST,CONSIGN.CODE THEN
            CONSIGN.DESC=DVEND.CUST<1>
            CODE=DVEND.CUST<2>
            VOUT=CODE
            VOUT<2>=CONSIGN.CODE
          END ELSE
            IF IN.OUT='I' THEN
              LN="Vendor code ":CONSIGN.CODE:" not on file. Do you want to add? N"
            END ELSE
              LN="Customer code ":CONSIGN.CODE:" not on file. Do you want to add? N"
            END
            HELP="Enter either Y or N. 'X'=Back."
            CALL GEN.IN(0,22,LN,'',Q,0,1,'','',1,-1,22,1,QI,HELP,0,23)
            QI=OCONV(QI,'MCU')
            BEGIN CASE
              CASE QI='' ! QI='N'
                SEL='NULL'
                RETURN
*
* Enter new vendor/customer code description and update file
*
              CASE QI='Y'
1020            CALL GEN.IN(0,22,'Enter description ':STR('-',30),'',Q,1,30,'','',2,-30,22,0,QI,DESC.HELP,0,23)
                HELP="Enter either Y or N."
                IF CLIENT.ID='00850' THEN
                  IF Q[1,2]#'E ' AND Q[1,2]#'I ' THEN
                    CALL ELINE('Error! MUST Start with I or E for Darling Store Fixtures')
                    GO 1020
                  END
                END
1030            LINE='The name is ':Q:'. Is this correct? Y'
                CALL GEN.IN(0,22,LINE,'',ACK,0,1,'','',1,-1,22,1,QI,HELP,0,23)
                QI=OCONV(QI,'MCU')
                BEGIN CASE
                  CASE QI='' ! QI='Y'
                    CONSIGN.DESC=Q
                    WRITEV CONSIGN.DESC ON F.VEND.CUST,CONSIGN.CODE,1
                    CALL SOUNDEX.EDIT('',CONSIGN.DESC,CONSIGN.CODE,'SDX.VEND.CUST,':CLIENT.ID)
                    SUB.VC.ID = CONSIGN.CODE  ; * JCW01
                    GOSUB UPDATE.VCN          ; * JCW01
                    GOSUB UPDATE.XREF         ; * JCW02
                  CASE QI='N'
                    GOTO 1020
                  CASE 1
                    GOTO 1030
                END CASE
            END CASE
          END
       CASE SEL='L' OR SEL MATCHES "'L '1A"
         STMT='SORT VEND.CUST,':CLIENT.ID
         IF LEN(SEL)=3 THEN
           STMT:=' WITH BIG1 = "':SEL[3,1]:']"'
         END
         STMT:=' BY BIG1 BY VC#'
         STMT:=' VC# NAME 5 6 7'
         STMT:=' ID-SUPP'
         STMT:=' HDR.SUP'
         STMT:=' FOOTING "Press Q to Quit, or"'
         EXECUTE STMT
         CRT @(-1):'Screen will be redawn once a Customer/Vendor is selected':
         CRT @(0,13):"Cust":
         GO 1000

*
* Search for vendor/customer code
*
        CASE 1
          CALL SOUNDEX.DISPLAY(SEL,'VEND.CUST,':CLIENT.ID,'SDX.VEND.CUST,':CLIENT.ID,'1,5,6,7',RESULT)
          IF DCOUNT(RESULT<1>,@VM) > 1 THEN RESULT=''
          IF RESULT NE '' THEN
            CONSIGN.CODE=RESULT<1,1>'R%5'
***         READV CONSIGN.DESC FROM F.VEND.CUST,CONSIGN.CODE,1 ELSE GO 1000
            READ DVEND.CUST FROM F.VEND.CUST,CONSIGN.CODE THEN
              CONSIGN.DESC=DVEND.CUST<1>
              CODE=DVEND.CUST<2>
              VOUT=CODE
              VOUT<2>=CONSIGN.CODE
            END ELSE
              GO 1000
            END
          END ELSE
            IF IN.OUT='I' THEN
              LN="Vendor ":SEL:" not on file. Do you want to add? N"
            END ELSE
              LN="Customer ":SEL:" not on file. Do you want to add? N"
            END
            HELP="Enter Y to add or N not to add.."
            CALL GEN.IN(0,22,LN,'',Q,0,1,'','',1,-1,22,1,QI,HELP,0,23)
            QI=OCONV(QI,'MCU')
            BEGIN CASE
              CASE QI='Y'
                ID=0
                LOOP
                  ID=ID+1000
                  ID=ID'R%5'
                  READ XX FROM F.VEND.CUST,ID ELSE XX='**'
                UNTIL XX='**' DO REPEAT
                ID=ID-999
                ID=ID'R%5'
                LOOP
                  ID=ID+100
                  ID=ID'R%5'
                  READ XX FROM F.VEND.CUST,ID ELSE XX='**'
                UNTIL XX='**' DO REPEAT
                ID=ID-99
                ID=ID'R%5'
                LOOP
                  ID=ID+1
                  ID=ID'R%5'
                  READ XX FROM F.VEND.CUST,ID ELSE XX='**'
                UNTIL XX='**' DO REPEAT
1120            CALL GEN.IN(0,22,'Enter exact description ':STR('-',30),'',Q,1,30,'','',2,-30,22,0,QI,DESC.HELP,0,23)
                HELP="Enter either Y or N."
                IF CLIENT.ID='00850' THEN
                  IF Q[1,2]#'E ' AND Q[1,2]#'I ' THEN
                    CALL ELINE('Error! MUST Start with I or E for Darling Store Fixtures')
                    GO 1120
                  END
                END
1130            LINE='The name is ':Q:'. Is this correct? Y'
                CALL GEN.IN(0,22,LINE,'',ACK,0,1,'','',1,-1,22,1,QI,HELP,0,23)
                QI=OCONV(QI,'MCU')
                BEGIN CASE
                  CASE QI='' ! QI='Y'
                    CONSIGN.DESC=Q
                    WRITEV CONSIGN.DESC ON F.VEND.CUST,ID,1
                    VOUT<2>=ID
                    CALL SOUNDEX.EDIT('',CONSIGN.DESC,ID,'SDX.VEND.CUST,':CLIENT.ID)
                    SUB.VC.ID = ID    ; * JCW01
                    GOSUB UPDATE.VCN  ; * JCW01
                    GOSUB UPDATE.XREF ; * JCW02
                  CASE QI='N'
                    GOTO 1120
                  CASE 1
                    GOTO 1130
                END CASE
              CASE QI='N' ! QI=''
                CONSIGN.DESC=''
                SEL='NULL'
            END CASE
            RETURN
          END
      END CASE
      RETURN

*** BEGIN JCW01
UPDATE.VCN:
      *** Get new VEND.CUST.NAMES ID
      OPEN 'FB.TABLES' TO F.TABLES ELSE RETURN
      OPEN 'VEND.CUST.NAMES,':CLIENT.ID TO F.VCN ELSE
        EXECUTE 'CREATE-FILE DATA VEND.CUST.NAMES,':CLIENT.ID:' 601,2' CAPTURING OUTPUT      ; * NKH01
        OPEN 'VEND.CUST.NAMES,':CLIENT.ID TO F.VCN ELSE RETURN
      END
      TABLE.ID = 'VCN*':CLIENT.ID
      READ TABLE.REC FROM F.TABLES, TABLE.ID ELSE
        TABLE.REC = ''
        TABLE.REC<1> = 0
      END
      DONE = 0
      LOOP UNTIL DONE DO
        VCN.ID = TABLE.REC<1>'R%5'
        TABLE.REC<1> += 1
        READ TEST FROM F.VCN, VCN.ID ELSE
          WRITE TABLE.REC ON F.TABLES, TABLE.ID
          DONE = 1
        END
      REPEAT
      *** Create and save new VEND.CUST.NAMES record
      VCN.REC = ''
      VCN.REC<1> = CONSIGN.DESC
      VCN.REC<2> = SUB.VC.ID
      WRITE VCN.REC TO F.VCN, VCN.ID
      CALL SOUNDEX.EDIT('',CONSIGN.DESC,SUB.VC.IC,'SDX.VCN,':CLIENT.ID)
      RETURN
*** END JCW01

UPDATE.XREF:
      *** Update VEND.CUST.XREF file  ; * JCW02
      ZIP = DVEND.CUST<7>             ; * JCW02
      DESC = DVEND.CUST<1>            ; * JCW02
      DESC = TRIM(OCONV(DESC,'MCU'))  ; * JCW02
      IF ZIP # 99999 THEN             ; * JCW02
         XREF.ID = DESC:'*':ZIP       ; * JCW02
      END ELSE                        ; * JCW02
         XREF.ID = DESC               ; * JCW02
      END                             ; * JCW02
      WRITEV SUB.VC.ID TO F.VC.XREF, XREF.ID, 1 ; * JCW02
      RETURN                          ; * JCW02
