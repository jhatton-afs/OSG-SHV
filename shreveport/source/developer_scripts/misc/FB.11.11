$OPTIONS HEADER.EJECT
$OPTIONS EXEC.EQ.PERF
$OPTIONS INFO.ABORT
      ERRMSG=''
* PROGRAM FB.11.11
* PROGRAM TO MODIFY CARRIER INVOICE NUMBER CAPTURE OPTIONS
*
* FREIGHT BILLING PACKAGE
*
* AUTHOR: MIKE KERBOW
* DATE  : JUNE 2006
***********************************************************
* MODIFICATIONS:
* 072312 - RLM01 - Sysaid #3073 - Replace former employees' email addresses with those of current employees
***********************************************************
*
* Initialize variables
*
      PROMPT''
      PROG.NAME='FB.11.11'
      PROG.DESC='CARRIER INVOICE NUMBER CAPTURE MAINTENANCE'
      PACK.NAME='FREIGHT BILLING'
      CALL AFS.SCR.HEAD(CO.ID,FILE.ID,'',PROG.NAME,PROG.DESC,PACK.NAME,CO.NAME,TIME.DATE,1)
       UPL.VIN = 'FBBP' ;  UPL.VIN<2> = 'FB.11.11' ; UPL.VOUT = ''                    ;* NPR_UPL 04/23/2010
       CALL UPD.PROGRAM.LOG(UPL.VIN,UPL.VOUT)                                                           ;* NPR_UPL 04/23/2010

      CALL GET.USER(USER.NAME)
      BEGIN CASE
            *
            * Authorized users
            *
         CASE USER.NAME = 'BRIAN'
         CASE USER.NAME = 'KAREN'
         CASE USER.NAME = 'OWEN'
         CASE USER.NAME = 'MYRA'
         CASE USER.NAME = 'MARY'
         CASE USER.NAME = 'MKERBOW'
         CASE USER.NAME = 'DAVID'
         CASE USER.NAME = 'NPRATT'
            *
            * Restrict everyone else
            *
         CASE 1
            SUBJ="Unauthorized Program Access by ":USER.NAME
            TEXT=''
            TEXT<1,-1>=USER.NAME:' ':DATE()'D2/':' ':OCONV(TIME(),'MTHS'):' ':PROG.NAME
            TEXT<1,-1>='User ':USER.NAME:' tried to access ':PROG.DESC
            TEXT<1,-1>="(This user's name was not included in the program to allow access)"
            MVIN=''
*           MVIN<1>='oweninfo@afs.net':@VM:'SMatthews@afs.net'  ; * RLM01
*           MVIN<1>='bfranklin@afs.net':@VM:'SMatthews@afs.net'  ; * RLM01
            MVIN<1>='SMatthews@afs.net'  ; * RLM01
            MVIN<2>=''
            MVIN<4>='AFS'
            MVIN<5>=SUBJ
            MVIN<6>=TEXT
            CALL SENDMAIL(MVIN,MVOUT)

            CALL FB.PRINT.CENT(1,80,12,'Restricted Access Application...')
            CALL FB.PRINT.CENT(1,80,14,'Access Denied! See CHRIS for Access')
            CALL ELINE('')
            STOP
      END CASE

*
* Open files
*
      OPEN '','FB.TABLES' TO F.TABLES ELSE CALL OPEN.ABORT('FB.TABLES',PROG.NAME)
      OPEN '','CARRIERS' TO F.CARRIERS ELSE CALL OPEN.ABORT('CARRIERS',PROG.NAME)
*
* Read Code Table from File
*
      READ DISP.REC FROM F.TABLES,'INVOICE.CARRIERS' ELSE DISP.REC=''
*
* First page of codes
*
      PAGE=1
      GOSUB 9000
*
* Enter selection code
*
100:  HELP="Enter line number. 'B'=Back. 'T'=Top, ''=Next page. 'E'=End-&-Save. 'EX'=Exit."
      CALL GEN.IN(0,22,"Enter line number please. ###",'',Q,0,3,'','',1,-3,22,0,QI,HELP,0,23)
      QI=OCONV(QI,'MCU')
      BEGIN CASE
         CASE QI=''
            PAGE=PAGE+1
            GOSUB 9000
         CASE QI='B'
            PAGE=PAGE-1
            IF PAGE=0 THEN PAGE=1
            GOSUB 9000
         CASE QI='T'
            PAGE=1
            GOSUB 9000
         CASE QI='EX'
            STOP
         CASE NUM(QI)
            SEL=QI
            IF SEL LT START ! SEL GT STOP THEN
               CALL ELINE('Invalid selection.')
               GOTO 100
            END
            GOSUB 8000
            GOSUB 8500
            GOSUB 9000
         CASE QI='E'
            X=DCOUNT(DISP.REC<1>,CHAR(253))
            FOR I=X TO 1 STEP -1
               IF DISP.REC<1,I>='' THEN
                  DISP.REC=DELETE(DISP.REC,1,I)
                  DISP.REC=DELETE(DISP.REC,2,I)
                  DISP.REC=DELETE(DISP.REC,3,I)
               END
            NEXT I
*
            WRITE DISP.REC ON F.TABLES,'INVOICE.CARRIERS'
            STOP
      END CASE
      GOTO 100
*
*------------------------------------------------------------------------
* INTERNAL SUBROUTINES
*------------------------------------------------------------------------
*
* Edit code
*
8000: ROW=3+REM(SEL,18)
      IF ROW=3 THEN ROW=21
      CARRIER=DISP.REC<1,SEL>
      CAP.INV=DISP.REC<2,SEL>
      PRT.INV=DISP.REC<3,SEL>
8010: HELP="Enter Carrier code. ''=Accept. 'D'=Delete. 'X'=Exit."
      CALL GEN.IN(4,ROW,CARRIER'L#5','',Q,0,5,'','',2,-5,ROW,0,QI,HELP,0,23)
      QI=OCONV(QI,'MCU')
      IF QI='X' THEN
         CRT @(4,ROW):CARRIER'L#5 '
         RETURN
      END
      IF QI='' AND CARRIER#'' THEN
         Q=CARRIER
         QI=CARRIER
      END
      IF QI='D' THEN
         FOR X = 1 TO 3
            DISP.REC=DELETE(DISP.REC,X,SEL)
         NEXT X
         GOSUB 9000
         RETURN
      END
      READ CARRIER.REC FROM F.CARRIERS,QI'R%5' ELSE
         CALL ELINE("Carrier ":QI'R%5':" not on file.")
         GO 8010
      END
      CARRIER=QI'R%5'
      CARRIER.NAME=CARRIER.REC<1>
      CRT @(4,ROW):CARRIER'L#5 '
      CRT @(12,ROW):CARRIER.NAME'L#35 '
*
8020: HELP="Capture Invoice Number . 'Y'=Yes. 'N'=No. '<'=Back to Carrier. 'X'=Exit."
      CALL GEN.IN(49,ROW,CAP.INV'L#2','',Q,0,2,'','',2,-2,ROW,0,QI,HELP,0,23)
      QI=OCONV(QI,'MCU')
      BEGIN CASE
         CASE QI='X' ! QI='<'
            CRT @(49,ROW):CAP.INV'L#10 '
            GOTO 8010
         CASE QI='' AND (CAP.INV='Y' OR CAP.INV='N')
            Q=CAP.INV
         CASE QI='Y' OR QI='N'
            NULL
         CASE 1
            CALL ELINE("Must be Y or N")
            GO 8020
      END CASE
      CAP.INV=Q
      CRT @(49,ROW):CAP.INV'L#10 '

8030: HELP="Print Invoice Number on Check. 'Y'=Yes. 'N'=No. '<'=Back to Capture. 'X'=Exit."
      CALL GEN.IN(60,ROW,PRT.INV'L#2','',Q,0,2,'','',2,-2,ROW,0,QI,HELP,0,23)
      QI=OCONV(QI,'MCU')
      BEGIN CASE
         CASE QI='X' ! QI='<'
            CRT @(60,ROW):PRT.INV'L#10 '
            GOTO 8020
         CASE QI='' AND (PRT.INV='Y' OR PRT.INV='N')
            Q=PRT.INV
         CASE QI='Y' OR QI='N'
            NULL
         CASE 1
            CALL ELINE("Must be Y or N")
            GO 8030
      END CASE
      PRT.INV=Q
      CRT @(60,ROW):PRT.INV'L#10 '
      DISP.REC<1,SEL>=CARRIER
      DISP.REC<2,SEL>=CAP.INV
      DISP.REC<3,SEL>=PRT.INV
      RETURN
*
* Sort Array
*
8500:*
      NEW.DISP.REC=''
      NVAL=DCOUNT(DISP.REC<1>,@VM)
      FOR VAL=1 TO NVAL
         CARRIER=DISP.REC<1,VAL>
         CAP.INV=DISP.REC<2,VAL>
         PRT.INV=DISP.REC<3,VAL>
         LOCATE CARRIER IN NEW.DISP.REC<1> BY 'AR' SETTING CPOS THEN NULL ELSE NULL
         INS CARRIER BEFORE NEW.DISP.REC<1,CPOS>
         INS CAP.INV BEFORE NEW.DISP.REC<2,CPOS>
         INS PRT.INV BEFORE NEW.DISP.REC<3,CPOS>
      NEXT VAL
      IF NEW.DISP.REC#DISP.REC THEN
         DISP.REC=NEW.DISP.REC
      END
      RETURN
*
* Display array
*
9000: CRT @(0,3):@(-3):"  #"
      CRT @(4,3):"Carrier #"
      CRT @(12,3):"Carrier Name"
      CRT @(49,3):"Capture"
      CRT @(60,3):"Print"
      START=(PAGE*18)-17
      IF START GT 108 THEN
         START=1
         PAGE=1
      END
      STOP=START+17
      FOR I=START TO STOP STEP 1
         ROW=3+REM(I,18)
         IF ROW=3 THEN ROW=21
         CARRIER=DISP.REC<1,I>
         CAP.INV=DISP.REC<2,I>
         PRT.INV=DISP.REC<3,I>
         READ CARRIER.REC FROM F.CARRIERS,CARRIER ELSE CARRIER.REC="** Not on file. **"
         IF CARRIER='' THEN CARRIER.REC=''
         CARRIER.NAME=CARRIER.REC<1>
         CRT @(0,ROW):I"R#3 "
         CRT @(4,ROW):CARRIER"L#5 "
         CRT @(12,ROW):CARRIER.NAME"L#35 "
         CRT @(49,ROW):CAP.INV"L#10 "
         CRT @(60,ROW):PRT.INV"L#10 "
      NEXT I
      RETURN
