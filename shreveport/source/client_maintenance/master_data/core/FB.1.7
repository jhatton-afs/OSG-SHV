****************************************************************
* Program : FB.1.7
* Author  : BRIAN M. QUEEN
* Date    : OCTOBER 5, 1991
* Library : FBBP
* Copyright (c) 2019 by AFS Logistics, LLC. - All Rights Reserved
* * PROGRAM TO MODIFY 3 DIGIT ZIP CODE TABLE
*
* Last updated by afs (AFSLOGISTICS\gbino) at 07:10:48 on 06/19/2019.
*
* Start modification history
* 061919 - GRB01 - Restrict access
* End modification history
***********************************************************************
$OPTIONS HEADER.EJECT
$OPTIONS EXEC.EQ.PERF
$OPTIONS INFO.ABORT
      ERRMSG=''

*
* Initialize variables.
*
      PROG.NAME='FB.1.7'
      PROG.DESC='EDIT 3 DIGIT ZIP CODES'
      PACK.NAME='FREIGHT BILLING'
      CALL AFS.SCR.HEAD(CO.ID,FILE.ID,'',PROG.NAME,PROG.DESC,PACK.NAME,CO.NAME,TIME.DATE,1)
      CALL GET.USER.NEW(USER,CH.NUM)

      UPL.VIN = 'FBBP'; UPL.VIN<2> = 'FB.1.7'; UPL.VOUT = ''   ; * NPR_UPL 04/23/2010
      UPL.VIN<3> = USER
      UPL.VIN<4> = CH.NUM
      CALL UPD.PROGRAM.LOG(UPL.VIN,UPL.VOUT)    ; * NPR_UPL 04/23/2010
      PROMPT''
      ADD.FLAG=0
*
* Open files.
*
      OPEN '','ZIPS.CODES' TO F.ZIPS ELSE CALL OPEN.ABORT("ZIPS",PROG.NAME)
      *Begin GRB01
      OPEN '','OPERATOR' TO F.OP ELSE CALL OPEN.ABORT("OPERATOR",PROG.NAME)
      READV OP.FLG FROM F.OP,USER,67 ELSE
         OP.FLG<1,1> = "N"
      END
      IF OP.FLG<1,1> # "Y" THEN
         *
* Restrict everyone else
*

         SUBJ="Unauthorized 3.DIGIT zip RECORD Access Attempt by ":USER
         TEXT=''
         TEXT<1,-1>=USER:' ':DATE()'D2/':' ':OCONV(TIME(),'MTHS'):' ':PROG.NAME
         TEXT<1,-1>='User ':USER:' tried to access ZIPS.CODES 3.DIGIT'
         TEXT<1,-1>="(This user's name was not included in the program to allow access)"
         MVIN=''
*          MVIN<1>='gbino@afs.net'
         MVIN<1>='david@afs.net'
         MVIN<1>='nmeddaugh@afs.net'
         MVIN<4>='AFS'
         MVIN<5>=SUBJ
         MVIN<6>=TEXT
         CALL SENDMAIL(MVIN,MVOUT)

         CALL FB.PRINT.CENT(1,80,12,'Restricted Access Application')
         CALL FB.PRINT.CENT(1,80,14,'Access Denied! See the greg, daid, nicole for Access')
         CALL ELINE('')
         STOP
      END
      * End GRB01
*
* Get three digit zip code table.
*
      READU ZIP.REC FROM F.ZIPS,'3.DIGIT' ELSE ZIP.REC=''
*
* First page of codes.
*
      PAGE=1
      GOSUB 9000
*
* Enter selection code.
*
100:  HELP="Enter line number. 'B'=Back. 'T'=Top, ''=Next page. 'E'=End. 'EX'=Exit."
      CALL GEN.IN(0,22,"Enter line number please. ###",'',Q,0,3,'','',1,-3,22,0,QI,HELP,0,23)
      QI=OCONV(QI,'MCU')
      BEGIN CASE
         CASE QI=''
            PAGE=PAGE+1
            GOSUB 9000
         CASE QI='B'
            PAGE=PAGE-1
            IF PAGE=0 THEN PAGE=1
            GOSUB 9000
         CASE QI='T'
            PAGE=1
            GOSUB 9000
         CASE QI='EX'
            STOP
         CASE NUM(QI)
            SEL=QI
            IF SEL LT START ! SEL GT STOP THEN
               CALL ELINE('Invalid selection.')
               GOTO 100
            END
            GOSUB 8000
         CASE QI='E'
            X=DCOUNT(ZIP.REC<1>,CHAR(253))
            FOR I=X TO 1 STEP -1
               IF ZIP.REC<1,I>='' THEN
                  ZIP.REC=DELETE(ZIP.REC,1,I)
                  ZIP.REC=DELETE(ZIP.REC,2,I)
               END
            NEXT I
            WRITE ZIP.REC ON F.ZIPS,'3.DIGIT'
            STOP
      END CASE
      GOTO 100
*
* Edit zip code.
*
8000: ROW=3+REM(SEL,18)
      IF ROW=3 THEN ROW=21
      CODE=ZIP.REC<1,SEL>
      LOW=ZIP.REC<2,SEL>
      HIGH=ZIP.REC<3,SEL>
*
8010: HELP="Enter new state code. ''=Accept. 'D'=Delete. 'X'=Exit."
      CALL GEN.IN(8,ROW,CODE'L#2','',Q,0,2,'','',2,-2,ROW,0,QI,HELP,0,23)
      QI=OCONV(QI,'MCU')
      BEGIN CASE
         CASE QI='X'
            CRT @(8,ROW):CODE'L#3'
            RETURN
         CASE QI=''
            Q=CODE
         CASE QI='EX' OR QI='ex"'
            CALL ELINE('Invalid selection.')
            GO 100
         CASE QI='D'
            ZIP.REC=DELETE(ZIP.REC,1,SEL)
            ZIP.REC=DELETE(ZIP.REC,2,SEL)
            ZIP.REC=DELETE(ZIP.REC,3,SEL)
            GOSUB 9000
            RETURN
      END CASE
      CODE=Q
      CRT @(8,ROW):CODE'L#2'
*
8020: HELP="Enter new low value. ''=Accept. 'X'=Back to state code."
      CALL GEN.IN(13,ROW,LOW'L#3','',Q,0,3,'R','0',2,-3,ROW,0,QI,HELP,0,23)
      QI=OCONV(QI,'MCU')
      BEGIN CASE
         CASE QI='X'
            CRT @(13,ROW):LOW'L#3'
            GOTO 8010
         CASE QI=''
            Q=LOW
         CASE NUM(QI)
            NULL
         CASE 1
            CALL ELINE('Invalid entry.')
      END CASE
      LOW=Q
      CRT @(13,ROW):LOW'L#3'
*
      HELP="Enter new high value. ''=Accept. 'X'=Back to low value."
      CALL GEN.IN(19,ROW,HIGH'L#3','',Q,0,3,'R','0',2,-3,ROW,0,QI,HELP,0,23)
      QI=OCONV(QI,'MCU')
      BEGIN CASE
         CASE QI='X'
            CRT @(19,ROW):HIGH'L#3'
            GOTO 8010
         CASE QI=''
            Q=HIGH
         CASE NUM(QI)
            NULL
         CASE 1
            CALL ELINE('Invalid entry.')
      END CASE
      HIGH=Q
      CRT @(19,ROW):HIGH'L#3'
*
      ZIP.REC<1,SEL>=CODE
      ZIP.REC<2,SEL>=LOW
      ZIP.REC<3,SEL>=HIGH
      RETURN
*
* DISPLAY EXPENSE ARRAY
*
9000: CRT @(0,3):@(-3):"Line State   Low  High"
      START=(PAGE*18)-17
      IF START GT 72 THEN
         START=1
         PAGE=1
      END
      STOP=START+17
      FOR I=START TO STOP STEP 1
         ROW=3+REM(I,18)
         IF ROW=3 THEN ROW=21
         CODE=ZIP.REC<1,I>
         LOW=ZIP.REC<2,I>
         HIGH=ZIP.REC<3,I>
         PRINT @(0,ROW):I"R#4":'    ':CODE"L#2":'   ':LOW'L#3':'   ':HIGH'L#3'
      NEXT I
      RETURN
