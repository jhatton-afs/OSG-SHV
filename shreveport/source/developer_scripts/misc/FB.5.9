$OPTIONS HEADER.EJECT
ERRMSG=''
*
$OPTIONS EXEC.EQ.PERF
$OPTIONS INFO.ABORT
*
* PROGRAM: FB.5.9
* PROGRAM TO ENTER CHECKS THAT HAVE CLEARED THE BANK
*
* GENERAL LEDGER PACKAGE
*
* AUTHOR: BRIAN M. QUEEN
* DATE: APRIL 30, 1992
*
* THIS PROGRAM IS A PROPRIETARY PRODUCT OF:
*
* VARSITY COMPUTING, INC.
* 6060 NORTH CENTRAL EXPRESSWAY, SUITE 538
* DALLAS, TX 75206
* (214)692-8484
*
* AND MAY NOT BE USED, COPIED OR MODIFIED WITHOUT THE
* EXPRESS WRITTEN CONSENT OF SAME.
*
* 090806 - JCW01 - USE NEW FB.CHECKS FILES
*
* Initialize variables
*
      PROMPT ''
      PROG.NAME='FB.5.9'
      PROG.DESC='ENTER CLEARED CHECKS'
      PACK.NAME='FREIGHT BILLING'
*
       UPL.VIN = 'FBBP' ;  UPL.VIN<2> = 'FB.5.9' ; UPL.VOUT = ''                    ;* NPR_UPL 04/23/2010
       CALL UPD.PROGRAM.LOG(UPL.VIN,UPL.VOUT)                                                           ;* NPR_UPL 04/23/2010
* Open files
*
*     OPEN '','FB.CHECKS' TO F.CHECKS ELSE CALL OPEN.ABORT("FB.CHECKS",PROG.NAME)         ; * JCW01
      OPEN '','FB.CHECKS.HDR' TO F.CHECKS ELSE CALL OPEN.ABORT("FB.CHECKS.HDR",PROG.NAME) ; * JCW01
      OPEN '','FB.BANKS' TO F.BANK ELSE CALL OPEN.ABORT("FB.BANKS",PROG.NAME)
*
* Display screen heading
*
      CALL AFS.SCR.HEAD(CO.ID,FILE.ID,'',PROG.NAME,PROG.DESC,PACK.NAME,CO.NAME,TIME.DATE,1)
*
* Select bank account
*
100   HELP="Enter bank account ID. ''=Exit."
      CALL GEN.IN(0,3,'Enter bank ID -','',Q,0,1,'','',0,-1,3,0,QI,HELP,0,23)
      QI=OCONV(QI,'MCU')
      IF QI='' THEN STOP
      READ BANK.REC FROM F.BANK,'BANK*':QI ELSE
        CALL ELINE('Invalid bank ID ':QI)
        GOTO 100
      END
      BANK.ID=QI
*
* Get statement date
*
200   HELP="Enter date of bank statement. ''=Accept. 'X'=Back to bank ID. 'EX'=Exit."
      CALL GEN.IN(40,3,'Enter statement date ':DATE()'D2/','DATE',Q,0,8,'','',0,-8,3,0,QI,HELP,0,23)
      QI=OCONV(QI,'MCU')
      BEGIN CASE
        CASE QI='X'
          CRT @(40,3):@(-4)
          GOTO 100
        CASE QI='EX'
          STOP
      END CASE
      CRT @(61,3):Q'D2/'
      IF Q > DATE() THEN
        CALL ELINE("Error! Date CANNOT be Greater than Today!")
        GO 200
      END
      IF DATE() - Q > 30 AND DATE() > 12136 THEN
        CALL ELINE("Error! Date CANNOT be more than 30 days ago")
        GO 200
      END
      TEST.DATE=Q+1
      OTEST.DATE=OCONV(TEST.DATE,'D2-')
      TEST.DAY=FIELD(OTEST.DATE,'-',2)
      IF TEST.DAY#1 THEN
        CALL ELINE("Error! Date MUST be the Last Day of the Month!")
        GO 200
      END
      CLEARED.DATE=Q
      CRT @(61,3):CLEARED.DATE'D2/'
*
* Start entering cleared checks and amounts
*
      CHECK.NUM=0
      LINE=4
300   IF LINE GT 21 THEN
        LINE=4
        CRT @(0,LINE):@(-3):
      END
      HELP="Enter check number. 'X'=End. 'EX'=Exit."
      CALL GEN.IN(0,LINE,'Check ':CHECK.NUM'R%6','',Q,0,6,'','',0,-6,LINE,0,QI,HELP,0,23)
      QI=OCONV(QI,'MCU')
      BEGIN CASE
        CASE QI='X' ! QI='EX'
          STOP
        CASE QI=''
          Q=CHECK.NUM
        CASE NUM(QI)
          NULL
        CASE 1
          CALL ELINE("Invalid entry.")
          GOTO 300
      END CASE
      CHECK.NUM=Q'R%6'
      CRT @(6,LINE):CHECK.NUM'R%6'
      CHECK.ID=CHECK.NUM:'*':BANK.ID
*
* Get check record and information
*
      READ CHECK.REC FROM F.CHECKS,CHECK.ID ELSE
        CALL ELINE("Check ":CHECK.NUM:" Bank ":BANK.ID:" not on file.")
        GOTO 300
      END
      CHECK.DATE=CHECK.REC<2>
      CHECK.AMT=CHECK.REC<4>
*
      IF CHECK.REC<6>='' THEN
        CLEARED.AMT=CHECK.AMT
      END ELSE
        CALL ELINE("Check ":CHECK.NUM:" Bank ":BANK.ID:" clear on ":CHECK.REC<5>'D2/':" for ":CHECK.REC<6>'R26':".")
        CLEARED.AMT=CHECK.REC<6>
      END
*
      CRT @(12,LINE):' Date ':CHECK.DATE'D2/':' Amount ':CHECK.AMT'R26,#13':' Cleared ':CLEARED.AMT'R26,#13'
325   HELP="Enter cleared amount. ' '=Unclear check. 'X'=Back."
      CALL GEN.IN(60,LINE,CLEARED.AMT'R26,#13','AMT2',Q,1,13,'','',0,-13,LINE,0,QI,HELP,0,23)
      QI=OCONV(QI,'MCU')
      BEGIN CASE
        CASE QI='X'
          CRT @(0,LINE):@(-4):
          GOTO 300
        CASE QI=' '
*DF           IF CHECK.REC<5>#'' AND CHECK.REC<5>+7 < DATE() THEN
*DF             CALL ELINE("Unable to Remove Cleared date!")
*DF             GO 325
*DF           END
          CLEARED.AMT=''
          CRT @(60,LINE):CLEARED.AMT'R26,#13'
          CHECK.REC<5>=''
          CHECK.REC<6>=''
          WRITE CHECK.REC ON F.CHECKS,CHECK.ID
          CHECK.NUM=CHECK.NUM+1
          LINE=LINE+1
          GOTO 300
       CASE NOT(NUM(Q))
         CALL ELINE("Error!  You must enter a number!")
         GO 325
        CASE Q NE CHECK.AMT
          CLEARED.AMT=Q
          CRT @(60,LINE):CLEARED.AMT'R26,#13'
          CRT @(0,22):CHAR(7):
          LN='Cleared amount not equal to check amount. Is cleared amount correct? N'
350       CALL GEN.IN(0,22,LN,'',Q,1,1,'','',0,-1,22,1,QI,HELP,0,23)
          QI=OCONV(QI,'MCU')
          BEGIN CASE
            CASE QI='N'
              GOTO 300
            CASE QI='Y'
              NULL
            CASE 1
              GOTO 350
          END CASE
        CASE Q EQ CHECK.AMT
          CLEARED.AMT=CHECK.AMT
        CASE 1
          GOTO 325
      END CASE
      CRT @(60,LINE):CLEARED.AMT'R26,#13'
*
      CHECK.REC<5>=CLEARED.DATE
      CHECK.REC<6>=CLEARED.AMT
      WRITE CHECK.REC ON F.CHECKS,CHECK.ID
      CHECK.NUM=CHECK.NUM+1
      LINE=LINE+1
      GOTO 300
