* PROGRAM TO UPDATE CORPORATE LOCATIONS FILE
*
* FREIGHT BILLING PACKAGE
*
* AUTHOR     : J. MICHAEL KERBOW
* DATE       : 06-2005
* DESCRIPTION: THIS PROGRAM UPDATES THE CORPORATE LOCATIONS FILE
*********************************************************************
* 031715 - DRF01 - Gave CBULGER the same access as JDARNELL
*
      $INCLUDE PROG.ADMIN INC_OPTIONS
*
* INITIALIZE VARIABLES
*
      PROMPT''
      ADD.FLAG=0
      PROG.NAME='UPDATE.LOCATIONS'                 ; * JMK01
      PROG.DESC='CORPORATE LOCATION CODES'         ; * JMK01
      PACK.NAME='FREIGHT BILLING'
      ERRMSG=''
      UPL.VIN = 'FBBP' ;  UPL.VIN<2> = 'UPDATE.LOCATIONS' ; UPL.VOUT = ''                    ;* NPR_UPL 04/23/2010
      CALL UPD.PROGRAM.LOG(UPL.VIN,UPL.VOUT)                                                           ;* NPR_UPL 04/23/2010
      LOC.ID = ''
      LOC.REC = ''
      OLD.LOC.REC = ''
      MODIFY=0
      CALL AFS.SCR.HEAD(CO.ID,FILE.ID,PERIOD,PROG.NAME,PROG.DESC,PACK.NAME,CO.NAME,TIME.DATE,1)
      CALL GET.USER(USER.NAME)
      USER.NAME=OCONV(USER.NAME,'MCU')
* Restrict access
      BEGIN CASE
         CASE USER.NAME='APERKINS'
         CASE USER.NAME='BPOOL'
         CASE USER.NAME='BRIAN'
         CASE USER.NAME='BRIANP'
         CASE USER.NAME='CBULGER'     ; * DRF01
         CASE USER.NAME='DAVID'
*        CASE USER.NAME='DEBBIE'
         CASE USER.NAME='DPAYNE'
         CASE USER.NAME='GSHAFER'
         CASE USER.NAME='JDARNELL'
*        CASE USER.NAME='KAREN'
         CASE USER.NAME='MKERBOW'
*        CASE USER.NAME='NPRATT'
*        CASE USER.NAME='OWEN'
         CASE USER.NAME='PBASS'
*        CASE USER.NAME='TWOODRUFF'
         CASE 1
            TEXT='This is a restricted access application - Access Denied'
            CALL CENTER(TEXT,80)
            CRT @(0,15):TEXT
            CALL ELINE('')
            STOP
      END CASE

*
* OPEN FILES
*
      OPEN '','CLIENTS' TO F.CLIENTS ELSE
         CALL OPEN.ABORT("CLIENTS",PROG.NAME)
      END
      OPEN '','LOCATIONS' TO F.LOCATIONS ELSE
         CALL OPEN.ABORT("LOCATIONS",PROG.NAME)
      END
      OPEN '','ZIPS.CODES' TO F.ZIPS ELSE
         CALL OPEN.ABORT("ZIPS",PROG.NAME)
      END
      OPEN '','CAN.ZIP.CODES' TO F.CAN.ZIPS ELSE
         CALL OPEN.ABORT("CAN.ZIP.CODES",PROG.NAME)
      END
      READ ZIP3.REC FROM F.ZIPS,'3.DIGIT' ELSE ZIP3.REC=''
      GOSUB DISPLAY.SCREEN
      GOSUB ASSIGN.DATA
      GOSUB DISPLAY.DATA

*********************************************************************
GET.LOC: * EDIT LOCATION NUMBER
*********************************************************************
      HELP="Enter Location code. ''=Exit. 'X'=Exit. 'L'=List all locs."
      CALL GEN.IN(17,5,'','',Q,0,5,'','',2,17,5,0,QI,HELP,0,23)
      QI=OCONV(QI,'MCU')
      Q=OCONV(Q,'MCU')
      BEGIN CASE
         CASE QI='X' OR QI = '' OR QI='EX'
            RELEASE F.LOCATIONS
            STOP
         CASE QI='L'
            EXECUTE \SORT LOCATIONS 1 HEADING "List of all locations'CL'"\
            CALL ELINE('Press <ENTER> to continue.')
            CALL AFS.SCR.HEAD(CO.ID,FILE.ID,PERIOD,PROG.NAME,PROG.DESC,PACK.NAME,CO.NAME,TIME.DATE,1)
            GOSUB DISPLAY.SCREEN
            GOSUB ASSIGN.DATA
            GOSUB DISPLAY.DATA
            GO GET.LOC
         CASE NUM(QI) AND LEN(QI) <= 5
            LOC.ID = Q'R%5'
            CRT @(17,5) : LOC.ID : @(-4) :
            GOSUB DISPLAY.SCREEN
            READ LOC.REC FROM F.LOCATIONS, LOC.ID THEN
               OLD.LOC.REC = LOC.REC
               MODIFY=1
* IF USER.NAME='NPRATT' THEN DEBUG
               GOSUB ASSIGN.DATA
* IF USER.NAME='NPRATT' THEN DEBUG
               GOSUB DISPLAY.DATA
* IF USER.NAME='NPRATT' THEN DEBUG
               GOSUB START.SEL
            END ELSE
               LOC.REC = ''
               MODIFY=0
               GOSUB ASSIGN.DATA
               GOSUB GET.DATA
               GOSUB START.SEL
            END
         CASE 1
            CALL ELINE('Invalid entry.')
      END CASE
      GO UPDATE.INFO

*********************************************************************
START.SEL: ***
*********************************************************************
      MODIFY=1
      GOSUB ASSIGN.DATA
      GOSUB DISPLAY.DATA
      HELP="Enter number of information to change. (1 to 10), [D]=Delete"
      CALL GEN.IN(0,22,'Enter Selection Number ##','',Q,0,2,'','',2,-2,22,1,QI,HELP,0,23)
      QI=OCONV(QI,'MCU')
      BEGIN CASE
         CASE QI=''
            GO UPDATE.INFO
         CASE QI='EX'
            STOP
         CASE QI='X'
            RETURN
         CASE NUM(QI)
            IF QI GE 1 AND QI LE 10 THEN
               ON QI GOSUB GET.LOC,GET.DESC,GET.PADDR1,GET.PADDR2,GET.PZIP,GET.MADDR1,GET.MADDR2,GET.MZIP,GET.PHONE,GET.PHONE
            END ELSE
               CALL ELINE('Invalid Entry, please try again')
            END
         CASE 1
      END CASE
      GO START.SEL
GET.DATA: * START OF ENTRY PROCESSES
*********************************************************************
GET.DESC: *  GET THE A BRIEF DESCRIPTION OF THE LOCATION
*********************************************************************
      HELP="Enter a brief description of the location. ''=Accept. 'X'=Exit."
      CALL GEN.IN(17,6,DESC,'',Q,0,35,'','',2,17,6,0,QI,HELP,0,23)
      QI=OCONV(QI,'MCU')
      BEGIN CASE
         CASE QI='EX'
            STOP
         CASE QI='X'
            CRT @(17,6): DESC
            RETURN
         CASE QI = ''
            NULL
         CASE QI = ' '
            DESC = ''
         CASE 1
            DESC = TRIM(Q)
      END CASE
      LOC.REC<1>=DESC
      IF MODIFY THEN RETURN
      GOSUB ASSIGN.DATA
      GOSUB DISPLAY.DATA

************************************************************************
GET.PADDR1: * GET THE PHYSICAL ADDRESS LINE 1
************************************************************************
      SAME=0
      COL=15
      ROW=9
      ADDR1=PADDR1
      GOSUB GET.ADDR1
      LOC.REC<2,1>=ADDR1
      IF MODIFY THEN RETURN
      GOSUB ASSIGN.DATA
      GOSUB DISPLAY.DATA

************************************************************************
GET.PADDR2: * GET THE PHYSICAL ADDRESS LINE 2
************************************************************************
      COL=15
      ROW=10
      ADDR2=PADDR2
      GOSUB GET.ADDR2
      LOC.REC<2,2>=ADDR2
      IF MODIFY THEN RETURN
      GOSUB ASSIGN.DATA
      GOSUB DISPLAY.DATA

*********************************************************************
GET.PZIP: * GET THE PHYSICAL ZIP CODE
*********************************************************************
      COL=15
      ROW=11
      ZIP.CODE=PZIP
      GOSUB GET.ZIP
      LOC.REC<3>=ZIP.CODE
      IF MODIFY THEN RETURN
      GOSUB ASSIGN.DATA
      GOSUB DISPLAY.DATA

************************************************************************
GET.MADDR1: * GET THE MAILING ADDRESS LINE 1
************************************************************************
      SAME=1
      COL=55
      ROW=9
      ADDR1=MAIL.ADDR1
      GOSUB GET.ADDR1
      IF ADDR1='S' THEN
         LOC.REC<4>=LOC.REC<2>
         LOC.REC<5>=LOC.REC<3>
         GOSUB ASSIGN.DATA
         GOSUB DISPLAY.DATA
         GOSUB GET.PHONE
         RETURN
      END ELSE
         LOC.REC<4,1>=ADDR1
      END
      IF MODIFY THEN RETURN
      GOSUB ASSIGN.DATA
      GOSUB DISPLAY.DATA

************************************************************************
GET.MADDR2: * GET THE MAILING ADDRESS LINE 2
************************************************************************
      COL=55
      ROW=10
      ADDR2=MAIL.ADDR2
      GOSUB GET.ADDR2
      LOC.REC<4,2>=ADDR2
      IF MODIFY THEN RETURN
      GOSUB ASSIGN.DATA
      GOSUB DISPLAY.DATA

*********************************************************************
GET.MZIP: * GET THE MAILING ZIP CODE
*********************************************************************
      COL=55
      ROW=11
      ZIP.CODE=MAIL.ZIP
      GOSUB GET.ZIP
      LOC.REC<5>=ZIP.CODE
      IF MODIFY THEN RETURN
      GOSUB ASSIGN.DATA
      GOSUB DISPLAY.DATA
      GOSUB GET.PHONE
      RETURN

************************************************************************
UPDATE.INFO: ***
************************************************************************
      IF OLD.LOC.REC # LOC.REC THEN
         HELP="Enter either Y or N."
         LNE='Location information changed. Update? -'
         CALL GEN.IN(0,22,LNE,'',Q,1,1,'','',2,-1,22,1,QI,HELP,0,23)
         QI=OCONV(QI,'MCU')
         BEGIN CASE
            CASE QI='Y'
               WRITE LOC.REC ON F.LOCATIONS,LOC.ID
            CASE QI='N'
            CASE 1
               GO UPDATE.INFO
         END CASE
      END
      CALL AFS.SCR.REFRESH(PROG.NAME,PROG.DESC,PACK.NAME,CO.NAME,TIME.DATE,1)
      LOC.REC=''
      LOC.ID=''
      GOSUB DISPLAY.SCREEN
      GOSUB ASSIGN.DATA
      GOSUB DISPLAY.DATA
      GO GET.LOC
      RETURN

************************************************************************
DISPLAY.SCREEN: ***
************************************************************************
      CRT @(00,05):' 1 Location....'
      CRT @(00,06):' 2 Description.'
      CRT @(16,08):'Physical'
      CRT @(56,08):'Mailing'
      CRT @(00,09):' 3 Address 1.'
      CRT @(00,10):' 4 Address 2.'
      CRT @(00,11):' 5 Zip.......'
      CRT @(00,12):'   State.....'
      CRT @(00,13):'   City......'
      CRT @(40,09):' 6 Address 1.'
      CRT @(40,10):' 7 Address 2.'
      CRT @(40,11):' 8 Zip.......'
      CRT @(40,12):'   State.....'
      CRT @(40,13):'   City......'
      CRT @(00,14):STR('=',80):
      CRT @(00,15):' 9 Phone....... Desc..................... Fax......... Desc.....................'
      RETURN

************************************************************************
DISPLAY.DATA: ***
************************************************************************
      CRT @(17,05):LOC.ID 'L#5'
      CRT @(17,06):DESC'L#35'
      CRT @(15,09):PADDR1'L#25'
      CRT @(15,10):PADDR2'L#25'
      CRT @(15,11):PZIP'L#5'
      CRT @(15,12):PSTATE'L#2'
      CRT @(15,13):PCITY'L#25'
      CRT @(55,09):MAIL.ADDR1'L#25'
      CRT @(55,10):MAIL.ADDR2'L#25'
      CRT @(55,11):MAIL.ZIP'L#5'
      CRT @(55,12):MAIL.STATE'L#2'
      CRT @(55,13):MAIL.CITY'L#25'
      CRT @(0,16):@(-3):
      FOR X=1 TO 5
         ROW=15+X
         IF VPHONE<1,X>#'' THEN
            CRT @(3,ROW):VPHONE<1,X>'R### ###-####'  ; * NPR 080509 - Fixed display issue
            CRT @(16,ROW):PH.TYPE<1,X>'L#25 '
         END
         IF FAX<1,X>#'' THEN
            CRT @(42,ROW):FAX<1,X> 'R### ###-####'   ; * NPR 080509 - Fixed display issue
            CRT @(55,ROW):FX.TYPE<1,X>'L#25':
         END
      NEXT X
      RETURN
************************************************************************
ASSIGN.DATA:
************************************************************************
      DESC=LOC.REC<1>
      PADDR1=LOC.REC<2,1>
      PADDR2=LOC.REC<2,2>
      PZIP=LOC.REC<3>
      IF PZIP='' THEN
         PCITY=''
         PSTATE=''
      END ELSE
         ZIP.ID=PZIP
         GOSUB READ.ZIP
         IF ZIP.DESC # 'ERROR' THEN
            GOOD.ANS = 1
            PCITY = ZIP.REC<1>
            PSTATE = ZIP.REC<2>
         END
      END
      MAIL.ADDR1=LOC.REC<4,1>
      MAIL.ADDR2=LOC.REC<4,2>
      MAIL.ZIP=LOC.REC<5>
      IF MAIL.ZIP='' THEN
         MAIL.CITY=''
         MAIL.STATE=''
      END ELSE
         ZIP.ID=MAIL.ZIP
         GOSUB READ.ZIP
         IF ZIP.DESC # 'ERROR' THEN
            GOOD.ANS = 1
            MAIL.CITY = ZIP.REC<1>
            MAIL.STATE = ZIP.REC<2>
         END
      END
      VPHONE=LOC.REC<6>
      PH.TYPE=LOC.REC<7>
      FAX=LOC.REC<8>
      FX.TYPE=LOC.REC<9>
      RETURN
*********************************************************************
READ.ZIP: * SUBROUTINE TO RETRIEVE CITY AND STATE
*********************************************************************
      BEGIN CASE
         CASE LEN(ZIP.ID)=5
            READ ZIP.REC FROM F.ZIPS, ZIP.ID ELSE
               ZIP.REC = "~~~ Unknown ~~~"
            END
            ZIP.DESC = ZIP.REC<1> : ' ' : ZIP.REC<2>
         CASE LEN(ZIP.ID)=6
            READ ZIP.REC FROM F.CAN.ZIPS, ZIP.ID ELSE
               ZIP.REC = '~~~ Unknown ~~~'
            END
            ZIP.DESC = ZIP.REC<1>:' ':ZIP.REC<2>
         CASE LEN(ZIP.ID)=3
            ZIP.DESC = '~~~ Unknown ~~~'
            X=DCOUNT(ZIP3.REC<1>,CHAR(253))
            FOR I=1 TO X
               IF ZIP.ID GE ZIP3.REC<2,I> & ZIP.ID LE ZIP3.REC<3,I> THEN
                  ZIP.DESC = ZIP3.REC<1,I>
                  EXIT
               END
            NEXT I
         CASE 1
            CALL ELINE('Invalid entry.')
            ZIP.DESC='ERROR'
      END CASE
      RETURN
************************************************************************
GET.ADDR1:
************************************************************************
      IF SAME THEN
         HELP="Enter addr 1. [space]=Clear. [S]=Same."
      END ELSE
         HELP="Enter addr 1. [space]=Clear."
      END
      MASK=ADDR1:STR('-',25-LEN(ADDR1))
      CALL GEN.IN(COL,ROW,MASK,'',Q,0,25,'','',2,-25,ROW,0,QI,HELP,0,23)
      QI=OCONV(QI,'MCU')
      BEGIN CASE
         CASE QI='EX'
            STOP
         CASE QI='X'
            GO START.SEL
         CASE QI='S'
            ADDR1 = 'S'
         CASE QI = ' '
            ADDR1 =''
         CASE QI=''
            NULL
         CASE 1
            ADDR1 = TRIM(Q)
      END CASE
      RETURN

************************************************************************
GET.ADDR2:
************************************************************************
      HELP="Enter addr 2. [space]=Clear."
      MASK=ADDR2:STR('-',25-LEN(ADDR2))
      CALL GEN.IN(COL,ROW,MASK,'',Q,0,25,'','',2,COL,ROW,0,QI,HELP,0,23)
      QI=OCONV(QI,'MCU')
      BEGIN CASE
         CASE QI='EX'
            STOP
         CASE QI='X'
            GO START.SEL
         CASE QI=''
            NULL
         CASE QI = ' '
            ADDR2 =''
         CASE 1
            ADDR2 = TRIM(Q)
      END CASE
      RETURN

*********************************************************************
GET.ZIP: *  GET THE ZIP CODE THAT NAMES THIS LOCATION
*********************************************************************
      HELP="Enter zip code. ''=Accept. 'X'=Exit."
      GOOD.ANS = 0
      LOOP UNTIL GOOD.ANS DO
         CRT @(COL,ROW):ZIP.CODE
         CALL GEN.IN(COL,ROW,ZIP.CODE,'',Q,0,6,'','',2,COL,ROW,0,QI,HELP,0,23)
         QI=OCONV(QI,'MCU')
         BEGIN CASE
            CASE QI='X'
               CRT @(COL,ROW):ZIP.CODE
               GOOD.ANS = 1
            CASE QI='EX'
               STOP
            CASE QI='' AND ZIP.CODE # ''
               GOOD.ANS = 1
            CASE QI # '' AND LEN(Q) <= 6
               ZIP.ID = Q
               GOSUB READ.ZIP
               IF ZIP.DESC # 'ERROR' THEN
                  GOOD.ANS = 1
                  ZIP.CODE = Q
               END
            CASE 1
               CALL ELINE('Invalid entry.')
         END CASE
      REPEAT
      RETURN
************************************************************************
GET.PHONE:
************************************************************************
*
* First page of codes
*
      PAGE=1
      DISP.REC=''
      DISP.REC<1>=LOC.REC<6>
      DISP.REC<2>=LOC.REC<7>
      DISP.REC<3>=LOC.REC<8>
      DISP.REC<4>=LOC.REC<9>
      GOSUB 9000
*
* Enter selection code
*
100   HELP="Enter line number. 'B'=Back. 'T'=Top, ''=Next page. 'E'=End. 'EX'=Exit."
      CALL GEN.IN(0,22,"Enter line number please. ##",'',Q,0,2,'','',1,-2,22,0,QI,HELP,0,23)
      QI=OCONV(QI,'MCU')
      BEGIN CASE
         CASE QI=''
            PAGE=PAGE+1
            GOSUB 9000
         CASE QI='B'
            PAGE=PAGE-1
            IF PAGE=0 THEN PAGE=1
            GOSUB 9000
         CASE QI='T'
            PAGE=1
            GOSUB 9000
         CASE QI='EX'
            STOP
         CASE NUM(QI)
            SEL=QI
            IF SEL LT START ! SEL GT STOP THEN
               CALL ELINE('Invalid selection.')
               GOTO 100
            END
            GOSUB 8000
         CASE QI='E'
            X=DCOUNT(DISP.REC<1>,CHAR(253))
            FOR I=X TO 1 STEP -1
               IF DISP.REC<1,I>='' THEN
                  DISP.REC=DELETE(DISP.REC,1,I)
                  DISP.REC=DELETE(DISP.REC,2,I)
                  DISP.REC=DELETE(DISP.REC,3,I)
                  DISP.REC=DELETE(DISP.REC,4,I)
               END
            NEXT I
            LOC.REC<6>=DISP.REC<1>
            LOC.REC<7>=DISP.REC<2>
            LOC.REC<8>=DISP.REC<3>
            LOC.REC<9>=DISP.REC<4>
            CRT @(0,22):@(-4):
            GOSUB DISPLAY.SCREEN
            GOSUB ASSIGN.DATA
            GOSUB DISPLAY.DATA
*
            RETURN
      END CASE
      GOTO 100
*
*------------------------------------------------------------------------
* INTERNAL SUBROUTINES
*------------------------------------------------------------------------
*
* Edit code
*
8000  ROW=15+REM(SEL,5)
      IF ROW=15 THEN ROW=20
      PHONE=DISP.REC<1,SEL>
      PH.MASK='### ###-####'
      IF PHONE#'' THEN
         PH.MASK=PHONE 'R### ###-####'
      END
      PH.DESC=DISP.REC<2,SEL>
      FAX=DISP.REC<3,SEL>
      FX.MASK='### ###-####'
      IF FAX#'' THEN
         FX.MASK=FAX 'R### ###-####'
      END
      FX.DESC=DISP.REC<4,SEL>
8010  HELP="Enter phone number. ''=Accept. 'D'=Delete. 'X'=Exit."
      CALL GEN.IN(3,ROW,PH.MASK'L#12','PHONE',Q,0,12,'','',2,3,ROW,0,QI,HELP,0,23)
      QI=OCONV(QI,'MCU')
      IF INDEX(QI,'X',1) THEN
         CRT @(3,ROW):PH.MASK'L#12'
         RETURN
      END
      IF QI='' THEN
         Q=PHONE
         QI=PHONE
      END
      IF QI='D' THEN
         FOR X = 1 TO 4
            DISP.REC=DELETE(DISP.REC,X,SEL)
         NEXT X
         GOSUB 9000
         RETURN
      END
      PHONE=QI
      CRT @(3,ROW):PHONE 'R### ###-####'
*
8020  HELP="Enter phone description. '<'=Back. 'X'=Exit."
      CALL GEN.IN(16,ROW,PH.DESC'L#25','',Q,0,25,'','',2,16,ROW,0,QI,HELP,0,23)
      QI=OCONV(QI,'MCU')
      BEGIN CASE
         CASE QI='X'
            CRT @(16,ROW):PH.DESC'L#25'
            RETURN
         CASE QI='<'
            CRT @(16,ROW):PH.DESC'L#25'
            GOTO 8010
         CASE QI=''
            Q=PH.DESC
      END CASE
      PH.DESC=Q
      CRT @(16,ROW):PH.DESC'L#25'
8030  HELP="Enter fax number. ''=Accept. '<'=Back. 'X'=Exit."
      CALL GEN.IN(42,ROW,FX.MASK'L#12','PHONE',Q,0,12,'','',2,42,ROW,0,QI,HELP,0,23)
      QI=OCONV(QI,'MCU')
      IF INDEX(QI,'X',1) THEN
         CRT @(42,ROW):FX.MASK'L#12'
         RETURN
      END
      IF QI='<' THEN
         CRT @(42,ROW):FX.MASK'L#12'
         GOTO 8020
      END
      IF QI='' THEN
         Q=FAX
         QI=FAX
      END
      FAX=QI
      CRT @(42,ROW):FAX 'R### ###-####'
*
8040  HELP="Enter fax description. '<'=Back. 'X'=Exit."
      CALL GEN.IN(55,ROW,FX.DESC'L#25','',Q,0,25,'','',2,55,ROW,0,QI,HELP,0,23)
      QI=OCONV(QI,'MCU')
      BEGIN CASE
         CASE QI='X'
            CRT @(55,ROW):FX.DESC'L#25'
            RETURN
         CASE QI='<'
            CRT @(55,ROW):FX.DESC'L#25'
            GOTO 8030
         CASE QI=''
            Q=FX.DESC
      END CASE
      FX.DESC=Q
      CRT @(55,ROW):FX.DESC'L#25'
      DISP.REC<1,SEL>=PHONE
      DISP.REC<2,SEL>=PH.DESC
      DISP.REC<3,SEL>=FAX
      DISP.REC<4,SEL>=FX.DESC
      RETURN
*
* Display array
*
9000 *
************************************************************************
***
************************************************************************
      CRT @(00,05):'   Location....'
      CRT @(00,06):'   Description.'
      CRT @(16,08):'Physical'
      CRT @(56,08):'Mailing'
      CRT @(00,09):'   Address 1.'
      CRT @(00,10):'   Address 2.'
      CRT @(00,11):'   Zip.......'
      CRT @(00,12):'   State.....'
      CRT @(00,13):'   City......'
      CRT @(40,09):'   Address 1.'
      CRT @(40,10):'   Address 2.'
      CRT @(40,11):'   Zip.......'
      CRT @(40,12):'   State.....'
      CRT @(40,13):'   City......'
      CRT @(00,14):STR('=',80):
      CRT @(00,15):'   Phone....... Desc..................... Fax......... Desc.....................'

      START=(PAGE*5)-4
      IF START GT 30 THEN
         START=1
         PAGE=1
      END
      STOP=START+4
      FOR I=START TO STOP STEP 1
         ROW=15+REM(I,5)
         IF ROW=15 THEN ROW=20
         PHONE=DISP.REC<1,I>
         IF PHONE#'' THEN
            PHONE=PHONE 'R### ###-####'
         END
         PH.TYPE=DISP.REC<2,I>
         FAX=DISP.REC<3,I>
         IF FAX#'' THEN
            FAX=FAX 'R### ###-####'
         END
         FX.TYPE=DISP.REC<4,I>
         CRT @(00,ROW):I'R#2 '
         CRT @(03,ROW):PHONE'L#12 '
         CRT @(16,ROW):PH.TYPE'L#25 '
         CRT @(42,ROW):FAX'L#12 '
         CRT @(55,ROW):FX.TYPE'L#25'
      NEXT I
      RETURN
      END
