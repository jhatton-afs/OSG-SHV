      CRT @(0,0):@(-1):
      EXECUTE "CREATE.FB.PRO.XREF"
      PROG.NAME='FB.DARLING.ACCRUAL.RPT'
      COMMAND=@SENTENCE
      IF FIELD(COMMAND,' ',1)='RUN' THEN
         OPTIONS=FIELD(COMMAND,' ',4,99)
      END ELSE
         OPTIONS=FIELD(COMMAND,' ',2,99)
      END
      EST.OPT=0
      IF INDEX(OPTIONS,'ESTIMATE',1) THEN
         EST.OPT=1
      END
      CALL GET.USER(USER)
      CALL CHANNEL(CH.NUM)
      TODAY=DATE()
      FNAME='BILLDATA.':USER
      OPEN '',FNAME TO F.BILLDATA ELSE
         CALL OPEN.ABORT(FNAME,PROG.NAME)
      END
      FNAME='FB.PRO.XREF'
      OPEN '',FNAME TO F.PRO.XREF ELSE
         CALL OPEN.ABORT(FNAME,PROG.NAME)
      END
      TB=CHAR(9)
      ATT=0
      BILLDATA.STMT='SSELECT BILLDATA.':USER:' WITH @ID = "Darling]"'
      EXECUTE BILLDATA.STMT RTNLIST BILLDATA.LIST CAPTURING BILLDATA.RESULTS
      BILLDATA.TCNT=@SELECTED
      BILLDATA.EOF=0
      BILLDATA.CNT=0
      BILLDATA.PER=''
      LOOP
         READNEXT BILLDATA.ID FROM BILLDATA.LIST ELSE BILLDATA.EOF=1
      UNTIL BILLDATA.EOF DO
         BILLDATA.CNT+=1
         CALL PROG.BAR(20,BILLDATA.CNT,BILLDATA.TCNT,BILLDATA.PER)
         READ BILLDATA.REC FROM F.BILLDATA,BILLDATA.ID THEN
            BILLDATA.ORG=BILLDATA.REC
            BILLDATA.REC=CHANGE(BILLDATA.REC,TB,@VM)
            BILLDATA.REC<1,16>="Remarks"
            BILLDATA.REC<1,17>="Est Darling Dt"
            BILLDATA.REC<1,18>="Paid Amount"
            NATT=DCOUNT(BILLDATA.REC,@AM)
            FOR ATT=2 TO NATT
               CALL PROG.BAR(22,ATT,NATT,'')
               BEGIN CASE
                  CASE BILLDATA.ID[1,14]="DarlingPiggott"
                     BOL_NO=''
                     PRO_NO=BILLDATA.REC<ATT,1>
                     SHIP.YR=ICONV(BILLDATA.REC<ATT,9>,'D')'DY'
                  CASE 1
                     BOL_NO=BILLDATA.REC<ATT,5>
                     PRO_NO=BILLDATA.REC<ATT,7>
                     SHIP.YR='20':BILLDATA.REC<ATT,1>[2,2]
               END CASE
               XREF.REC=''
               IF BOL_NO#'' THEN
                  CALL GET.ROOT.NUMBER(BOL_NO,BOL.BASE)
                  XREF.ID='B*':SHIP.YR:'*':BOL.BASE
                  READ XREF.REC FROM F.PRO.XREF,XREF.ID ELSE XREF.REC=''
               END
               IF XREF.REC='' THEN
                  IF PRO_NO#'' THEN
                     CALL GET.ROOT.NUMBER(PRO_NO,PRO.BASE)
                     XREF.ID='P*':SHIP.YR:'*':PRO.BASE
                     READ XREF.REC FROM F.PRO.XREF,XREF.ID ELSE XREF.REC=''
                  END
               END
               IF XREF.REC='' AND BILLDATA.REC<ATT,6>="Fedex Ground" THEN
                  CALL GET.ROOT.NUMBER(PRO_NO[4,9999],PRO.BASE)
                  XREF.ID='P*':SHIP.YR:'*':PRO.BASE
                  READ XREF.REC FROM F.PRO.XREF,XREF.ID ELSE XREF.REC=''
               END
               BEGIN CASE
                  CASE XREF.REC=''
                     IF BILLDATA.REC<ATT,16>='' THEN
                        BILLDATA.REC<ATT,16>='No record of this shipment at AFS'
                     END
                  CASE XREF.REC<5,1>=''
                     IF BILLDATA.REC<ATT,16>='' THEN
                        BILLDATA.REC<ATT,16>='FB received, but not yet processed'
                     END
                  CASE XREF.REC<7,1>=''
                     IF BILLDATA.REC<ATT,16>='' THEN
                        BILLDATA.REC<ATT,16>='FB processed ':XREF.REC<5,1>'D4/':', waiting for receipt'
                     END
                  CASE 1
                     IF XREF.REC<8,1>#'' THEN
                        BILLDATA.REC<ATT,17>=XREF.REC<8,1>'D4/'
                        BILLDATA.REC<ATT,18>=XREF.REC<12,1>'R26'
                     END
               END CASE
            NEXT ATT
            BILLDATA.REC=CHANGE(BILLDATA.REC,@VM,TB)
            IF BILLDATA.REC#BILLDATA.ORG THEN
               WRITE BILLDATA.REC ON F.BILLDATA,'UPD-':BILLDATA.ID
            END
SKIP.REC: *
         END
      REPEAT
9000: *
      END
