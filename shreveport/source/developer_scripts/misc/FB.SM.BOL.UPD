* PROGRAM TO CREATE OR MODIFY BOL STATUS FOR SWEDISH MATCH
*
* FREIGHT BILLING PACKAGE
*
* AUTHOR:      MIKE KERBOW
* DATE:        MAR 16 2005
* DESCRIPTION:
*
* FORMATTING MODS:
*********************************************************************
* 070808 - JMK01 - C081697 - Grant access to 1,6,11,11 Update BOL for Swedish Match
* 060414 - JMK02 - SysAid#10019 - Accrual File
* 060414 - JMK03 - SysAid#10027 - Cindy Lindsey - AccuTerm Access
* 080615 - DRF01 - Gave Cynthia access
* 071316 - GRB01 - SysAid 25819 - add operator table
$INCLUDE PROG.ADMIN INC_OPTIONS
*
* INITIALIZE VARIABLES
*
      PROMPT''
      ADD.FLAG=0
      PROG.NAME='FB.SM.BOL.UPD'
      PROG.DESC='SWEDISH MATCH SALES ORDERS'
      PACK.NAME='FREIGHT BILLING'
      ERRMSG=''
      UPL.VIN = 'FBBP' ; UPL.VIN<2> = 'FB.SM.BOL.UPD' ; UPL.VOUT = ''  ; * NPR_UPL 04/23/2010
      CALL UPD.PROGRAM.LOG(UPL.VIN,UPL.VOUT)       ; * NPR_UPL 04/23/2010
      BOL.REC = ''
      OLD.BOL.REC = ''
      CALL AFS.SCR.HEAD(CO.ID,FILE.ID,PERIOD,PROG.NAME,PROG.DESC,PACK.NAME,CO.NAME,TIME.DATE,1)
      CALL GET.USER(USER)
      CALL CHANNEL(CH.NUM)
      OPEN '','OPERATOR' TO F.OP ELSE CALL OPEN.ABORT("OPERATOR",PROG.NAME)      ; * GRB01	
      * Begin GRB01
      READV OP.FLG FROM F.OP,USER,19 ELSE
         OP.FLG = "N"
      END
      IF OP.FLG = "Y" THEN
         **    BEGIN CASE
*
* Authorized Users
*
         **** CASE USER = 'CLINDSEY'          ; * JMK03
         **** CASE USER = 'CYNTHIA'          ; * DRF01
         **** CASE USER = 'DAVID'
         **** CASE USER = 'MARY'
         **** CASE USER = 'SCHUTAI'           ; * JMK02
         **** CASE USER = 'SMATTHEWS'
         **** CASE USER = 'WPETTIGREW'
*
* Restrict the other Users
*
         **      CASE 1
      END ELSE
         CALL FB.PRINT.CENT(1,80,10,USER:' DOES NOT HAVE ACCESS TO PROCESS!')
         CALL FB.PRINT.CENT(1,80,12,'Restricted Access Application')
         CALL ELINE('Access Denied! - See Info Svcs')
         STOP
         **    END CASE
      END
      * End GRB01
*
* OPEN FILES
*
      OPEN '','CLIENTS' TO F.CLIENTS ELSE
         CALL OPEN.ABORT("CLIENTS",PROG.NAME)
      END
      CLIENT.ID = '01344'
      READ CLIENT.REC FROM F.CLIENTS,CLIENT.ID ELSE
         CALL ELINE(CLIENT.ID:' not on file')
         STOP
      END
      CLIENT.NAME = CLIENT.REC<2>
      OPEN '','FB.BOL.CRX,':CLIENT.ID TO F.CRX ELSE
         CALL OPEN.ABORT("FB.BOL.CRX,":CLIENT.ID,PROG.NAME)
      END

*********************************************************************
GET.BOL:* EDIT SALES ORDER ID
*********************************************************************
      CRT @(0,3) : 'Client Number.......': CLIENT.ID : ' ' : CLIENT.NAME :
      CRT @(0,4) : 'Sales Order Number..': @(-3) :
      HELP="Enter sales order number. ''=Exit. 'X'=Exit."
      CALL GEN.IN(20,4,'','',Q,0,15,'','',2,20,4,0,QI,HELP,0,23)
      QI=OCONV(QI,'MCU')
      Q=OCONV(Q,'MCU')
      BEGIN CASE
         CASE QI='X' OR QI = '' OR QI='EX'
            STOP
            RETURN
         CASE 1
            BOL.ID = Q
            CRT @(20,4) : BOL.ID : @(-4) :
            GOSUB DISPLAY.SCREEN
            READU BOL.REC FROM F.CRX, BOL.ID THEN
               OLD.BOL.REC = BOL.REC
               GOSUB ASSIGN.DATA
               GOSUB DISPLAY.DATA
               GOSUB START.SEL
            END ELSE
               CALL ELINE('Sales order not on file. Press <ENTER> to continue.')
               RELEASE F.CRX,BOL.ID
               GO GET.BOL
            END
      END CASE
      GO UPDATE.INFO

*********************************************************************
START.SEL:***
*********************************************************************
      HELP="Enter 'S' to change the status of this order."
      CALL GEN.IN(0,22,'Update status ##','',Q,0,2,'','',2,-2,22,1,QI,HELP,0,23)
      QI=OCONV(QI,'MCU')
      BEGIN CASE
         CASE QI=''
            GO UPDATE.INFO
         CASE QI='EX'
            STOP
         CASE QI='X'
            STOP
         CASE QI='S'
            GO GET.STATUS
         CASE 1
            CALL ELINE('Invalid Entry, please try again')
      END CASE
      GO START.SEL

*********************************************************************
GET.STATUS:*  CHANGE THE STATUS OF THIS ORDER TO VOID
*********************************************************************
      HELP="Change status of this sales order. 'V'=Void. ' '=Active. 'X'=Exit."
      CALL GEN.IN(15,16,'','',Q,0,2,'','',2,15,16,0,QI,HELP,0,23)
      QI=OCONV(QI,'MCU')
      BEGIN CASE
         CASE QI='EX'
            STOP
         CASE QI='X'
            IF SM.STS='V' THEN
               CRT @(15,16) :'VOID'
            END ELSE
               CRT @(15,16) :'ACTIVE'
            END
         CASE QI = ''
            NULL
         CASE QI = ' '
            SM.STS = ''
         CASE QI = 'V'
            SM.STS = 'V'
         CASE 1
            CALL ELINE('Invalid Entry, please try again')
            IF SM.STS='V' THEN
               CRT @(15,16) :@(-4):'VOID'
            END ELSE
               CRT @(15,16) :@(-4):'ACTIVE'
            END
            GO GET.STATUS
      END CASE
      IF SM.STS='V' THEN
         CRT @(15,16) :@(-4):'VOID'
      END ELSE
         CRT @(15,16) :@(-4):'ACTIVE'
      END
      BOL.REC<23>=SM.STS
      GO START.SEL

************************************************************************
UPDATE.INFO:***
************************************************************************
      IF OLD.BOL.REC # BOL.REC THEN
         HELP="Enter either Y or N."
         LNE='Sales order information changed. Update? -'
         CALL GEN.IN(0,22,LNE,'',Q,1,1,'','',2,-1,22,1,QI,HELP,0,23)
         QI=OCONV(QI,'MCU')
         BEGIN CASE
            CASE QI='Y'
               LN=0
               WRITE BOL.REC ON F.CRX,BOL.ID
               GO GET.BOL
            CASE QI='N'
               LN=0
               GO GET.BOL
            CASE 1
               GO UPDATE.INFO
         END CASE
      END
      CALL AFS.SCR.REFRESH(PROG.NAME,PROG.DESC,PACK.NAME,CO.NAME,TIME.DATE,1)
      GO GET.BOL
      RETURN

************************************************************************
DISPLAY.SCREEN:***
************************************************************************
      CRT @(00,06) :'Ship Whse....'
      CRT @(00,07) :'Ship City....                              State'
      CRT @(00,08) :'Ship Zip.....'

      CRT @(00,10) :'Dest Name....'
      CRT @(00,11) :'Dest City....                              State'
      CRT @(00,12) :'Dest Zip.....'

      CRT @(00,14) :'Class........'
      CRT @(00,15) :'Weight.......'
      CRT @(00,16) :'Status.......'
      RETURN

************************************************************************
DISPLAY.DATA:***
************************************************************************
      CRT @(15,06) :SM.SWHSE'L#35'
      CRT @(15,07) :SM.SOCTY'L#25'
      CRT @(50,07) :SM.SOSTE'L#2'
      CRT @(15,08) :SM.SOZIP'L#25'

      CRT @(15,10) :SM.SNAME'L#35'
      CRT @(15,11) :SM.SDCTY'L#25'
      CRT @(50,11) :SM.SDSTE'L#2'
      CRT @(15,12) :SM.SDZIP'L#25'

      CRT @(15,14) :SM.SCLS1'R#10'
      CRT @(45,14) :SM.SCLS2'R#10'
      CRT @(15,15) :SM.SWGT1'R#10'
      CRT @(45,15) :SM.SWGT2'R#10'
      IF SM.STS='V' THEN
         CRT @(15,16) :'VOID'
      END ELSE
         CRT @(15,16) :'ACTIVE'
      END
      RETURN

************************************************************************
ASSIGN.DATA:
************************************************************************
      SM.AMT=BOL.REC<1>
      SM.SSDTE=BOL.REC<2>
      SM.SORDR=BOL.REC<8>
      SM.SACCT=BOL.REC<9>
      SM.SFTYP=BOL.REC<10>
      SM.SSCAC=BOL.REC<11>
      SM.SCASE=BOL.REC<12>
      SM.SWHSE=BOL.REC<13>
      SM.SOZIP=BOL.REC<14>
      SM.SOCTY=BOL.REC<15>
      SM.SOSTE=BOL.REC<16>
      SM.SNAME=BOL.REC<17>
      SM.SDZIP=BOL.REC<18>
      SM.SDCTY=BOL.REC<19>
      SM.SDSTE=BOL.REC<20>
      SM.SCLS1=BOL.REC<21,1>
      SM.SWGT1=BOL.REC<22,1>
      SM.SCLS2=BOL.REC<21,2>
      SM.SWGT2=BOL.REC<22,2>
      SM.STS=BOL.REC<23>

      RETURN
   END
