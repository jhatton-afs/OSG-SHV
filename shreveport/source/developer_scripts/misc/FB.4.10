$OPTIONS HEADER.EJECT
      ERRMSG=''
*
$OPTIONS EXEC.EQ.PERF
$OPTIONS INFO.ABORT
*
* PROGRAM: FB.4.10
* PROGRAM TO BUILD MILEAGES LINKS TO ALL ZIP CODES
*
* FREIGHT BILL PACKAGE
*
* AUTHOR: BRIAN M. QUEEN
* DATE: OCTOBER 29, 1992
*
* Initialize variables
*
      PROG.NAME='FB.4.10'
      PROG.DESC='BUILD ZIP CODE MILEAGES'
      PACK.NAME='FREIGHT BILLING'
      CALL CHANNEL(CH.NUM)
*
* Open files
*
      OPEN '','MILEAGES' TO F.MILES ELSE CALL OPEN.ABORT("MILEAGES",PROG.NAME)
      OPEN '','INPUT' TO F.IN ELSE CALL OPEN.ABORT("INPUT",PROG.NAME)
      OPEN '','OUTPUT' TO F.OUTPUT ELSE CALL OPEN.ABORT("OUTPUT",PROG.NAME)
      CALL AFS.SCR.HEAD(CO.ID,FILE.ID,PERIOD,PROG.NAME,PROG.DESC,PACK.NAME,"AFS Logistics, LLC",TIME.DATE,1)
*
100:  TEXT='WARNING - This program will process for several hours/days'
      CALL CENTER(TEXT,80)
      CRT @(0,10):CHAR(7):CHAR(7):TEXT
      HELP="Enter Y to continue or N to stop"
      CALL GEN.IN(0,22,'Continue? N','',Q,0,1,'','',1,-1,22,1,QI,HELP,0,23)
      QI=OCONV(QI,'MCU')
      BEGIN CASE
         CASE QI='Y'
            CRT @(0,3):@(-3)
200:        HELP="Enter zip code. ''=<cr>=Exit."
            CALL GEN.IN(0,3,"Enter zip code #####",'',Q,0,5,'','',1,-5,3,1,QI,HELP,0,23)
            IF QI='' THEN STOP
            IF QI MATCHES "5N" THEN
               ZIP=QI
            END ELSE
               CALL ELINE('Invalid entry.')
               GOTO 200
            END
*
            EXECUTE 'GET-LIST ZIPS' PASSLIST CAPTURING OUTPUT
            IF SYSTEM(11) ELSE
               EXECUTE 'SSELECT ZIPS WITH 1 NE "Apo]""Fpo]"' PASSLIST CAPTURING OUTPUT
               IF SYSTEM(11) ELSE
                  CALL ELINE('No zip codes on file.')
                  STOP
               END
               EXECUTE 'SAVE-LIST ZIPS' PASSLIST CAPTURING OUTPUT
               EXECUTE 'GET-LIST ZIPS' PASSLIST CAPTURING OUTPUT
            END
            NUM.ITMS = @SELECTED
            COUNTER=0
*
            LOOP
               READNEXT ID ELSE ID='**'
            UNTIL ID='**' DO
               COUNTER=COUNTER+1
               CALL PROG.BAR(22,COUNTER,NUM.ITMS,PER)
               IF ID LE ZIP THEN
                  REC=''
                  REC<1>=ID:' ':ZIP
               END ELSE
                  REC=''
                  REC<1>=ZIP:' ':ID
               END
*
               CRT @(0,20):'Processing zip codes ':REC<1>:@(-4)
               DELETE F.OUTPUT,CH.NUM
               MILES.ID=FIELD(REC<1>,' ',1):'*':FIELD(REC<1>,' ',2)
               READ REC FROM F.MILES,MILES.ID ELSE
                  WRITE REC ON F.IN,CH.NUM
                  X.TIME=0
                  DONE=0
                  LOOP
                     X.TIME=X.TIME+1
                  UNTIL DONE ! X.TIME=20 DO
                     READ REC FROM F.OUTPUT,CH.NUM THEN
                        DONE=1
                        DELETE F.IN,CH.NUM
                     END ELSE
                        SLEEP 2
                     END
                  REPEAT
               END
            REPEAT
         CASE QI='N' ! QI=''
            STOP
         CASE 1
            GOTO 100
      END CASE
      STOP
