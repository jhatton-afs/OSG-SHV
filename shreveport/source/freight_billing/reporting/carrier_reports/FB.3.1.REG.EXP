$OPTIONS EXEC.EQ.PERF
$OPTIONS INFO.ABORT
*
*
      SUBROUTINE FB.3.1.REG.EXP(CLIENT.ID,CLIENT.REC,BEG.DATE,END.DATE)
$OPTIONS HEADER.EJECT
* SUMMARY REPORT OF EXPENSE CODE CHARGES BY REGION
*
* AUTHOR: VALERIE DOTY
* DATE: DECEMBER 2, 1993
*
* PACKAGE: FREIGHT BILLING
*
* Initialize variables
*
      REG.TOT=''
      GRAND.TOT=''
      EXP.CHG=0
      COUNTER=0
      OLD.EXP.CODE=''
      PREV.REG='FIRST'
      ERRMSG=''
*
* Open data file
*
      OPEN '','FB.BILLS.HIST,':CLIENT.ID TO F.TRANS ELSE
        CALL OPEN.ABORT("FB.BILLS.HIST,":CLIENT.ID,"FB.3.1.REG.EXP")
      END
*
*  PRINT THE HEADING
*
      PAGE
      TEXT='REGION EXPENSE SUMMARY'
      CALL CENTER(TEXT,132)
      PRINT TEXT
      TEXT=STR('=',24)
      CALL CENTER(TEXT,132)
      PRINT TEXT
      PRINT
      DARRAY=''
*
* Select data
*
      PREC='SSELECT FB.BILLS.HIST,':CLIENT.ID:' BY DIV.REG BY 31'
      PREC := ' WITH 37 GE "':BEG.DATE'D2/':'"'
      PREC := ' AND WITH 37 LE "':END.DATE'D2/':'"'
      PREC := ' AND WITH VERSION = "0"'
      PREC := ' AND WITH 39 # "R"'
      EXECUTE PREC PASSLIST CAPTURING OUTPUT
      NUM.ITMS = @SELECTED
      IF SYSTEM(11) ELSE
        PRINT 'No transactions on file with given criteria.'
        STOP
      END
*
* Process report
*
      COUNTER=0
      DONE = 0
      LOOP
        READNEXT ID ELSE DONE = 1
      UNTIL DONE = 1 DO
        COUNTER=COUNTER+1
        CALL PROG.BAR(22,COUNTER,NUM.ITMS,PER)
        READ REC FROM F.TRANS,ID THEN
          EXP.CODE=REC<31>
          REG=OCONV(REC<9>,'TDIVISION.CODES,':CLIENT.ID:';X;;45')
          CHARGES=REC<33>
          IF REG NE PREV.REG THEN GOSUB 8000; * Region break
          EXP.COUNT=DCOUNT(EXP.CODE,CHAR(253))
          IF EXP.COUNT=0 THEN EXP.COUNT=1
          FOR I=1 TO EXP.COUNT
             LOCATE EXP.CODE<1,I> IN DARRAY<1> BY 'AL' SETTING POS THEN
                DARRAY<2,POS>=DARRAY<2,POS>+CHARGES<1,I>
             END ELSE
                DARRAY=INSERT(DARRAY,1,POS,0,EXP.CODE<1,I>)
                DARRAY=INSERT(DARRAY,2,POS,0,CHARGES<1,I>)
                DARRAY=INSERT(DARRAY,3,POS,0,REG)
             END
             REG.TOT=REG.TOT+CHARGES<1,I>
          NEXT I
        END
      REPEAT
      GOSUB 8000; * Print region totals
*
* Print Grand Totals
*
      PRINT SPACE(82) : STR('-',13)
      PRINT
      PRINT SPACE(82) : GRAND.TOT 'R26,#13'
      PRINT SPACE(82) : STR('=',13)
*
       RETURN
*
*----------------------------------------------------------------------
*
* INTERNAL SUBROUTINES
*
*----------------------------------------------------------------------
*
* Print region break totals
*
8000   IF PREV.REG # 'FIRST' THEN
         NUM.OF.ITEMS=DCOUNT(DARRAY<1>,CHAR(253))
         FOR X = 1 TO NUM.OF.ITEMS
            LN = DARRAY<3,X> 'R#40' : SPACE(16) : DARRAY<1,X> 'R#12'
            LN := SPACE(14) : DARRAY<2,X> 'R26,#13'
            PRINT LN
         NEXT X
         GRAND.TOT=GRAND.TOT+DIV.TOT
         PRINT SPACE(82) : STR('-',13)
         PRINT SPACE(62) : 'TOTAL:' : SPACE(14) : REG.TOT 'R26,#13'
         PRINT
         DIV.TOT=0
      END
      PREV.REG=REG
      RETURN
*
   END
