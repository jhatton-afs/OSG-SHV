********************************************************************************
*  Program : MAIL.CONS.BENCHMARK.SETUP
*  Author  : David R. Friend
*  Date    : January 2012
********************************************************************************
*
*      COPYRIGHT (C) 2002 by AFS LOGISTICS, LLC. - ALL RIGHTS RESERVED
*
*  This program is the sole property of AFS Logistics, LLC (AUTOMATED FREIGHT
*  SYSTEMS) and may not be used, copied, or modified without the express
*  written consent of Brian Barker of AFS Logistics, LLC.
********************************************************************************
* Includes
      $INCLUDE FILE.DEFS INC_ADDS60.CODES; * These require specific PROCOMM settings

* Equates
      TERM.TYPE=SYSTEM(7)
      BEGIN CASE
         CASE TERM.TYPE='REGENT60'
            REVERSE.BLINK.ON = TERM.ESC.SEQ:CHAR(TERM.REV+TERM.BLINK)   ; * Reverse Blinking text ON
            REVERSE.ON = TERM.ESC.SEQ:CHAR(TERM.REV)          ; * Reverse text ON
            REVERSE.OFF = TERM.ESC.SEQ:CHAR(TERM.NORMAL)      ; * Reverse text OFF
         CASE 1
            REVERSE.BLINK.ON = @(-13):@(-5)   ; * Reverse Blinking text ON
            REVERSE.ON = @(-13)         ; * Reverse text ON
            REVERSE.OFF = @(-14):@(-6)      ; * Reverse text OFF
      END CASE
      EQU TRUE TO 1
      EQU FALSE TO 0

* Initialize general program variables
      PROMPT''
      PORT.NUM=SYSTEM(19)
      CALL GET.USER(USER.NAME)
      PROG.NAME='MAIL.CONS.BENCHMARK.SETUP'
      PROG.DESC='Mail Cons Logistics Benchmarks'
      PACK.NAME='USER : ':USER.NAME
      CO.NAME='AFS Logistics, LLC'
      UPL.VIN = 'FBBP' ;  UPL.VIN<2> = 'MAIL.CONS.BM.SETUP' ; UPL.VOUT = ''                    ;* NPR_UPL 04/23/2010
      CALL UPD.PROGRAM.LOG(UPL.VIN,UPL.VOUT)                                                           ;* NPR_UPL 04/23/2010
      EDIT.MASK.CHAR='-'
      PLACE.HOLDER='~~ PLACE*HOLDER ~~'

* Initialize operating variables
*     PAGE.SIZE=14;       * Number of lines per page ; * JMK01
*     START.Y=6;          * Starting y-position on the screen minus one ; * JMK01
      PAGE.SIZE=13                       ; * Number of lines per page ; * JMK01
      START.Y=7                          ; * Starting y-position on the screen minus one ; * JMK01

      BENCHMARK.REC=''
      OLD.BENCHMARK.REC=''

      CLIENT.ID=''
      SHIPPER=''                         ; * JMK01
      CLIENT.NAME=''
      CLIENT.CITY=''
      CLIENT.STATE=''

* Set access privileges
* 3 Levels of access:
*    0 - Access Denied
*    1 - View Access only
*    2 - Full View/Edit Access

      BEGIN CASE
         CASE USER.NAME='OWEN'
            USER.ACCESS=2
         CASE USER.NAME='CHRIS'
            USER.ACCESS=2
         CASE USER.NAME='JDARWIN'
            USER.ACCESS=2
         CASE USER.NAME='NRATH'
            USER.ACCESS=2
         CASE USER.NAME='NPRATT'
            USER.ACCESS=2
         CASE USER.NAME='RRANEY'
            USER.ACCESS=2
         CASE USER.NAME='DAVID'
            USER.ACCESS=2
         CASE USER.NAME='MKERBOW'
            USER.ACCESS=2
         CASE USER.NAME='JAMES'
            USER.ACCESS=2
         CASE USER.NAME='BRIANP'
            USER.ACCESS=2
         CASE USER.NAME='NPRATT'
            USER.ACCESS=2
         CASE USER.NAME='DWIMBERLY'
            USER.ACCESS=2
         CASE USER.NAME='AHERBEL'
            USER.ACCESS=2
         CASE USER.NAME='LCORKERN'
            USER.ACCESS=2
         CASE 1
            USER.ACCESS=1
      END CASE

      IF USER.ACCESS=0 THEN PROG.DESC:=' - Access Denied'
      IF USER.ACCESS=1 THEN PROG.DESC:=' - View Access'
      IF USER.ACCESS=2 THEN PROG.DESC:=' - Full Access'
      GOSUB DISPLAY.SCREEN

      IF USER.ACCESS=0 THEN
         CALL FB.PRINT.CENT(1,80,12,'Restricted Access Application - Access Denied')
         CALL FB.PRINT.CENT(1,80,14,'Request access through Chris Mendenhall')
         CALL ELINE('')
         GOTO THE.END
      END

* Open files
      OPEN '','CLIENTS' TO F.CLIENTS ELSE
         CALL OPEN.ABORT('CLIENTS',PROG.NAME)
      END
      OPEN '','FB.TABLES' TO F.TABLES ELSE
         CALL OPEN.ABORT('FB.TABLES',PROG.NAME)
      END
      OPEN '','MAIL.CONS.BM' TO F.MAIL.CONS.BM ELSE
         CALL OPEN.ABORT('MAIL.CONS.LOGISTICS.BM',PROG.NAME)
      END

GET.CLIENT: ***
      IF CLIENT.ID THEN
         EDIT.MASK=CLIENT.ID'R%5'
      END ELSE
         EDIT.MASK=STR(EDIT.MASK.CHAR,5)
      END
      TEMP.MAIL.CONS.ID=''
      HELP='Enter client ID, [L]ist existing setups'
      CALL GEN.IN(9,3,EDIT.MASK,'',Q,0,20,'','',2,9,3,0,QI,HELP,0,23)
      QI=OCONV(QI,'MCU')
      IF QI='' AND CLIENT.ID THEN QI=CLIENT.ID
      BEGIN CASE
         CASE QI='EX'
            GOTO THE.END
         CASE QI='L'
            GOSUB DISPLAY.BENCHMARKS
            GOSUB DISPLAY.SCREEN
            GOTO GET.CLIENT
         CASE NOT(QI MATCHES "0N")
            CALL SOUNDEX.DISPLAY(QI,'CLIENTS','SDX.CLIENTS,NAME','2,3,4',ITEM.LIST)
            BEGIN CASE
               CASE ITEM.LIST=''
               CASE DCOUNT(ITEM.LIST<1>,@VM) GT 1
               CASE ITEM.LIST#''
                  IF NUM(ITEM.LIST<1,1>) THEN DATA ITEM.LIST<1,1>
            END CASE
            GOSUB DISPLAY.SCREEN
            GOTO GET.CLIENT
         CASE NUM(QI) AND QI#''
            QI=QI'R%5'
            READ CLIENT.REC FROM F.CLIENTS,QI THEN
               CLIENT.ID=QI
               CLIENT.NAME=CLIENT.REC<2>
               CLIENT.CITY=CLIENT.REC<4>
               CLIENT.STATE=CLIENT.REC<5>
            END ELSE
               CALL ELINE('ERROR - Client ':QI:' is not a valid client.')
               GOTO GET.CLIENT
            END
         CASE 1
            GOTO GET.CLIENT
      END CASE
      CLIENT.TXT=CLIENT.ID'R%5':' ':CLIENT.NAME:' (':CLIENT.CITY:', ':CLIENT.STATE:')'
      CRT @(9,3):CLIENT.TXT'L#50'        ; * JMK01

GET.SHIPPER: ***
      IF SHIPPER#'' THEN
         EDIT.MASK=SHIPPER'L#6'
      END ELSE
         EDIT.MASK=STR(EDIT.MASK.CHAR,6)
      END
      HELP='Enter Mail Cons Shipper Account #. [EX]it'
      CALL GEN.IN(9,4,EDIT.MASK,'',Q,0,6,'','',6,9,4,1,QI,HELP,0,23)
      QI=OCONV(QI,'MCU')
      IF QI='' AND SHIPPER#'' THEN QI=SHIPPER
      BEGIN CASE
         CASE QI='EX'
            GOTO THE.END
         CASE QI='X'
            CRT @(9,4):'  '
            GOTO GET.CLIENT
      END CASE
      SHIPPER=QI
      IF SHIPPER#'' THEN                 ; * JMK01
         CRT @(9,4):SHIPPER'L#6'         ; * JMK01
         MAIL.CONS.ID=CLIENT.ID:'*':SHIPPER
      END ELSE
         MAIL.CONS.ID=CLIENT.ID
      END


* Load BENCHMARK record
* Add any new service descriptions that have been inserted into FB.TABLES
      READ BENCHMARK.REC FROM F.MAIL.CONS.BM,MAIL.CONS.ID THEN
         NULL
      END ELSE
         TEMP.MAIL.CONS.ID='INACTIVE-':MAIL.CONS.ID
         READ BENCHMARK.REC FROM F.MAIL.CONS.BM,TEMP.MAIL.CONS.ID THEN
            CALL ELINE('Inactive benchmark. Must be copied to active before making changes')
            USER.ACCESS=1
         END ELSE
            BENCHMARK.REC=''
         END
      END
      OLD.BENCHMARK.REC=BENCHMARK.REC
GET.BENCHMARK: ***
      HELP='Enter the mark percent'
      IF BENCHMARK.REC='' THEN
         MASK='------'
      END ELSE
         MASK=BENCHMARK.REC'R26#6'
      END
      CALL GEN.IN(20,7,MASK,'AMT2',Q,0,6,'','',2,20,7,0,QI,HELP,0,23)
      QI=OCONV(QI,'MCU')
      IF BENCHMARK.REC#'' THEN
         IF QI='' THEN Q=BENCHMARK.REC
      END
      BEGIN CASE
         CASE QI='EX'
            STOP
         CASE QI='X'
            CRT @(20,7):'      '
            GOTO GET.SHIPPER
         CASE NUM(Q)
            IF Q < "0" THEN
               CALL ELINE('Benchmark cannot be less than zero')
               GOTO GET.BENCHMARK
            END
            IF Q >= "10000" THEN
               CALL ELINE('Benchmark cannot be 100.00 or more')
               GOTO GET.BENCHMARK
            END
            IF Q#'' THEN
               BENCHMARK.REC=Q
               CRT @(20,7):BENCHMARK.REC'R26#6'
            END
         CASE 1
            CALL ELINE('Invalid entry.')
            GOTO GET.BENCHMARK
      END CASE

MAIN.START: ***
      CRT @(0,22):'Enter option: '
      EDIT.MASK=STR(EDIT.MASK.CHAR,2)
      HELP='[U]pdate, [D]elete or [C]opy Act/Inact'
      CALL GEN.IN(15,22,EDIT.MASK,'',Q,0,3,'','',3,15,22,0,QI,HELP,0,23)
      QI=OCONV(QI,'MCU')
      BEGIN CASE
         CASE QI='EX'                    ; * Exit
            IF USER.ACCESS=1 THEN GOTO THE.END
            GOTO LAST.CHANCE.TO.SAVE
         CASE QI='X'                     ; * Back to client
            IF USER.ACCESS=1 THEN
               BENCHMARK.REC=''
               GOSUB DISPLAY.SCREEN
               GOTO GET.CLIENT
            END
            GOTO GET.BENCHMARK
         CASE QI='U'                     ; * Update (and exit)
            IF USER.ACCESS=1 THEN GOTO THE.END
            IF BENCHMARK.REC#OLD.BENCHMARK.REC THEN
               CRT @(0,22):'Saving...':@(-3)
               SLEEP 1
               GOSUB SAVE.BENCHMARK.REC
            END ELSE
               CRT @(0,22):'No changes to save.':@(-3)
               SLEEP 1
            END
            GOTO THE.END
         CASE QI='C'                     ; * Copy (and exit)
            IF TEMP.MAIL.CONS.ID='' THEN
               IF USER.ACCESS=1 THEN GOTO THE.END
            END
            CRT @(0,22):'Copying...':@(-3)
            SLEEP 1
            IF TEMP.MAIL.CONS.ID='' THEN
               DELETE F.MAIL.CONS.BM,MAIL.CONS.ID
               MAIL.CONS.ID='INACTIVE-':MAIL.CONS.ID
            END ELSE
               DELETE F.MAIL.CONS.BM,TEMP.MAIL.CONS.ID
            END
            GOSUB SAVE.BENCHMARK.REC
            GOTO THE.END
         CASE QI='D'                     ; * Delete
            IF USER.ACCESS=1 THEN GOTO THE.END
            YN.MSG='Are you sure you want to delete this benchmark?'
            YN.MASK=''
            GOSUB ASK.Y.OR.N
            IF YN.ANSWER='Y' THEN
               CRT @(0,22):'Deleting...':@(-3)
               SLEEP 1
               DELETE F.MAIL.CONS.BM,MAIL.CONS.ID
            END
            BENCHMARK.REC=''
            GOSUB DISPLAY.SCREEN
            GOTO GET.CLIENT
         CASE QI='*' AND USER.NAME='JAMES'         ; * Show contents of BENCHMARK.REC
            CRT @(-1)
            CRT @(0,0):BENCHMARK.REC
            CALL ELINE('Viewing DEBUG Screen.')
            GOSUB DISPLAY.SCREEN
            GOTO MAIN.START
         CASE 1
            GOTO MAIN.START
      END CASE

********************************************************************************
* Start of internal subroutines
********************************************************************************

DISPLAY.SCREEN: ***
* Display the basic screen setup

      CALL AFS.SCR.REFRESH(PROG.NAME,PROG.DESC,PACK.NAME,CO.NAME,TIME.DATE,1)
      CRT @(0,2):STR('=',79):
      CRT @(0,3):'CLIENT :'
      CRT @(0,4):'SHIPPER:'              ; * JMK01
      CRT @(0,5):STR('=',79):            ; * JMK01
      CRT @(0,21):STR('=',79):
      CRT @(0,7):'Mark up percent :'
      IF CLIENT.ID THEN
         CLIENT.TXT=CLIENT.ID'R%5':' ':CLIENT.NAME:' (':CLIENT.CITY:', ':CLIENT.STATE:')'
         CRT @(9,3):CLIENT.TXT'L#55'
      END
      IF SHIPPER#'' THEN                 ; * JMK01
         CRT @(9,4):SHIPPER'L#6'         ; * JMK01
      END                                ; * JMK01
      IF BENCHMARK.REC#'' THEN
         CRT @(20,7):BENCHMARK.REC'R26#6'
      END


      RETURN

********************************************************************************
********************************************************************************


********************************************************************************
********************************************************************************


********************************************************************************

SAVE.BENCHMARK.REC: ***
* Save BENCHMARK record

      WRITE BENCHMARK.REC ON F.MAIL.CONS.BM,MAIL.CONS.ID
      OLD.BENCHMARK.REC=BENCHMARK.REC
      RETURN

********************************************************************************

DISPLAY.BENCHMARKS: ***

      CRT @(0,0):@(-1)
      STMT='SORT MAIL.CONS.BM 1 2'
      EXECUTE STMT
      CALL ELINE('')
      RETURN

********************************************************************************

ASK.Y.OR.N: ***
* This is a general Yes/No prompt routine
* Inputs:  YN.MSG, YN.MASK
* Outputs: YN.ANSWER

      YN.ANSWER=''

Y.OR.N:
      CRT @(0,22):YN.MSG:@(-4)
      HELP='Entry Options: [Y]es [N]o'
      EDIT.MASK=YN.MASK'L#1'
      CALL GEN.IN(LEN(YN.MSG)+1,22,EDIT.MASK,'',Q,0,1,'','',2,LEN(YN.MSG)+1,22,0,QI,HELP,0,23)
      QI=TRIM(OCONV(QI,'MCU'))
      IF QI='' THEN QI=TRIM(EDIT.MASK)
      BEGIN CASE
         CASE QI='Y'
         CASE QI='N'
         CASE 1
            CALL ELINE('Invalid entry.')
            GOTO Y.OR.N
      END CASE
      CRT @(0,22):@(-4):
      CRT @(0,23):@(-4):
      YN.ANSWER=QI
      RETURN

********************************************************************************

LAST.CHANCE.TO.SAVE: ***
* If you tried to EX out and there are unsaved changes - falls thru to THE.END

      IF BENCHMARK.REC#OLD.BENCHMARK.REC THEN
         YN.MSG='You have unsaved changes. Save before exiting?'
         YN.MASK=''
         GOSUB ASK.Y.OR.N
         IF YN.ANSWER='Y' THEN
            CRT @(0,22):'Saving...':@(-3)
            SLEEP 1
            GOSUB SAVE.BENCHMARK.REC
         END
      END

********************************************************************************

THE.END: ***
      CRT @(0,22):@(-3):
      END                                   ; * End program
