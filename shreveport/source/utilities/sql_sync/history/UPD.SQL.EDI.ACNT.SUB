      SUBROUTINE UPD.SQL.EDI.ACNT.SUB(VIN,VOUT)
***********************************************************************
      UPL.VIN = 'FBBP' ; UPL.VIN<2> = 'UPD.SQL.EDI.ACNT.SUB' ; UPL.VOUT = ''     ; * NPR_UPL 04/23/2010
      CALL UPD.PROGRAM.LOG(UPL.VIN,UPL.VOUT)       ; * NPR_UPL 04/23/2010
* ANALYZE BILLS ACTED UPON BY AUDITORS
* COPYRIGHT (C) AFS LOGISTICS, LLC., 2002
*
* AUTHOR : OWEN HOLMAN
* DATE   : JANUARY 2002
***********************************************************************
* 061912 - JMK01 - Choose development SQL server if not production server AFS
* 011613 - JMK02 - SHV/SLC System Integration
*************************************************************************
!
* INCLUDE THE ODBC DEFINITIONS
!
$INCLUDE UNIVERSE.INCLUDE ODBC.H
      CALL GET.SERVER.INFO(SVR.VIN,SVR.VOUT)       ; * JMK01
      SVR.NAME=SVR.VOUT<1>               ; * JMK01

      USER=VIN<1>
      CARRIER.ID=VIN<2>
      ACCOUNT=VIN<3>
      CLIENT.ID=VIN<4>
*     IF CLIENT.ID+0>89999 THEN RETURN   ; * JMK02
      DIV.ID=VIN<5>
      CL.ACNT.NAME=VIN<6>                ; * JMK02
      CL.ACNT.ADDR=VIN<7>                ; * JMK02
      CL.ACNT.CITY=VIN<8> ; * JMK02
      CL.ACNT.STATE=VIN<9> ; * JMK02
      CL.ACNT.ZIP=VIN<10> ; * JMK02
      CL.ACNT.CNTRY=VIN<11> ; * JMK02
      CL.ACNT.ALIAS=VIN<12> ; * JMK02
      CL.ACNT.EDI=VIN<13> ; * JMK02

      FORM = "T##########"
      DASH = "----------"
      EXPECT = ""
      NULL=""
      TOT.NUM.BILLS=0

      ERROR.CNT=0
10:   ERRORS=''

      GOSUB SETUP.SQL

* DEFINE FIELDS TO UPDATE FROM CLIENT.REC HERE
      SQL.CARRIERNUM=CARRIER.ID
      SQL.ACCOUNTNUM=ACCOUNT
      SQL.CLIENTNUM=CLIENT.ID
      SQL.DIVISIONNUM=DIV.ID
      SQL.ACCOUNTNAME=CL.ACNT.NAME ; * JMK02
      SQL.ADDRESS=CL.ACNT.ADDR ; * JMK02
      SQL.CITY=CL.ACNT.CITY ; * JMK02
      SQL.STATE=CL.ACNT.STATE ; * JMK02
      SQL.ZIPCODE=CL.ACNT.ZIP ; * JMK02
      SQL.COUNTRYCODE=CL.ACNT.CNTRY ; * JMK02
      SQL.NICKNAME=CL.ACNT.ALIAS ; * JMK02
      SQL.EDINUMBER=CL.ACNT.EDI ; * JMK02

      FN = "SQLExecute"
      STATUS = SQLEXECUTE(UPDACCTENV)
      STMTENV=UPDACCTENV
      GOSUB CKSTMTENV


THE.END:***
*     END OF PROGRAM
*
* NOW JUST CLEAN UP THE ENVIRONMENT AND LEAVE
*
      FN = "SQLFreeStmt"
      STATUS = SQLFREESTMT(STMTENV, SQL.DROP)
      GOSUB CKSTMTENV

      FN = "SQLDisconnect"
      STATUS = SQLDISCONNECT(CONENV)
      GOSUB CKCONENV

      FN = "SQLFreeConnect"
      STATUS = SQLFREECONNECT(CONENV)
      GOSUB CKCONENV

      FN = "SQLFreeEnv"
      STATUS = SQLFREEENV(DBCENV)
      IF STATUS <> SQL.SUCCESS THEN
         ERRORS<1,-1>="Failed to release ODBC environment"
      END

      IF ERRORS#'' THEN
         ERROR.CNT+=1
         IF ERROR.CNT>3 THEN
            MVIN=''
            MVIN<1>='UPD.SQL.Group@afs.net'
            MVIN<4>='AFS_UPD.SQL.EDI.ACNT.SUB'
            MVIN<5>='SQL Account Mapping Update Errors'
            TEXT='On ':DATE()'D2/':' ':OCONV(TIME(),'MTHS'):', ':USER:' updated '
            TEXT<1,-1>='Carrier ':CARRIER.ID:', Account ':ACCOUNT:', client ':CLIENT.ID:', but did not succeed in updating SQL due to:'
            TEXT<1,-1>=' '
            TEXT<1,-1>=ERRORS
            MVIN<6>=TEXT
            MVIN<7>=1
            CALL SENDMAIL(MVIN,MVOUT)
         END ELSE
            GO 10
         END
      END

      RETURN

      STOP

SETUP.SQL:*
!
* CREATE AN ODBC ENVIRONMENT & A CONNECTION ENVIRONMENT. USE THE
* SSQLSETCONNECTOPTION TO SPECIFY THE OPERATING SYSTEM USER ID AND
* PASSWORD.

      STATUS = SQLALLOCENV(DBCENV)
      IF STATUS <> SQL.SUCCESS THEN
         ERRORS<1,-1>="Failed to allocate an ODBC environment"
         RETURN TO THE.END
      END

      STATUS = SQLALLOCCONNECT(DBCENV,CONENV)
      IF STATUS <> SQL.SUCCESS THEN
         ERRORS<1,-1>="Failed to allocate a CONNECT environment"
         RETURN TO THE.END
      END

*
* GATHER THE DATA SOURCE NAME, USERID AND PASSWORD FOR THE SERVER O/S AND
* INFORMATION FOR THE DBMS AT THE SERVER. THE DBMS INFO IS OFTEN ALSO
* A USER NAME AND PASSWORD, BUT NOT IN ALL CASES.
*
*     SQLSVR.INFO=OCONV('SQLSVR','TFB.TABLES;X;;1')          ; * JMK01
*     SOURCE=FIELD(SQLSVR.INFO<1>,' ',1)           ; * JMK01
*     UID=FIELD(SQLSVR.INFO<1>,' ',2)    ; * JMK01
*     PWD=FIELD(SQLSVR.INFO<1>,' ',3)    ; * JMK01
      BEGIN CASE                         ; * JMK01
         CASE SVR.NAME='AFS'             ; * JMK01
            SOURCE='PRODODS'             ; * JMK01
            UID="uv_sql"                 ; * JMK01
            PWD="4@gAbreg"               ; * JMK01
         CASE 1                          ; * JMK01
            SOURCE='DEVODS'              ; * JMK01
            UID="uv_sql"                 ; * JMK01
            PWD="4@gAbreg"               ; * JMK01
      END CASE                           ; * JMK01
*
* ESTABLISH A SESSION TO THE SPECIFIED DATA SOURCE
*
*     PRINT "Connecting to data source: ": SOURCE
      FN = "SQLConnect"
      STATUS = SQLCONNECT(CONENV,SOURCE,UID,PWD)
      GOSUB CKCONENV

UPDATE.SQL.SHIPMENT:*
*
* NOW ALLOCATE AN SQL STATEMENT ENVIRONMENT
* TO BE USED FOR EXECUTING SQL STATEMENTS
*
      FN = "SQLAllocStmt"
      STATUS = SQLALLOCSTMT(CONENV,UPDACCTENV)
      GOSUB CKCONENV

*
* NOTE THAT WE ARE USING PARAMETER MARKERS IN THE SQL STATEMENT TO FACILITATE
* LOADING MULTIPLE ROWS OF DATA EFFICIENTLY
*
*     OPT.STR = STR(' ?,',4) ; * JMK02
      OPT.STR = STR(' ?,',12) ; * JMK02
      OPT.STR = OPT.STR[2,LEN(OPT.STR)-2]
      INST1 = "{CALL PX_UpdAccountMapping (":OPT.STR:")}"    ; * 64 PLACEHOLDERS

      ROWNUM = 0
      FN = "SQLSetParam"
*
* DEFINE THE FIELD UPDATE PARAMETERS USING THE FOLLOWING FORMAT
*     STATUS = SQLSETPARAM(STMTENV, COL, SQL.B.BASIC, SQL.DATA.TYPE, PREC, SCALE, VAR.NAME) ; GOSUB CKSTMTENV
*
      STATUS = SQLSETPARAM(UPDACCTENV, 1, SQL.B.BASIC, SQL.VARCHAR, 10, 0, SQL.CARRIERNUM) ; STMTENV=UPDACCTENV ; GOSUB CKSTMTENV
      STATUS = SQLSETPARAM(UPDACCTENV, 2, SQL.B.BASIC, SQL.VARCHAR, 50, 0, SQL.ACCOUNTNUM) ; STMTENV=UPDACCTENV ; GOSUB CKSTMTENV
      STATUS = SQLSETPARAM(UPDACCTENV, 3, SQL.B.BASIC, SQL.VARCHAR, 10, 0, SQL.CLIENTNUM) ; STMTENV=UPDACCTENV ; GOSUB CKSTMTENV
      STATUS = SQLSETPARAM(UPDACCTENV, 4, SQL.B.BASIC, SQL.VARCHAR, 50, 0, SQL.DIVISIONNUM) ; STMTENV=UPDACCTENV ; GOSUB CKSTMTENV

      STATUS = SQLSETPARAM(UPDACCTENV, 5, SQL.B.BASIC, SQL.VARCHAR, 100, 0, SQL.ACCOUNTNAME) ; STMTENV=UPDACCTENV ; GOSUB CKSTMTENV ; * JMK02
      STATUS = SQLSETPARAM(UPDACCTENV, 6, SQL.B.BASIC, SQL.VARCHAR, 100, 0, SQL.ADDRESS) ; STMTENV=UPDACCTENV ; GOSUB CKSTMTENV ; * JMK02
      STATUS = SQLSETPARAM(UPDACCTENV, 7, SQL.B.BASIC, SQL.VARCHAR, 100, 0, SQL.CITY) ; STMTENV=UPDACCTENV ; GOSUB CKSTMTENV ; * JMK02
      STATUS = SQLSETPARAM(UPDACCTENV, 8, SQL.B.BASIC, SQL.VARCHAR, 10, 0, SQL.STATE) ; STMTENV=UPDACCTENV ; GOSUB CKSTMTENV ; * JMK02
      STATUS = SQLSETPARAM(UPDACCTENV, 9, SQL.B.BASIC, SQL.VARCHAR, 10, 0, SQL.ZIPCODE) ; STMTENV=UPDACCTENV ; GOSUB CKSTMTENV ; * JMK02
      STATUS = SQLSETPARAM(UPDACCTENV, 10, SQL.B.BASIC, SQL.VARCHAR, 10, 0, SQL.COUNTRYCODE) ; STMTENV=UPDACCTENV ; GOSUB CKSTMTENV ; * JMK02
      STATUS = SQLSETPARAM(UPDACCTENV, 11, SQL.B.BASIC, SQL.VARCHAR, 100, 0, SQL.NICKNAME) ; STMTENV=UPDACCTENV ; GOSUB CKSTMTENV ; * JMK02
      STATUS = SQLSETPARAM(UPDACCTENV, 12, SQL.B.BASIC, SQL.VARCHAR, 50, 0, SQL.EDINUMBER) ; STMTENV=UPDACCTENV ; GOSUB CKSTMTENV ; * JMK02

      FN = "SQLPrepare"
      STATUS = SQLPREPARE(UPDACCTENV, INST1)
      STMTENV=UPDACCTENV
      ENVSRC='UPDACCT'
      GOSUB CKSTMTENV

      RETURN

CKCONENV:

      COUNT = -1
      IF STATUS EQ -2 THEN
         ERRORS<1,-1>="INVALID CONNECTION HANDLE"
         STMT='BBSEND MIKE UPD.SQL.EDI.ACNT.SUB INVALID CONNECTION HANDLE ABORT'
         EXECUTE STMT CAPTURING RESULTS
         ABORT
      END
      IF STATUS NE 0 THEN
201:     *
         ST = SQLERROR(-1,CONENV,-1,STATE,NATCODE,ERRTXT)
         IF ST <> SQL.NO.DATA.FOUND
         THEN
            COUNT = 1
            IF EXPECT NE 0 AND STATE = EXPECT AND ST <> SQL.NO.DATA.FOUND THEN
               ERRORS<1,-1>="Allowed error of ":STATE:" returned for func ":FN
               GOTO 299
            END ELSE
               ERRORS<1,-1>="Status for ":FN:" call is: ":STATUS
               ERRORS<1,-1>="SQLSTATE,NATCOD are:" : STATE:"  ":NATCODE
               ERRORS<1,-1>="Error text is"
               ERRORS<1,-1>="    " : ERRTXT
            END
            IF ST = SQL.SUCCESS THEN GOTO 201
         END
         IF STATUS = -1 AND COUNT = 1 THEN
            ERRORS<1,-1>="EXITING CKCONENV"
            RETURN TO THE.END
         END
      END
299:  *
      RETURN

CKSTMTENV:
      IF STATUS EQ -2 THEN
         ERRORS<1,-1>="INVALID STATEMENT HANDLE"
         STMT='BBSEND MIKE UPD.SQL.EDI.ACNT.SUB INVALID STATEMENT HANDLE ABORT'
         EXECUTE STMT CAPTURING RESULTS
         ABORT
      END

      IF STATUS EQ 100 THEN RETURN
      IF STATUS NE 0 THEN
301:     *
         ST = SQLERROR(-1,-1,STMTENV,STATE,NATCODE,ERRTXT)
         IF ST <> SQL.NO.DATA.FOUND
         THEN
            COUNT = 1
            IF EXPECT NE 0 AND STATE = EXPECT AND ST <> SQL.NO.DATA.FOUND THEN
               ERRORS<1,-1>="Allowed error of ":STATE:" returned for func ":FN
               GOTO 399
            END ELSE
               ERRORS<1,-1>="Status for ":FN:" call is: ":STATUS
               ERRORS<1,-1>="SQLSTATE,NATCOD are:" : STATE:"  ":NATCODE
               ERRORS<1,-1>="Error text is"
               ERRORS<1,-1>="    " : ERRTXT
            END
            IF ST = 0 THEN GOTO 301
         END
         IF STATUS = -1 AND COUNT = 1 THEN
            ERRORS<1,-1>="EXITING CKSTMTENV"
            RETURN TO THE.END
         END
      END
399:  *
      RETURN
