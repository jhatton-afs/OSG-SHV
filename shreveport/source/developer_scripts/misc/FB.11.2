*******************************************************
* Program name : FB.11.2
* Author       : BRIAN M. QUEEN
* Date written : AUGUST 29,1992
* Library      : EDIBP
* COPYRIGHT (C) 2016 - AFS LOGISTICS LLC - ALL RIGHTS RESERVED.
*
* MODIFY TRASPORT MODES
*
* Last updated by afs (AFSLOGISTICS\nmeddaugh) at 09:22:51 on 03/26/2019.
*
* Start modification history
* 03/26/19 : NKH01 : ACCU-316 Modified CREATE-FILE file sizes
* End modification history
*
*******************************************************

$OPTIONS HEADER.EJECT
$OPTIONS EXEC.EQ.PERF
$OPTIONS INFO.ABORT
      ERRMSG=''
*
* Initialize variables
*
      PROMPT''
      PROG.NAME='FB.11.2'
      PROG.DESC='EDIT INTERNATIONAL DESTINATIONS'
      PACK.NAME='FREIGHT BILLING'
      CALL AFS.SCR.HEAD(CO.ID,FILE.ID,'',PROG.NAME,PROG.DESC,PACK.NAME,CO.NAME,TIME.DATE,1)
       UPL.VIN = 'FBBP' ;  UPL.VIN<2> = 'FB.11.2' ; UPL.VOUT = ''                    ;* NPR_UPL 04/23/2010
       CALL UPD.PROGRAM.LOG(UPL.VIN,UPL.VOUT)                                                           ;* NPR_UPL 04/23/2010
      PRINT@(0,2):'Client : '
*
* Open files
*
      OPEN '','BCUST' TO F.CLIENT ELSE CALL OPEN.ABORT("BCUST",PROG.NAME)
      OPEN '','FB.DESTINATION' TO F.FILE ELSE
         EXECUTE 'CREATE-FILE FB.DESTINATION 31,1' PASSLIST CAPTURING OUTPUT     ; * NKH01
         OPEN '','FB.DESTINATION' TO F.FILE ELSE CALL OPEN.ABORT("FB.DESTINATION",PROG.NAME)
      END
*
* Select codes and build master array
*
      MAST.REC=""
      SELECT F.FILE
      LOOP
         READNEXT ID ELSE ID=""
      UNTIL ID="" DO
         READ MAST.LIST FROM F.FILE,ID ELSE MAST.LIST='~~~ Not on file ~~~'
         CITY=MAST.LIST<1>
         COUNTRY=MAST.LIST<2>
         IF MAST.REC="" THEN
            MAST.REC<1>=ID
            MAST.REC<2>=CITY
            MAST.REC<3>=COUNTRY
         END ELSE
            LOCATE(ID,MAST.REC,1;POS;"AR") ELSE
               MAST.REC=INSERT(MAST.REC,1,POS,0,ID)
               MAST.REC=INSERT(MAST.REC,2,POS,0,CITY)
               MAST.REC=INSERT(MAST.REC,3,POS,0,COUNTRY)
            END
         END
      REPEAT
      DISP.REC=MAST.REC
*
10:   CRT @(0,2):@(-3)
      HELP="Enter client number or name to search. [A]=All, [X;EX;'']=Exit"
      CALL GEN.IN(0,2,'Enter Client Number : #####','',Q,0,20,'','',2,-5,2,0,QI,HELP,0,23)
      QI=OCONV(QI,'MCU')
      BEGIN CASE
         CASE QI='A'
            Q='ALL'
         CASE QI='X' OR QI='EX' OR QI=''
            STOP
         CASE ALPHA(QI)
            CALL SOUNDEX.DISPLAY(QI,'BCUST','SDX.CLIENTS,NAME',2,ITEM.LIST)
            CALL AFS.SCR.REFRESH(PROG.NAME,PROG.DESC,PACK.NAME,CO.NAME,TIME.DATE,1)
            IF ITEM.LIST='' ! DCOUNT(ITEM.LIST<1>,@VM) > 1 THEN GOTO 10
            IF NOT(NUM(ITEM.LIST<1,1>)) THEN GOTO 10
            Q=ITEM.LIST<1,1>'R%5'
         CASE NUM(QI)
            Q=QI'R%5'
         CASE 1
            GOTO 10
      END CASE
*
* Read client item
*
      IF Q='ALL' THEN
         CLIENT.ID='ALL'
         CLIENT.REC=''
      END ELSE
         CLIENT.ID=Q
         READ CLIENT.REC FROM F.CLIENT,Q ELSE
            CALL ELINE("Cannot find client ":Q:" in the CLIENT file.")
            GOTO 10
         END
         OPEN '','FB.DESTINATION,':CLIENT.ID TO F.FILE ELSE
            EXECUTE 'CREATE-FILE DATA FB.DESTINATION,':CLIENT.ID:' 31,1' PASSLIST CAPTURING OUTPUT    ; * NKH01
            OPEN '','FB.DESTINATION,':CLIENT.ID TO F.FILE ELSE CALL OPEN.ABORT("FB.DESTINATION,":CLIENT.ID,PROG.NAME)
         END
         OPEN '','SDX.DESTINATION,':CLIENT.ID TO F.SDX.FILE ELSE
            EXECUTE 'CREATE-FILE DATA SDX.DESTINATION,':CLIENT.ID:' 31,1' PASSLIST CAPTURING OUTPUT   ; * NKH01
            OPEN '','SDX.DESTINATION,':CLIENT.ID TO F.SDX.FILE ELSE CALL OPEN.ABORT("FB.DESTINATION,":CLIENT.ID,PROG.NAME)
         END
*
* Select codes and build client array
*
         CDISP.REC=""
         SELECT F.FILE
         LOOP
            READNEXT ID ELSE ID=""
         UNTIL ID="" DO
            READ CVLST FROM F.FILE,ID ELSE CVLST='~~~ Not on file ~~~'
            CITY=CVLST<1>
            COUNTRY=CVLST<2>
            IF CDISP.REC="" THEN
               CDISP.REC<1>=ID
               CDISP.REC<2>=CITY
               CDISP.REC<3>=COUNTRY
            END ELSE
               LOCATE(ID,CDISP.REC,1;POS;"AR") ELSE
                  CDISP.REC=INSERT(CDISP.REC,1,POS,0,ID)
                  CDISP.REC=INSERT(CDISP.REC,2,POS,0,CITY)
                  CDISP.REC=INSERT(CDISP.REC,3,POS,0,COUNTRY)
               END
            END
         REPEAT
         DISP.REC=CDISP.REC
      END
      OLD.DISP.REC=DISP.REC
      IF CLIENT.ID#'ALL' THEN
         PRINT @(0,2):@(-4):'Client : ':CLIENT.ID:' ':CLIENT.REC<2>
      END ELSE
         PRINT @(0,2):@(-4):
      END
*
* First page of codes
*
      PAGE=1
      GOSUB 9000
*
* Enter selection code
*
100:  HELP="Enter line number. 'B'=Back. 'T'=Top, ''=Next page. 'E'=End. 'EX'=Exit."
      CALL GEN.IN(0,22,"Enter line number please. ###",'',Q,0,3,'','',1,-3,22,0,QI,HELP,0,23)
      QI=OCONV(QI,'MCU')
      BEGIN CASE
         CASE QI=''
            PAGE=PAGE+1
            GOSUB 9000
         CASE QI='B'
            PAGE=PAGE-1
            IF PAGE=0 THEN PAGE=1
            GOSUB 9000
         CASE QI='T'
            PAGE=1
            GOSUB 9000
         CASE QI='EX'
            STOP
         CASE NUM(QI)
            SEL=QI
            IF SEL LT START ! SEL GT STOP THEN
               CALL ELINE('Invalid selection.')
               GOTO 100
            END
            GOSUB 8000
         CASE QI='E'
            X=DCOUNT(DISP.REC<1>,CHAR(253))
            FOR I=X TO 1 STEP -1
               IF DISP.REC<1,I>='' THEN
                  DISP.REC=DELETE(DISP.REC,1,I)
                  DISP.REC=DELETE(DISP.REC,2,I)
                  DISP.REC=DELETE(DISP.REC,3,I)
               END
            NEXT I
*
            Z=0
            LOOP
               Z=Z+1
               CODE=DISP.REC<1,Z>
            UNTIL CODE="" DO
               READ ITEM FROM F.FILE,CODE ELSE ITEM=''
               ITEM<1>=DISP.REC<2,Z>
               ITEM<2>=DISP.REC<3,Z>
               WRITE ITEM ON F.FILE,CODE
               IF CLIENT.ID#'ALL' THEN
                  CALL SOUNDEX.EDIT(OLD.DISP.REC<2,Z>,DISP.REC<2,Z>,CODE,'SDX.DESTINATION,':CLIENT.ID)
                  CALL SOUNDEX.EDIT(OLD.DISP.REC<3,Z>,DISP.REC<3,Z>,CODE,'SDX.DESTINATION,':CLIENT.ID)
               END
            REPEAT
            DISP.REC=MAST.REC
            GO 10
      END CASE
      GOTO 100
*
*-----------------------------------------------------------------------
* INTERNAL SUBROUTINES
*-----------------------------------------------------------------------
*
* Edit code
*
8000: ROW=3+REM(SEL,18)
      IF ROW=3 THEN ROW=21
      CODE=DISP.REC<1,SEL>
      CITY=DISP.REC<2,SEL>
      COUNTRY=DISP.REC<3,SEL>
8010:* ENTER CODE
      IF CLIENT.ID='ALL' THEN
         HELP="Enter code. ''=Accept. 'X'=Exit."
      END ELSE
         HELP="Enter code. ''=Accept. 'D'=Delete. 'L'=List. 'X'=Exit."
      END
      CALL GEN.IN(5,ROW,CODE'L#4','',Q,0,4,'','',2,-4,ROW,0,QI,HELP,0,23)
      QI=OCONV(QI,'MCU')
      IF QI='X' THEN
         CRT @(5,ROW):CODE'L#4 '
         RETURN
      END
      IF QI='' THEN
         Q=CODE
      END
      IF QI='D' AND CLIENT.ID#'ALL' THEN
         CALL SOUNDEX.EDIT(OLD.DISP.REC<2,SEL>,'',CODE,'SDX.DESTINATION,':CLIENT.ID)
         CALL SOUNDEX.EDIT(OLD.DISP.REC<3,SEL>,'',CODE,'SDX.DESTINATION,':CLIENT.ID)
         DELETE F.FILE,CODE
         DISP.REC=DELETE(DISP.REC,1,SEL)
         DISP.REC=DELETE(DISP.REC,2,SEL)
         DISP.REC=DELETE(DISP.REC,3,SEL)
         GOSUB 9000
         RETURN
      END
      IF QI='L' AND CLIENT.ID#'ALL' THEN
         SENTENCE = 'SORT FB.DESTINATION'
         SENTENCE := ' BY DESC DESC'
         EXECUTE SENTENCE
         CALL ELINE('')
         CALL AFS.SCR.REFRESH(PROG.NAME,PROG.DESC,PACK.NAME,CO.NAME,TIME.DATE,1)
         IF CLIENT.ID#'ALL' THEN
            PRINT @(0,2):@(-4):'Client : ':CLIENT.ID:' ':CLIENT.REC<2>
         END
         GOSUB 9000
         GOTO 8000
      END
      IF CLIENT.ID#'ALL' THEN
         LOCATE Q IN MAST.REC<1> SETTING CPOS THEN
            CITY=MAST.REC<2,CPOS>
            COUNTRY=MAST.REC<3,CPOS>
         END ELSE
            CALL ELINE(Q:' - Is not in Master List. Try again')
            GO 8010
         END
      END
      CODE=Q
      CRT @(5,ROW):CODE'L#4'
*
8020:* ENTER THE CITY OR FIRST PART OF DESCRIPTION
      IF CLIENT.ID='ALL' THEN
         HELP="Enter new code description. Use ~ for input points. '<'=Back to code. 'X'=Exit."
         CALL GEN.IN(12,ROW,CITY'L#25','',Q,0,25,'','',2,-25,ROW,0,QI,HELP,0,23)
         QI=OCONV(QI,'MCU')
         BEGIN CASE
            CASE QI='X' ! QI='<'
               CRT @(12,ROW):CITY'L#25'
               GOTO 8010
            CASE QI=''
               Q=CITY
         END CASE
         CITY=Q
      END
      CRT @(12,ROW):CITY'L#25'
8030:* ENTER THE COUNTRY OR SECOND PART OF DESCRIPTION
      IF CLIENT.ID='ALL' THEN
         HELP="Enter new code description. Use ~ for input points. '<'=Back to code. 'X'=Exit."
         CALL GEN.IN(40,ROW,COUNTRY'L#15','',Q,0,15,'','',2,-15,ROW,0,QI,HELP,0,23)
         QI=OCONV(QI,'MCU')
         BEGIN CASE
            CASE QI='X' ! QI='<'
               CRT @(40,ROW):COUNTRY'L#15'
               GOTO 8020
            CASE QI=''
               Q=COUNTRY
         END CASE
         COUNTRY=Q
      END
      CRT @(40,ROW):COUNTRY'L#15'
      DISP.REC<1,SEL>=CODE
      DISP.REC<2,SEL>=CITY
      DISP.REC<3,SEL>=COUNTRY
      RETURN
*
* Display array
*
9000: CRT @(0,3):@(-3):"  #":@(5,3):"Code":@(12,3):"City":@(40,3):"Country"
      MAX.ITMS=DCOUNT(DISP.REC<1>,@VM)
      START=(PAGE*18)-17
      IF START GT MAX.ITMS THEN
         START=1
         PAGE=1
      END
      STOP=START+17
      FOR I=START TO STOP STEP 1
         ROW=3+REM(I,18)
         IF ROW=3 THEN ROW=21
         CODE=DISP.REC<1,I>
         CITY=DISP.REC<2,I>
         COUNTRY=DISP.REC<3,I>
         PRINT @(0,ROW):I"R#3 ":@(5,ROW):CODE"L#4 ":@(12,ROW):CITY"L#27 ":@(40,ROW):COUNTRY"L#15"
      NEXT I
      RETURN
