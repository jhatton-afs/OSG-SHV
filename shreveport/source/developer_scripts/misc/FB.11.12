$OPTIONS HEADER.EJECT
$OPTIONS EXEC.EQ.PERF
$OPTIONS INFO.ABORT
      ERRMSG=''
* PROGRAM FB.11.12
* PROGRAM TO MODIFY TRASPORT MODES
*
* FREIGHT BILLING PACKAGE
*
* AUTHOR: BRIAN M. QUEEN
* DATE: AUGUST 29, 1992
*
* Modications
* 080312 - RLM01 - Sysaid #3073 - Replace former employees' email addresses with those of current employees
*
*
* Initialize variables
*
      PROMPT''
      PROG.NAME='FB.11.12'
      PROG.DESC='EDIT ALTERNATE CARRIER MODES'
      PACK.NAME='FREIGHT BILLING'
      CALL AFS.SCR.HEAD(CO.ID,FILE.ID,'',PROG.NAME,PROG.DESC,PACK.NAME,CO.NAME,TIME.DATE,1)
       UPL.VIN = 'FBBP' ;  UPL.VIN<2> = 'FB.11.12' ; UPL.VOUT = ''                    ;* NPR_UPL 04/23/2010
       CALL UPD.PROGRAM.LOG(UPL.VIN,UPL.VOUT)                                                           ;* NPR_UPL 04/23/2010

      CALL GET.USER(USER.NAME)
      BEGIN CASE
            *
            * Authorized users
            *
         CASE USER.NAME = 'BRIAN'
         CASE USER.NAME = 'KAREN'
         CASE USER.NAME = 'OWEN'
         CASE USER.NAME = 'MYRA'
         CASE USER.NAME = 'MARY'
         CASE USER.NAME = 'MKERBOW'
         CASE USER.NAME = 'DAVID'
         CASE USER.NAME = 'NPRATT'
            *
            * Restrict everyone else
            *
         CASE 1
            SUBJ="Unauthorized Program Access by ":USER.NAME
            TEXT=''
            TEXT<1,-1>=USER.NAME:' ':DATE()'D2/':' ':OCONV(TIME(),'MTHS'):' ':PROG.NAME
            TEXT<1,-1>='User ':USER.NAME:' tried to access ':PROG.DESC
            TEXT<1,-1>="(This user's name was not included in the program to allow access)"
            MVIN=''
*           MVIN<1>='oweninfo@afs.net':@VM:'SMatthews@afs.net'  ; * RLM01
*           MVIN<1>='bfranklin@afs.net':@VM:'SMatthews@afs.net'  ; * RLM01
            MVIN<1>='SMatthews@afs.net'  ; * RLM01
            MVIN<2>=''
            MVIN<4>='AFS'
            MVIN<5>=SUBJ
            MVIN<6>=TEXT
            CALL SENDMAIL(MVIN,MVOUT)

            CALL FB.PRINT.CENT(1,80,12,'Restricted Access Application...')
            CALL FB.PRINT.CENT(1,80,14,'Access Denied! See CHRIS for Access')
            CALL ELINE('')
            STOP
      END CASE

      PRINT@(0,2):'CARRIER : '
*
* Open files
*
      OPEN '','CARRIERS' TO F.CARRIER ELSE CALL OPEN.ABORT("CARRIERS",PROG.NAME)
      OPEN '','CARRIER.ALT.MODE' TO F.FILE ELSE
         EXECUTE 'CREATE-FILE CARRIER.ALT.MODE 1 7' PASSLIST CAPTURING OUTPUT
         OPEN '','CARRIER.ALT.MODE' TO F.FILE ELSE CALL OPEN.ABORT("CARRIER.ALT.MODE",PROG.NAME)
      END
*
* Select codes and build master array
*
      MAST.REC=""
      SELECT F.FILE
      LOOP
         READNEXT ID ELSE ID=""
      UNTIL ID="" DO
         READ MAST.LIST FROM F.FILE,ID ELSE MAST.LIST='~~~ Not on file ~~~'
         DESC=MAST.LIST<1>
         IF MAST.REC="" THEN
            MAST.REC<1>=ID
            MAST.REC<2>=DESC
         END ELSE
            LOCATE(ID,MAST.REC,1;POS;"AR") ELSE
               MAST.REC=INSERT(MAST.REC,1,POS,0,ID)
               MAST.REC=INSERT(MAST.REC,2,POS,0,DESC)
            END
         END
      REPEAT
      DISP.REC=MAST.REC
*
10:   CRT @(0,2):@(-3)
      HELP="Enter carrier number or name to search. [A]=All, [X;EX;'']=Exit"
      CALL GEN.IN(0,2,'Enter carrier Number : #####','',Q,0,20,'','',2,-5,2,0,QI,HELP,0,23)
      QI=OCONV(QI,'MCU')
      BEGIN CASE
         CASE QI='A'
            Q='ALL'
         CASE QI='X' OR QI='EX' OR QI=''
            STOP
         CASE ALPHA(QI)
            CALL SOUNDEX.DISPLAY(QI,'CARRIERS','SDX.CARRIER,NAME','1,3,4',ITEM.LIST)
            CALL AFS.SCR.REFRESH(PROG.NAME,PROG.DESC,PACK.NAME,CO.NAME,TIME.DATE,1)
            IF ITEM.LIST='' ! DCOUNT(ITEM.LIST<1>,@VM) > 1 THEN GOTO 10
            IF NOT(NUM(ITEM.LIST<1,1>)) THEN GOTO 10
            Q=ITEM.LIST<1,1>'R%5'
         CASE NUM(QI)
            Q=QI'R%5'
         CASE 1
            GOTO 10
      END CASE
*
* Read carrier item
*
      IF Q='ALL' THEN
         CARRIER.ID='ALL'
         CARRIER.REC=''
      END ELSE
         CARRIER.ID=Q
         READ CARRIER.REC FROM F.CARRIER,Q ELSE
            CALL ELINE("Cannot find carrier ":Q:" in the carrier file.")
            GOTO 10
         END
         T.MODE.BY.WGT=CARRIER.REC<71>
         IF T.MODE.BY.WGT#'' THEN
            CALL ELINE("You must remove transport mode by weight from this carrier.")
            GOTO 10
         END
         OPEN '','CARRIER.ALT.MODE,':CARRIER.ID TO F.FILE ELSE
            EXECUTE 'CREATE-FILE DATA CARRIER.ALT.MODE,':CARRIER.ID:' 1 7' PASSLIST CAPTURING OUTPUT
            OPEN '','CARRIER.ALT.MODE,':CARRIER.ID TO F.FILE ELSE CALL OPEN.ABORT("CARRIER.ALT.MODE,":CARRIER.ID,PROG.NAME)
         END
         OPEN '','SDX.ALT.MODE,':CARRIER.ID TO F.SDX.FILE ELSE
            EXECUTE 'CREATE-FILE DATA SDX.ALT.MODE,':CARRIER.ID:' 1 7' PASSLIST CAPTURING OUTPUT
            OPEN '','SDX.ALT.MODE,':CARRIER.ID TO F.SDX.FILE ELSE CALL OPEN.ABORT("SDX.ALT.MODE,":CARRIER.ID,PROG.NAME)
         END
*
* Select codes and build carrier array
*
         CDISP.REC=""
         SELECT F.FILE
         LOOP
            READNEXT ID ELSE ID=""
         UNTIL ID="" DO
            READ CVLST FROM F.FILE,ID ELSE CVLST='~~~ Not on file ~~~'
            DESC=CVLST<1>
            IF CDISP.REC="" THEN
               CDISP.REC<1>=ID
               CDISP.REC<2>=DESC
            END ELSE
               LOCATE(ID,CDISP.REC,1;POS;"AR") ELSE
                  CDISP.REC=INSERT(CDISP.REC,1,POS,0,ID)
                  CDISP.REC=INSERT(CDISP.REC,2,POS,0,DESC)
               END
            END
         REPEAT
         DISP.REC=CDISP.REC
      END
      OLD.DISP.REC=DISP.REC
      IF CARRIER.ID#'ALL' THEN
         PRINT @(0,2):@(-4):'CARRIER : ':CARRIER.ID:' ':CARRIER.REC<1>
      END ELSE
         PRINT @(0,2):@(-4):
      END
*
* First page of codes
*
      PAGE=1
      GOSUB 9000
*
* Enter selection code
*
100:  HELP="Enter line number. 'B'=Back. 'T'=Top, ''=Next page. 'E'=End. 'EX'=Exit."
      CALL GEN.IN(0,22,"Enter line number please. ###",'',Q,0,3,'','',1,-3,22,0,QI,HELP,0,23)
      QI=OCONV(QI,'MCU')
      BEGIN CASE
         CASE QI=''
            PAGE=PAGE+1
            GOSUB 9000
         CASE QI='B'
            PAGE=PAGE-1
            IF PAGE=0 THEN PAGE=1
            GOSUB 9000
         CASE QI='T'
            PAGE=1
            GOSUB 9000
         CASE QI='EX'
            STOP
         CASE NUM(QI)
            SEL=QI
            IF SEL LT START ! SEL GT STOP THEN
               CALL ELINE('Invalid selection.')
               GOTO 100
            END
            GOSUB 8000
         CASE QI='E'
            X=DCOUNT(DISP.REC<1>,CHAR(253))
            FOR I=X TO 1 STEP -1
               IF DISP.REC<1,I>='' THEN
                  DISP.REC=DELETE(DISP.REC,1,I)
                  DISP.REC=DELETE(DISP.REC,2,I)
               END
            NEXT I
*
            Z=0
            LOOP
               Z=Z+1
               CODE=DISP.REC<1,Z>
            UNTIL CODE="" DO
               READ ITEM FROM F.FILE,CODE ELSE ITEM=''
               ITEM<1>=DISP.REC<2,Z>
               WRITE ITEM ON F.FILE,CODE
               IF CARRIER.ID#'ALL' THEN
                  CALL SOUNDEX.EDIT(OLD.DISP.REC<2,Z>,DISP.REC<2,Z>,CODE,'SDX.ALT.MODE,':CARRIER.ID)
               END
            REPEAT
            DISP.REC=MAST.REC
            GO 10
      END CASE
      GOTO 100
*
*-----------------------------------------------------------------------
* INTERNAL SUBROUTINES
*-----------------------------------------------------------------------
*
* Edit code
*
8000: ROW=3+REM(SEL,18)
      IF ROW=3 THEN ROW=21
      CODE=DISP.REC<1,SEL>
      DESC=DISP.REC<2,SEL>
8010:* ENTER CODE
      IF CARRIER.ID='ALL' THEN
         HELP="Enter code. ''=Accept. 'X'=Exit."
      END ELSE
         HELP="Enter code. ''=Accept. 'D'=Delete. 'L'=List. 'X'=Exit."
      END
      CALL GEN.IN(5,ROW,CODE'L#4','',Q,0,4,'','',2,-4,ROW,0,QI,HELP,0,23)
      QI=OCONV(QI,'MCU')
      IF QI='X' THEN
         CRT @(5,ROW):CODE'L#4 '
         RETURN
      END
      IF QI='' THEN
         Q=CODE
      END
      IF QI='D' AND CARRIER.ID#'ALL' THEN
         CALL SOUNDEX.EDIT(OLD.DISP.REC<2,SEL>,'',CODE,'SDX.ALT.MODE,':CARRIER.ID)
         DELETE F.FILE,CODE
         DISP.REC=DELETE(DISP.REC,1,SEL)
         DISP.REC=DELETE(DISP.REC,2,SEL)
         GOSUB 9000
         RETURN
      END
      IF QI='L' AND CARRIER.ID#'ALL' THEN
         SENTENCE = 'SORT CARRIER.ALT.MODE'
         SENTENCE := ' BY DESC DESC'
         EXECUTE SENTENCE
         CALL ELINE('')
         CALL AFS.SCR.REFRESH(PROG.NAME,PROG.DESC,PACK.NAME,CO.NAME,TIME.DATE,1)
         IF CARRIER.ID#'ALL' THEN
            PRINT @(0,2):@(-4):'CARRIER : ':CARRIER.ID:' ':CARRIER.REC<1>
         END
         GOSUB 9000
         GOTO 8000
      END
      IF CARRIER.ID#'ALL' THEN
         LOCATE Q IN MAST.REC<1> SETTING CPOS THEN
            DESC=MAST.REC<2,CPOS>
         END ELSE
            CALL ELINE(Q:' - Is not in Master List. Try again')
            GO 8010
         END
      END
      CODE=Q
      CRT @(5,ROW):CODE'L#4'
*
8020:* ENTER FIRST PART OF DESCRIPTION
      IF CARRIER.ID='ALL' THEN
         HELP="Enter new code description. Use ~ for input points. '<'=Back to code. 'X'=Exit."
         CALL GEN.IN(12,ROW,DESC'L#25','',Q,0,25,'','',2,-25,ROW,0,QI,HELP,0,23)
         QI=OCONV(QI,'MCU')
         BEGIN CASE
            CASE QI='X' ! QI='<'
               CRT @(12,ROW):DESC'L#25'
               GOTO 8010
            CASE QI=''
               Q=DESC
         END CASE
         DESC=Q
      END
      CRT @(12,ROW):DESC'L#25'
      DISP.REC<1,SEL>=CODE
      DISP.REC<2,SEL>=DESC
      RETURN
*
* Display array
*
9000: CRT @(0,3):@(-3):"  #":@(5,3):"Code":@(12,3):"Description"
      MAX.ITMS=DCOUNT(DISP.REC<1>,@VM)
      START=(PAGE*18)-17
      IF START GT MAX.ITMS THEN
         START=1
         PAGE=1
      END
      STOP=START+17
      FOR I=START TO STOP STEP 1
         ROW=3+REM(I,18)
         IF ROW=3 THEN ROW=21
         CODE=DISP.REC<1,I>
         DESC=DISP.REC<2,I>
         PRINT @(0,ROW):I"R#3 ":@(5,ROW):CODE"L#4 ":@(12,ROW):DESC"L#27 "
      NEXT I
      RETURN
