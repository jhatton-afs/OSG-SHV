*---- PROGRAM: FB.3.20.17
* AUTHOR: DAVID R FRIEND
* DATE: MAY 31, 2016
* DESCRIPTION: IMAGING COUNT REPORT

* THIS PROGRAM IS A PROPRIETARY PRODUCT OF AUTOMATED FREIGHT SYSTEMS
* AND MAY NOT BE USED, COPIED OR MODIFIED WITHOUT THE EXPRESS
* WRITTEN CONSENT OF SAME.
*-----------------------------------------------------------------------
*
$INCLUDE PROG.ADMIN INC_OPTIONS
*
* Initialize variables
*
      EQU TAB TO CHAR(9)
*
      CALL GET.USER(USER.NAME)
      CALL CHANNEL(CH.NUM)
      PROG.NAME='FB.3.20.17'
      PROG.DESC='Imaging Count Report'
      PACK.NAME='Freight Billing'
      PER.FLAG = ''
       UPL.VIN = 'FBBP' ;  UPL.VIN<2> = 'FB.3.20.15' ; UPL.VOUT = ''                    ;* NPR_UPL 04/23/2010
       CALL UPD.PROGRAM.LOG(UPL.VIN,UPL.VOUT)                                                           ;* NPR_UPL 04/23/2010
      BEG.DATE = DATE()
*DF      END.DATE = DATE()

*
* Open files
*
      OPEN '','AR.BRIDGE.LAST' TO F.BRIDGE ELSE
         CALL OPEN.ABORT('AR.BRIDGE.LAST',PROG.NAME)
      END
      OPEN '','CLIENTS' TO F.CLIENTS ELSE CALL OPEN.ABORT("CLIENTS",PROG.NAME)
      OPEN 'DICT','FBWORK':CH.NUM TO F.DICT.WORK ELSE
         CALL OPEN.ABORT('DICT FBWORK':CH.NUM,PROG.NAME)
      END
      OPEN '','VOC' TO F.VOC ELSE
         CALL OPEN.ABORT('VOC',PROG.NAME)
      END
      READ BILLDATA.REC FROM F.VOC,"BILLDATA" ELSE
         CALL ELINE('UNABLE TO OPEN "VOC BILLDATA"')
         STOP
      END
      BILLDATA.REC<2>:='\':USER.NAME
      WRITE BILLDATA.REC ON F.VOC,"BILLDATA.":USER.NAME ELSE
         CALL ELINE("YOUR USER NAME '":USER.NAME:"' IS NOT IN THE BILLDATA FOLDER - PLEASE SEE OWEN/DAVID")
         STOP
      END
      OPEN '','BILLDATA.':USER.NAME TO F.BILLDATA ELSE
         CALL ELINE('UNABLE TO OPEN "BILLDATA."':USER.NAME:"'")
         STOP
      END
*
* Display screen heading
*
30:   CALL AFS.SCR.HEAD(CO.ID,FILE.ID,'',PROG.NAME,PROG.DESC,PACK.NAME,CO.NAME,TIME.DATE,1)
*
* Get current period beginning date
*
100:  HELP='Enter the data processed date [X]=Back [EX]=Exit'
      CALL GEN.IN(0,8,'Data processed date.  ':BEG.DATE'D2/','DATE',Q,0,8,'','',0,22,8,0,QI,HELP,0,23)
      QI=OCONV(QI,'MCU')
      IF QI='X' THEN CRT @(32,8):@(-4); GOTO EXIT.PROGRAM
      IF QI='EX' THEN GOTO EXIT.PROGRAM
      IF QI > DATE() THEN
         CALL ELINE('Date cannot be greater than today')
         GO 100
      END
      BEG.DATE = Q
      CRT @(22,8):BEG.DATE'D2/'

*
*  Verify date entry range
*
500:  
      HELP="Is this correct?  X=Change. EX=Exit. RETURN=Continue."
      CALL GEN.IN(0,22,'Enter selection please. --','',Q,0,2,'','',0,-2,22,1,QI,HELP,0,23)
      CRT @(0,11):@(-4)
      CRT @(0,12):@(-4)
      CRT @(0,13):@(-4)
      CRT @(22,8):BEG.DATE'D2/'
      QI=OCONV(QI,'MCU')
      BEGIN CASE
         CASE QI='X' OR QI = 'N'
            CRT @(0,22):@(-3):
            GO 100
         CASE QI='EX'
            GOTO EXIT.PROGRAM
         CASE QI='' OR QI = 'Y'
            NULL
         CASE 1
            CALL ELINE('Invalid entry. Must be X, EX, or RETURN.')
            GOTO 500
      END CASE

*
* Start processing
*
600:  TEXT="Processing - Please Wait ..."
      CALL CENTER(TEXT,80)
      CRT @(0,15):@(-3):TEXT
*
* Select data from CLIENTS file
*
      TEXT='Selecting Imaging Clients.'
      CALL CENTER(TEXT,80)
      CRT @(0,12):TEXT
      STMT='GET-LIST IMAGING'
      EXECUTE STMT RTNLIST CLIENT.LIST CAPTURING OUTPUT
      ITEMS = @SELECTED
      IF @SELECTED < 1 THEN
        CALL ELINE('ERROR - No clients on file.')
        GOTO EXIT.PROGRAM
      END
      COUNTER.1=0
      PER=0

*
*  Create report headings
*
      *** Main Report Heading
      HEAD1 = PROG.DESC:'  ':PROG.NAME:'  ':TIMEDATE()
      *** Second Report Heading
      HEAD2 = ' '
      *** Blank Line
      HEAD3 = ' For ':OCONV(BEG.DATE,'D2/')
      HEAD4 = ''
      HEAD4<-1> = 'Client ID'
      HEAD4<-1> = 'Client Name'
      HEAD4<-1> = 'Count'
      CONVERT CHAR(254) TO CHAR(9) IN HEAD4

*
* Create spreadsheet and save to U drive
*
      OUTPUT.REC = ''
      D = DATE()'D4/'
      T = OCONV(TIME(),'MT')
      OUTPUT.REC<-1> = HEAD1
      OUTPUT.REC<-1> = HEAD2
      OUTPUT.REC<-1> = HEAD3
      OUTPUT.REC<-1> = HEAD4
      LOOP
        READNEXT CLIENT.ID FROM CLIENT.LIST ELSE CLIENT.ID='DONE'
      UNTIL CLIENT.ID='DONE' DO
        COUNTER.1=COUNTER.1+1
        IF CLIENT.ID='FB.BILLS.HIST' OR CLIENT.ID[1,2]='99' THEN GO SKIP.CLIENT
        READ DCLIENT FROM F.CLIENTS,CLIENT.ID THEN
          IF DCLIENT<30> # 'A' THEN GO SKIP.CLIENT
        END ELSE
          GO SKIP.CLIENT
        END
        CLIENT.NAME=DCLIENT<2>
        BRIDGE.ID=CLIENT.ID:'*':BEG.DATE
        READ DBRIDGE FROM F.BRIDGE,BRIDGE.ID ELSE
           GO SKIP.CLIENT
        END
        COUNT=''
        IF DBRIDGE<1,1>='1' OR DBRIDGE<1,1>[2,3]='001' THEN
           COUNT=DBRIDGE<2,1>
        END
        IF COUNT#'' THEN
           DLINE=''
           DLINE<-1>=CLIENT.ID
           DLINE<-1>=CLIENT.NAME
           DLINE<-1>=COUNT
           CONVERT CHAR(254) TO CHAR(9) IN DLINE
           OUTPUT.REC<-1>=DLINE
        END
SKIP.CLIENT:
      REPEAT

      RECORD.NAME = 'ImagingCountReport-'
      RECORD.NAME:=D[7,4]:D[1,2]:D[4,2]
      RECORD.NAME:='-':T[1,2]:T[4,2]:'.XLS'
      WRITE OUTPUT.REC TO F.BILLDATA, RECORD.NAME
      FOR I = 12 TO 21
        CRT @(0,I):@(-4)
      NEXT I
      CRT @(0,12):'Report file  "':RECORD.NAME:'"'
      CRT @(0,13):'has been written to the BILLDATA/':USER.NAME:' folder'
      CALL ELINE(' ')

EXIT.PROGRAM:***
      STOP
