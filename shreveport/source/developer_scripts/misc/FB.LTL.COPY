*======================================================================
* PROGRAM : FB.LTL.COPY
*
* PROGRAMMER : David R Friend
*
*
*======================================================================
*   COPYRIGHT (C) 1996 - AFS LOGISTICS LLC - ALL RIGHTS RESERVED
*       ----------------------------------------------------
*  This program is sole property of AFS Logistics, LLC (AUTOMATED
*  FREIGHT SYSTEMS) and may not be used, copied, or modified without
*  the express written consent of Brian Barker of AFS Logistics, LLC.
*======================================================================
*
* 123009 NPR01 - T091182 - Add option for Copying ALL tables from client to client.

$INCLUDE PROG.ADMIN INC_OPTIONS

   ORGIN.TABLE.NUM = ''
   DEST.TABLE.NUM = ''
   PROMPT''
   CALL GET.USER(USER.NAME)
   PROG.NAME='FB.LTL.COPY'
   PROG.DESC='LTL Audit Table Copy'
   PACK.NAME='User :':USER.NAME
   CO.NAME='AFS Logistics, LLC'
    UPL.VIN = 'FBBP' ;  UPL.VIN<2> = 'FB.LTL.COPY' ; UPL.VOUT = ''                    ;* NPR_UPL 04/23/2010
    CALL UPD.PROGRAM.LOG(UPL.VIN,UPL.VOUT)                                                           ;* NPR_UPL 04/23/2010

   OPEN '','CLIENTS' TO F.CLIENTS ELSE
      CALL OPEN.ABORT("CLIENTS",PROG.NAME)
   END
   OPEN '','CARRIERS' TO F.CARRIERS ELSE
      CALL OPEN.ABORT("CARRIERS",PROG.NAME)
   END

10 :

   GOSUB DSP.SCREEN
   ORGIN.CLIENT.ID = ''
   ORGIN.CARRIER.ID = ''
   DEST.CLIENT.ID=''
   DEST.CARRIER.ID=''
   ORGIN.CARRIER.NAME = ''
   ORGIN.CARRIER.CITY = ''
   ORGIN.CARRIER.STATE = ''
   DEST.CARRIER.NAME = ''
   DEST.CARRIER.CITY = ''
   DEST.CARRIER.STATE = ''

   ORGIN.TABLE.REC = ''

   ORGIN.TABLE.DESC = ''
   DEST.TABLE.DESC  = ''

50:
   HELP = "Enter client name or number to copy from. [EX] = Exit"
   IF ORGIN.CLIENT.ID='' THEN
     MASK = '#####'
     CRT @(20, 3):@(-4)
     CRT @(20, 4):@(-4)
     CRT @(20, 5):@(-4)
   END ELSE
     MASK = ORGIN.CLIENT.ID
   END
   CALL GEN.IN(20,3,MASK,'',Q,0,20,'','',2,-5,3,1,QI,HELP,0,23)
   IF QI='' THEN QI=ORGIN.CLIENT.ID
   QI = OCONV(QI,'MCU')
   BEGIN CASE
      CASE QI='EX' OR QI='X'
         STOP
      CASE QI='' AND ORGIN.CLIENT.ID=''
         STOP
      CASE NUM(QI) AND QI # ''
           ORGIN.CLIENT.ID = QI'R%5'
      CASE NOT(QI MATCHES "0N") AND QI # ''
           CALL SOUNDEX.DISPLAY(QI,'BCUST','SDX.CLIENTS,NAME',2,ITEM.LIST)
           GOSUB DSP.SCREEN
           BEGIN CASE
              CASE ITEM.LIST = ''
                 GOTO 50                            ;* START OVER
              CASE DCOUNT(ITEM.LIST,@AM) GT 1
                 GOTO 50                            ;* START OVER
              CASE 1
                 DATA ITEM.LIST
                 GOTO 50                            ;* START OVER
           END CASE
      CASE 1
           CALL ELINE('Invalid Client Name, Try Again.')
           GOTO 50
   END CASE

   *
   * Open Client Records
   *
   READ ORGIN.CLIENT.REC FROM F.CLIENTS,ORGIN.CLIENT.ID ELSE
      CALL ELINE("Client number ":ORGIN.CLIENT.ID:" is not a valid client #")
      GOTO 50
   END

   ORGIN.CLIENT.NAME=ORGIN.CLIENT.REC<2>
   CRT @(20, 3):ORGIN.CLIENT.ID:', ':ORGIN.CLIENT.NAME:@(-4)
   CRT @(20, 4):ORGIN.CLIENT.REC<3,1>:' ':ORGIN.CLIENT.REC<3,2>:@(-4)
   CRT @(20, 5):ORGIN.CLIENT.REC<4>:', ':ORGIN.CLIENT.REC<5>:' ':ORGIN.CLIENT.REC<6>:@(-4)

   *
   * Open Client Audit File
   *
   OPEN '','FB.AUDIT.TABLES,':ORGIN.CLIENT.ID TO F.ORGIN.AUDIT.TABLES ELSE
      CALL ELINE('Error ':ORGIN.CLIENT.ID:' is not setup for any audit tables.')
      GOTO 50
   END


   *------------------------------------------
   * ASK FOR CARRIER
   *------------------------------------------
150:

* NPR01   HELP="Enter carrier number, 'name' for search, [L]ist, [EX]it."
   HELP="Enter carrier number, [ALL], 'name' for search, [L]ist, [EX]it."   ;* NPR01
   IF ORGIN.CARRIER.ID THEN
      MASK = ORGIN.CARRIER.ID
   END ELSE
      MASK = '#####'
      CRT @(20, 7):@(-4)
      CRT @(20, 8):@(-4)
   END

200:
   CALL GEN.IN(20,7,MASK,'',Q,0,20,'','',2,20,7,0,QI,HELP,0,23)
   IF QI='' THEN QI=MASK
   QI=OCONV(QI,'MCU')

   BEGIN CASE
     CASE QI='X'
        GOSUB DSP.DATA
        GOTO 50

     CASE QI='EX'
        STOP

     CASE QI EQ 'L'
        EXECUTE 'SORT FB.AUDIT,':ORGIN.CLIENT.ID:' BY CARRIER# CARRIER.NAME CARRIER.CITY CARRIER.STATE'
        CALL ELINE('')
        GOSUB DSP.SCREEN
        GOSUB DSP.DATA
        GOTO 200

     CASE NUM(QI)
        ORGIN.CARRIER.ID=QI'R%5'
        CRT @(20,7):ORGIN.CARRIER.ID
     CASE QI = "ALL"                  ;* NPR01
        ORGIN.CARRIER.ID = QI         ;* NPR01
        ORGIN.TABLE.REC = ''          ;* NPR01
        GOTO 300                      ;* NPR01
     CASE 1
       CALL SOUNDEX.DISPLAY(Q,'CARRIERS','SDX.CARRIER,NAME','1,3,4',QX)
       CT=DCOUNT(QX<1>,@VM)
       IF CT=1 THEN
          DATA QX<1,1>
       END
       GOSUB DSP.SCREEN
       GOSUB DSP.DATA
       GOTO 200

     CASE 1
       CALL ELINE('Invalid Entry.')
   END CASE
   *
   * Open Carrier Records
   *
   READ ORGIN.CARRIER.REC FROM F.CARRIERS,ORGIN.CARRIER.ID ELSE
       CALL ELINE('"':ORGIN.CARRIER.ID:'" is an invalid Carrier Number. Try Again.')
       GOTO 150
   END

   ORGIN.CARRIER.NAME  = ORGIN.CARRIER.REC<1>
   ORGIN.CARRIER.CITY  = ORGIN.CARRIER.REC<3>
   ORGIN.CARRIER.STATE = ORGIN.CARRIER.REC<4>
   GOSUB DSP.DATA

   *------------------------------------------
   * TABLE NUMBER
   *------------------------------------------
300:


*NPR01   HELP = "Enter TABLE NUMBER to copy from. [SPACE]=Select [EX] = Exit"
   HELP = "Enter TABLE NUMBER to copy from., [ALL], [SPACE]=Select [EX] = Exit"
   IF ORGIN.TABLE.NUM THEN
      MASK = ORGIN.TABLE.NUM:' '
   END ELSE
      MASK = '##'
      CRT @(20, 10):@(-4)
      CRT @(20, 11):@(-4)
   END

310:
   CALL GEN.IN(20,10,MASK,'',Q,0,20,'','',2,20,10,1,QI,HELP,0,23)
   IF QI='' AND ORGIN.TABLE.NUM THEN QI=ORGIN.TABLE.NUM
   IF QI=' ' THEN QI=''
   QI = TRIM(OCONV(QI,'MCU'))
   BEGIN CASE
      CASE QI = 'EX'
         STOP

      CASE QI='X'
         GOSUB DSP.DATA
         GOTO 150
      CASE QI='ALL'            ;* NPR01
         ORGIN.TABLE.NUM = QI  ;* NPR01
         CRT @(20, 10):ORGIN.TABLE.NUM:@(-4)
         GOTO 450              ;* NPR01
      CASE QI=''
          VIN=ORGIN.CLIENT.ID
          VIN<2>=ORGIN.CARRIER.ID
          VOUT=''
          CALL AUDIT.TABLE.SELECT(VIN,VOUT)
          BEGIN CASE
             CASE VOUT<1> = 'EX'
                STOP
             CASE VOUT<1> = 'X'
                GOSUB DSP.SCREEN
                GOSUB DSP.DATA
                GOTO 300
             CASE 1
                GOSUB DSP.SCREEN
                GOSUB DSP.DATA
                ORGIN.TABLE.NUM = VOUT<1>
          END CASE


      CASE (QI GE '0' AND QI LE '99') OR (QI GE 'A' AND QI LE 'Z')
         ORGIN.TABLE.NUM = QI

      CASE 1
         CALL ELINE('Invalid Table Number, Try Again.')
         ORGIN.TABLE.NUM=''
         GOTO 310
   END CASE

325:
   CRT @(20, 10):ORGIN.TABLE.NUM:@(-4)

   ORGIN.KEY = ORGIN.CARRIER.ID:'*':ORGIN.TABLE.NUM
   READ ORGIN.TABLE.REC FROM F.ORGIN.AUDIT.TABLES, ORGIN.KEY ELSE
     CALL ELINE('Table ':ORGIN.TABLE.NUM:' is not on file. Pick an existing table to copy from')
     ORGIN.TABLE.NUM=''
     GOTO 300
   END
   CRT @(20,11):ORGIN.TABLE.REC<31>[1,59]:@(-4)
450:
   HELP = "Enter client name or number to copy to. [EX] = Exit"
   IF DEST.CLIENT.ID='' THEN
     MASK = '#####'
     CRT @(20, 13):@(-4)
     CRT @(20, 14):@(-4)
     CRT @(20, 15):@(-4)
   END ELSE
     MASK = DEST.CLIENT.ID
   END
   CALL GEN.IN(20,13,MASK,'',Q,0,20,'','',2,-5,13,1,QI,HELP,0,23)
   IF QI='' THEN QI=DEST.CLIENT.ID
   QI = OCONV(QI,'MCU')
   BEGIN CASE
      CASE QI='EX'
         STOP
      CASE QI='X'
        GOSUB DSP.DATA
        GOTO 300
      CASE NUM(QI) AND QI # ''
           DEST.CLIENT.ID = QI'R%5'
      CASE NOT(QI MATCHES "0N") AND QI # ''
           CALL SOUNDEX.DISPLAY(QI,'BCUST','SDX.CLIENTS,NAME',2,ITEM.LIST)
           GOSUB DSP.SCREEN
           GOSUB DSP.DATA
           BEGIN CASE
              CASE ITEM.LIST = ''
                 GOTO 450                            ;* START OVER
              CASE DCOUNT(ITEM.LIST,@AM) GT 1
                 GOTO 450                            ;* START OVER
              CASE 1
                 DATA ITEM.LIST
                 GOTO 450                            ;* START OVER
           END CASE
      CASE 1
           CALL ELINE('Invalid Client Name, Try Again.')
           GOTO 450
   END CASE

   *
   * Open Client Records
   *
   READ DEST.CLIENT.REC FROM F.CLIENTS,DEST.CLIENT.ID ELSE
      CALL ELINE("Client number ":DEST.CLIENT.ID:" is not a valid client #")
      GOTO 450
   END

   DEST.CLIENT.NAME=DEST.CLIENT.REC<2>
   CRT @(20, 13):DEST.CLIENT.ID:', ':DEST.CLIENT.NAME:@(-4)
   CRT @(20, 14):DEST.CLIENT.REC<3,1>:' ':DEST.CLIENT.REC<3,2>:@(-4)
   CRT @(20, 15):DEST.CLIENT.REC<4>:', ':DEST.CLIENT.REC<5>:' ':DEST.CLIENT.REC<6>:@(-4)

   *
   * Open Client Audit File
   *
   OPEN '','FB.AUDIT.TABLES,':DEST.CLIENT.ID TO F.DEST.AUDIT.TABLES ELSE
      CALL ELINE('Error ':DEST.CLIENT.ID:' is not setup for any audit tables.')
      GOTO 450
   END

*NPR01 Start changes
   IF ORGIN.CARRIER.ID = 'ALL' OR ORGIN.TABLE.NUM = 'ALL' THEN
      GOSUB MULTIPLE.COPY
      GOTO 10
   END
*NPR01 End Changes

   *------------------------------------------
   * ASK FOR CARRIER
   *------------------------------------------
550:
   HELP="Enter carrier number, 'name' for search, [L]ist, [EX]it."
   IF DEST.CARRIER.ID='' THEN 
      DEST.CARRIER.ID=ORGIN.CARRIER.ID
     CRT @(20, 17):@(-4)
     CRT @(20, 18):@(-4)
   END
   MASK=DEST.CARRIER.ID

600:
   CALL GEN.IN(20,17,MASK,'',Q,0,20,'','',2,20,17,0,QI,HELP,0,23)
   IF QI='' THEN QI=MASK
   QI=OCONV(QI,'MCU')

   BEGIN CASE
     CASE QI='X'
        GOSUB DSP.DATA
        GOTO 450

     CASE QI='EX'
        STOP

     CASE QI EQ 'L'
        EXECUTE 'SORT FB.AUDIT,':DEST.CLIENT.ID:' BY CARRIER# CARRIER.NAME CARRIER.CITY CARRIER.STATE'
        CALL ELINE('')
        GOSUB DSP.SCREEN
        GOSUB DSP.DATA
        GOTO 600

     CASE NUM(QI)
        DEST.CARRIER.ID=QI'R%5'
        CRT @(20,17):DEST.CARRIER.ID

     CASE 1
       CALL SOUNDEX.DISPLAY(Q,'CARRIERS','SDX.CARRIER,NAME','1,3,4',QX)
       CT=DCOUNT(QX<1>,@VM)
       IF CT=1 THEN
          DATA QX<1,1>
       END
       GOSUB DSP.SCREEN
       GOSUB DSP.DATA
       GOTO 600

     CASE 1
       CALL ELINE('Invalid Entry.')
   END CASE

   *
   * Open Carrier Records
   *
   READ DEST.CARRIER.REC FROM F.CARRIERS,DEST.CARRIER.ID ELSE
       CALL ELINE('"':DEST.CARRIER.ID:'" is an invalid Carrier Number. Try Again.')
       GOTO 550
   END

   DEST.CARRIER.NAME  = DEST.CARRIER.REC<1>
   DEST.CARRIER.CITY  = DEST.CARRIER.REC<3>
   DEST.CARRIER.STATE = DEST.CARRIER.REC<4>
   GOSUB DSP.DATA

   *------------------------------------------
   * TABLE NUMBER
   *------------------------------------------
700:
   HELP = "Enter TABLE NUMBER to copy to. [SPACE]=Select [EX] = Exit"
   IF DEST.TABLE.NUM THEN
      MASK = DEST.TABLE.NUM:' '
   END ELSE
      MASK = '##'
     CRT @(20, 20):@(-4)
     CRT @(20, 21):@(-4)
   END

710:
   CALL GEN.IN(20,20,MASK,'',Q,0,20,'','',2,20,20,1,QI,HELP,0,23)
   IF QI='' AND DEST.TABLE.NUM THEN QI=DEST.TABLE.NUM
   IF QI=' ' THEN QI=''
   QI = TRIM(OCONV(QI,'MCU'))
   BEGIN CASE
      CASE QI = 'EX'
         STOP

      CASE QI='X'
         GOSUB DSP.DATA
         GOTO 550

      CASE QI=''
          VIN=DEST.CLIENT.ID
          VIN<2>=DEST.CARRIER.ID
          VOUT=''
          CALL AUDIT.TABLE.SELECT(VIN,VOUT)
          BEGIN CASE
             CASE VOUT<1> = 'EX'
                STOP
             CASE VOUT<1> = 'X'
                GOSUB DSP.SCREEN
                GOSUB DSP.DATA
                GOTO 700
             CASE 1
                GOSUB DSP.SCREEN
                GOSUB DSP.DATA
                DEST.TABLE.NUM = VOUT<1>
                IF DEST.TABLE.NUM='' THEN GOTO 700
          END CASE


      CASE (QI GE '0' AND QI LE '99') OR (QI GE 'A' AND QI LE 'Z')
         DEST.TABLE.NUM = QI

      CASE 1
         CALL ELINE('Invalid Table Number, Try Again.')
         GOTO 710
   END CASE

725:
   CRT @(20, 20):DEST.TABLE.NUM:@(-4)

   DEST.KEY = DEST.CARRIER.ID:'*':DEST.TABLE.NUM
   READ DEST.TABLE.REC FROM F.DEST.AUDIT.TABLES, DEST.KEY THEN
     CALL ELINE('Table ':DEST.TABLE.NUM:' is already on file. Pick an nonexisting table to copy to')
     GOTO 700
   END ELSE
     DEST.TABLE.REC=ORGIN.TABLE.REC
   END
   *
   * Enter a Description for the copy to table
   *
      DEST.TABLE.DESC=DEST.TABLE.REC<31>
750:
      HELP = "Enter a Table Description for the table to copy to. [X]=Back [EX]=Exit"
      CRT @(20, 21):@(-4)
      IF DEST.TABLE.DESC THEN
         MASK = DEST.TABLE.DESC
         REQ = 0
      END ELSE
         MASK=STR('#',59)
         REQ = 1
      END
      CALL GEN.IN(20,21,MASK,'',Q,REQ,59,'','',2,20,21,1,QI,HELP,0,23)
      QI = TRIM(OCONV(QI,'MCU'))
      IF QI='' THEN QI=DEST.TABLE.DESC
      BEGIN CASE
         CASE QI = 'EX'
            STOP
         CASE QI='X'
            GOTO 700
         CASE QI=''
            CALL ELINE('Entry is required')
            GOTO 750
      END CASE
      DEST.TABLE.DESC=QI
      CRT @(20,21):DEST.TABLE.DESC:@(-4)
      DEST.TABLE.REC<31>=DEST.TABLE.DESC
800:
     HELP='[U]pdate, [X]-back, or [EX]it'
     CALL GEN.IN(0,22,'Enter Option: --','',Q,0,4,'','',2,-2,22,1,QI,HELP,0,23)
     QI = OCONV(QI,'MCU')
     BEGIN CASE
        CASE QI = 'EX'
           STOP
        CASE QI = 'X'
           GOTO 750
        CASE QI = 'U' 
           WRITE DEST.TABLE.REC ON F.DEST.AUDIT.TABLES,DEST.KEY
        CASE 1
           CALL ELINE('Invalid Entry - Please Try Again')
           GOTO 800
     END CASE

     GOTO 10

     STOP

*----------------------------------------------------------------------
* Display Screen
*----------------------------------------------------------------------
DSP.SCREEN:***

    CALL AFS.SCR.REFRESH(PROG.NAME,PROG.DESC,PACK.NAME,CO.NAME,TIME.DATE,1)
    CRT @(0,2):STR('-',79);
    CRT @(0, 3):'Copy From: Client :':@(-4)
    CRT @(11, 7):'Carrier:':@(-4)
    CRT @(11,10):'Table  :':@(-4)
    CRT @(11,11):'Desc   :':@(-4)
    CRT @(0, 13):'Copy To: Client :':@(-4)
    CRT @(9, 17):'Carrier:':@(-4)
    CRT @(9,20):'Table  :':@(-4)
    CRT @(9,21):'Desc   :':@(-4)

RETURN


*----------------------------------------------------------------------
* Display Screen Data
*----------------------------------------------------------------------
DSP.DATA:***

    IF ORGIN.CLIENT.ID THEN
        CRT @(20, 3):ORGIN.CLIENT.ID:', ':ORGIN.CLIENT.NAME:@(-4)
        CRT @(20, 4):ORGIN.CLIENT.REC<3,1>:' ':ORGIN.CLIENT.REC<3,2>:@(-4)
        CRT @(20, 5):ORGIN.CLIENT.REC<4>:', ':ORGIN.CLIENT.REC<5>:' ':ORGIN.CLIENT.REC<6>:@(-4)
    END
    IF ORGIN.CARRIER.ID THEN
        CRT @(20, 7):ORGIN.CARRIER.ID:', ':ORGIN.CARRIER.NAME:@(-4)
        CRT @(20, 8):ORGIN.CARRIER.CITY:', ':ORGIN.CARRIER.STATE:@(-4)
    END ELSE
        CRT @(20, 7):@(-4)
        CRT @(20, 8):@(-4)
    END
    IF ORGIN.TABLE.NUM THEN
        CRT @(20, 10):ORGIN.TABLE.NUM:@(-4)
        CRT @(20,11):ORGIN.TABLE.REC<31>[1,59]:@(-4)
    END ELSE
        CRT @(20, 10):@(-4)
        CRT @(20, 11):@(-4)
    END
    IF DEST.CLIENT.ID THEN
        CRT @(20, 13):DEST.CLIENT.ID:', ':DEST.CLIENT.NAME:@(-4)
        CRT @(20, 14):DEST.CLIENT.REC<3,1>:' ':DEST.CLIENT.REC<3,2>:@(-4)
        CRT @(20, 15):DEST.CLIENT.REC<4>:', ':DEST.CLIENT.REC<5>:' ':DEST.CLIENT.REC<6>:@(-4)
    END
    IF DEST.CARRIER.ID THEN
        CRT @(20, 17):DEST.CARRIER.ID:', ':DEST.CARRIER.NAME:@(-4)
        CRT @(20, 18):DEST.CARRIER.CITY:', ':DEST.CARRIER.STATE:@(-4)
    END ELSE
        CRT @(20, 17):@(-4)
        CRT @(20, 18):@(-4)
    END
    IF DEST.TABLE.NUM THEN
        CRT @(20, 20):DEST.TABLE.NUM:@(-4)
    END ELSE
        CRT @(20, 20):@(-4)
    END


RETURN

*NPR01 Start changes
*
MULTIPLE.COPY:* Copy more than one table   - NPR01

VIN = '' ; VOUT = ''

VIN<1>  = ORGIN.CLIENT.ID
VIN<2>  = ORGIN.CLIENT.NAME

VIN<3>  = DEST.CLIENT.ID
VIN<4>  = DEST.CLIENT.NAME

VIN<5>  = ORGIN.CARRIER.ID
VIN<6>  = ORGIN.CARRIER.NAME

VIN<7>  = DEST.CARRIER.ID
VIN<8>  = DEST.CARRIER.NAME

VIN<9>  = ORGIN.TABLE.NUM
VIN<10> = ORGIN.TABLE.DESC

VIN<11> = DEST.TABLE.NUM
VIN<12> = DEST.TABLE.DESC

MODE = "EDIT"
CALL FB.LTL.MULTIPLE.COPY(VIN,VOUT,MODE)

TABLE.ARRAY = VOUT

900 * Update prompt for multiple table copy

HELP='[U]pdate, [RETURN], or [EX]it'
CRT @(0,22):@(-4)
CALL GEN.IN(0,22,'Enter Option: --','',Q,0,4,'','',2,14,22,1,QI,HELP,0,23)
QI = OCONV(QI,'MCU')
BEGIN CASE
   CASE QI = 'EX'
      STOP
   CASE QI = ''
      RETURN
   CASE QI = 'U'
      N.TAB = DCOUNT(TABLE.ARRAY,@AM)
      FOR TB = 1 TO N.TAB
         PROCESS.FLAG = TABLE.ARRAY<TB,7>
         IF PROCESS.FLAG = "Y" THEN
            ORGIN.CARRIER = TABLE.ARRAY<TB,1> 
            ORGIN.TABLE   = TABLE.ARRAY<TB,2> 
            ORGIN.ID      = ORGIN.CARRIER:"*":ORGIN.TABLE
            READ ORGIN.REC FROM F.ORGIN.AUDIT.TABLES, ORGIN.ID THEN
               DEST.CARRIER = TABLE.ARRAY<TB,4> 
               DEST.TABLE   = TABLE.ARRAY<TB,5> 
               DEST.ID      = DEST.CARRIER:"*":DEST.TABLE
               WRITE ORGIN.REC ON F.DEST.AUDIT.TABLES,DEST.ID
            END
         END
      NEXT TB
   CASE 1
       CALL ELINE('Invalid Entry - Please Try Again')
       GOTO 900
END CASE

RETURN 

*NPR01 End changes
