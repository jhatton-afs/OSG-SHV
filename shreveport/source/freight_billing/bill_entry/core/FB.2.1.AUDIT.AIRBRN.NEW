      SUBROUTINE FB.2.1.AUDIT.AIRBRN.NEW(VIN,VOUT,DAUDIT,DFSC,F.RATE.TABLES,F.PCIN,F.PCOUT)
*
 UPL.VIN = 'FBBP' ;  UPL.VIN<2> = 'FB.2.1.AUDIT.AIRBRN.NEW' ; UPL.VOUT = ''                    ;* NPR_UPL 04/23/2010
 CALL UPD.PROGRAM.LOG(UPL.VIN,UPL.VOUT)                                                           ;* NPR_UPL 04/23/2010
* SUBROUTINE TO ENTER ZIP CODES
*
* FREIGHT BILLING PACKAGE
*
* AUTHOR:       OWEN HOLMAN
* DATE:        DECEMBER 8, 1995
* DESCRIPTION: THIS PROGRAM ALLOW THE ENTRY OF SPECIFIC INFORMATION
*              NEEDED TO AUDIT FREIGHT BILLS DURING DATA ENTRY
*
* THIS PROGRAM IS A PROPRIETARY PRODUCT OF AUTOMATED FREIGHT SYSTEMS
* AND MAY NOT BE USED, COPIED OR MODIFIED WITHOUT THE EXPRESS
* WRITTEN CONSENT OF SAME.
*
*********************************************************************
*VIN<1> = OPT ; *'I'nquire or 'V'erify
*
$INCLUDE PROG.ADMIN INC_OPTIONS
*
* Initialize variables
*
      PROMPT ''
      X.EX.FLAG=''
      OPT = VIN<1>
      IF OPT = 'I' THEN
         INQUIRE = 1
         MODE = 'EDIT'
      END ELSE
         INQUIRE = 0
         MODE = 'INPUT'
      END
      LN = 0
      CALL FB.CLEAR.WINDOW
      TRANS.MODE=''
      BILL.DATE=VIN<3>
      TRANS.MODE=VIN<4>
      PIECES=VIN<5>
      WEIGHT=VIN<6>
      MISC.CHARGE=VIN<7>
      CARRIER.CHARGE=VIN<8>
      LOGISTICS.CHARGE=VIN<9>
      CARRIER.ID=VIN<10>
      CLIENT=VIN<11>
      CC=VIN<25>
      AUTO.RUN=VIN<26>
      AUDIT='Y'
      LOGISTICS.AMT=0
      ERROR=''
      IF AUTO.RUN THEN GOTO 125
*
* Display Prompts
     LINE="On-Line Audit"
     CTR=33+(INT(47/2-LEN(LINE)/2))
     CRT @(CTR,6):LINE:
     CRT @(33,07):STR('-',47):
     CRT @(33,08):"Audit this Bill (Y/N):":
     CRT @(33,10):"Bill Date:":
     CRT @(33,11):"Mode of Shipment:":
     CRT @(33,12):"Pieces Shipped:":
     CRT @(33,13):"Total Weight:":
     CRT @(33,14):"Misc. Charge:":
     CRT @(33,15):"Carrier Charge:":
*
* Start input
*
*
* 
* Get Audit Y/N
*
100 ***
     HELP="Audit this bill? [Y]es, [N]o, [R]eject Bill"
     MASK='Y '
     CALL GEN.IN(56,8,MASK,'',Q,0,2,'L','',2,-2,8,1,QI,HELP,0,23)
     CC+=LEN(QI)+1
     QI = ICONV(QI,'MCU')
     IF QI='' THEN QI='Y'
     IF QI='1' THEN QI='Y'
     IF QI='0' THEN QI='N'
     IF QI='-' THEN QI='R'
     IF QI='X' THEN
       X.EX.FLAG='X'
       GO 9000
     END
     IF QI='EX' THEN
       X.EX.FLAG='EX'
       GO 9000
     END
     IF QI='N' THEN
       AUDIT='N'
       GO 9000
     END
     IF QI='R' THEN
       AUDIT='R'
       GO 9000
     END
     IF QI#'N' AND QI#'Y' THEN
       CALL ELINE('Must be <Y>es or <N>o')
       CC+=1
       GO 100
     END
     PRINT @(56,8):AUDIT:
175:*** Get Bill Date
     IF BILL.DATE='' THEN
       MASK='MM/DD/YY'
     END ELSE
       MASK=BILL.DATE'D2/'
     END
     HELP="Enter the Pro Date (Shipment Date)"
     CALL GEN.IN(44,10,MASK,'DATE',Q,0,8,'L','',2,44,10,1,QI,HELP,0,23)
     CC+=LEN(QI)+1
     QI = ICONV(QI,'MCU')
     IF QI='X' THEN GO 100
     IF QI='EX' THEN
       X.EX.FLAG='EX'
       GO 9000
     END
     IF QI='' THEN
       IF BILL.DATE#'' THEN Q=BILL.DATE ELSE
         CALL ELINE('Entry is Required') ; GO 175
         CC+=1
       END
     END
     IF Q > DATE() THEN
       CALL ELINE('Cannot Be in the Future')
       CC+=1
       GO 175
     END
     IF Q < DATE()-182 THEN
       CALL ELINE("Warning - Date is older than 6 months")
       CC+=1
     END
     BILL.DATE=Q
     PRINT @(44,10):BILL.DATE'D2/':@(-4):
200:*** Get Mode of Shipment
     IF DAUDIT<6>#'' THEN
       IF QI='X' THEN GO 175
       TRANS.MODE=DAUDIT<6> ; * Override Mode
       PRINT @(51,11):TRANS.MODE:
       GO 300
     END ELSE
       VALID.TRANS.MODES=DAUDIT<7>
     END
     MODES=''
     FOR X = 1 TO DCOUNT(VALID.TRANS.MODES<1>,@VM)
       MODES=MODES:X:'-':VALID.TRANS.MODES<1,X>:' '
     NEXT X
     HELP="Enter Mode or Mode# ":MODES
     HELP=HELP[1,79]
     MASK=TRANS.MODE
     CALL GEN.IN(51,11,MASK,'',Q,0,5,'L','',2,51,11,1,QI,HELP,0,23)
     CC+=LEN(QI)+1
     QI = ICONV(QI,'MCU')
     IF QI='X' THEN GO 175
     IF QI='EX' THEN
       X.EX.FLAG='EX'
       GO 9000
     END
     IF QI='' THEN QI=MASK
     IF NUM(QI) THEN
       IF INT(QI) = QI AND QI > 0 THEN
         QI=VALID.TRANS.MODES<1,QI>
       END
     END
     LOCATE QI IN VALID.TRANS.MODES<1> SETTING POS ELSE
       CALL ELINE('Not a Valid Mode for This Carrier')
       CC+=1
       GO 200
     END
     TRANS.MODE=QI
     PRINT @(51,11):TRANS.MODE:@(-4):
125:*** See if the Bill Rates Are On File - If Not Cannot Audit
     * Get Effective Date
     FIRST.DATE=DAUDIT<2,1>
     IF FIRST.DATE='' THEN
       ERROR='Cannot Audit!  Contract Carrier has no dates setup'
       AUDIT='U'
       IF AUTO.RUN THEN
         GOTO 9000
       END
       CALL ELINE(ERROR)
       CC+=1
       GOSUB 8000
       GO 175
     END
     LAST.DATE=DAUDIT<3,DCOUNT(DAUDIT<2>,@VM)>
     IF BILL.DATE < FIRST.DATE THEN; * Is bill Before Beginning Date
       ERROR='Cannot Audit! Bill date is before 1st effective date.'
       AUDIT='U'
       IF AUTO.RUN THEN
         GOTO 9000
       END
       CALL ELINE(ERROR)
       CC+=1
       GOSUB 8000
       GO 175
     END
     IF LAST.DATE#'' AND (BILL.DATE > LAST.DATE) THEN
       ERROR='Cannot Audit Bill!  Bill Date is after Last Effective Date.'
       AUDIT='U'
       IF AUTO.RUN THEN
         GOTO 9000
       END
       CALL ELINE(ERROR)
       CC+=1
       GOSUB 8000
       GO 175
     END
     LOCATE BILL.DATE IN DAUDIT<2> BY 'AR' SETTING POS ELSE
       POS=POS-1
       IF POS < 1 THEN
         ERROR='Serious error Contract Carrier POS is less than 0, Call Owen'
         IF AUTO.RUN THEN
           AUDIT='U'
           GOTO 9000
         END
         CALL ELINE(ERROR)
         CC+=1
         GO 125
       END
     END
     EFF.DATE=DAUDIT<2,POS>
     END.DATE=DAUDIT<3,POS>
     IF END.DATE#'' AND (BILL.DATE > END.DATE) THEN
       ERROR='Cannot Audit Bill!  Bill Date is after Effective Date.'
       AUDIT='U'
       IF AUTO.RUN THEN
         GOTO 9000
       END
       CALL ELINE(ERROR)
       CC+=1
       GOSUB 8000
       GO 175
     END
     ZONE='AIR'
     RATE.TABLES.ID=CLIENT:"*":CARRIER.ID:"*":TRANS.MODE:"*":EFF.DATE:"*":ZONE:"*1"
     READ DRATE.TABLE FROM F.RATE.TABLES,RATE.TABLES.ID ELSE
       ERROR="Cannot Audit! Table Not On File ":RATE.TABLES.ID
       AUDIT='U'
       IF AUTO.RUN THEN
         GOTO 9000
       END
       CC+=1
       GOSUB 8000
       GO 200
     END
     TRIED.BOTH.WEIGHTS=0
     IF AUTO.RUN THEN GOTO 1000
300:*** Get Number of Pieces Shipped
*    IF CARRIER.ID='00015' THEN
*      IF QI='X' THEN GO 200
*      PIECES=1
*      PRINT @(49,12):PIECES:
*      TRIED.BOTH.WEIGHTS=0
*      GO 400
*    END
     HELP="Enter the number of pieces shipped"
     MASK=PIECES'L#5'
     CALL GEN.IN(49,12,MASK,'',Q,0,5,'L','',2,49,12,1,QI,HELP,0,23)
     CC+=LEN(QI)+1
     QI = ICONV(QI,'MCU')
     IF QI='' THEN QI=MASK
     IF QI='X' THEN GO 200
     IF QI='EX' THEN
       X.EX.FLAG='EX'
       GO 9000
     END
     IF NOT(NUM(QI)) THEN
       CALL ELINE('Must be a whole number greater than 0')
       CC+=1
       GO 300
     END
     IF QI < 1 OR INT(QI)#QI THEN
       CALL ELINE('Must be a whole number greater than 0')
       CC+=1
       GO 300
     END
     PIECES=QI
     PRINT @(49,12):PIECES:
400:*** Get Total Weight of shipment
     HELP="Enter the total weight of the shipment <0>-LX"
     IF WEIGHT#'' THEN
       MASK=WEIGHT'L#5'
     END ELSE
       MASK=''
     END
     CALL GEN.IN(47,13,MASK,'',Q,0,5,'L','',2,47,13,1,QI,HELP,0,23)
     CC+=LEN(QI)+1
     QI = ICONV(QI,'MCU')
     IF QI='' THEN QI=WEIGHT
     IF QI=0 THEN QI='LX'
     IF QI='X' THEN GO 300
     IF QI='EX' THEN
       X.EX.FLAG='EX'
       GO 9000
     END
     BEGIN CASE
       CASE QI='LX' ; NULL
       CASE NOT(NUM(QI))
         CALL ELINE('Must be a whole number greater than 0')
         CC+=1
         GO 400
       CASE QI < 1 OR INT(QI)#QI
         CALL ELINE('Must be a whole number greater than 0')
         CC+=1
         GO 400
       CASE QI < PIECES
         CALL ELINE('Must be equal or greater than the number of pieces')
         CC+=1
           GO 400
     END CASE
     WEIGHT=QI
     PRINT @(47,13):WEIGHT:
500:*** Get Misc Charge Amount
     HELP="Enter Misc Charge if any"
     IF MISC.CHARGE='' THEN MISC.CHARGE=0
     MASK=MISC.CHARGE'R26'
     CALL GEN.IN(47,14,MASK,'AMT2',Q,0,10,'L','',2,47,14,1,QI,HELP,0,23)
     CC+=LEN(QI)+1
     QI = ICONV(QI,'MCU')
     IF QI='X' THEN GO 400
     IF QI='EX' THEN
       X.EX.FLAG='EX'
       GO 9000
     END
     IF QI='' THEN
       QI=MISC.CHARGE
       Q=MISC.CHARGE
     END
     IF NOT(NUM(QI)) THEN
       CALL ELINE('Must be a number 0 or greater')
       CC+=1
       GO 500
     END
     IF QI < 0 THEN
       CALL ELINE('Must be a number 0 or greater')
       CC+=1
       GO 500
     END
     MISC.CHARGE=Q
     PRINT @(47,14):MISC.CHARGE'R26':
600:*** Get Carrier Charge Amount
     HELP="Enter Carrier Charge (Bottom Line on Freight Bill)"
     MASK=CARRIER.CHARGE
     IF MASK#'' THEN MASK=MASK'R26'
     CALL GEN.IN(49,15,MASK,'AMT2',Q,0,10,'L','',2,49,15,1,QI,HELP,0,23)
     CC+=LEN(QI)+1
     QI = ICONV(QI,'MCU')
     IF QI='X' THEN GO 500
     IF QI='EX' THEN
       X.EX.FLAG='EX'
       GO 9000
     END
     IF QI='' THEN
       QI=CARRIER.CHARGE
       Q=CARRIER.CHARGE
     END
     IF NOT(NUM(QI)) THEN
       CALL ELINE('Must be a number 0 or greater')
       CC+=1
       GO 600
     END
     IF QI < 0 THEN
       CALL ELINE('Must be a number 0 or greater')
       CC+=1
       GO 600
     END
     CARRIER.CHARGE=Q
     PRINT @(49,15):CARRIER.CHARGE'R26':
1000:*** Calculate Weight Array
     IF WEIGHT='LX' THEN
       IF PIECES#1 THEN
         ERROR='Cannot Audit! More than 1 Piece with a weight of LX'
         AUDIT='U'
         IF AUTO.RUN THEN
           GOTO 9000
         END
         CALL ELINE(ERROR)
         CC+=1
         GOSUB 8000
         GO 400
       END
       WEIGHT.ARRAY='LX'
       GO 1100
     END
     WEIGHT.ARRAY=''
     AVG.WEIGHT = WEIGHT/PIECES
     LEFTOVER=0
     FOR X = 1 TO PIECES
       CALC.WEIGHT=AVG.WEIGHT+LEFTOVER
       PIECE.WEIGHT=CALC.WEIGHT'R0'
       LEFTOVER=CALC.WEIGHT-PIECE.WEIGHT
       WEIGHT.ARRAY<X>=PIECE.WEIGHT
     NEXT X
     IF LEFTOVER#0 THEN
       ERROR='Serious Error! LEFTOVER#0 Call Owen'
       IF AUTO.RUN THEN
         AUDIT='U'
         GOTO 9000
       END
       CALL ELINE(ERROR)
       CC+=1
       GO 1000
     END
1100:*** Lookup Carrier Charges in Table
*CRT @(0,23):"Press Break..... Looking up Logistics Amount ":;INPUT NUL
     CHARGE.ARRAY=''
     TOTAL.CHARGES=0
     FOR X = 1 TO PIECES
       LOCATE WEIGHT.ARRAY<X> IN DRATE.TABLE<1> SETTING POS ELSE
         ERROR='Cannot Audit! Cannot Find Weight of ':WEIGHT.ARRAY<X>:' in Rate Table'
         AUDIT='U'
         IF AUTO.RUN THEN
           GOTO 9000
         END
         CALL ELINE(ERROR)
         CC+=1
         GOSUB 8000
         GO 400
       END
       ITEM.CHARGE=DRATE.TABLE<2,POS>
       CHARGE.ARRAY<X>=ITEM.CHARGE
       TOTAL.CHARGES+=ITEM.CHARGE
     NEXT X
*******************************************************************
     FSC.PERC=0
     IF DFSC='' THEN GO 1250
     FIRST.DATE=DFSC<2,1>
     IF BILL.DATE < FIRST.DATE THEN GO 1250
     LAST.DATE=DFSC<3,DCOUNT(DFSC<3>,@VM)>
     IF LAST.DATE#'' AND (BILL.DATE > LAST.DATE) THEN GO 1250
     LOCATE BILL.DATE IN DFSC<2> BY 'AR' SETTING FS.POS ELSE
       FS.POS=FS.POS-1
       IF FS.POS < 1 THEN
         ERROR='Serious error FSC Date Date POS is less than 1, Call Owen'
         IF AUTO.RUN THEN
           AUDIT='U'
           GOTO 9000
         END
         CALL ELINE(ERROR)
         CC+=1
         GO 1100
       END
     END
     FSC.BEG.DATE=DFSC<2,FS.POS>
     FSC.END.DATE=DFSC<3,FS.POS>
     IF FSC.END.DATE#'' AND (BILL.DATE > FSC.END.DATE) THEN GO 1250
     FSC.PERC=DFSC<4,FS.POS>
     FSC.MIN=DFSC<6,FS.POS>
1250:*** Have FSC.PERC Calculate Charge
     CONTR.FSC.AMT=OCONV(TOTAL.CHARGES*FSC.PERC/10000,'MR0')
     IF CONTR.FSC.AMT LT FSC.MIN THEN
        CONTR.FSC.AMT = FSC.MIN
     END
*******************************************************************
1200:*** Compare Calculated Charges to Entered Charges to see if OK
   IF TOTAL.CHARGES+MISC.CHARGE+CONTR.FSC.AMT#CARRIER.CHARGE THEN
     IF WEIGHT=1 OR WEIGHT='LX' AND TRIED.BOTH.WEIGHTS=0 THEN
       IF WEIGHT=1 THEN WEIGHT='LX' ELSE WEIGHT=1
       IF NOT(AUTO.RUN) THEN PRINT @(47,13):WEIGHT'L#3'
       TRIED.BOTH.WEIGHTS=1
       GO 1000 ; * Try the other weight
     END
     ERROR='Cannot Audit! Carrier Charge does not calculate using rate tables.'
     AUDIT='U'
     IF AUTO.RUN THEN
       GOTO 9000
     END
     CALL ELINE(ERROR)
     CC+=1
     GOSUB 8000
     GO 600
   END
1300:*** Check Bill Date against Benchmark Date Range to see if OK
     * Get Effective Date
     FIRST.DATE=DAUDIT<22,1>
     IF FIRST.DATE='' THEN
       LOGISTICS.AMT=CARRIER.CHARGE
       GO 9000
     END
     LAST.DATE=DAUDIT<23,DCOUNT(DAUDIT<22>,@VM)>
     IF BILL.DATE < FIRST.DATE THEN; * Is bill Before Beginning Date
       LOGISTICS.AMT=CARRIER.CHARGE
       GO 9000
     END
     IF LAST.DATE#'' AND (BILL.DATE > LAST.DATE) THEN
       LOGISTICS.AMT=CARRIER.CHARGE
       GO 9000
     END
     LOCATE BILL.DATE IN DAUDIT<22> BY 'AR' SETTING POS ELSE
       POS=POS-1
       IF POS < 1 THEN
         ERROR='Serious error Logistics POS is less than 0, Call Owen'
         IF AUTO.RUN THEN
           AUDIT='U'
           GOTO 9000
         END
         CALL ELINE(ERROR)
         CC+=1
         GO 1300
       END
     END
     EFF.DATE=DAUDIT<22,POS>
     ZONE='AIR'
     LOG.CARRIER.ID=DAUDIT<21>
     LOG.RATE.TABLES.ID=CLIENT:"*":LOG.CARRIER.ID:"*":TRANS.MODE:"*":EFF.DATE:"*":ZONE:"*1"
     READ DLOG.RATE.TABLE FROM F.RATE.TABLES,LOG.RATE.TABLES.ID ELSE
       ERROR="Cannot Calculate Logistics! Table Not On file ":RATE.TABLES.ID
       AUDIT='U'
       IF AUTO.RUN THEN
         GOTO 9000
       END
       CALL ELINE(ERROR)
       CC+=1
       GO 9000
     END
1400:*** Lookup Logistics Charges in Logistics Table
     LOG.CHARGE.ARRAY=''
     TOTAL.LOG.CHARGES=0
     FOR X = 1 TO PIECES
       LOCATE WEIGHT.ARRAY<X> IN DLOG.RATE.TABLE<1> SETTING POS ELSE
         ERROR='Cannot Calculate Logistics! Cannot Find Logistics Weight of ':WEIGHT.ARRAY<X>
         IF AUTO.RUN THEN
           AUDIT='U'
           LOGISTICS.AMT=CARRIER.CHARGE
           GOTO 9000
         END
         CALL ELINE(ERROR)
         CC+=1
         LOGISTICS.AMT=CARRIER.CHARGE
         GO 9000
       END
       LOG.ITEM.CHARGE=DLOG.RATE.TABLE<2,POS>
       LOG.CHARGE.ARRAY<X>=LOG.ITEM.CHARGE
       TOTAL.LOG.CHARGES+=LOG.ITEM.CHARGE
     NEXT X
     BENCH.FSC.AMT=OCONV(TOTAL.LOG.CHARGES*FSC.PERC/10000,'MR0')
     IF BENCH.FSC.AMT LT FSC.MIN THEN
       BENCH.FSC.AMT = FSC.MIN
     END
     LOGISTICS.AMT=TOTAL.LOG.CHARGES+MISC.CHARGE+BENCH.FSC.AMT
     IF CARRIER.CHARGE>LOGISTICS.AMT THEN LOGISTICS.AMT=CARRIER.CHARGE
*OH*PRINT @(0,15):WEIGHT.ARRAY
*OH*FOR X = 1 TO PIECES
*OH*  PRINT WEIGHT.ARRAY<X>,CHARGE.ARRAY<X>'R26',LOG.CHARGE.ARRAY<X>'R26'
*OH*NEXT X
*OH*PRINT '',TOTAL.CHARGES'R26',TOTAL.LOG.CHARGES'R26'
*OH*PRINT
*OH*PRINT 'CARRIER.ID CHARGE ':LOGISTICS.AMT'R26'
*OH*PRINT 'ACTUAL CHARGE  ':CARRIER.CHARGE'R26'
*OH*INPUT NUL
*OH*EXECUTE 'RUN BP OH'
*OH*STOP
     GO 9000
8000:*** Exit program or Retry
      HELP = ''
      CALL GEN.IN(0,22,'[Enter] to Skip Bill or press any key to Correct Data:   ','',Q,0,2,'','',2,-2,22,1,QI,HELP,0,23)
     CC+=LEN(QI)+1
      QI = OCONV(QI,'MCU')
      IF QI='' THEN RETURN TO 9000
      AUDIT='Y' ; ** Correct Audit Flag
      RETURN
9000:*** Return to calling Program
      IF NOT(AUTO.RUN) THEN CALL FB.CLEAR.WINDOW
      VOUT=''
      VOUT<1>=X.EX.FLAG
      VOUT<2>=AUDIT
      VOUT<3>=BILL.DATE
      VOUT<4>=TRANS.MODE
      VOUT<5>=PIECES
      VOUT<6>=WEIGHT
      VOUT<7>=MISC.CHARGE
      VOUT<8>=CARRIER.CHARGE
      VOUT<9>=LOGISTICS.AMT
      VOUT<25>=CC
      VOUT<26>=ERROR
      RETURN
   END
