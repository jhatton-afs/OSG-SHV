***********************************************************************
* Program name : UPD.SQL.CLIENT.PX
* Author       : Owen Holman
* Date written : January 2002
* Library      : FBBP
* COPYRIGHT (C) 2016 - AFS LOGISTICS LLC - ALL RIGHTS RESERVED.
*
* VEND/CUST SQL UPDATE
*
* Last updated by afs (AFSLOGISTICS\gbino) at 08:45:33 on 12/19/2018.
*
* Start modification history

***********************************************************************

$INCLUDE UNIVERSE.INCLUDE ODBC.H

      form = "T##########"
      dash = "----------"
      Expect = ""
      NULL=""
      EOL=CHAR(13):CHAR(10)
      TOT.DIV=0
      TOT.CV=0

      CALL GET.USER.NEW(USER.NAME,CH.NUM)
      USER.NAME = OCONV(USER.NAME,'MCU')
      UPL.VIN = 'FBBP' ; UPL.VIN<2> = 'UPD.SQL.CLIENT.PX' ; UPL.VOUT = ''
      UPL.VIN<3> = USER.NAME
      UPL.VIN<4> = CH.NUM
      CALL UPD.PROGRAM.LOG(UPL.VIN,UPL.VOUT)
      BEGIN CASE
         CASE USER.NAME='DAVID'
         CASE USER.NAME='GBINO'
         CASE USER.NAME = 'SV.UVAUTO'
         CASE 1
            CRT "YOU ARE NOT AUTHORIZED TO USE THIS PROGRAM"
            STOP
      END CASE

      GET (ARG.,1) LIST.NAME THEN
         LIST.STMT='GET-LIST ':LIST.NAME
      END ELSE
****         LIST.STMT='SSELECT CLIENTS WITH STATUS = "A" AND @ID # "9]"'
         LIST.STMT='SSELECT CLIENTS WITH @ID # "9]"'
      END
      GET (ARG.,2) OPTIONS ELSE OPTIONS='100'
      CLIENT.OPT=OPTIONS[1,1]
      DIV.OPT=OPTIONS[2,1]
      VC.OPT=OPTIONS[3,1]
      EXECUTE LIST.STMT RTNLIST CLIENT.LIST CAPTURING RESULTS
      NUM.CLIENTS=@SELECTED
      OPEN '','CLIENTS' TO F.CLIENT ELSE ABORT 201,'CLIENTS'
      CRT @(0,0):@(-1):@(0,3):TIMEDATE():' SQL update Processing Clients...'

      CLIENT.EOF=0
      CLIENT.CNT=0
      LOOP
         READNEXT CLIENT.ID FROM CLIENT.LIST ELSE CLIENT.EOF=1
      UNTIL CLIENT.EOF DO
         CLIENT.CNT+=1
         CLIENT.PCT=ICONV(CLIENT.CNT/NUM.CLIENTS*100,'MR2')

         READ CLIENT.REC FROM F.CLIENT,CLIENT.ID THEN
            CRT @(0,5):CLIENT.CNT'R#4':' - ':CLIENT.PCT'R26#6':'% ':' ':CLIENT.ID:' - ':CLIENT.REC<2>:
            IF CLIENT.OPT THEN
* DEFINE FIELDS TO UPDATE FROM CLIENT.REC HERE

               SQLVIN=''                 ; * JMK35
               SQLVIN<1>=USER.NAME            ; * JMK35
               SQLVIN<2>=CLIENT.ID       ; * JMK35
               SQLVOUT=''                ; * JMK35
               CALL UPD.SQL.CLIENT.SUB(SQLVIN,SQLVOUT,CLIENT.REC)      ; * JMK35
            END
*
            IF DIV.OPT THEN
               OPEN '','DIVISION.CODES,':CLIENT.ID TO F.DIV THEN
                  STMT='SSELECT DIVISION.CODES,':CLIENT.ID
                  EXECUTE STMT RTNLIST DIV.LIST CAPTURING RESULTS
                  NUM.DIVS=@SELECTED
                  TOT.DIV+=NUM.DIVS
                  CRT @(0,5):EOL:CLIENT.CNT'R#10':' - ':CLIENT.PCT'R26#6':'% ':' ':CLIENT.ID:' - ':CLIENT.REC<2>:' #Div - ':NUM.DIVS:' Tot ':TOT.DIV:
                  DIV.EOF=0
                  DIV.CNT=0
                  LOOP
                     READNEXT DIV.ID FROM DIV.LIST ELSE DIV.EOF=1
                  UNTIL DIV.EOF DO
                     DIV.CNT+=1
                     DIV.PCT=ICONV(DIV.CNT/NUM.DIVS*100,'MR2')

                     READ DIV.REC FROM F.DIV,DIV.ID THEN
* DEFINE FIELDS TO UPDATE FROM CLIENT.REC HERE
                        IF MOD(DIV.CNT,50)=0 THEN
                           CRT @(0,10):SPACE(5):'DIV ':DIV.CNT'R#10':' - ':DIV.PCT'R26#6':'% ':' DIV# ':DIV.ID:' - ':DIV.REC<11>
                        END
                        SQLVIN=''        ; * JMK06
                        SQLVIN<1>=USER.NAME   ; * JMK06
                        SQLVIN<2>=CLIENT.ID        ; * JMK06
                        SQLVIN<3>=DIV.ID           ; * JMK06
                        SQLVOUT=''       ; * JMK06
                        CALL UPD.SQL.DIV.SUB(SQLVIN,SQLVOUT,CLIENT.REC,DIV.REC)  ; * JMK06
                     END
                  REPEAT
               END
            END
*
            IF VC.OPT THEN
               OPEN '','VEND.CUST,':CLIENT.ID TO F.VC THEN
                  STMT='SSELECT VEND.CUST,':CLIENT.ID
                  EXECUTE STMT RTNLIST VC.LIST CAPTURING RESULTS
                  NUM.VCS=@SELECTED
                  TOT.CV+=NUM.VCS
                  CRT @(0,5):EOL:CLIENT.CNT'R#10':' - ':CLIENT.PCT'R26#6':'% ':' ':CLIENT.ID:' - ':CLIENT.REC<2>:' #VC - ':NUM.VCS:' Tot ':TOT.CV:
                  VC.EOF=0
                  VC.CNT=0
                  LOOP
                     READNEXT VC.ID FROM VC.LIST ELSE VC.EOF=1
                  UNTIL VC.EOF DO
                     VC.CNT+=1
                     VC.PCT=ICONV(VC.CNT/NUM.VCS*100,'MR2')

                     READ VC.REC FROM F.VC,VC.ID THEN
* DEFINE FIELDS TO UPDATE FROM CLIENT.REC HERE
                        IF MOD(VC.CNT,100)=0 THEN
                           CRT @(0,10):SPACE(5):'VC ':VC.CNT'R#10':' - ':VC.PCT'R26#6':'% ':' VC# ':VC.ID:' - ':VC.REC<1>
                        END

                        SQLVIN=''        ; * JMK06
                        SQLVIN<1>=USER.NAME   ; * JMK06
                        SQLVIN<2>=CLIENT.ID        ; * JMK06
                        SQLVIN<3>=VC.ID  ; * JMK06
                        SQLVOUT=''       ; * JMK06
                        CALL UPD.SQL.VC.SUB(SQLVIN,SQLVOUT,VC.REC)     ; * JMK06
                     END
                  REPEAT
               END
            END
            CRT CHAR(13)
         END
      REPEAT

********************************************************
THE.END:***
********************************************************
*     End of program

      MVIN=''
      MVIN<1>='nmeddaugh@afs.net'

      MVIN<4>='AFS'
      MVIN<5>='SQL Client Load for "':LIST.STMT:'" has finished!'
      MVIN<7>=1
      CALL SENDMAIL(MVIN,MVOUT)
      STOP
