***********************************************************************
* Program name :  TAB.FB.AUDIT.BILLS
* Author       :  Nicole Hennick
* Date written :  January 22, 2025
* Library      :  FBBP
$* COPYRIGHT (C) 2025 - AFS LOGISTICS LLC - ALL RIGHTS RESERVED.
*
* Freight Bills needing to be audited
*
* Last updated by afs (AFSLOGISTICS\nmeddaugh) at 09:23:01 on 12/16/2020.
*
* Start modification history
* 01/22/25 : NKH00 : ADDU-2839 New program
* End modification history
***********************************************************************
      * Initalize Variables
      PROG.NAME='TAB.FB.AUDIT.BILLS' 
      PROG.DESC='FB Bills to Audit'
      PACK.NAME=''
           
      CALL GET.USER.NEW(USER.NAME,CH.NUM)
      
      DELIM = CHAR(9)  
      
      * Open Files           
      OPEN 'VOC' TO VOC ELSE ABORT      
      
      OPEN '','CARRIERS' TO F.CARRIERS ELSE
         CALL OPEN.ABORT("CARRIERS",PROG.NAME)
      END
      OPEN '','CLIENTS' TO F.CLIENTS ELSE
         CALL OPEN.ABORT("CLIENTS",PROG.NAME)
      END      
      
      TDATE = OCONV(DATE(),'D2-') 
      FILE.ID = 'FB.AUDIT.BILLS_':TDATE:'_':TIME():'.XLS'
      DISP.NAME=FILE.ID
      OPENSEQ 'BILLDATA.':USER.NAME, FILE.ID TO OUTSEQ.FILE ELSE
         CREATE OUTSEQ.FILE ELSE
         END
      END
      WEOFSEQ OUTSEQ.FILE                 
      
      * Main Loop
      GOSUB CREATE.HEADING
      GOSUB CREATE.REPORT
      GO END.PROGRAM
      
*****************************************************
CREATE.HEADING:
***************************************************** 
      HDR =''
      HDR = 'FB Audit Bills ':TDATE
      WRITESEQ HDR TO OUTSEQ.FILE ELSE NULL
      HDR = 'Client':DELIM
      HDR:= 'Pro':DELIM
      HDR:= 'Carrier Name':DELIM
      HDR:= 'Carrier Charge':DELIM
      HDR:= 'Actual Charge':DELIM
      HDR:= 'Ver':DELIM
      HDR:= 'Status':DELIM
      HDR:= 'Entry Date':DELIM
      HDR:= '':DELIM
      WRITESEQ HDR TO OUTSEQ.FILE ELSE NULL

      RETURN
      
*****************************************************
CREATE.REPORT:
*****************************************************                       
      CALL AFS.SCR.HEAD(CO.ID,FILE.ID,PERIOD,PROG.NAME,PROG.DESC,PACK.NAME,CO.NAME,TIME.DATE,1)
      FILENAME='FB.BILLS'
     
      READ VOC.REC FROM VOC, FILENAME ELSE NULL
      HIGH.ATTR = 0 ; HIGH.CLIENT = '' ; HIGH.REC = ''
      CLIENT.COUNT = DCOUNT(VOC.REC<7>,@VM)
      FOR X = 1 TO CLIENT.COUNT
         CLIENT.ID = VOC.REC<7,X> 
         READV CLIENT.STATUS FROM F.CLIENTS, CLIENT.ID, 30 ELSE
            CLIENT.STATUS = 'I'
         END
         IF CLIENT.STATUS # 'I' THEN
            CRT @(10,10):'Processing Client: ':CLIENT.ID         
            ID = FILENAME:",":CLIENT.ID
            OPEN ID TO FILENAME.FILE THEN
               CMD  = 'SELECT ':ID:' WITH STATUS # "O" AND WITH STATUS # "R" '
               CMD := " BY STATUS BY CARRIER BY PRO"
               CMD := ' AND WITH VERSION = "0"'
               HUSH ON
               EXECUTE CMD
               HUSH OFF
               IF @SELECTED THEN
                  EOF = 0
                  LOOP
                     READNEXT REC.ID ELSE EOF = 1
                  UNTIL EOF
                     READ FILENAME.REC FROM FILENAME.FILE, REC.ID THEN
                        DTL=''
                        DTL = CLIENT.ID:DELIM
                        DTL:= FIELD(REC.ID,'*',2,1):DELIM
                        CARRIER.ID=FIELD(REC.ID,'*',1,1)
                        READ CARRIER.REC FROM F.CARRIERS, CARRIER.ID THEN
                           CARRIER.NAME = CARRIER.REC<1>
                        END ELSE
                           CARRIER.NAME = CARRIER.ID:' Name not found'
                        END
                        DTL:= CARRIER.NAME:DELIM          
                        DTL:= FILENAME.REC<7>:DELIM
                        DTL:= FILENAME.REC<8>:DELIM
                        DTL:= FILENAME.REC<39>:DELIM
                        DTL:= FILENAME.REC<68>:DELIM
                        DTL:= OCONV(FILENAME.REC<30>,'D4/'):DELIM                          
                        WRITESEQ DTL TO OUTSEQ.FILE ELSE NULL                                                                                                                   
                     END
                  REPEAT
               END
            END
         END
      NEXT X    

      RETURN

*****************************************************
END.PROGRAM:
***************************************************** 
      CLOSESEQ OUTSEQ.FILE
      CALL ELINE("File ":DISP.NAME:" may be found in your own U: drive directory")

      STOP
