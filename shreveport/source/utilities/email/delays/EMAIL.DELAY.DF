*======================================================================
*
*  PROGRAM   : EMAIL.DELAY.DF
*
*
*  PURPOSE   : Build a Delimited File for exporting EMAIL.DELAY
*              records to Excel.
*
*  PROGRAMMER:  Steve Barbee
*
*
*   DATE   | USER | COMMENT
* -------- | ---- | -------
* 12/13/11 |  SWB | Unit Created.
* 01/11/12 | SWB01| Updated authorized users and eliminated a
*                   non-existant option from the HELP for the prompt
* 04/07/14 | DRF01| SysAid 9293 Added KSHELTON to have access as Dina's
*                   back up.
* 07/01/15 | DRF02| SysAid 18393 Gave Robbie Jester access
*======================================================================
*    COPYRIGHT (C) 2011 - AFS LOGISTICS LLC - ALL RIGHTS RESERVED
*       ----------------------------------------------------
*  This program is sole property of AFS Logistics, LLC (AUTOMATED
*  FREIGHT SYSTEMS) and may not be used, copied, or modified without
*  the express written consent of Brian Barker of AFS Logistics, LLC.
*======================================================================

      CALL GET.USER(USER)

* Authorized Users
*

      BEGIN CASE
*Always give these folks access
         CASE USER='BRIAN' ; NULL
         CASE USER='BRIANP' ; NULL
         CASE USER='BROSE' ; NULL
         CASE USER='KAREN' ; NULL
         CASE USER='GBINO' ; NULL

*Additional authorized Buisness users
         CASE USER='DEBBIE' ; NULL
         CASE USER='DGARRETT' ; NULL
         CASE USER='KSHELTON' ; NULL    ; * DRF01
         CASE USER='RJESTER' ; NULL    ; * DRF02

*Now some of the I.T. folks
         CASE USER='MKERBOW' ; NULL
         CASE USER='PBASS' ; NULL
         CASE USER='SBARBEE' ; NULL

         CASE 1
            CALL ELINE('Access Denied! - See IT')
            STOP
      END CASE
*
      USER=OCONV(USER,'MCL')
*
      TODAY=DATE()
      TAB=CHAR(9)
      PROMPT''
      ERRMSG=''
      PROG.NAME='EMAIL.DELAY.DF'
      PROG.DESC="Build File of Delayed Emails"
      PACK.NAME="Freight Billing"
      CALL CHANNEL(CH.NUM)
      UPL.VIN = 'FBBP' ; UPL.VIN<2> = 'EMAIL.DELAY.DF' ; UPL.VOUT = ''
      CALL UPD.PROGRAM.LOG(UPL.VIN,UPL.VOUT)

      PROMPT.ANSWER = ''
      PROMPT.MSG=''
      EXTENSION='.XLS'
      LIN.MODE='HDR'
      DF.ARRAY=''

      OPEN 'EMAIL.DELAY' TO F.EMAIL.DELAY ELSE
         CALL OPEN.ABORT("EMAIL.DELAY",PROG.NAME)
      END

      OPEN '','VOC' TO F.VOC ELSE
         GOSUB CALL.NET.ADMIN
         CALL ELINE('UNABLE TO OPEN VOC FILE')
         STOP
      END

      READ BILLDATA.REC FROM F.VOC,"BILLDATA" ELSE
         GOSUB CALL.NET.ADMIN
         CALL ELINE('UNABLE TO OPEN "VOC BILLDATA"')
         STOP
      END

      BILLDATA.REC<2>:='\':USER
      WRITE BILLDATA.REC ON F.VOC,"BILLDATA.":USER ELSE
         GOSUB CALL.NET.ADMIN
         CALL ELINE("YOUR USER NAME '":USER:"' IS NOT IN THE BILLDATA FOLDER - PLEASE SEE JIM/OWEN")
         STOP
      END

      OPEN '','BILLDATA.':USER TO BILLDATA ELSE
         GOSUB CALL.NET.ADMIN
         CALL ELINE('UNABLE TO OPEN "BILLDATA.":USER')
         STOP
      END
      RECORD.NAME=''                     ; * Dest record name for output file...

      BILLDATA.REC = ''

      RECORD.NAME=''                     ; * Dest record name for output file...

      CALL AFS.SCR.HEAD(CO.ID, FILE.ID, '', PROG.NAME, PROG.DESC, PACK.NAME, CO.NAME, TIME.DATE, 1)
*---------------------------------------------
* Ask if the user wants an 'Error only' report
*---------------------------------------------
ASK.IF.ERROR.VERSION:
      PROMPT.MSG = 'Include [A]ll Delayed Emails or only ones with [E]rrors: '
      ACTIVE.INFO='ALL'
      HELP = CHAR(7):"Entry Options: [A]-All [E]rror [X]-Back [EX]-Exit"         ; * SWB01
      CRT @(0,5):PROMPT.MSG:@(-4)
      Q=''
      CALL GEN.IN(LEN(PROMPT.MSG)+1,5,'','',Q,0,3,'','',2,LEN(PROMPT.MSG)+1,5,0,QI,HELP,0,23)
      QI = OCONV(QI,'MCU')
      BEGIN CASE
         CASE QI = 'A'
            RESP=QI
            ACTIVE.INFO='ALL'
*Build the TITLE
            DF.ARRAY<1>=CHAR(9):CHAR(9):CHAR(9):CHAR(9):'Emails Pending Delivery'
            DF.ARRAY<2>=CHAR(9)
*Build the Column Headings
            GOSUB PROCESS.CUR.LINE
         CASE QI = 'E'
            RESP=QI
            ACTIVE.INFO='ERRORS'
*Build the TITLE
            DF.ARRAY<1>=CHAR(9):CHAR(9):CHAR(9):CHAR(9):'Emails Pending Delivery with Errors'
            DF.ARRAY<2>=CHAR(9)
*Build the Column Headings
            GOSUB PROCESS.CUR.LINE
         CASE QI = 'X'
            STOP
         CASE QI = 'EX'
            STOP
         CASE 1
            CALL ELINE('Invalid entry.')
            GOTO ASK.IF.ERROR.VERSION
      END CASE
      CRT @(LEN(PROMPT.MSG)+1,5):RESP

*---------------------------------------------
* Ask IS THIS CORRECT
*---------------------------------------------
ASK.CORRECT:
      YN.MASK = 'Y '

ASK.CORRECT.1:
      YN.MSG = 'Is the above correct? :'
      HELP = CHAR(7):"Entry Options: [Y]-Yes [N]-No [X]-Back"
      CRT @(0,18):YN.MSG:@(-4)
      MASK = YN.MASK
      Q=''
      CALL GEN.IN(LEN(YN.MSG)+1,18,MASK,'',Q,0,3,'','',2,LEN(YN.MSG)+1,18,0,QI,HELP,0,23)
      QI = OCONV(QI,'MCU')
      IF QI='' THEN QI=TRIM(MASK)
      BEGIN CASE
         CASE QI = 'Y'
            CRT @(LEN(YN.MSG)+1,18):'Yes'
         CASE QI = 'X' OR QI='N'
            GOTO ASK.IF.ERROR.VERSION
         CASE QI = 'EX'
            STOP
         CASE 1
            CALL ELINE('Invalid entry.')
            GOTO ASK.CORRECT.1
      END CASE

*---------------------------------------------
* Export File
*---------------------------------------------
      RECORD.NAME = 'DELAYED-EMAILS'
      RECORD.NAME:=EXTENSION
      GOSUB EXPORT.EMAIL.DELAY

      CRT @(0,18):"EMAIL.DELAY File Export completed.":@(-4):
      CRT @(0,19):@(-4):
      CRT @(0,20):"The Exported file name is '":RECORD.NAME:"'":@(-4):
      CRT @(0,21):@(-4):
      CRT @(0,22):@(-4):

      CALL ELINE('Done.')

      EMAIL.DELAY.ID='EX'

      STOP

*--------------------------------------------------------------
* EXPORT.EMAIL.DELAY - Export the EMAIL.DELAY file
*--------------------------------------------------------------
EXPORT.EMAIL.DELAY:

      STMT="SSELECT EMAIL.DELAY"
      IF ACTIVE.INFO # 'ALL' THEN
         STMT:= ' WITH 15 # "" '
      END
      EXECUTE STMT RTNLIST EMAIL.DELAY.LIST CAPTURING OUTPUT
      EMAIL.DELAY.ITEMS = @SELECTED
      IF @SELECTED < 1 THEN
         IF ACTIVE.INFO # 'ALL' THEN
            CALL ELINE('No Delayed Emails with errors on file.')
            STOP
         END ELSE
            CALL ELINE('ERROR - No Delayed Emails on file.')
            STOP
         END
      END

      EMAIL.DELAY.CNT=0
      PER.1=''
      LOOP
         READNEXT FULL.EMAIL.DELAY.ID FROM EMAIL.DELAY.LIST ELSE FULL.EMAIL.DELAY.ID='DONE'
      UNTIL FULL.EMAIL.DELAY.ID='DONE' DO
         EMAIL.DELAY.CNT+=1
         CALL PROG.BAR(20,EMAIL.DELAY.CNT,EMAIL.DELAY.ITEMS,PER.1)
         READ EMAIL.DELAY.REC FROM F.EMAIL.DELAY,FULL.EMAIL.DELAY.ID ELSE EMAIL.DELAY.REC='*** Not On File ***'
         CRT @(0,17):FULL.EMAIL.DELAY.ID:" ":EMAIL.DELAY.REC<1>:" - Now Exporting Information":@(-4):
         LIN.MODE='DET'
*For readability reasons in EXCEL, we are going to convert Value Marks
*to CHAR(10) which is the equivalent of Alt Enter in Excel and
*remove Double Quote marks from the text and then wrap the text in
*Double Quotes
         GOSUB MANIPULATE.TEXT
         GOSUB PROCESS.CUR.LINE
      REPEAT

      GOSUB WRITE.FILE

      RETURN
*
PROCESS.CUR.LINE:
*
      CUR.LINE=''
*
*Don't put the EMAIL.DELAY id on the report but instead show the 3
*components of the ID in their own columns
*--- Sub-System ---*
*
      IF LIN.MODE='HDR' THEN T='Sub-System'
      IF LIN.MODE='DET' THEN
         EMAIL.DELAY.ID=FIELD(FULL.EMAIL.DELAY.ID, '|', 1)
         ID.PT.2=FIELD(EMAIL.DELAY.ID, '_', 2)
         T=QUOTE(FIELD(EMAIL.DELAY.ID, '_', 1))
      END
      GOSUB APPEND.LINE
*
*--- Check # ---*
*
      IF LIN.MODE='HDR' THEN T='Check #'
      IF LIN.MODE='DET' THEN
         T=QUOTE(FIELD(ID.PT.2, '*', 1))
      END
      GOSUB APPEND.LINE
*
*--- Bank ---*
*
      IF LIN.MODE='HDR' THEN T='Bank #'
      IF LIN.MODE='DET' THEN
         ID.PT.2=FIELD(EMAIL.DELAY.ID, '_', 2)
         T=QUOTE(FIELD(ID.PT.2, '*', 2))
      END
      GOSUB APPEND.LINE
*
*--- Email Delay Counter ---*
*
      IF LIN.MODE='HDR' THEN T='ID Ctr'
      IF LIN.MODE='DET' THEN
         EMAIL.DELAY.ID.CTR=FIELD(FULL.EMAIL.DELAY.ID, '|', 2)
         T=EMAIL.DELAY.ID.CTR
      END
      GOSUB APPEND.LINE
*
*--- Email To Address ---*
*
      IF LIN.MODE='HDR' THEN T='Email To Addr'
      IF LIN.MODE='DET' THEN T=EMAIL.DELAY.REC<1>
      GOSUB APPEND.LINE
*
*--- Email CC Address ---*
*
      IF LIN.MODE='HDR' THEN T='Email CC Addr'
      IF LIN.MODE='DET' THEN T=EMAIL.DELAY.REC<2>
      GOSUB APPEND.LINE
*
*--- Email BCC Address ---*
*
      IF LIN.MODE='HDR' THEN T='Email BCC Addr'
      IF LIN.MODE='DET' THEN T=EMAIL.DELAY.REC<3>
      GOSUB APPEND.LINE
*
*--- Email From Address ---*
*
      IF LIN.MODE='HDR' THEN T='Email From Addr'
      IF LIN.MODE='DET' THEN T=EMAIL.DELAY.REC<4>
      GOSUB APPEND.LINE
*
*--- Email Subject ---*
*
      IF LIN.MODE='HDR' THEN T='Subject'
      IF LIN.MODE='DET' THEN T=EMAIL.DELAY.REC<5>
      GOSUB APPEND.LINE
*
*--- Email Message ---*
*
      IF LIN.MODE='HDR' THEN T='Message'
      IF LIN.MODE='DET' THEN T=EMAIL.DELAY.REC<6>
      GOSUB APPEND.LINE
*
*--- Auto Format Email ---*
*
      IF LIN.MODE='HDR' THEN T='AutoFmt'
      IF LIN.MODE='DET' THEN T=EMAIL.DELAY.REC<7>
      GOSUB APPEND.LINE
*
*
*--- Email Options ---*
*
      IF LIN.MODE='HDR' THEN T='Options'
      IF LIN.MODE='DET' THEN T=EMAIL.DELAY.REC<8>
      GOSUB APPEND.LINE
*
*--- Email Attachments ---*
*
      IF LIN.MODE='HDR' THEN T='Attachments'
      IF LIN.MODE='DET' THEN T=EMAIL.DELAY.REC<11>

      GOSUB APPEND.LINE
*
*--- Email Delay To Date ---*
*
      IF LIN.MODE='HDR' THEN T='Delay To Date'
      IF LIN.MODE='DET' THEN T=OCONV(EMAIL.DELAY.REC<12>,'D2/')
      GOSUB APPEND.LINE
*
*--- Email Delay To Time ---*
*
      IF LIN.MODE='HDR' THEN T='Delay To Time'
      IF LIN.MODE='DET' THEN T=OCONV(EMAIL.DELAY.REC<13>,'MTH')
      GOSUB APPEND.LINE
*
*--- EMAIL.DELAY Orig ID ---*
*
      IF LIN.MODE='HDR' THEN T='Delay To Time'
      IF LIN.MODE='DET' THEN T=EMAIL.DELAY.REC<14>
      GOSUB APPEND.LINE
*
*--- Errors causing Email to not be sent ---*
*
      IF LIN.MODE='HDR' THEN T='Errors needing attention'
      IF LIN.MODE='DET' THEN T=EMAIL.DELAY.REC<15>
      GOSUB APPEND.LINE
*
      DF.ARRAY<-1>=CUR.LINE

      RETURN

************
APPEND.LINE:
************

      T=CHANGE(T,@VM,' ')
      IF INDEX(T,',',1) THEN T='"':T:'"'
      IF TRIM(T)='' THEN T=' '
      CUR.LINE<1,-1>=T

      RETURN

*--------------------------------------------------------------
* WRITE.FILE
*--------------------------------------------------------------
WRITE.FILE:

      DF.ARRAY=CHANGE(DF.ARRAY,@VM,TAB)
      WRITE.TEST=1
      WRITE DF.ARRAY ON BILLDATA,RECORD.NAME ELSE
         IF WRITE.TEST < 5 THEN
            CALL ELINE("Cannot write the file.  Please see if this is open.")
            WRITE.TEST+=1
            GO WRITE.FILE
         END ELSE
            CALL ELINE("Cannot write the file.  Please try again later.")
         END
      END

      RETURN


*--------------------------------------------------------------
* CRITICAL ERROR HANDLER - GENERIC ROUTINE (TERMINATES PROGRAM)
*--------------------------------------------------------------
CALL.NET.ADMIN:***
      CALL ELINE("CALL NETWORK ADMINISTRATOR!")
      CALL ELINE("CALL NETWORK ADMINISTRATOR!!!!!")

      RETURN

*----------------------------------------------------------------------
* ASK.Y.OR.N
*
* A General Yes No prompt routine.
*
*----------------------------------------------------------------------
ASK.Y.OR.N:***

      PROMPT.ANSWER = ''

Y.OR.N:

      HELP = CHAR(7):"Entry Options: [Y]-Yes [N]-No [X]-Back"
      CRT @(0,22):YN.MSG:@(-4)
      MASK = YN.MASK'L#1'
      Q=''
      CALL GEN.IN(LEN(YN.MSG)+1,22,MASK,'',Q,0,1,'','',2,LEN(YN.MSG)+1,22,0,QI,HELP,0,23)
      QI = OCONV(QI,'MCU')

      IF QI='' THEN QI=MASK
      BEGIN CASE
         CASE QI = '1'
            QI='Y'
         CASE QI = '0'
            QI='N'
         CASE QI = 'Y'
         CASE QI = 'N'
         CASE QI = 'X'
         CASE 1
            CALL ELINE('Invalid entry.')
            GOTO Y.OR.N
      END CASE

      CRT @(0,22):@(-4):
      CRT @(0,23):@(-4):
      PROMPT.ANSWER = QI

      RETURN
*
MANIPULATE.TEXT:
      EDR.CNT=DCOUNT(EMAIL.DELAY.REC,@FM)
      FOR EDR=1 TO EDR.CNT
         EMAIL.LINE=EMAIL.DELAY.REC<EDR>
         IF INDEX(EMAIL.LINE,@VM,1) THEN
            EMAIL.LINE=CHANGE(EMAIL.LINE,'"','')
            EMAIL.LINE=DQUOTE(EMAIL.LINE)
            EMAIL.LINE=CHANGE(EMAIL.LINE,@VM,' ')
            EMAIL.DELAY.REC<EDR>=EMAIL.LINE
         END
      NEXT EDR

      RETURN
