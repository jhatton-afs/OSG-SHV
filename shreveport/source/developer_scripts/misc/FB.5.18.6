
*  AUTHOR: Owen Holman
*    DATE: April 05, 2002 - Allow user to Select FB.PAY Bills
*  REVISIONS:
* Instructions to create file
*  Get an Excel file save as U:\-USER-\00000-SELECT.txt
*    where 00000 is the client number
*    '-SELECT' is a literal
*    and .txt is the Formatted Text TAB Delmited option
********************************************************************
*
$INCLUDE PROG.ADMIN INC_OPTIONS
* INITIALIZE VARIABLES
*
    SEQ=0
    PROMPT''
    CALL GET.USER(USER)
*
********************************************************************
*
*  TABLE TO CHANGE FIELD NUMBERS (VALUES) OF DATA
*
********************************************************************
*
* ASK FOR CLIENT
*
      PROG.NAME='FB.7.4.4'
      PROG.DESC="Select FB.PAY Bills from SS Spec 1"
      PACK.NAME="Freight Billing"
      TODAY=DATE()
       UPL.VIN = 'FBBP' ;  UPL.VIN<2> = 'FB.5.18.6' ; UPL.VOUT = ''                    ;* NPR_UPL 04/23/2010
       CALL UPD.PROGRAM.LOG(UPL.VIN,UPL.VOUT)                                                           ;* NPR_UPL 04/23/2010

      CALL AFS.SCR.HEAD(CO.ID, FILE.ID, '', PROG.NAME, PROG.DESC, PACK.NAME, CO.NAME, TIME.DATE, 1)
      OPEN '','CLIENTS' TO F.CLIENTS ELSE
         CALL OPEN.ABORT("CLIENTS",PROG.NAME)
      END
      OPEN '','FB.OPEN.BILLS' TO F.OPEN.BILLS ELSE
         CALL OPEN.ABORT("FB.OPEN.BILLS",PROG.NAME)
      END
      OPEN '','FB.PAY' TO F.PAY ELSE
         CALL OPEN.ABORT("FB.PAY",PROG.NAME)
      END
      OPEN '','VOC' TO F.VOC ELSE
         CALL OPEN.ABORT("VOC",PROG.NAME)
      END
      OPEN '','TC.HOLIDAYS' TO F.TC.HOLIDAYS ELSE CALL OPEN.ABORT("TC.HOLIDAYS",PROG.NAME)
      OPEN '','FB.TABLES' TO F.TABLES ELSE CALL OPEN.ABORT("FB.TABLES",PROG.NAME)
      READ DDEFAULT FROM F.TABLES,'DEFAULT.PAYMENT.SCH' ELSE 
        CALL ELINE('Default payment schedule is not setup')
        STOP
      END
      CASH.RECEIPT.DATE=TODAY
      DEPOSIT.DATE=''


100   HELP='Enter the Client Number or [EX]it'
      CRT @(0,03):@(-3):
      CRT @(0,07):'Instructions for use BEFORE Continuing:'
      CRT @(0,08):'Step  1 Create a Spreadsheet of bills using Spec 1'
      CRT @(0,09):'Step  2 Open Excel THEN Open the Spreadsheet (its located in your U:\':USER:'\ Drive)'
      CRT @(0,10):'Step  3 On the Excel Text Import Wizzard CLICK [Next] then [Next] again'
      CRT @(0,11):'Step  4 Scroll to the right and click (Highlight) the Pro # Column'
      CRT @(0,12):"Step  5 CLICK the 'Text' Button at the top right of the screen"
      CRT @(0,13):'Step  6 Select Finish'
      CRT @(0,14):'Step  7 Remove all PAID bills from the Sreadsheet (they will cause an error)'
      CRT @(0,15):'Step  8 Remove all bills EXCEPT the bills you want to select for payment'
      CRT @(0,16):'Step  9 When finished you have a list of the bills to be SELECTED for payment'
      CRT @(0,17):"Step 10 CLICK File, CLICK Save As, 'Change Save as type:' to:"
      CRT @(0,18):'        Text (Tab Delimited)(*.txt)'
      CRT @(0,19):'Step 11 Name the file CLIENT#-select.txt (Example: 99999-select.txt)'
      CRT @(0,20):"        Ignore the 'Not Compatible' messages you receive and click OK, then YES"
 
      CALL GEN.IN(0,3,'Enter Client Number        #####','',Q,0,20,'','',1,-5,3,0,QI,HELP,0,23)
      QI=OCONV(QI,'MCU')
      BEGIN CASE
        CASE QI='' ! QI='EX' ! QI='X'
          GOTO EXIT.PROGRAM
        CASE NOT(NUM(QI))
          CALL ELINE('CLIENT MUST BE NUMERIC')
          GOTO 100
      END CASE
      CLIENT=Q'R%5'
      READ CLIENT.REC FROM F.CLIENTS,CLIENT ELSE
        CALL ELINE('Client#: ':CLIENT:' is not on file!')
        GO 100
      END
      CLIENT.NAME=CLIENT.REC<2>
      CRT @(27,3):CLIENT:" - ":CLIENT.NAME:@(-4):
      IF CLIENT.REC<81>#'F' THEN
        CALL ELINE('NOT ALLOWED!  This client is NOT A Freight Payment Client!')
        GO 100
      END
      OPEN '','FB.BILLS.HIST,':CLIENT TO F.HIST ELSE
        CALL OPEN.ABORT("FB.BILLS.HIST,":CLIENT,PROG.NAME)
        GO 100
      END
      OPEN '','FB.OPEN.DATE.XREF,':CLIENT TO F.OPEN.DATE.XREF ELSE
        CALL ELINE("Cannot Create FB.OPEN.DATE.XREF,":CLIENT)
        GO 100
      END
      TYPE.OF.PAYMENT=''
*
* Enter cash receipt date.
*
200   CRT @(0,4):@(-4)
      HELP="Enter cash receipt date. nn=Day. nn/nn=Month & Day. X or EX=Exit."
      LN='Enter Cash Receipt date.   ':CASH.RECEIPT.DATE'D2/'
      CALL GEN.IN(0,4,LN,'DATE',Q,0,8,'','',0,-8,4,0,QI,HELP,0,23)
      QI=OCONV(QI,'MCU')
      BEGIN CASE
        CASE QI=''
          Q=CASH.RECEIPT.DATE
        CASE QI='X'
          CRT @(0,4):@(-4):
          GOTO 100
        CASE QI='EX' 
          STOP
      END CASE
      IF Q='' THEN
        CALL ELINE('You Must Enter a Cash Receipt Date')
        GO 200
      END
      IF Q > TODAY THEN
        CALL ELINE('You Cannot Enter a Date in the Future')
        GO 200
      END
      IF Q < TODAY-7 THEN
        CALL ELINE('You Cannot Enter a Date more than 7 days in the Past')
        GO 200
      END
      CASH.RECEIPT.DATE=Q
      CRT @(27,4):CASH.RECEIPT.DATE'D2/'
*
* Enter type of payment.
*
300   CRT @(0,5):@(-4)
      HELP="Enter Type of Payment. [W]ire, [A]CH/DDS, [C]heck"
      IF TYPE.OF.PAYMENT='' THEN
        LN='Enter type of payment.     ##'
        CALL GEN.IN(0,5,LN,'',Q,0,2,'','',0,-2,5,0,QI,HELP,0,23)
      END ELSE
        LN="Enter type of payment.     ":TYPE.OF.PAYMENT'L#1'
        CALL GEN.IN(0,5,LN,'',Q,0,2,'','',0,-1,5,0,QI,HELP,0,23)
      END
      QI=OCONV(QI,'MCU') 
      IF QI='' THEN
        QI=TYPE.OF.PAYMENT
        Q=QI
      END
      BEGIN CASE
        CASE QI='X'
          CRT @(0,5):@(-4):
          GOTO 200
        CASE QI='EX'
          STOP
        CASE QI='A' 
          TYPE.OF.PAYMENT.TEXT='ACH/DDS'
        CASE QI='W'
          TYPE.OF.PAYMENT.TEXT='Wire'
        CASE QI='C'
          TYPE.OF.PAYMENT.TEXT='Check'
        CASE 1
          CALL ELINE('Invalid Entry')
          GO 300
      END CASE
      TYPE.OF.PAYMENT=QI
      IF DEPOSIT.DATE='' THEN DEPOSIT.DATE=CASH.RECEIPT.DATE
      CRT @(27,5):TYPE.OF.PAYMENT'L#1':" -":TYPE.OF.PAYMENT.TEXT'L#7'
      CRT @(45,5):"Deposit Date. ":DEPOSIT.DATE'D2/'
      GO 600
*
* Enter deposit date.
*
400   HELP="Enter deposit date. nn=Day. nn/nn=Month & Day. X=Back to client. EX=Exit."
        LN='Deposit Date. ':DEPOSIT.DATE'D2/'
        CALL GEN.IN(45,5,LN,'DATE',Q,0,8,'','',0,-8,5,0,QI,HELP,0,23)
        QI=OCONV(QI,'MCU')
        BEGIN CASE
          CASE QI=''
            Q=DEPOSIT.DATE
          CASE QI='X'
            CRT @(45,5):@(-4):
            GOTO 300
          CASE QI='EX'
            STOP
        END CASE
        IF Q='' THEN
          CALL ELINE('You Must Enter a Deposit Date')
          GO 400
        END
        IF Q > TODAY THEN
          CALL ELINE('You Cannot have a Deposit Date in the Future')
          GO 400
        END
        IF Q < TODAY-7 THEN
          GOSUB VERIFY.WARNING
          IF WARNING.OK # 'Y' THEN GO 400
        END
        DEPOSIT.DATE=Q
        CRT @(59,5):DEPOSIT.DATE'D2/'
*
* Return to continue
*
600   CRT @(0,22):@(-3)
*DF      HELP="Press Return to Continue"
      HELP=""
      LN='Press Return to continue.   '
      CALL GEN.IN(0,22,LN,'',Q,0,2,'','',0,-2,22,0,QI,HELP,0,23)
      QI=OCONV(QI,'MCU') 
      BEGIN CASE
        CASE QI='X'
          CRT @(0,22):@(-3):
          GOTO 400
        CASE QI='EX'
          STOP
        CASE QI=''
        CASE 1
          CALL ELINE('Invalid Entry')
          GOTO 600
      END CASE
      CRT @(0,22):@(-3):
*
* OPEN FILE
*
      NUM.OF.DAYS=''
      BEGIN CASE
        CASE TYPE.OF.PAYMENT='C'
          IF CLIENT.REC<121>#'' AND NUM(CLIENT.REC<121>) THEN
            NUM.OF.DAYS=CLIENT.REC<121>
          END
        CASE TYPE.OF.PAYMENT='A'
          IF CLIENT.REC<120>#'' AND NUM(CLIENT.REC<120>) THEN
            NUM.OF.DAYS=CLIENT.REC<120>
          END
        CASE TYPE.OF.PAYMENT='W'
          IF CLIENT.REC<119>#'' AND NUM(CLIENT.REC<119>) THEN
            NUM.OF.DAYS=CLIENT.REC<119>
          END
      END CASE
      IF NUM.OF.DAYS='' THEN
        LOCATE TYPE.OF.PAYMENT IN DDEFAULT<1> SETTING POS THEN
          NUM.OF.DAYS=DDEFAULT<3,POS>
        END ELSE
          CALL ELINE('Type of payment ':TYPE.OF.PAYMENT:' is not setup in the DEFAULT.PAYMENT.SCH Table')
          STOP
        END
      END
      VIN=DEPOSIT.DATE
      VIN<2>=NUM.OF.DAYS
      CALL CALC.DATE(VIN,VOUT,F.TC.HOLIDAYS)
      CHECK.PRINT.DATE=VOUT<1>
      READ BILLDATA.REC FROM F.VOC,"BILLDATA" ELSE
         GOSUB CALL.NET.ADMIN
         CALL ELINE('UNABLE TO OPEN "VOC BILLDATA"')
         STOP
      END

      BILLDATA.REC<2>:='\':USER
      WRITE BILLDATA.REC ON F.VOC,"BILLDATA.":USER ELSE
        CALL ELINE('UNABLE TO WRITE VOC "BILLDATA.":USER')
        GOSUB CALL.NET.ADMIN
        STOP
      END
      OPEN '','BILLDATA.':USER TO F.BILLDATA ELSE
        CALL ELINE(USER:" is not a folder in U:\Billdata. Add the folder and try again")
        GOSUB CALL.NET.ADMIN
        STOP
      END

*
* READ RAW.DATA
*
    RAW.ID=CLIENT:"-select.txt"
    READ RAW.DATA FROM F.BILLDATA,RAW.ID ELSE
      CALL ELINE('U:\':USER:'\':RAW.ID:' is not on file.  Must be present to select bills')
      GOTO 100
    END
    RAW.DATA=CHANGE(RAW.DATA,CHAR(9),@VM); * Replace Tabs with Values
*
* GET INFO AND WRITE TO FILE
*
    NUM.LINES=DCOUNT(RAW.DATA,@AM)

*
* Verify selection criteria is correct.
*
VERIFY.SELECTION:***
    CRT @(0,20):"By typing [I-UNDERSTAND] I agree to select ":NUM.LINES-3:" Freight Bills for payment"
    HELP="X=Change. EX=Exit. [I-UNDERSTAND]=Continue."
    CALL GEN.IN(0,22,'Enter selection please. ------------','',Q,0,12,'','',0,-12,22,1,QI,HELP,0,23)
    QI=OCONV(QI,'MCU')
    BEGIN CASE
       CASE QI='X'
          CRT @(0,19):@(-3):
          GOTO 100
       CASE QI='EX'
          STOP
       CASE QI='I-UNDERSTAND'
          NULL
       CASE 1
          CALL ELINE('Invalid entry. Must be [X], [EX], or [I-UNDERSTAND].')
          GOTO VERIFY.SELECTION
    END CASE
    CRT @(0,22)


    FOR X = 1 TO NUM.LINES
      IF X < 4 THEN GO SKIP.LINE
      LINE=RAW.DATA<1>
      IF TRIM(LINE)='' THEN GO SKIP.LINE
      CARRIER=LINE<1,1>'R%5'
      PRO=LINE<1,5>
      PRO.ID=CARRIER:"*":PRO:"*0"
      READ DHIST FROM F.HIST,PRO.ID ELSE
        CALL ELINE('On Line ':X:' PRO.ID ':PRO.ID:' is missing from FB.BILLS.HIST File!')
         GO SKIP.LINE
      END
      IF DHIST<14>#'' THEN
        CALL ELINE(PRO.ID:' has a check # ':DHIST<14>:' associated with it - Skipping')
        GO SKIP.LINE
      END
      PAY.ID=CLIENT:'*':PRO.ID
      READ REC FROM F.OPEN.BILLS,PAY.ID ELSE
        CALL ELINE('No Open Item to Select - Skipping')
        GO SKIP.LINE
      END
      DELETE F.OPEN.BILLS,PAY.ID
      REC<133>=CASH.RECEIPT.DATE
      REC<134>=CHECK.PRINT.DATE
      REC<135>=DEPOSIT.DATE
      WRITE REC ON F.PAY,PAY.ID
      PROCESS.DATE=REC<37>
      READ DOPEN.LIST FROM F.OPEN.DATE.XREF,PROCESS.DATE THEN
        LOCATE PAY.ID IN DOPEN.LIST SETTING POS THEN
          DEL DOPEN.LIST<POS>
          IF DOPEN.LIST='' THEN
            DELETE F.OPEN.DATE.XREF,PROCESS.DATE
          END ELSE
            WRITE DOPEN.LIST ON F.OPEN.DATE.XREF,PROCESS.DATE
          END
        END
      END
      CRT PRO.ID:' selected'
SKIP.LINE:* 
      DEL RAW.DATA<1>
    NEXT X
    CRT
    CRT 'Press [ENTER] to Continue':
    INPUT NUL
    GO EXIT.PROGRAM
CALL.NET.ADMIN:***
    CALL ELINE("CALL NETWORK ADMINISTRATOR!")
    CALL ELINE("CALL NETWORK ADMINISTRATOR!!!!!")
    RETURN
VERIFY.WARNING:
      HELP='WARNING: Deposit Date is at least 7 days prior today'
      CALL GEN.IN(0,23,'ENTER I UNDERSTAND to accept Or ENTER to reject ------------','',Q2,0,12,'','',2,-12,23,1,Q3,HELP,0,22)
      IF Q2='I UNDERSTAND' THEN
        WARNING.OK='Y'
      END ELSE
        IF Q2#'' THEN
          CALL ELINE('Entry Not Changed')
        END
        WARNING.OK='N'
      END
      RETURN
*
EXIT.PROGRAM:*
*
    STOP
