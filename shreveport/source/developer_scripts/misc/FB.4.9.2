* PROGRAM: FB.4.9.2
* PROGRAM TO IMPORT CHECK REGISTER TO A PC FILE IN LOTUS FORMAT
*
* FREIGHT BILL PACKAGE
*
* AUTHOR: BRIAN M. QUEEN
* DATE: July 1, 1993
*
* THIS PROGRAM IS A PROPRIETARY PRODUCT OF AUTOMATED FREIGHT SYSTEMS
* AND MAY NOT BE USED, COPIED OR MODIFIED WITHOUT THE EXPRESS
* WRITTEN CONSENT OF SAME.
*
********************************************************************
*
* Last updated by AFS.SOURCE (valerie) at 12:02:49 on 08/04/1994.
*   CHANGED IO TO IOL.  SEE FB.4.9.1
* 091506 : JCW01 : USE NEW FB.CHECKS FILES
********************************************************************
*
$INCLUDE PROG.ADMIN INC_OPTIONS
*
* Initialize variables
*
      INPUTCLEAR
      PROMPT ''
      PROG.NAME = 'FB.4.9.2'
      PROG.DESC = 'Create Check Register Lotus File'
      PACK.NAME = 'Freight Payment'
      CALL CHANNEL(CH.NUM)
       UPL.VIN = 'FBBP' ;  UPL.VIN<2> = 'FB.4.9.2' ; UPL.VOUT = ''                    ;* NPR_UPL 04/23/2010
       CALL UPD.PROGRAM.LOG(UPL.VIN,UPL.VOUT)                                                           ;* NPR_UPL 04/23/2010
*
* Open files
*
      OPEN '','FB.CHECKS.HDR' TO F.CHECKS ELSE      ; * JCW01
        CALL OPEN.ABORT("FB.CHECKS.HDR",PROG.NAME)  ; * JCW01
      END                                           ; * JCW01
      OPEN '','FB.CHECKS.DTL' TO F.CHECKS.D ELSE    ; * JCW01
        CALL OPEN.ABORT("FB.CHECKS.DTL",PROG.NAME)  ; * JCW01
      END                                           ; * JCW01
      OPEN '','CARRIERS' TO F.CARRIERS ELSE
        CALL OPEN.ABORT("CARRIERS",PROG.NAME)
      END
      OPEN '','FBWORK':CH.NUM TO F.WORK ELSE
        EXECUTE 'CREATE-FILE FBWORK':CH.NUM:' 1 1001' CAPTURING OUTPUT
        OPEN '','FBWORK':CH.NUM TO F.WORK ELSE
          CALL OPEN.ABORT('FBWORK':CH.NUM,PROG.NAME)
        END
      END
      OPEN 'DICT','FBWORK':CH.NUM TO F.D.WORK ELSE
        CALL OPEN.ABORT("DICT FBWORK":CH.NUM,PROG.NAME)
      END
      CLEARFILE F.WORK
*
* Create temporary dictionary items
*
      TEMP = ''
      TEMP<1> = 'A'
      TEMP<2> = 1
      TEMP<9> = 'L'
      TEMP<10> = '25'
      WRITE TEMP ON F.D.WORK,'1'
*
      TEMP<2> = 2
      TEMP<9> = 'R'
      TEMP<10> = 6
      WRITE TEMP ON F.D.WORK,'2'
*
      TEMP<2> = 3
      TEMP<8> = 'D2/'
      TEMP<9> = 'R'
      TEMP<10> = 8
      WRITE TEMP ON F.D.WORK,'3'
*
      TEMP<2> = 4
      TEMP<8> = 'MR2,'
      TEMP<9> = 'R'
      TEMP<10> = 15
      WRITE TEMP ON F.D.WORK,'4'
*
      TEMP<2> = 5
      TEMP<8> = ''
      TEMP<9> = 'L'
      TEMP<10> = 20
      WRITE TEMP ON F.D.WORK,'5'
*
* Display main menu
*
100   EXECUTE 'TERM 79'
      CALL AFS.SCR.HEAD(CO.ID,FILE.ID,'',PROG.NAME,PROG.DESC,PACK.NAME,
        'AFS Logistics, LLC',TIME.DATE,1)
*
* Input company number
*
200   CALL GEN.IN(0,3,'Company number......#####','',CO.NUM,0,5,'R',
        '0',1,-5,3,0,QI,'Enter company number',0,23)
      IF QI = '' THEN STOP
      IF NOT(CO.NUM MATCHES "0N") THEN GOTO 200
*
* Select disk drive
*
300   HELP = "Enter drive ID. 'X'=Back to File type"
      CALL GEN.IN(0,5,'Disk Drive....................-','',Q,1,1,'',
        '',1,-1,5,0,QI,HELP,0,23)
      QI = OCONV(QI,'MCU')
      BEGIN CASE
        CASE QI = 'X'
          CRT @(0,7):@(-3)
          GOTO 200
        CASE ALPHA(QI)
          DRIVE = QI
        CASE 1
          CALL ELINE('Invalid entry')
          GOTO 300
      END CASE
      CRT @(30,5):DRIVE
*
*     FILE.NAME = 'FB.CHECKS,':CO.NUM         ; * JCW01
*     OPEN '',FILE.NAME TO F.FILE ELSE        ; * JCW01
*       CALL OPEN.ABORT(FILE.NAME,PROG.NAME)  ; * JCW01
*     END                                     ; * JCW01
*
* Input beginning processing date
*
400   HELP = "Enter beginning process date. 'X'=Back to drive"
      CALL GEN.IN(0,7,'Beginning processing date.....':DATE()'D2/',
        'DATE',Q,0,8,'','',1,-8,7,0,QI,HELP,0,23)
      QI = OCONV(QI,'MCU')
      IF QI = 'X' THEN
        CRT @(0,7):@(-3)
        GOTO 300
      END
      BEG.DATE = Q
      CRT @(30,7):BEG.DATE'D2/'
*
* Input ending processing date
*
      HELP = "Enter ending process date. 'X'=Back to drive"
      CALL GEN.IN(0,9,'Ending processing date........':DATE()'D2/',
        'DATE',Q,0,8,'','',1,-8,9,0,QI,HELP,0,23)
      QI = OCONV(QI,'MCU')
      IF QI = 'X' THEN
        CRT @(0,9):@(-3)
        GOTO 400
      END
      END.DATE = Q
      CRT @(30,9):END.DATE'D2/'
*
* Perform lotus transfer
*
      CALL ELINE('Ready?')
*
* Convert data
*
      COUNTER = 0
*     EXECUTE 'SELECT ':FILE.NAME:' WITH 2 GE "':BEG.DATE'D2/':'" AND WITH 2 LE "':END.DATE'D2/':'"' PASSLIST CAPTURING OUTPUT ;P * JCW01
      STMT = 'SELECT FB.CHECKS.HDR WITH 2 GE "':BEG.DATE'D2/':'" AND WITH 2 LE "':END.DATE'D2/':'"' ; * JCW01
      EXECUTE STMT CAPTURING OUTPUT ; * JCW01
      LOOP
        READNEXT ID ELSE ID = '**'
      UNTIL ID = '**' DO
        READ REC FROM F.CHECKS,ID THEN
          T.REC = ''
          READV CARRIER.NAME FROM F.CARRIERS,REC<3>,1 ELSE
            CARRIER.NAME = ''
          END
          T.REC<1> = CARRIER.NAME'L#25'
          T.REC<2> = REC<1>; * Check number
          T.REC<3> = REC<2>; * Check date
          T.REC<4> = REC<4>; * Check amount
*         X = DCOUNT(REC<7>,CHAR(253))  ; * JCW01
          X = REC<25>                   ; * JCW01
          FOR I = 1 TO X
            DTL.ID = ID:'*':I                          ; * JCW01
            READ DTL.REC FROM F.CHECKS.D, DTL.ID ELSE  ; * JCW01
              MSG = 'Could not read record ':DTL.ID:' from FB.CHECKS.DTL; program will stop.' ; * JCW01
              MSG := CHAR(10):CHAR(13)                 ; * JCW01
              MSG := 'Contact Jay Webert in Information Systems':CHAR(10):CHAR(13)  ; * JCW01
              CALL ELINE(MSG)                          ; * JCW01
              STOP                                     ; * JCW01
            END                                        ; * JCW01
            IF DTL.REC<3> = CO.NUM THEN                ; * JCW01
              COUNTER = COUNTER+1
*             T.REC<5> = FIELD(REC<7,I>,'*',2)         ; * JCW01
              T.REC<5> = FIELD(DTL.REC<1>,'*',2)       ; * JCW01
              WRITE T.REC ON F.WORK,COUNTER'R%5'
            END                                        ; * JCW01
          NEXT I
        END
      REPEAT
*
      PCCMD = CHAR(27):CHAR(8)
      ENDCMD = CHAR(0)
      PREC = PCCMD:'KEY /P <ALT+F>I':'FBWORK':CH.NUM
      PREC := '<CR>SSELECT FBWORK':CH.NUM
      PREC := '<CR>1 5 2 3 4<CR>':DRIVE:':\':CO.NUM
      PREC := '.WK1<CR>IOL<CR><CR><ALT>':ENDCMD
      PRINT PREC:
      EXECUTE 'PCCTRL'
*
      GOTO 100
*
*********************************************************************
*
   END
