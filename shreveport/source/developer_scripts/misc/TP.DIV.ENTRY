*****************************************************************
* Program name : TP.DIV.ENTRY
* Author       : Nicole Hennick
* Date written : June 18, 2024
* Library      : FBBP
* COPYRIGHT (C) 2024 - AFS LOGISTICS LLC - ALL RIGHTS RESERVED.
*
* TP Division Entry
*
* Last updated by afs (AFSLOGISTICS\nmeddaugh) at 08:22:52 on 06/18/2024.
*
* Start modification history
* 06/18/24 : NKH00 : INT-437 New program 
* End modification history
*****************************************************************
$INCLUDE PROG.ADMIN INC_OPTIONS
                            
      * Initialize variables
      PROG.NAME='TP.DIV.ENTRY'
      PROG.DESC='THIRD PARTY DIVISION ENTRY'
      PACK.NAME=''
      CO.NAME='AFS LOGISTICS, LLC'
      CALL GET.USER.NEW(USER.NAME,CH.NUM)
      PROMPT''
      CLS=@(-1)
      ARR.VAL = 1
      DISP.VAL=''

      * Check for authorized users
*      READV OP.FLG FROM F.OP,USER.NAME,43 ELSE
*         OP.FLG = "N"
*      END
*      IF OP.FLG[1,1] = "Y" THEN
*      END ELSE
*         CALL FB.PRINT.CENT(1,80,10,'Restricted Access Application')
*         CALL FB.PRINT.CENT(1,80,12,'Access Denied! Contact Information Systems for Access')
*         CALL ELINE('')
*         STOP
*      END

      DIM CLIENT.ARR(30)      
      
      GOSUB OPEN.FILES
      GOSUB MAIN.LOOP
      GOSUB PROGRAM.END
      
*****************************************************************
MAIN.LOOP:
*****************************************************************     
      FILE.DONE = 0
      OPTION = 'DISPLAY'
      LOOP
      UNTIL FILE.DONE         
         BEGIN CASE
            CASE OPTION = 'DISPLAY' ; GOSUB INIT.SUB ; GOSUB DISPLAY.SCREEN
            CASE OPTION = 'ENTER.CLIENT.ID' ; GOSUB ENTER.CLIENT.ID
            CASE OPTION = 'ENTER.DIV' ; GOSUB ENTER.DIV            
            CASE OPTION = 'ENTER.DIV.LEN' ; GOSUB ENTER.DIV.LEN
            CASE OPTION = 'INPUT.PROMPT' ; GOSUB INPUT.PROMPT
         END CASE
      REPEAT

      RETURN

*****************************************************************
INIT.SUB:
*****************************************************************
      RELEASE
      MAT CLIENT.ARR = ''
      DIV.LEN=5
      OPTION = 'ENTER.CLIENT.ID'

      RETURN

*****************************************************************
DISPLAY.SCREEN:
*****************************************************************
      CALL AFS.SCR.REFRESH(PROG.NAME,PROG.DESC,PACK.NAME,CO.NAME,TIME.DATE,1)
      BUFFER = ''
      BUFFER := @(0,3): '     Client ID...........'
      BUFFER := @(0,5):  ' 1.  Division ..........'
      BUFFER := @(0,6):  ' 2.  Division.Length....' 
      BUFFER := @(26,6):'5'     

      CRT BUFFER:

      RETURN

*****************************************************************
DISPLAY.SCREEN.DATA:
*****************************************************************
      BUFFER = '' ; ARR.VAL = ''

      BUFFER := @(26,3):CLIENT.ID'R%5'
      BUFFER := @(33,3):CLIENT.REC<2>[1,30]      
      ARR.VAL = 1
      BUFFER := @(26,5):CLIENT.ARR(ARR.VAL)
      ARR.VAL = 2 
      BUFFER := @(26,6):CLIENT.ARR(ARR.VAL)

      CRT BUFFER:

      RETURN
                        
*****************************************************************
ENTER.CLIENT.ID:
*****************************************************************
      GOSUB INIT.SUB ; GOSUB DISPLAY.SCREEN
      
      HELP = "Enter Client ID Number .  [EX]=Exit"
      MASK = '#####'
      MASK.CHAR='-'
      ANS.OK = 0
      CRT @(26,3):STR(' ',30)
      LOOP
      UNTIL ANS.OK DO
         CALL GEN.IN(26,3,MASK,'',Q,0,50,'','',2,26,3,1,QI,HELP,0,23)
         QI = OCONV(QI,'MCU')
         BEGIN CASE
            CASE QI = 'EX' OR QI=''
               GOSUB PROGRAM.END
            CASE OPTION # 'ENTER.CLIENT.ID'
               ANS.OK = 1
            CASE NUM(QI)
               CLIENT.ID = QI'R%5'
               READ CLIENT.REC FROM F.CLIENTS,CLIENT.ID THEN                              
                  MATREAD CLIENT.ARR FROM F.TP.DIV, CLIENT.ID THEN
                     DIV.LEN = CLIENT.ARR(2)
                     GOSUB DISPLAY.SCREEN.DATA
                     OPTION = 'INPUT.PROMPT'
                     ANS.OK = 1
                  END ELSE
                     MAT CLIENT.ARR = ""
                     OPTION = 'ENTER.DIV'
                     ANS.OK = 1
                  END
               END ELSE
                  CALL ELINE('"':CLIENT.ID:'" is an invalid CLIENT Number. Try Again.')
                  ANS.OK = 0
               END                
            CASE 1
               CALL ELINE('Invalid Client Number, Try Again.')
               ANS.OK = 0
         END CASE
      REPEAT
      CRT @(26,3):CLIENT.ID'R%5':'  ':CLIENT.REC<2>
      
      OPEN '','DIVISION.CODES,':CLIENT.ID TO F.DIVISION.CODES ELSE
         CALL OPEN.ABORT('DIVISION.CODES':CLIENT.ID,PROG.NAME)
      END
            
      RETURN
      
*****************************************************************
ENTER.DIV:
*****************************************************************
      COL=26 ; ROW=5 ; ARR.VAL = 1
      ANS.OK = 0
      LOOP
      UNTIL ANS.OK DO
         CRT @(COL,ROW):STR(' ',10)    
         IF DIV.LEN=6 THEN
            MASK = '######'
         END ELSE
            MASK = "#####"
         END
         HELP = "Division Code, [X]=Back [EX]=Exit" 
         IF DIV.LEN=6 THEN        
            CALL GEN.IN(COL,ROW,MASK,'',Q,0,6,'','',2,COL,ROW,0,QI,HELP,0,23)
         END ELSE
            CALL GEN.IN(COL,ROW,MASK,'',Q,0,5,'','',2,COL,ROW,0,QI,HELP,0,23)            
         END                  
         QI = TRIM(OCONV(QI,'MCU'))
         BEGIN CASE
            CASE QI = 'EX' 
               GOSUB PROGRAM.END
               RETURN     
            CASE QI='X' 
               IF CLIENT.ARR(ARR.VAL) = '' THEN CRT @(COL,ROW):"      "
               CRT @(COL,ROW):CLIENT.ARR(ARR.VAL)
               RETURN     
            CASE QI = ''
               DISP.VAL = ''
               ANS.OK=1
            CASE 1
               Q = OCONV(QI,'MCU')                           
               IF DIV.LEN= 6 THEN                  
                  Q = QI'R%6'
               END ELSE
                  Q = QI'R%5'               
               END
               READ DIV.CODES.REC FROM F.DIVISION.CODES, Q THEN   
                  IF DIV.CODES.REC<7> # 'I' THEN            
                     DISP.VAL=Q
                     CLIENT.ARR(ARR.VAL) = Q               
                     ANS.OK=1
                  END ELSE
                  CALL ELINE('Division Code: ':Q:' is inactive')                  
                  END
               END ELSE
                  CALL ELINE('Division Code: ':Q:' Not on File')
               END
         END CASE  
         CRT @(COL,ROW):DISP.VAL             
         OPTION = 'INPUT.PROMPT'        
      REPEAT

      RETURN      

*****************************************************************
ENTER.DIV.LEN:
*****************************************************************
      COL=26 ; ROW=6 ; ARR.VAL = 2
      ANS.OK = 0
      LOOP
      UNTIL ANS.OK DO
         CRT @(COL,ROW):STR(' ',10)                  
         MASK = "# "
         HELP = "Division Code Length,[X]=Back [EX]=Exit"         
         CALL GEN.IN(COL,ROW,MASK,'',Q,0,1,'','',2,COL,ROW,0,QI,HELP,0,23)
         QI = TRIM(OCONV(QI,'MCU'))
         BEGIN CASE
            CASE QI = 'EX' 
               GOSUB PROGRAM.END
            CASE QI='X' 
               CRT @(COL,ROW):CLIENT.ARR(ARR.VAL)
               RETURN     
            CASE NOT(NUM(QI))
               CALL ELINE('Division Length must be 5 or 6')
            CASE QI = ''
               Q = 5
               DISP.VAL = Q
            CASE QI # 5 AND QI # 6
               CALL ELINE('Division Length must be 5 or 6')
               Q = 5
               DISP.VAL = Q
            CASE 1
               ANS.OK=1
               Q = QI
               DISP.VAL = Q
               DIV.LEN = Q
               CLIENT.ARR(ARR.VAL) = Q
               OPTION = 'INPUT.DIV'
         END CASE  
         CRT @(COL,ROW):DISP.VAL             
      REPEAT

      RETURN

*****************************************************************
INPUT.PROMPT:
*****************************************************************
      MOD.OK = 0
      LOOP
      UNTIL MOD.OK DO
         MASK = "#"      
         HELP = 'Enter # to change, [U]=Update [X]=Back [EX]=Exit '         
         CALL GEN.IN(0,22,'Enter Option: --','',Q,0,4,'','',2,-2,22,1,QI,HELP,0,23)
         QI = TRIM(OCONV(QI,'MCU'))
         BEGIN CASE
            CASE QI = 'EX' 
               GOSUB PROGRAM.END
            CASE QI = 'X'
               OPTION = 'DISPLAY' ; MOD.OK=1               
            CASE QI = 'U'
               GOSUB SAVE.REC
               MOD.OK=1
               OPTION = 'DISPLAY'
            CASE NUM(QI) 
               ON QI GOSUB ENTER.DIV, ENTER.DIV.LEN
               IF OPTION = 'INPUT.DIV' THEN GOSUB ENTER.DIV
            CASE 1
               CALL ELINE('Invalid option')
         END CASE

      REPEAT

      RETURN

*****************************************************************
OPEN.FILES:
*****************************************************************
      OPEN '','CLIENTS' TO F.CLIENTS ELSE
         CALL OPEN.ABORT("CLIENTS",PROG.NAME)
      END
      OPEN '','TP.DIVISIONS' TO F.TP.DIV ELSE
         CALL OPEN.ABORT("TP.DIVISIONS",PROG.NAME)
      END
      OPEN '','OPERATOR' TO F.OP ELSE CALL OPEN.ABORT("OPERATOR",PROG.NAME)    

      RETURN
      
*****************************************************************
SAVE.REC:
*****************************************************************
      MATWRITE CLIENT.ARR ON F.TP.DIV,CLIENT.ID

      RETURN

*****************************************************************
PROGRAM.END:
*****************************************************************
      STOP
   
