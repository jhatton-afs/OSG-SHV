$OPTIONS HEADER.EJECT
      ERRMSG=''
*
$OPTIONS EXEC.EQ.PERF
$OPTIONS INFO.ABORT
*
* PROGRAM FB.1.17
* PROGRAM TO MODIFY COMPANY CODES
*
* FREIGHT BILLING PACKAGE
*
* AUTHOR: OWEN HOLMAN
* DATE: JANUARY 23, 1994
*
* 032612 - JMK01 - UFM Logistics acquisition / add company code
* Initialize variables
*
      PROMPT''
      ADD.FLAG=0
      PLACE.HOLDER='!@#$%^&*()'
      PROG.NAME='FB.1.17'
      PROG.DESC='COMPANY MAINTENANCE'
      PACK.NAME='FREIGHT BILLING'
      CALL AFS.SCR.HEAD(CO.ID,FILE.ID,'',PROG.NAME,PROG.DESC,PACK.NAME,CO.NAME,TIME.DATE,1)
       UPL.VIN = 'FBBP' ;  UPL.VIN<2> = 'FB.1.17' ; UPL.VOUT = ''                    ;* NPR_UPL 04/23/2010
       CALL UPD.PROGRAM.LOG(UPL.VIN,UPL.VOUT)                                                           ;* NPR_UPL 04/23/2010
*
* Open files
*
      OPEN '','FB.COMPANY' TO F.COMPANY ELSE
         CALL OPEN.ABORT("FB.COMPANY",PROG.NAME)
      END
      OPEN '','ZIPS.CODES' TO F.ZIPS ELSE
         CALL OPEN.ABORT("FB.ZIPS",PROG.NAME)
      END
      CALL GET.USER(USER)
      DISPLAY.MODE=1
      SUPER.USER=0
      VIN=USER
      CALL SUPER.USER(VIN,VOUT)
      SUPER.USER=VOUT<1>
      IF USER='CBEASLEY' THEN SUPER.USER=1
*
* Display page heading
*
      CALL AFS.SCR.HEAD(CO.ID,FILE.ID,PERIOD,PROG.NAME,PROG.DESC,PACK.NAME,CO.NAME,TIME.DATE,1)
*
* Enter mode VIEW or CHANGE
*
50:   IF SUPER.USER THEN
         HELP="Enter [V]iew for View only or [C]hange to make changes/additions."
         CALL GEN.IN(0,22,'Enter mode V','',Q,0,1,'','',0,-1,22,0,QI,HELP,0,23)
         QI=OCONV(QI,'MCU')
         BEGIN CASE
            CASE QI='X' OR QI='EX'
               STOP
            CASE QI='V' OR QI=''
               DISPLAY.MODE=1
            CASE QI='C'
               DISPLAY.MODE=0
            CASE 1
               CALL ELINE("Invalid Entry")
               GOTO 50
         END CASE
      END
      IF DISPLAY.MODE THEN
         PROG.DESC=PROG.DESC:' - View'
      END ELSE
         PROG.DESC=PROG.DESC:' - Change'
      END
      CALL AFS.SCR.HEAD(CO.ID,FILE.ID,PERIOD,PROG.NAME,PROG.DESC,PACK.NAME,CO.NAME,TIME.DATE,1)
*
* Enter company number. If not on file then create if desired.
*
100:  CRT @(0,2):@(-3)
      IF DISPLAY.MODE THEN
         HELP="Enter company number or name to search. [EX]=Exit"
      END ELSE
         HELP="Enter company number or name to search. [N]=New [L]=List [EX]=Exit"
      END
      CALL GEN.IN(0,3,'Enter Company Number : #####','',Q,0,20,'','',2,-5,3,0,QI,HELP,0,23)
      QI=OCONV(QI,'MCU')
      BEGIN CASE
         CASE QI='X' OR QI='EX' OR QI=''
            STOP
         CASE QI='N' AND NOT(DISPLAY.MODE)
            IF QI='N' THEN ID=0
            LOOP
               ID=ID+1
               READ XX FROM F.COMPANY,ID'R%5' ELSE XX='**'
            UNTIL XX='**' DO REPEAT
            Q=ID'R%5'
         CASE QI='L'
            EXECUTE 'SORT FB.COMPANY BY 1 1 15'
            CALL ELINE('')
            GO 100
         CASE ALPHA(QI)
            CALL SOUNDEX.DISPLAY(QI,'FB.COMPANY','SDX.COMPANY',1,ITEM.LIST)
            CALL AFS.SCR.REFRESH(PROG.NAME,PROG.DESC,PACK.NAME,CO.NAME,TIME.DATE,1)
            IF ITEM.LIST='' ! DCOUNT(ITEM.LIST<1>,@VM) > 1 THEN GOTO 100
            IF NOT(NUM(ITEM.LIST<1,1>)) THEN GOTO 100
            Q=ITEM.LIST<1,1>'R%5'
         CASE NUM(QI)
            Q=QI'R%5'
         CASE 1
            GOTO 100
      END CASE
*
* Read company item
*
      COMPANY.ID=Q
      COMPANY.REC=''
      IF DISPLAY.MODE THEN
         READ COMPANY.REC FROM F.COMPANY,Q ELSE
            CALL ELINE("Cannot find company ":Q:" in the COMPANY file.")
            GOTO 100
         END
      END ELSE
         READU COMPANY.REC FROM F.COMPANY,Q ELSE
***************************************************
            ADD.COMPANY=0
            IF USER='BRIAN' THEN ADD.COMPANY=1
            IF USER='BRIANP' THEN ADD.COMPANY=1    ; * JMK01
            IF USER='BROSE' THEN ADD.COMPANY=1     ; * JMK01
*           IF USER='KAREN' THEN ADD.COMPANY=1     ; * JMK01
*           IF USER='OWEN' THEN ADD.COMPANY=1      ; * JMK01

            IF USER='DAVID' THEN ADD.COMPANY=1
            IF USER='MKERBOW' THEN ADD.COMPANY=1

            IF USER='CBEASLEY' THEN ADD.COMPANY=1
            IF ADD.COMPANY=0 THEN
               RELEASE F.COMPANY,Q
               CALL ELINE("You are not authorized to create a new Company.")
               GOTO 100
            END
            HELP="Enter either Y for Yes or N for No."
            LN='Company number ':Q:' not on file.  Do you want to create? N'
            CALL GEN.IN(0,22,LN,'',ACK,0,1,'','',2,-1,22,1,QI,HELP,0,23)
            QI=OCONV(QI,'MCU')
            IF QI='Y' THEN
               CALL AFS.SCR.REFRESH(PROG.NAME,PROG.DESC,PACK.NAME,CO.NAME,TIME.DATE,1)
            END ELSE
               GOTO 100
            END
            ADD.FLAG=1
         END
      END
      OLD.COMPANY.REC=COMPANY.REC
*
* Convert phone numbers
*
      VAR=COMPANY.REC<7>
      CALL REMOVE.PUNC(VAR)
      CALL DEL.CHARACTER(VAR,' ')
      COMPANY.REC<7>=VAR
      VAR=COMPANY.REC<8>
      CALL REMOVE.PUNC(VAR)
      CALL DEL.CHARACTER(VAR,' ')
      COMPANY.REC<8>=VAR
*
* Display menu and company data
*
      COMPANY.NUMBER.TEXT='Company Number ':Q
      CALL CENTER(COMPANY.NUMBER.TEXT,80)
      CRT @(0,2):COMPANY.NUMBER.TEXT
      CRT @(0,3):@(-4):
      GOSUB 80000
      IF ADD.FLAG THEN GO 1000
*
* Get selection from operator
*
200:  IF DISPLAY.MODE THEN
         HELP="Enter return to continue"
      END ELSE
         HELP="Enter number of information to change: (1 to 14) [D]=Delete"
      END
      ADD.FLAG=0
      CALL GEN.IN(0,22,'Enter Selection Number ##','',Q,0,2,'','',2,-2,22,1,QI,HELP,0,23)
      QI=OCONV(QI,'MCU')
      IF QI='' THEN GOTO 99000
      IF NOT(DISPLAY.MODE) AND QI='D' THEN
         HELP='Enter Y to delete or N not to delete.'
         CALL GEN.IN(0,22,'Are you sure? N','',Q,0,1,'','',2,-1,22,1,QI,HELP,0,23)
         QI=OCONV(QI,'MCU')
*
* Delete company
*
         IF QI='Y' THEN
            IF USER#'OWEN' THEN
1234:          CALL ELINE('Cannot delete a company...Call Owen.')
               GO 1234
            END
            TEXT = "Deleting record ":COMPANY.ID:" from file FB.COMPANY."
            CALL CENTER(TEXT,80)
            CRT @(0,21):@(-3):TEXT:
            DELETE F.COMPANY,COMPANY.ID
            CALL SOUNDEX.EDIT(OLD.COMPANY.REC<1>,'',COMPANY.ID,'SDX.COMPANY')
            CALL AFS.SCR.HEAD(CO.ID,FILE.ID,PERIOD,PROG.NAME,PROG.DESC,PACK.NAME,CO.NAME,TIME.DATE,1)
            GOTO 100
         END ELSE
            GOTO 200
         END
      END
      IF DISPLAY.MODE AND NUM(QI) THEN ON QI GOSUB 99999,99999,99999,99999,99999,99999,99999,99999,99999,99999,99999,99999,99999,99999
      IF NOT(DISPLAY.MODE) AND NUM(QI) THEN ON QI GOSUB 1000,2000,3000,4000,5000,6000,7000,8000,9000,10000,11000,12000,13000,14000,15000
      GOTO 200
*
* Change company name
*
1000: HELP="Enter name. [space]=Clear."
      MASK=COMPANY.REC<1>:STR('-',30-LEN(COMPANY.REC<1>))
      CALL GEN.IN(15,3,MASK,'',Q,0,30,'','',2,15,3,0,QI,HELP,0,23)
      IF INDEX(Q,"'",1) THEN
         CALL ELINE("Error!  Company Name Cannot Contain a Single Quote!")
         GO 1000
      END
      IF Q#'' THEN COMPANY.REC<1>=TRIM(Q)
      IF Q=' ' THEN COMPANY.REC<1>=''
      CRT @(15,3):COMPANY.REC<1>'L#30'
      IF ADD.FLAG=0 THEN RETURN
*
* Change company address 1
*
2000: HELP="Enter addr 1. [space]=Clear"
      MASK=COMPANY.REC<2,1>:STR('-',30-LEN(COMPANY.REC<2,1>))
      CALL GEN.IN(15,4,MASK,'',Q,0,30,'','',2,15,4,0,QI,HELP,0,23)
      IF Q#'' THEN COMPANY.REC<2,1>=TRIM(Q)
      IF Q=' ' THEN COMPANY.REC<2,1>=''
      CRT @(15,4):COMPANY.REC<2,1>'L#30'
      IF ADD.FLAG=0 THEN RETURN
*
* Change company address 2
*
3000: HELP="Enter addr 2. [space]=Clear"
      MASK=COMPANY.REC<2,2>:STR('-',30-LEN(COMPANY.REC<2,2>))
      CALL GEN.IN(15,5,MASK,'',Q,0,30,'','',2,15,5,0,QI,HELP,0,23)
      IF Q#'' THEN COMPANY.REC<2,2>=TRIM(Q)
      IF Q=' ' THEN COMPANY.REC=DELETE(COMPANY.REC,2,2,0)
      CRT @(15,5):COMPANY.REC<2,2>'L#30'
      IF ADD.FLAG=0 THEN RETURN
*
* Change company zip code
*
4000: HELP="Enter zip code. [space]=Clear"
      IF COMPANY.REC<6> NE '' THEN
         MASK=COMPANY.REC<6>:STR('#',10-LEN(COMPANY.REC<6>))
      END ELSE
         MASK='#####-####'
      END
      CALL GEN.IN(15,6,MASK,'',Q,0,10,'','',2,15,6,0,QI,HELP,0,23)
      IF Q#'' THEN COMPANY.REC<6>=TRIM(Q) ELSE Q=COMPANY.REC<6>
      IF Q=' ' THEN Q=''
      COMPANY.REC<6>=TRIM(Q)
      CODE=FIELD(Q,'-',1)
      IF CODE#'' & NUM(CODE) THEN
         READ ZIP.REC FROM F.ZIPS,CODE THEN
            COMPANY.REC<4>=ZIP.REC<1>
            COMPANY.REC<5>=ZIP.REC<2>
         END
      END
      CRT @(15,6):COMPANY.REC<6>'L#10'
      CRT @(34,6):COMPANY.REC<5>'L#2'
      CRT @(15,7):COMPANY.REC<4>'L#30'
      IF ADD.FLAG=0 THEN RETURN
*
* Change company state
*
5000: HELP="Enter state code. [space]=Clear"
      MASK=COMPANY.REC<5>:STR('-',2-LEN(COMPANY.REC<5>))
      CALL GEN.IN(34,6,MASK,'',Q,0,2,'','',2,34,6,0,QI,HELP,0,23)
      IF Q#'' THEN COMPANY.REC<5>=TRIM(Q)
      IF Q=' ' THEN COMPANY.REC<5>=''
      CRT @(34,6):COMPANY.REC<5>'L#2'
      IF ADD.FLAG=0 THEN RETURN
*
* Change company city
*
6000: HELP="Enter city. [space]=Clear"
      MASK=COMPANY.REC<4>:STR('-',30-LEN(COMPANY.REC<4>))
      CALL GEN.IN(15,7,MASK,'',Q,0,30,'','',2,15,7,0,QI,HELP,0,23)
      IF Q#'' THEN COMPANY.REC<4>=TRIM(Q)
      IF Q=' ' THEN COMPANY.REC<4>=''
      CRT @(15,7):COMPANY.REC<4>'L#30'
      IF ADD.FLAG=0 THEN RETURN
*
* Change company phone number
*
7000: HELP="Enter company's phone number. [space]=Clear"
      CALL GEN.IN(15,8,COMPANY.REC<7>'L#13','PHONE',Q,0,13,'','',2,15,8,0,QI,HELP,0,23)
      BEGIN CASE
         CASE QI=' '
            COMPANY.REC<7>=''
         CASE QI NE ''
            COMPANY.REC<7>=TRIM(Q)
         CASE QI=''
            NULL
         CASE 1
            GOTO 7000
      END CASE
      VAR=COMPANY.REC<7>
      CALL REMOVE.PUNC(VAR)
      CALL DEL.CHARACTER(VAR,' ')
      COMPANY.REC<7>=VAR
      CRT @(15,8):'(':COMPANY.REC<7>[1,3]:')':COMPANY.REC<7>[4,3]:'-':COMPANY.REC<7>[7,4]
      IF ADD.FLAG=0 THEN RETURN
*
* Change company fax number
*
8000: HELP="Enter company's fax number. [space]=Clear"
      CALL GEN.IN(15,9,COMPANY.REC<8>'L#13','',Q,0,13,'','',2,15,9,0,QI,HELP,0,23)
      BEGIN CASE
         CASE QI=' '
            COMPANY.REC<8>=''
         CASE QI NE ''
            COMPANY.REC<8>=TRIM(Q)
         CASE QI=''
            NULL
         CASE 1
            GOTO 8000
      END CASE
      VAR=COMPANY.REC<8>
      CALL REMOVE.PUNC(VAR)
      CALL DEL.CHARACTER(VAR,' ')
      COMPANY.REC<8>=VAR
      CRT @(15,9):
      LFAX=LEN(COMPANY.REC<8>)
      BEGIN CASE
         CASE LFAX=10
            PRINT '(':COMPANY.REC<8>[1,3]:')':COMPANY.REC<8>[4,3]:'-':COMPANY.REC<8>[7,4]
         CASE LFAX=7
            PRINT COMPANY.REC<8>[1,3]:'-':COMPANY.REC<8>[4,7]
         CASE 1
            PRINT
      END CASE
      IF ADD.FLAG=0 THEN RETURN
*
* Change company short report heading
*
9000: HELP="Enter short report heading. [space]=Clear [cr]=Accept"
      MASK=COMPANY.REC<9>
      CALL GEN.IN(15,10,MASK,'',Q,0,30,'','',2,15,10,0,QI,HELP,0,23)
      BEGIN CASE
         CASE Q=' '
            COMPANY.REC<9>=''
         CASE QI=''
            NULL
         CASE QI NE ''
            COMPANY.REC<9>=Q
         CASE 1
            GOTO 9000
      END CASE
      CRT @(15,10):COMPANY.REC<9>
      IF ADD.FLAG=0 THEN RETURN
*
* Change company long report heading
*
10000:HELP="Enter long report heading. [space]=Clear [cr]=Accept"
      MASK=COMPANY.REC<10>
      CALL GEN.IN(15,11,MASK,'',Q,0,60,'','',2,15,11,0,QI,HELP,0,23)
      BEGIN CASE
         CASE Q=' '
            COMPANY.REC<10>=''
         CASE QI=''
            NULL
         CASE QI NE ''
            COMPANY.REC<10>=Q
         CASE 1
            GOTO 10000
      END CASE
      CRT @(15,11):COMPANY.REC<10>
      IF ADD.FLAG=0 THEN RETURN
*
* Change company from email address
*
11000:HELP="Enter from email address. [space]=Clear [cr]=Accept"
      MASK=COMPANY.REC<11>
      CALL GEN.IN(15,12,MASK,'',Q,0,50,'','',2,15,12,0,QI,HELP,0,23)
      BEGIN CASE
         CASE Q=' '
            COMPANY.REC<11>=''
         CASE QI=''
            NULL
         CASE QI NE ''
            COMPANY.REC<11>=Q
         CASE 1
            GOTO 10000
      END CASE
      CRT @(15,12):COMPANY.REC<11>'L#50'
      IF ADD.FLAG=0 THEN RETURN
*
* Change company forms logo file name
*
12000:HELP='Enter forms logo file name. [space]=Clear [cr]=Accept'
      MASK=COMPANY.REC<12>
      CALL GEN.IN(15,13,MASK,'',Q,0,20,'','',2,15,13,0,QI,HELP,0,23)
      BEGIN CASE
         CASE QI=' '
            COMPANY.REC<12>=''
            CRT @(15,13):SPACE(20)
            IF ADD.FLAG=0 THEN RETURN
         CASE QI=''
            NULL
         CASE QI NE ''
            COMPANY.REC<12>=Q
         CASE 1
            GOTO 12000
      END CASE
      CRT @(15,13):COMPANY.REC<12>'L#20'
      IF ADD.FLAG=0 THEN RETURN
*
* Change company forms invoice file name
*
13000:HELP='Enter forms invoice file name. [space]=Clear [cr]=Accept'
      MASK=COMPANY.REC<13>
      CALL GEN.IN(15,14,MASK,'',Q,0,20,'','',2,15,14,0,QI,HELP,0,23)
      BEGIN CASE
         CASE QI=' '
            COMPANY.REC<13>=''
            CRT @(15,14):SPACE(20)
            IF ADD.FLAG=0 THEN RETURN
         CASE QI=''
            NULL
         CASE QI NE ''
            COMPANY.REC<13>=Q
         CASE 1
            GOTO 13000
      END CASE
      CRT @(15,14):COMPANY.REC<13>'L#20'
      IF ADD.FLAG=0 THEN RETURN
*
* Change company GL CC Number
*
14000:HELP='Enter GL Cost Center number. [space]=Clear [cr]=Accept'
      CALL GEN.IN(15,15,COMPANY.REC<14>'R#3','',Q,0,2,'','',1,15,15,0,QI,HELP,0,23)
      BEGIN CASE
         CASE QI=' '
            COMPANY.REC<14>=''
            CRT @(15,15):SPACE(3)
            IF ADD.FLAG=0 THEN RETURN
         CASE QI=''
            NULL
         CASE QI NE ''
            COMPANY.REC<14>=Q
         CASE 1
            GOTO 14000
      END CASE
      CRT @(15,15):COMPANY.REC<14>'R#3'
      IF ADD.FLAG=0 THEN RETURN
*
* Change CAR Report Code
*
15000:HELP='Enter GL Cost Center number. [space]=Clear [cr]=Accept'
      CALL GEN.IN(15,16,COMPANY.REC<15>'L#3','',Q,0,4,'','',1,15,16,0,QI,HELP,0,23)
      BEGIN CASE
         CASE QI=' '
            COMPANY.REC<15>=''
            CRT @(15,16):SPACE(3)
            IF ADD.FLAG=0 THEN RETURN
         CASE QI=''
            NULL
         CASE QI NE ''
            COMPANY.REC<15>=Q
         CASE 1
            GOTO 15000
      END CASE
      CRT @(15,16):COMPANY.REC<15>'L#3'
      IF ADD.FLAG=0 THEN RETURN
      GOTO 200
*
* Company screen display
*
80000:* SCREEN DISPLAY
      CRT @(0, 3):' 1 Name....... ':COMPANY.REC<1>'L#30'
      CRT @(0, 4):' 2 Address 1.. ':COMPANY.REC<2,1>'L#30'
      CRT @(0, 5):' 3 Address 2.. ':COMPANY.REC<2,2>'L#30'
      CRT @(0, 6):' 4 Zip........ ':COMPANY.REC<6>'L#10':' 5 State ':COMPANY.REC<5>'L#2'
      CRT @(0, 7):' 6 City....... ':COMPANY.REC<4>'L#30'
      CRT @(0, 8):' 7 Phone...... ':'(':COMPANY.REC<7>[1,3]:')':COMPANY.REC<7>[4,3]:'-':COMPANY.REC<7>[7,4]
      CRT @(0, 9):' 8 Fax........ ':
      LFAX=LEN(COMPANY.REC<8>)
      BEGIN CASE
         CASE LFAX=10
            PRINT '(':COMPANY.REC<8>[1,3]:')':COMPANY.REC<8>[4,3]:'-':COMPANY.REC<8>[7,4]
         CASE LFAX=7
            PRINT COMPANY.REC<8>[1,3]:'-':COMPANY.REC<8>[4,7]
         CASE 1
            PRINT
      END CASE
      CRT @(0,10):' 9 Short Rpt.. ':COMPANY.REC<9>'L#30'
      CRT @(0,11):'10 Long Rpt... ':COMPANY.REC<10>'L#60'
      CRT @(0,12):'11 From email. ':COMPANY.REC<11>'L#30'
      CRT @(0,13):'12 Logo File.. ':COMPANY.REC<12>'L#20'
      CRT @(0,14):'13 Inv File... ':COMPANY.REC<13>'L#20'
      CRT @(0,15):'14 GL CC No... ':COMPANY.REC<14>'R#3'
      CRT @(0,16):'15 CAR Rpt.... ':COMPANY.REC<15>'L#3'
      RETURN
*
* Check for changes and update file
*
99000:IF OLD.COMPANY.REC # COMPANY.REC THEN
         HELP='Options:  [Y]es [N]o'
         LN='Company information changed. Update? -'
         CALL GEN.IN(0,22,LN,'',Q,1,1,'','',2,-1,22,1,QI,HELP,0,23)
         QI=OCONV(QI,'MCU')
         BEGIN CASE
            CASE QI='Y'
               WRITE COMPANY.REC ON F.COMPANY,COMPANY.ID
               CALL SOUNDEX.EDIT(OLD.COMPANY.REC<1>,COMPANY.REC<1>,COMPANY.ID,'SDX.COMPANY')
            CASE QI='N'
               RELEASE F.COMPANY,COMPANY.ID
               GOTO 100
            CASE 1
               GOTO 99000
         END CASE
      END
      CALL AFS.SCR.REFRESH(PROG.NAME,PROG.DESC,PACK.NAME,CO.NAME,TIME.DATE,1)
      IF NOT(DISPLAY.MODE) THEN RELEASE F.COMPANY,COMPANY.ID
      GOTO 100
99999:CALL ELINE('Invalid selection - View Only')
      RETURN
