$OPTIONS EXEC.EQ.PERF
$OPTIONS INFO.ABORT
*
*
SUBROUTINE FB.2.1.CLASS(CLASS,QI,OPT)
$OPTIONS HEADER.EJECT
ERRMSG=''
* SUBROUTINE TO ENTER CLASSES AND WEIGHTS
*
* FREIGHT PAYMENT PACKAGE
*
* AUTHOR: BRIAN M. QUEEN
* DATE: November 3, 1993
*
* Initialize variables
*
      PROMPT''
      PROG.NAME='FB.2.1.CLASS'
*
      BEGIN CASE
        CASE OPT='I'
          MODE='EDIT'
          INQUIRE=1
        CASE OPT='E'
          MODE='INPUT'
          INQUIRE=0
        CASE OPT='V'
          MODE='INPUT'
          INQUIRE=0
      END CASE
      LN=0
      CALL FB.CLEAR.WINDOW
*
* Open files
*
      OPEN '','FB.CLASS' TO F.CLASS ELSE CALL OPEN.ABORT("FB.CLASS",PROG.NAME)
*
* Display window headings
*
      CRT @(41,6):"--- Class ---- Weight ----------------"
*
* Start input
*
      PAGE=1
      GOSUB 9000
*
* Enter class line
*
100   IF MODE='INPUT' THEN
        LN=LN+1
        QI=LN
        IF LN GT STOP THEN
          PAGE=PAGE+1
          GOSUB 9000
        END
      END ELSE
        HELP="Enter Line Num. [cr]=End. [B]=Back page. [F]=Fwd page. [X]=Back. [EX]=Exit."
        CALL GEN.IN(41,16,'Enter line number. ###','',Q,0,3,'','',0,-3,16,0,QI,HELP,0,23)
      END
      QI=OCONV(QI,'MCU')
      BEGIN CASE
        CASE QI='B'
          PAGE=PAGE-1
          IF PAGE=0 THEN PAGE=1
          GOSUB 9000
        CASE QI='F'
          PAGE=PAGE+1
          GOSUB 9000
        CASE QI=''
          CALL FB.CLEAR.WINDOW
          GOTO 7900
        CASE QI='X'
          CALL FB.CLEAR.WINDOW
          RETURN
        CASE QI='EX'
          STOP
        CASE NUM(QI)
          IF INQUIRE THEN
            CALL ELINE('Can not access detail in inquiry!')
          END ELSE
            SEL=QI
            GOSUB 8000
            IF OPT='V' & QI='' THEN GOTO 7900
          END
      END CASE
      GOTO 100
*
* End of program
*
7900  X=DCOUNT(CLASS<1>,CHAR(253))
      FOR I=X TO 1 STEP -1
        IF CLASS<1,I>='' THEN CLASS=DELETE(CLASS,1,I,0)
      NEXT I
      IF OPT='V' THEN
        X=DCOUNT(CLASS<1>,CHAR(253))
        FOR I=1 TO X
          IF CLASS<3,I> NE '*' THEN
            CALL ELINE('All classes have not been verified.')
            GOTO 100
          END
        NEXT I
        OPT='I'
        PAGE=1
        GOSUB 9000
      END
      CLASS=DELETE(CLASS,3,0,0)
      RETURN
*-----------------------------------------------------------------------
*
* INTERNAL SUBROUTINES
*
*-----------------------------------------------------------------------
*
* Enter class
*
8000  IF SEL LT START ! SEL GT STOP THEN
        CALL ELINE('Invalid entry.')
        RETURN
      END
      GOSUB 9100
*
8010  HELP="Enter class. [cr]=Accept [D]=Delete [E]=Edit Mode [X]=Back [EX]=Exit"
      IF OPT='V' THEN MASK=SPACE(5) ELSE MASK=CLASS<1,SEL>'R#5'
      CALL GEN.IN(45,LINE,MASK,'',Q,0,5,'','',2,-5,LINE,0,QI,HELP,0,23)
      QI=OCONV(QI,'MCU')
      BEGIN CASE
        CASE QI='' & OPT EQ 'V'
          RETURN
        CASE QI='EX'
          STOP
        CASE QI='X'
          CRT @(45,LINE):CLASS<1,SEL>'R#5'
          MODE='EDIT'
          DATA 'X'
          RETURN
        CASE QI='D' & OPT NE 'V'
          CLASS=DELETE(CLASS,1,SEL)
          CLASS=DELETE(CLASS,2,SEL)
          GOSUB 9000
          RETURN
        CASE QI='E'
          CRT @(45,LINE):CLASS<1,SEL>'R#5'
          MODE='EDIT'
          RETURN
        CASE QI='' AND CLASS<2,SEL> # '' 
          CRT @(45,LINE):CLASS<1,SEL>'R#5'
          GO 8020
        CASE QI='' AND CLASS<2,SEL> = '' 
          CRT @(45,LINE):CLASS<1,SEL>'R#5'
          MODE='EDIT'
          RETURN

      END CASE
*
* Verify class
*
      IF OPT='V' THEN
        POS=0
        DONE=0
        VERIFIED=0
        LOOP
        UNTIL DONE ! VERIFIED DO
          POS=POS+1
          IF CLASS<1,POS>=QI & CLASS<3,POS> NE '*' THEN VERIFIED=1
          IF CLASS<1,X>='' THEN DONE=1
        REPEAT
        IF VERIFIED=0 THEN
          CALL ELINE('Invalid class.')
          GOTO 8010
        END
      END ELSE
        READ DUMMY FROM F.CLASS,Q ELSE
          CALL ELINE('Invalid class code.')
          GOTO 8010
        END
        CLASS<1,SEL>=Q
      END
      CRT @(45,LINE):CLASS<1,SEL>'R#5'
*
* Enter weight
*
8020  HELP="Enter weight. [cr]=Accept [X]=Back [EX]=Exit"
      IF OPT='V' THEN MASK=SPACE(10) ELSE MASK=CLASS<2,SEL>'R#10'
      CALL GEN.IN(52,LINE,MASK,'',Q,0,10,'','',2,-10,LINE,0,QI,HELP,0,23)
      QI=OCONV(QI,'MCU')
      BEGIN CASE
        CASE QI='' & OPT NE 'V'
          Q=CLASS<2,SEL>
          IF Q='' & MODE='INPUT' THEN MODE='EDIT'
        CASE QI='' & OPT EQ 'V'
          RETURN
        CASE QI='EX'
          STOP
        CASE QI='X'
          CRT @(52,LINE):CLASS<2,SEL>'R#10'
          GOTO 8010
      END CASE
*
* Verify weight
*
      IF OPT='V' THEN
        IF CLASS<2,POS>=QI THEN VERIFIED=1 ELSE VERIFIED=0
        IF VERIFIED=0 THEN
          CALL ELINE('Invalid weight.')
          GOTO 8020
        END
        CLASS<3,POS>='*'
      END ELSE
        CLASS<2,SEL>=Q
      END
      CRT @(52,LINE):CLASS<2,SEL>'R#10'
*
* Print page
*
9000  START=(PAGE*9)-8
      STOP=START+8
      FOR SEL=START TO STOP
        GOSUB 9100
        IF CLASS<1,SEL>='' ! OPT='V' THEN
          IF OPT='V' THEN
            CRT @(41,LINE):@(-4):
          END ELSE
            CRT @(41,LINE):SEL'L#3':@(-4):
          END
        END ELSE
          CRT @(41,LINE):SEL'L#3':' ':CLASS<1,SEL>'R#5':'  ':CLASS<2,SEL>'R#10'
        END
      NEXT SEL
      RETURN
*
* Calculate line
*
9100  X=REM(SEL,9)
      IF X=0 THEN X=9
      IF OPT='V' THEN X=1
      LINE=6+X
      RETURN
