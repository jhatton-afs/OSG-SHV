$OPTIONS HEADER.EJECT
$OPTIONS EXEC.EQ.PERF
$OPTIONS INFO.ABORT
ERRMSG=''
* PROGRAM FB.6.2
* PROGRAM TO PROCESS ORIGIN & DESTINATION REPORT BY DIVISION (BW/IP)
*
* AUTHOR: BRIAN M. QUEEN
* DATE: July 2, 1993
*
* PACKAGE: FREIGHT BILLING
*
* Initialize variables
*
      CALL CHANNEL(CH.NUM)
      PROG.NAME='FB.6.2'
      PROG.DESC='Origin & Destination by Divisions'
      PACK.NAME='FREIGHT BILLING'
*
       UPL.VIN = 'FBBP' ;  UPL.VIN<2> = 'FB.6.2' ; UPL.VOUT = ''                    ;* NPR_UPL 04/23/2010
       CALL UPD.PROGRAM.LOG(UPL.VIN,UPL.VOUT)                                                           ;* NPR_UPL 04/23/2010
* Open files
*
      OPEN '','CLIENTS' TO F.CLIENTS ELSE CALL OPEN.ABORT('CLIENTS',PROG.NAME)
*
* Display screen heading
*
30    CALL AFS.SCR.HEAD(CO.ID,FILE.ID,'',PROG.NAME,PROG.DESC,PACK.NAME,CO.NAME,TIME.DATE,1)
*
* Get client number to process
*
50    HELP="Enter the client number you want to process. 'X'=Exit."
      CALL GEN.IN(0,3,'Enter Client Number #####','',Q,0,20,'','',2,-5,3,0,QI,HELP,0,23)
      QI=OCONV(QI,'MCU')
      BEGIN CASE
        CASE QI='EX' ! QI='X'
          STOP
        CASE QI=''
          STOP
        CASE NOT(QI MATCHES "0N")
          CALL SOUNDEX.DISPLAY(QI,'BCUST','SDX.CLIENTS,NAME',2,ITEM.LIST)
          CALL AFS.SCR.REFRESH(PROG.NAME,PROG.DESC,PACK.NAME,CO.NAME,TIME.DATE,1)
          BEGIN CASE
            CASE ITEM.LIST=''
              GOTO 50
            CASE DCOUNT(ITEM.LIST<1>,@VM) GT 1
              GOTO 50
            CASE ITEM.LIST NE ''
              IF NUM(ITEM.LIST<1,1>) THEN DATA ITEM.LIST<1,1>
              GOTO 50
          END CASE
        CASE NUM(QI)
          Q=QI'R%5'
        CASE 1
          GOTO 100
      END CASE
      CLIENT.ID=Q
      CLIENT.ID=CLIENT.ID'R%5'
      CRT @(20,3):CLIENT.ID:@(-4)
*
      READ CLIENT.REC FROM F.CLIENTS,CLIENT.ID ELSE
        CALL ELINE("ERROR - Client ":CLIENT.ID:" not on file.")
        GOTO 50
      END
      CONAME.1=CLIENT.REC<2>
      CRT @(27,3):CONAME.1
*
* Get current period beginning date
*
100   HELP='Enter the current period beginning date.'
      CALL GEN.IN(0,5,'Current period beginning date.  ':DATE()'D2/','DATE',BEG.DATE,1,8,'','',1,-8,5,0,QI,HELP,0,23)
      QI=OCONV(QI,'MCU')
      BEGIN CASE
        CASE QI='X'
          GOTO 50
        CASE QI='EX'
          STOP
      END CASE
      CRT @(32,5):BEG.DATE'D2/'
*
* Get current period ending date
*
200   END.DATE=DATE()
      HELP='Enter the current period ending date.'
      CALL GEN.IN(0,7,'Current period ending date.     ':END.DATE'D2/','DATE',Q,0,8,'','',1,-8,7,0,QI,HELP,0,23)
      QI=OCONV(QI,'MCU')
      BEGIN CASE
        CASE QI=''
          Q=END.DATE
        CASE QI='X'
          GOTO 100
        CASE QI='EX'
          STOP
      END CASE
      END.DATE=Q
      IF END.DATE LT BEG.DATE THEN
        CALL ELINE('Invalid ending date.')
        GOTO 200
      END
      CRT @(32,7):END.DATE'D2/'
*
* Get divisions to process
*
300   HELP='Enter division to process [A]=All.'
      CALL GEN.IN(0,9,'Enter division code. #####','',Q,0,5,'','',1,-5,9,0,QI,HELP,0,23)
      QI=OCONV(QI,'MCU')
      BEGIN CASE
        CASE QI='X'
          GOTO 200
        CASE QI='EX'
          STOP
        CASE QI='A'
          Q='ALL'
        CASE NUM(QI)
          Q=QI'R%5'
        CASE 1
          CALL ELINE('Invalid entry.')
          GOTO 300
      END CASE
      DIVISION=Q
      CRT @(21,9):DIVISION
*
* Build report heading
*
      VIN=''
      VIN<3>='R'
      CALL LASER.CODE(VIN,VOUT)
      IF VOUT<1>#'' THEN 
         CALL ELINE(VOUT<1>)
         STOP
      END
      IF VOUT<3>#'' THEN EXECUTE VOUT<3>
      PRINTER ON
      IF VOUT<2>#'' THEN PRINT VOUT<2>:
      CALL FULL.DATE(BEG.DATE,ALPHA.BEG.DATE)
      CALL FULL.DATE(END.DATE,ALPHA.END.DATE)
      ODATE=ALPHA.BEG.DATE:' to ':ALPHA.END.DATE
      CALL PAGE.HEAD('FB.6.2',CONAME.1,'','Origins/Destinations by Divisions',ODATE,132,HED)
      HED=HED:" 'C' For division - 'B' 'L'"
*
* Process report
*
      PREC='SORT FB.BILLS.HIST,':CLIENT.ID
      PREC=PREC:' WITH 37 GE "':BEG.DATE'D2/':'" AND WITH 37 LE "':END.DATE'D2/':'"'
      IF DIVISION NE 'ALL' THEN PREC=PREC:' AND WITH 9 = "':DIVISION:'"'
      PREC=PREC:' BY 9 BY CARRIER BREAK-ON 9 "':'Total':"'":"BVULP'":'" 1 2 CARRIER PRO.NUM ORIGIN DESTINATION'
      PREC=PREC:' TOTAL 8 GRAND-TOTAL "Report total ':"'P'":'"'
      PREC=PREC:' HEADING "':HED:'" (IP'
      EXECUTE PREC
      PRINTER OFF
      STOP
