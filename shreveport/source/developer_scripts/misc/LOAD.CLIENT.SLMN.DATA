*************************************************************************
* Program : LOAD.CLIENT.SLMN.DATA
* Desc: Load Saleman and Project Manager Data
* Author  : TAG
* Date    : 05/01/2017
*
* Copyright (c) 2016 by AFS Logistics, LLC. - All Rights Reserved



* Last updated by afs (AFSLOGISTICS\tgorton) at 10:57:47 on 05/02/2017.



* Comments
*************************************************************************
* 050117 - TAG01 - SysAid 32201 Program Created
*************************************************************************
      COMMON PROGPARAMS(24)
      EQU PROGFROM TO PROGPARAMS(1)
      EQU USER TO PROGPARAMS(2)
      EQU PROCDATE TO PROGPARAMS(3)
      EQU CHNUM TO PROGPARAMS(4)
*
      PROG.NAME = "LOAD.CLIENT.SLMN.DATA"
      CALL GET.USER.NEW(USER,CH.NUM)
      VIN1=""
      VIN1<1> = PROG.NAME
      VIN1<2> = USER
      VIN1<3> = SYSTEM(18)
      VIN1<4> = "S"
      CALL PROGTRACK(VIN1,VOUT1)
* Initialize general variables
      TODAY.DATE = OCONV(DATE(),'D2/')
      BILLDATA.USER = OCONV(USER,'MCL')
      TAB = CHAR(9)
* Open Files
      OPEN '','CLIENTS' TO F.CLIENTS ELSE CALL OPEN.ABORT('CLIENTS',PROG.NAME)
      OPEN '','BILLDATA.':BILLDATA.USER TO F.BILLDATA ELSE CALL OPEN.ABORT("BILLDATA.":BILLDATA.USER,PROG.NAME)
      OPEN '','AR.SLSMAN.1000.1' TO F.SLSMAN ELSE CALL OPEN.ABORT('AR.SLSMAN.1000.1',PROG.NAME)
      OPEN '','FB.ADJUST.CLIENT' TO F.ADJUST.CLIENT ELSE CALL OPEN.ABORT("FB.ADJUST.CLIENT",PROG.NAME)
* Read In Spreadsheet
      SS.NAME = 'ClientSlmnUpd.txt'
      READ TEST FROM F.BILLDATA,SS.NAME ELSE
         CALL ELINE('BILLDATA.':BILLDATA.USER:' ':SS.NAME:' is not on file.. Program Stopping!')
         GO END.PGM
      END
* Open Spreadsheet For Reading Data
      OPENSEQ 'BILLDATA.':BILLDATA.USER, SS.NAME TO INSEQ.FILE ELSE
         CREATE INSEQ.FILE ELSE
            CALL ELINE('Cannot Open Spreadsheet For Processing!!! Program Stopping')
            GO END.PGM
         END
      END
* Create Result Spreadsheet
      SS.STAT.NAME = 'ClientSlmnUpdResults.txt'
      OPENSEQ 'BILLDATA.':BILLDATA.USER, SS.STAT.NAME TO OUTSEQ.FILE ELSE
         CREATE OUTSEQ.FILE ELSE
            CALL ELINE('Cannot Open Spreadsheet For Writing Results!!! Program Stopping')
            GO END.PGM
         END
      END
* Start Processing
      INSEQ.DONE=0
      LOOP
         READSEQ DATA.IN FROM INSEQ.FILE ELSE INSEQ.DONE=1
      UNTIL INSEQ.DONE DO
         RESULTS.OUT = ""
         DATA.IN=CHANGE(DATA.IN,TAB,@VM)
         DATA.OUT = DATA.IN
* Header Line
         IF DATA.IN<1,1> = 'Client Name' THEN
            RESULTS.OUT := 'Results Of Change'
            GOSUB WRITE.RESULTS
            CONTINUE
         END
         CLIENT.ID = DATA.IN<1,2>'R%5'
         READU CLIENT.REC FROM F.CLIENTS,CLIENT.ID ELSE
            RESULTS.OUT := '*** Client ID: ':CLIENT.ID:' Not On File ***'
            GOSUB WRITE.RESULTS
            RELEASE F.CLIENTS,CLIENT.ID
            CONTINUE
         END
         IF DATA.IN<1,6> # "" THEN BD.CHG = DATA.IN<1,6>'R%5' ELSE BD.CHG = ""
         IF BD.CHG # "" THEN
            READ SLSM.REC FROM F.SLSMAN,BD.CHG ELSE
               RESULTS.OUT := 'BD Code :':BD.CHG:' Not On file'
            END
         END
         IF DATA.IN<1,8> # "" THEN PM.CHG = DATA.IN<1,8>'R%5' ELSE PM.CHG = ""
         IF PM.CHG # "" THEN
            READ SLSM.REC FROM F.SLSMAN,BD.CHG ELSE
               RESULTS.OUT := 'PM Code :':BD.CHG:' Not On file'
            END
         END
         IF RESULTS.OUT # "" THEN
            RESULTS.OUT := ' *** Client Record Not Updated!!! ***'
            GOSUB WRITE.RESULTS
            CONTINUE
         END
         BD.B4 = CLIENT.REC<114>
         PM.B4 = CLIENT.REC<115>
         CLIENT.REC<114> = BD.CHG
         CLIENT.REC<115> = PM.CHG
         WRITE CLIENT.REC ON F.CLIENTS, CLIENT.ID
         RELEASE F.CLIENTS,CLIENT.ID
         RESULTS.OUT := 'BD.CODE Changed From ':BD.B4:' To ':BD.CHG
         RESULTS.OUT := ' PM.CODE Changed From ':PM.B4:' To ':PM.CHG
* Update SQL Server
         SQLVIN=''
         SQLVIN<1>=USER
         SQLVIN<2>=CLIENT.ID
         SQLVOUT=''
         CALL UPD.SQL.CLIENT.SUB(SQLVIN,SQLVOUT,CLIENT.REC)
         CALL UPD.SQL.QCLIENT.SUB(SQLVIN,SQLVOUT,CLIENT.REC,F.SLSMAN)
         ADJUST.CLIENT.ID=CLIENT.ID:'*':DATE():'*':TIME():'*':USER
         WRITE CLIENT.REC ON F.ADJUST.CLIENT,ADJUST.CLIENT.ID
         GOSUB WRITE.RESULTS
      REPEAT
      CLOSESEQ OUTSEQ.FILE
      CLOSESEQ INSEQ.FILE
      GO END.PGM
*
WRITE.RESULTS:* Write Results To Spreadsheet
      DATA.OUT := @VM:RESULTS.OUT
      DATA.OUT = CHANGE(DATA.OUT,@VM,TAB)
      WRITESEQ DATA.OUT ON OUTSEQ.FILE ELSE
         CALL ELINE('Cannot Write Out Results!! Program Ending!')
         GO END.PGM
      END
      RETURN
****************
END.PGM:
****************
      CALL ELINE('Check Your Billdata folder for ClientSlmnUpdResults.txt Spreadsheet')
      VIN1=''
      VIN1<1> = PROG.NAME
      VIN1<2> = USER
      VIN1<3> = SYSTEM(18)
      VIN1<4> = "E"
      CALL PROGTRACK(VIN1,VOUT1)
      STOP
