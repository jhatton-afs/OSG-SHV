*======================================================================
* SUBROUTINE : FB.2.1.CARRIER.LETTER        - Print Carrier Incorrect Billing Address
*
* PROGRAMMER : Jim T. Row
*
* PARAMETERS:
*    VIN<1>  = CARRIER.ID
*    VIN<2>  = CLIENT.ID
*    VIN<3>  = PRO.ID
*    VIN<4>  = "P" - Print letter
*              ELSE - Email Letter
*
* RETURNS:
*    VOUT<1> = Output from EMail (if VIN<4>="P")
*
* EDIT HISTORY:
*
*    NAME :   DATE   : DESCRIPTION
*  --------------------------------------------------------------
*     JTR : 02/04/96 : Basic subroutine started.
*     JTR : 06/04/98 : Subroutine changed to EMail by default and
*         :          : to print when requested.  Default email address
*         :          : is stored in FB.TABLES CAR.ADDR.EMAIL
*   JMK01 : 05/26/04 : Change print resolution from 300 to 600
*         :          :
*======================================================================
*  COPYRIGHT (C) 1995,96,98 - AFS LOGISTICS LLC - ALL RIGHTS RESERVED
*       ----------------------------------------------------
*  This program is sole property of AFS Logistics, LLC (AUTOMATED
*  FREIGHT SYSTEMS) and may not be used, copied, or modified without
*  the express written consent of Brian Barker of AFS Logistics, LLC.
*======================================================================

      SUBROUTINE FB.2.1.CARRIER.LETTER(VIN, VOUT)

      $INCLUDE PROG.ADMIN INC_OPTIONS
      $INCLUDE FILE.DEFS INC_CLIENTS

*
* Equates
*
      EQU TRUE TO 1
      EQU FALSE TO 0
      EQU RESTRICT.ACCESS TO FALSE

*
* Process passed values
*
      CARRIER.ID = VIN<1>
      CLIENT.ID = VIN<2>
      PRO.ID = VIN<3>
      PRINT.IT = (VIN<4>="P")            ; * Boolean Logic

*
* Initialize GENERAL APPLICATION variables
*
      PROMPT''
      PORT.NUM = SYSTEM(19)              ; * Port user is sitting on
*    USER.NAME = OCONV(PORT.NUM,'MCU')       ;* User's Name
      CALL GET.USER(USER.NAME)
      DEBUG.MSG = ''
      MAT CLIENT.REC = ''
      TO.EMAIL = ''
      CC.EMAIL = ''
      BCC.EMAIL = ''

      IF RESTRICT.ACCESS THEN
         IF USER.NAME # 'JIM' THEN
            CALL ELINE('*** Underconstruction !  Try again later today! -Jim Row')
            RETURN
         END
      END

*   IF USER.NAME = 'JIM' THEN CALL ELINE('PRESS ENTER')

*
* Load Rejection Codes
*
      OPEN '','CLIENTS' TO F.CLIENTS ELSE
         CALL OPEN.ABORT("CLIENTS",PROG.NAME)
      END
      OPEN 'DICT','CLIENTS' TO F.DICT.CLIENTS ELSE
         CALL OPEN.ABORT("DICT CLIENTS",PROG.NAME)
      END
      OPEN '','CARRIERS' TO F.CARRIERS ELSE
         CALL OPEN.ABORT("CARRIERS",PROG.NAME)
      END
      OPEN 'DICT','CARRIERS' TO F.DICT ELSE
         CALL OPEN.ABORT("DICT CARRIERS",PROG.NAME)
      END
*
* Load EMail information if PRINTING is not forced.
*
      IF NOT(PRINT.IT) THEN
         OPEN '','FB.TABLES' TO F.TABLES ELSE
            CALL OPEN.ABORT("FB.TABLES",PROG.NAME)
         END
         GOSUB LOAD.CARRIER.EMAIL
      END

      GOSUB LOAD.CARRIER.DESC
      GOSUB LOAD.CLIENT.DESC

      IF PRINT.IT THEN
         GOSUB PRINT.ADDR.LETTER
      END ELSE
         GOSUB EMAIL.ADDR.LETTER
      END

      RETURN


*----------------------------------------------------------------------
* EMAIL.ADDR.LETTER
*----------------------------------------------------------------------
EMAIL.ADDR.LETTER:

      SUBJECT = 'Incorrect Billing Address (':CLIENT.ID:') ':PRO.ID
      MSG = STR('-',80)
      MSG<1,-1> ='Automated Email:  Incorrect Billing Address'
      MSG<1,-1> = STR('-',80)
      MSG<1,-1> = '':CHAR(10)
      MSG<1,-1> = '':CHAR(10)
      MSG<1,-1> = '':CHAR(10)
      MSG<1,-1> = '':CHAR(10)
      MSG<1,-1> = '':CHAR(10)
      MSG<1,-1> = '':CHAR(10)
      MSG<1,-1> = CARRIER.NAME
      MSG<1,-1> = CARRIER.ADDR
      MSG<1,-1> = CARRIER.CITY:', ':CARRIER.STATE:' ':CARRIER.ZIP
      IF CARRIER.CONTACT # '' THEN MSG<1,-1>='CONTACT:':CARRIER.CONTACT
      IF CARRIER.PHONE # '' THEN MSG<1,-1>='PHONE #:':CARRIER.PHONE
      IF CARRIER.FAX # '' THEN MSG<1,-1>='FAX   #:':CARRIER.FAX
      MSG<1,-1> = '':CHAR(10)
      MSG<1,-1> = 'RE: Incorrect Billing Address for Pro Number ':PRO.ID:CHAR(10)
      MSG<1,-1> = '':CHAR(10)
      MSG<1,-1> = '':CHAR(10)
      MSG<1,-1> = 'Dear Carrier,':CHAR(10)
      MSG<1,-1> = '':CHAR(10)
      MSG<1,-1> = 'This letter acts as a formal request to change the billing address as'
      MSG<1,-1> = 'indicated below.  Bills that are mailed to the incorrect address will'
      MSG<1,-1> = 'cause delays in your payment.  In the future please make sure that'
      MSG<1,-1> = 'all billing correspondence for:':CHAR(10)
      MSG<1,-1> = '':CHAR(10)
      MSG<1,-1> = '     ':CLIENT.NAME
      MSG<1,-1> = '     ':CLIENT.ADDR<1,1>
      IF CLIENT.ADDR<1,2> # '' THEN MSG<1,-1> = '     ':CLIENT.ADDR<1,2>
      MSG<1,-1> = '     ':CLIENT.CITY:', ':CLIENT.STATE:' ':CLIENT.ZIP
      MSG<1,-1> = '':CHAR(10)
      MSG<1,-1> = 'is mailed as follows:':CHAR(10)
      MSG<1,-1> = '':CHAR(10)
      MSG<1,-1> = '     ':CLIENT.NAME
      MSG<1,-1> = '     C/O AFS Logistics, LLC'
      MSG<1,-1> = '     PO Box 18170'
      MSG<1,-1> = '     Shreveport, LA 71138'
      MSG<1,-1> = '':CHAR(10)
      MSG<1,-1> = 'Please take the necessary steps to assure that the correct address is'
      MSG<1,-1> = 'used in future billings which will help us in providing you with prompt'
      MSG<1,-1> = 'and accurate payments.  If you have any questions concerning this matter,'
      MSG<1,-1> = 'please do not hesitate to contact us.':CHAR(10)
      MSG<1,-1> = '':CHAR(10)
      MSG<1,-1> = '':CHAR(10)

      VIN=''
      VIN<1>= TO.EMAIL                   ; * To Address(s)
      VIN<2>= CC.EMAIL                   ; * CC Address(s)
      VIN<3>= BCC.EMAIL                  ; * BCC Address(s)
      VIN<4>= 'AFS'                      ; * FROM Address (AFS@AFSLOGISTICS.COM)
      VIN<5>= SUBJECT                    ; * Subject
      VIN<6>= MSG                        ; * Message
      VOUT=''
      CALL SENDMAIL(VIN,VOUT)

      CALL ELINE("Carrier Address Correction Letter EMailed to ":TO.EMAIL)

      RETURN




*----------------------------------------------------------------------
* PRINT.ADDR.LETTER
*----------------------------------------------------------------------
PRINT.ADDR.LETTER:

      ESC=CHAR(27)
      COPIES = 1
*DF     EXECUTE 'SP-ASSIGN F15,':COPIES ; * Default to IBM 4039-16L+ Laser
      STMT="SETPTR ,,,0,0,,AT OPTRAN, BRIEF"
      EXECUTE STMT
      PRINTER ON

      X = 25
      Y = 10
      CALL GET.AFS.LOGO.IMG(X,Y,LOGO)

      LASER = ''
      LASER<1>= 0                        ; * Portrait
      LASER<2>= 6                        ; * Lines per inch
      LASER<3>= 61                       ; * Page length
      LASER<4>= 14                       ; * Characters per inch
      LASER<5>= 0
      LASER<6>= 0
      LASER<7>= 1
      LASER<8>= 0
      MARGIN = 5

      HED=''

      HED:=ESC:"&l":LASER<1>:"O"         ; * Set portrait
      HED:=ESC:"&l":LASER<2>:"D"         ; * Lines per inch
      HED:=ESC:"&l":LASER<3>:"F"         ; * Page length
      HED:=ESC:"(s":LASER<4>:"h"         ; * Set pitch
      HED:=LASER<5>:"p"                  ; * Proportional printing
      HED:=LASER<6>:"s"                  ; * Italics off
      HED:="0T"                          ; * Line Printer Font
*     HED:=ESC:"&l":LASER<7>:"H"           ;* Bin selection
      HED:=ESC:"&a":MARGIN:"L"
*     HED:=ESC:'"g':LASER<8>:'F'           ;* Paper output

      HED:=LOGO

*     HED:=ESC:'*p':(X+(1.45*300)):'x':(Y+(0.60*300)):'Y'    ; * Position Cursor; * JMK01
      HED:=ESC:'*p':(X+(.725*600)):'x':(Y+(0.30*600)):'Y'    ; * Position Cursor; * JMK01
     ****       HED:='Corporate Headquarters 330 Marshall Street, Suite 400 - Shreveport, LA 71101 - 318-798-2111'
                HED:='Corporate Headquarters 670 Emberwood Drive - Shreveport, LA 71106 - 318-798-2111'
*     HED:=ESC:'*p':X+(1.45*300):'x':Y+(.79*300):'Y'         ; * JMK01
      HED:=ESC:'*p':X+(.725*600):'x':Y+(.395*600):'Y'        ; * JMK01
      HED:='Mailing Address: P.O. Box 18170 - Shreveport, LA 71138 - Fax 318-797-3628'
*     HED:=ESC:'*p0x':Y+(1.25*300):'Y'   ; * JMK01
      HED:=ESC:'*p0x':Y+(.625*600):'Y'   ; * JMK01
      LASER<4>= 10                       ; * Characters per inch
      HED:= ESC:"(s":LASER<4>:"h"        ; * Set pitch
      PRINT HED
      PRINT
      PRINT
      PRINT CARRIER.NAME
      PRINT CARRIER.ADDR
      PRINT CARRIER.CITY:', ':CARRIER.STATE:' ':CARRIER.ZIP
*     IF CARRIER.CONTACT # '' THEN PRINT 'CONTACT:':CARRIER.CONTACT
      IF CARRIER.PHONE # '' THEN PRINT CARRIER.PHONE'R(###-###-####)'
      IF CARRIER.FAX # '' THEN PRINT 'FAX:':CARRIER.FAX'R(###-###-####)'
      PRINT
*     PRINT 'RE: Incorrect Billing Address for Pro Number ':FIELD(PRO.ID,'*',2)
      PRINT 'RE: Incorrect Billing Address'
      PRINT
      PRINT
      PRINT 'Dear Carrier,'
      PRINT
*     PRINT 'This letter acts as a formal notice that the above freight bill was'
      PRINT 'This letter acts as a formal request to change the billing address as'
      PRINT 'indicated below.  Bills that are mailed to the incorrect address will'
      PRINT 'cause delays in your payment.  In the future please make sure that'
      PRINT 'all billing correspondence for:'
*     PRINT 'mailed to the incorrect billing address which causes delays in your'
*     PRINT 'payment.  In the future please make sure that all billing correspondence'
*     PRINT 'for :'
      PRINT
      PRINT '     ':CLIENT.NAME
      PRINT '     ':CLIENT.ADDR<1,1>
      IF CLIENT.ADDR<1,2> # '' THEN PRINT '     ':CLIENT.ADDR<1,2>
      PRINT '     ':CLIENT.CITY:', ':CLIENT.STATE:' ':CLIENT.ZIP
      PRINT
      PRINT 'is mailed as follows:'
      PRINT
      PRINT '     ':CLIENT.NAME
      PRINT '     C/O AFS Logistics, LLC'
      PRINT '     PO Box 18170'
      PRINT '     Shreveport, LA 71138'
      PRINT
      PRINT 'Please take the necessary steps to assure that the correct address is'
      PRINT 'used in future billings which will help us in providing you with prompt'
      PRINT 'and accurate payments.  If you have any questions concerning this matter,'
      PRINT 'please do not hesitate to contact us.'
      PRINT
      PRINT
*     PRINT ESC:'*p':(00):'x':(9*300):'Y'          ; * Position Cursor; * JMK01
      PRINT ESC:'*p':(00):'x':(4.5*600):'Y'        ; * Position Cursor; * JMK01
      PRINT 'SIGNATURE:':STR('_',40):SPACE(8):'DATE: ':DATE()"D2/":
      PRINT
      PRINT
*     PRINT 'REFERENCE :':CLIENT.ID:'*':PRO.ID

      CRT @(0,0):
      PRINTER OFF
      PRINTER CLOSE

      CALL ELINE("Carrier Address Correction Letter printed.")

      RETURN




*--------------------------------------------------------------------------
* Load Client Description
*
*  Loads the CLIENT.REC and set CLIENT.NAME
*
*  Sets FAILURE flag to TRUE if errors occur.. No other values are changed
*--------------------------------------------------------------------------
LOAD.CLIENT.DESC:
*
* Read Client Information
*
      MATREAD CLIENT.REC FROM F.CLIENTS,CLIENT.ID THEN
         CLIENT.NAME = TRIM(CLIENT.REC(2))
         CLIENT.ADDR = TRIM(CLIENT.REC(3))
         CLIENT.CITY = TRIM(CLIENT.REC(4))
         CLIENT.STATE = TRIM(CLIENT.REC(5))
         CLIENT.ZIP = TRIM(CLIENT.REC(6))
      END ELSE
         CALL ELINE("Unable to load Client Information for Client ID ":CLIENT.ID)
         STOP
      END

      RETURN



*--------------------------------------------------------------------------
* Load Carrier Description
*
*  Loads the CARRIER.REC and set CARRIER.NAME, CARRIER.CITY,
*  and CARRIER.STATE info based upon the variable CARRIER.ID
*
*  Sets FAILURE flag to TRUE if errors occur.. No other values are changed
*--------------------------------------------------------------------------
LOAD.CARRIER.DESC:
*
* Read Carrier Information
*
      READ CARRIER.REC FROM F.CARRIERS, CARRIER.ID THEN
         T.FAIL = 0
         T.OFFSET = 0
         FOR T.X = 31 TO 35
            IF CARRIER.REC<T.X> = '' THEN T.FAIL+=1
         NEXT
         IF T.FAIL = 0 THEN T.OFFSET=30
         CARRIER.NAME = TRIM(CARRIER.REC<1+T.OFFSET,1>)
         CARRIER.ADDR = TRIM(CARRIER.REC<2+T.OFFSET,1>)
         CARRIER.CITY = TRIM(CARRIER.REC<3+T.OFFSET,1>)
         CARRIER.STATE = TRIM(CARRIER.REC<4+T.OFFSET,1>)
         CARRIER.ZIP = TRIM(CARRIER.REC<5+T.OFFSET,1>)
         CARRIER.PHONE = TRIM(CARRIER.REC<6,1>)
         CARRIER.FAX = TRIM(CARRIER.REC<8,1>)
         CARRIER.CONTACT=TRIM(CARRIER.REC<9,1>)
      END ELSE
         CALL ELINE("Unable to load Carrier Information for Carrier ID ":CARRIER.ID)
         STOP
      END

      RETURN


*--------------------------------------------------------------------------
* Load Carrier Email
*
*  Loads the CAR.ADDR.EMAIL record and set TO.EMAIL and CC.EMAIL
*--------------------------------------------------------------------------
LOAD.CARRIER.EMAIL:
*
* Read Carrier Information
*
      READ CAR.ADDR.EMAIL FROM F.TABLES,"CAR.ADDR.EMAIL" THEN
         TO.EMAIL = CAR.ADDR.EMAIL<1>
         CC.EMAIL = CAR.ADDR.EMAIL<2>
      END ELSE
*
* Error loading Email address information - Force a PRINT and do not email.
*
         T = "Unable to load Carrier Email Information from FB.TABLES, CAR.ADDR.EMAIL"
         T<-1> = "Letter will be printed"
         CALL ELINE(T)
         PRINT.IT = TRUE
      END

      RETURN
