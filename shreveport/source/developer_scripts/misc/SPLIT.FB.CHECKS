* SPLIT.FB.CHECKS
*
* WRITTEN BY JAY WEBERT, AFS LOGISTICS, 08/28/06
*
* THIS PROGRAM SPLITS THE FB.CHECKS FILE INTO TWO RELATED
* FILES; FB.CHECKS.HDR AND FB.CHECKS.DTL.
*-----------------------------------------------
      EXECUTE 'SLEEP 23:00'
      EXECUTE 'DATE'
*-----------------------------------------------
      PROG.NAME = 'SPLIT.FB.CHECKS'
      OPEN 'FB.CHECKS' TO F.CHECKS ELSE
        CALL OPEN.ABORT('FB.CHECKS', PROG.NAME)
      END
      OPEN 'FB.CHECKS.HDR' TO F.HDR ELSE
        CALL OPEN.ABORT('FB.CHECKS.HDR', PROG.NAME)
      END
      OPEN 'FB.CHECKS.DTL' TO F.DTL ELSE
        CALL OPEN.ABORT('FB.CHECKS.DTL', PROG.NAME)
      END
      OPEN 'FB.BILLS.CRX' TO F.CRX ELSE
        CALL OPEN.ABORT('FB.BILLS.CRX', PROG.NAME)
      END
*-----------------------------------------------
      STMT = 'SELECT FB.CHECKS WITH 2 GE "01/01/94" AND WITH 2 LE "12/31/95"'
      CRT STMT
      EXECUTE STMT
      IF @SELECTED < 1 THEN
        CRT 'NO FB.CHECKS RECORDS SELECTED; PROGRAM STOPPED.'
        STOP
      END
      SEL.CNT = @SELECTED
      REC.CNT = 0
      DONE = 0
      LOOP UNTIL DONE DO
        READNEXT CHECK.ID THEN
          REC.CNT += 1
          IF MOD(REC.CNT,10000) = 0 THEN
            CRT REC.CNT:' OF ':SEL.CNT:' RECORD PROCESSED.' 
          END
          READ CHECK.REC FROM F.CHECKS, CHECK.ID THEN
            HDR.REC = CHECK.REC
            IF HDR.REC<7,1> # 'Void' THEN HDR.REC<7> = ''
            HDR.REC<11> = ''
            HDR.REC<12> = ''
            AMTS = CHECK.REC<12> ; *** INVIVIDUAL BILL AMOUNTS
            VOID = (OCONV(CHECK.REC<7,1>,'MCU') = 'VOID')
            IF VOID THEN
              BILL.IDS = CHECK.REC<11> ; *** BILL IDS FROM VOIDED CHECK
            END ELSE
              BILL.IDS = CHECK.REC<7>  ; *** BILL IDS
            END
            DTL.CNT = DCOUNT(BILL.IDS,CHAR(253)) ; *** NUMBER OF BILL IDS
            HDR.REC<25> = DTL.CNT
            FOR I = 1 TO DTL.CNT
              DTL.REC = ''
              DTL.ID = CHECK.ID:'*':I
              BILL.ID = BILL.IDS<1,I>
              READV CLIENT.ID FROM F.CRX, BILL.ID, 1 ELSE CLIENT.ID = ''
              DTL.REC<1> = BILL.ID
              DTL.REC<2> = AMTS<1,I>
              DTL.REC<3> = CLIENT.ID
              WRITE DTL.REC ON F.DTL, DTL.ID
            NEXT I
            WRITE HDR.REC ON F.HDR, CHECK.ID
          END ELSE
            CRT 'COULD NOT READ FB.CHECKS RECORD ':CHECK.ID
          END
        END ELSE
          DONE = 1
        END
      REPEAT
*---------------------------------------------
      EXECUTE 'DATE'

