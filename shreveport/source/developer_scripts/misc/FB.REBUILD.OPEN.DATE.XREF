*******************************************************
* Program name : FB.REBUILD.OPEN.DATE.XREF
* Author       : UNKNOWN
* Date written : UNKNOWN
* Library      : FBBP
* COPYRIGHT (C) 2016 - AFS LOGISTICS LLC - ALL RIGHTS RESERVED.
*
* Last updated by afs (AFSLOGISTICS\nmeddaugh) at 09:22:51 on 04/03/2019.
*
* Start modification history
* 05/10/06 : JCW01 : CHANGES TO @SELECTED, CLEARSELECT, AND STMT<X>
* 04/03/19 : NKH01 : ACCU-316 Modified CREATE-FILE file sizes
* End modification history
*
*******************************************************
      CRT @(-1):
      PROG.NAME='FB.REBUILD.OPEN.DATE.XREF'
      OPEN '','CLIENTS' TO F.CLIENTS ELSE
         CALL OPEN.ABORT('CLIENTS',PROG.NAME)
      END
      OPEN '','FB.OPEN.BILLS' TO F.OPEN ELSE
         CALL OPEN.ABORT('FB.OPEN.BILLS',PROG.NAME)
      END
      PRINT 'WARNING! This process CLEARS and REBUILDS the FB.OPEN.DATE.XREF,nnnnn Files!'
      PRINT 'No Freight Bills should be processed until this process in complete!'
      PRINT 'Do not select bills for payment while this program is running'
      PRINT 'Press [Enter] to begin or any other key to terminate'
      INPUT NUL,1
      IF NUL='' ELSE STOP
**************************************** Clear Part ********************
      CRT @(0,17):"Now Selecting FB.OPEN.DATE.XREF Data Files to be Cleared":@(-4):
      STMT="SELECT VOC 'FB.OPEN.DATE.XREF' *A7"
      EXECUTE STMT RTNLIST CLEAR.LIST CAPTURING OUTPUT
      FIRST.ITEMS = @SELECTED
*     IF NOT( @SELECTED ) THEN GO REBUILD ; * JCW01
      IF @SELECTED < 1 THEN GO REBUILD    ; * JCW01
      COUNTER.1=0
      PER=0
      LOOP
         READNEXT CLEAR.LIST.ID FROM CLEAR.LIST ELSE CLEAR.LIST.ID='DONE'
      UNTIL CLEAR.LIST.ID='DONE' DO
         COUNTER.1=COUNTER.1+1
         READV CLIENT.NAME FROM F.CLIENTS,CLEAR.LIST.ID,2 ELSE
            CLIENT.NAME='*** Not On File ***'
         END
         CRT @(0,17):"Now Clearing Open Bill Xref for ":CLEAR.LIST.ID:" ":CLIENT.NAME:@(-4):
         CALL PROG.BAR(20,COUNTER.1,FIRST.ITEMS,PER.1)
         EXECUTE "CLEAR-FILE DATA FB.OPEN.DATE.XREF,":CLEAR.LIST.ID CAPTURING OUTPUT
      REPEAT
**************************************** Rebuild Part ******************
*CALL ELINE('PRESS BREAK')
REBUILD: *** Select and Rebuild File
      LAST.CLIENT='START'
      CRT @(0,17):"Now Selecting FB.OPEN.BILLS to Rebuild FB.OPEN.DATE.XREF files....":@(-4):
      STMT='SSELECT FB.OPEN.BILLS BY CLIENT.NAME BY CLIENT# BY 37'
      EXECUTE STMT RTNLIST DO.LIST CAPTURING OUTPUT
      SECOND.ITEMS = @SELECTED
*     CRT @(0,17):CLEAR.LIST.ID:" ":CLIENT.NAME:" - Now processing ":SECOND.ITEMS:" Freight Bills":@(-4):
      COUNTER.2=0
      LOOP
         DONE=0
         READNEXT OPEN.ID FROM DO.LIST ELSE
            DONE=1
            IF OPEN.LIST#'' THEN
               WRITE OPEN.LIST ON F.OPEN.DATE.XREF,LAST.DATE
            END
         END
      UNTIL DONE=1 DO
         COUNTER.2=COUNTER.2+1
*       PRINT @(0,23):COUNTER.2'R(%6 )':OPEN.ID:
*       CRT @(0,17):CLEAR.LIST.ID:' ':OPEN.ID:@(-4):
         IF SECOND.ITEMS>0 THEN CALL PROG.BAR(22,COUNTER.2,SECOND.ITEMS,PER.2)
         READ DBILL FROM F.OPEN,OPEN.ID THEN
            PROC.DATE=DBILL<37>
            CLIENT=FIELD(OPEN.ID,"*",1)
            IF CLIENT#LAST.CLIENT THEN
               IF LAST.CLIENT='START' ELSE
                  WRITE OPEN.LIST ON F.OPEN.DATE.XREF,LAST.DATE
               END
               OPEN.LIST=''
               LAST.DATE=PROC.DATE
               LAST.CLIENT=CLIENT
               READV CLIENT.NAME FROM F.CLIENTS,CLIENT,2 ELSE
                  CLIENT.NAME='*** Not On File ***'
               END
               CRT @(0,17):"Processing ":PROC.DATE'D2/':" Bills for ":CLIENT:" ":CLIENT.NAME:@(-4):
               OPEN '','FB.OPEN.DATE.XREF,':CLIENT TO F.OPEN.DATE.XREF ELSE
                  EXECUTE 'CREATE-FILE DATA FB.OPEN.DATE.XREF,':CLIENT:' 23,1' CAPTURING OUTPUT    ; * NKH01
                  OPEN '','FB.OPEN.DATE.XREF,':CLIENT TO F.OPEN.DATE.XREF ELSE
                     CALL ELINE("Cannot Create FB.OPEN.DATE.XREF,":CLIENT:" Bill Skipped ":OPEN.ID)
                     GO SKIP.BILL
                  END
               END
            END
            IF PROC.DATE#LAST.DATE THEN
               WRITE OPEN.LIST ON F.OPEN.DATE.XREF,LAST.DATE
               OPEN.LIST=''
               LAST.DATE=PROC.DATE
            END
            OPEN.LIST<-1>=OPEN.ID
         END
SKIP.BILL: ***
      REPEAT
