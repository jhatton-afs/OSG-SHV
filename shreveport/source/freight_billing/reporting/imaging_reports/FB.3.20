      $OPTIONS HEADER.EJECT
      ERRMSG=''
*
      $OPTIONS EXEC.EQ.PERF
      $OPTIONS INFO.ABORT
*
* PROGRAM
* PROGRAM TO PRINT REPORT OF CLIENTS PAYMENT TYPES BY NAME
* 011310 - JMK01 - C100047 - Client Payment Types
*
*
*
*
************************************************************************
*
* Initialize variables
*
      PROMPT''
      PROG.NAME='FB.3.20'
      PROG.DESC='Report of Client Payment Types'
      PACK.NAME='Report'
      CALL GET.USER(USER) ; * JMK01
       UPL.VIN = 'FBBP' ;  UPL.VIN<2> = 'FB.3.20' ; UPL.VOUT = ''                    ;* NPR_UPL 04/23/2010
       CALL UPD.PROGRAM.LOG(UPL.VIN,UPL.VOUT)                                                           ;* NPR_UPL 04/23/2010
*
      TB=CHAR(9)
      ESC=CHAR(27)
      TODAY=DATE()
      OTODAY=OCONV(TODAY,'D4/')
      MM=FIELD(OTODAY,'/',1)
      DD=FIELD(OTODAY,'/',2)
      YY=FIELD(OTODAY,'/',3)
*
* Open files
*
      OPEN '','CLIENTS' TO F.CLIENTS ELSE
         CALL OPEN.ABORT('CLIENTS',PROG.NAME)
         GOTO THE.END
      END
      OPEN '','AR.SLSMAN.1000.1' TO F.SLSMAN ELSE
         CALL OPEN.ABORT('AR.SLSMAN.1000.1',PROG.NAME)
         GOTO THE.END
      END
      OPEN '','VOC' TO F.VOC ELSE
         CALL OPEN.ABORT('VOC',PROG.NAME)
         GOTO THE.END
      END
      READ BILLDATA.REC FROM F.VOC,'BILLDATA' ELSE
         CALL ELINE('Unable to open VOC BILLDATA - Call IS Department.')
         GOTO THE.END
      END
      BILLDATA.REC<2>:='\':USER
      WRITE BILLDATA.REC ON F.VOC,'BILLDATA.':USER ELSE
         CALL ELINE('Your user name ':USER:' is not in the BILLDATA folder - Call IS Department.')
         GOTO THE.END
      END
      OPEN '','BILLDATA.':USER TO F.BILLDATA ELSE
         CALL OPEN.ABORT('BILLDATA.':USER,PROG.NAME)
         GOTO THE.END
      END
*
* Display screen heading
*
30:   CALL AFS.SCR.HEAD(CO.ID,FILE.ID,'',PROG.NAME,PROG.DESC,PACK.NAME,CO.NAME,TIME.DATE,1)
*
* DISPLAY SELECTIONS
*
      CRT @(28,06):"SELECTION OPTIONS"
      CRT @(25,07):"-----------------------"
      CRT @(25,09):"Payment Type"
      CRT @(25,10):"Active/Inactive" ; * JMK01
      CRT @(25,11):"Print/File"      ; * JMK01
*
100: * GET TYPE OF REPORT TO RUN
*
      COL=45 ; ROW=9
      HELP = "Enter Payment Type to Report. W=Wire, D=DDS, C=Check, <RTN>=All, '<'=Exit."
      CALL GEN.IN(COL,ROW,'','',Q,0,2,'','',1,COL,ROW,0,QI,HELP,0,23)
      BEGIN CASE
         CASE QI EQ '<'
            RETURN
         CASE QI EQ 'W'
         CASE QI EQ 'D'
         CASE QI EQ 'C'
         CASE QI EQ ''
            Q='ALL'
         CASE 1
            GO 100
      END CASE
      PYMT.TYPE=Q
      CRT @(COL,ROW):PYMT.TYPE
*
200: * GET STATUS OF REPORT TO RUN
*
      COL=45 ; ROW=10
      HELP = "Enter Client Status to Report. A=Active, I=Inactive, <RTN>=All, '<'=Back."
      CALL GEN.IN(COL,ROW,'','',Q,0,2,'','',1,COL,ROW,0,QI,HELP,0,23)
      BEGIN CASE
         CASE QI EQ '<'
            GO 100
         CASE QI EQ 'A'
         CASE QI EQ 'I'
         CASE QI EQ ''
            Q='ALL'
         CASE 1
            GO 200
      END CASE
      CLIENT.TYPE=Q
      CRT @(COL,ROW):CLIENT.TYPE
*
300: * GET STATUS OF REPORT TO RUN
*
      COL=45 ; ROW=11
      HELP = "Enter Client Status to Report. P=Printer, <RTN>=File, '<'=Back."
      CALL GEN.IN(COL,ROW,'','',Q,0,2,'','',1,COL,ROW,0,QI,HELP,0,23)
      BEGIN CASE
         CASE QI EQ '<'
            RETURN
         CASE QI EQ 'P'
         CASE QI EQ 'F'
         CASE QI EQ ''
            Q='F'
         CASE 1
            GO 200
      END CASE
      RPT.TYPE=Q
      CRT @(COL,ROW):RPT.TYPE
      BEGIN CASE
         CASE RPT.TYPE='P'
*
* Print report
*
            HED=''
            CALL PAGE.HEAD(PROG.NAME,'AFS Logistics, LLC','',PROG.DESC,'',132,HED)
            HED.PTYP="With Payment Type (": PYMT.TYPE:")'CL'"

            EXECUTE 'TERM ,,,,,,180,80'
*     EXECUTE 'SP-ASSIGN F15,1 O' ; * Default to IBM 4039-16L+ Laser
            PRINTER ON
            LASER = ''
            LASER<1>= 0                        ; * LANDSCAPE
            LASER<2>= 8                        ; * Lines per inch
            LASER<3>= 84                       ; * Page length
            LASER<4>= 16.7                     ; * Characters per inch
            LASER<5>= 0
            LASER<6>= 0
            LASER<7>= 1
            LASER<8>= 0
            MARGIN = 0
            PRINT ESC:"E"
            PRINT ESC:"&l":LASER<1>:"O":       ; * Set landscape
            PRINT ESC:"&l":LASER<2>:"D":       ; * Lines per inch
            PRINT ESC:"&l":LASER<3>:"F":       ; * Page length
            PRINT ESC:"(s":LASER<4>:"h":       ; * Set pitch
            PRINT LASER<5>:"p":                ; * Proportional printing
            PRINT LASER<6>:"s":                ; * Italics off
            PRINT "0T":                        ; * Line Printer Font
            PRINT ESC:"&l":LASER<7>:"H":       ; * Bin selection
            PRINT ESC:"&a":MARGIN:"L":
            PRINT ESC:'"g':LASER<8>:'F':       ; * Paper output

            STMT='SORT CLIENTS WITH @ID # "99]"'
            IF PYMT.TYPE#'ALL' THEN
               STMT:=' WITH 152 = "':PYMT.TYPE:'"'
            END
            IF CLIENT.TYPE#'ALL' THEN ; * JMK01
               IF PYMT.TYPE#'ALL' THEN ; *JMK01
                  STMT:=' AND' ; *JMK01
               END ; *JMK01
               BEGIN CASE ; * JMK01
                  CASE CLIENT.TYPE='A' ; * JMK01
                     STMT:=' WITH STATUS # "I"' ; * JMK01
                  CASE CLIENT.TYPE='I' ; * JMK01
                     STMT:=' WITH STATUS = "I"' ; *JMK01
               END CASE ; * JMK01
            END ; * JMK01
            STMT:=' BY NAME.40'
*           STMT:=' CLIENT# NAME.40 152'       ; * JMK01
            STMT:=' CLIENT# NAME.40 LEAD.SALESMAN SECOND.SALESMAN 152'       ; * JMK01
            STMT:=' HEADING "':HED:HED.PTYP:'"':" (I,P"
            EXECUTE STMT
            NUM.ITEMS=@SELECTED
            IF NUM.ITEMS+0 = 0 THEN
               PRINTER ON
               HEADING HED:HED.PTYP
               PRINT 'No Items Present...'
               PRINTER OFF
            END
*
            EXECUTE 'TERM ,,,,,,79,60'
         CASE RPT.TYPE='F'
*
* Download report
*
            EXP.REC=''
            EXP.ID='CLIENT_PYMT_TYPES-':YY:MM:DD:'.XLS'

            EXP.LINE=''
            EXP.LINE<1,1>=PROG.NAME
            EXP.LINE<1,3>='AFS Logistics, LLC'
            EXP.LINE<1,5>=TIMEDATE()
            EXP.REC<-1>=CHANGE(EXP.LINE,@VM,TB)
            
            EXP.LINE=''
            EXP.LINE<1,1>=USER
            EXP.LINE<1,3>=PROG.DESC
            IF PYMT.TYPE#'ALL' THEN
               EXP.LINE:=' With Payment Type (': PYMT.TYPE:')'
            END
            IF CLIENT.TYPE#'ALL' THEN
               IF PYMT.TYPE#'ALL' THEN
                  EXP.LINE:=' and'
               END
               BEGIN CASE ; * JMK01
                  CASE CLIENT.TYPE='A' ; * JMK01
                     EXP.LINE:=' With Client Status (Active)'
                  CASE CLIENT.TYPE='I' ; * JMK01
                     EXP.LINE:=' With Client Status (Inactive)'
               END CASE ; * JMK01
            END
            EXP.REC<-1>=CHANGE(EXP.LINE,@VM,TB)
            
            EXP.LINE=''
            EXP.LINE<1,1>='Client #'
            EXP.LINE<1,2>='Client Name'
            EXP.LINE<1,3>='Estab Salesman'
            EXP.LINE<1,4>='Service Salesman'
            EXP.LINE<1,5>='Pymt Type'
            EXP.REC<-1>=CHANGE(EXP.LINE,@VM,TB)

            STMT='SSELECT CLIENTS WITH @ID # "99]"'
            IF PYMT.TYPE#'A' THEN
               STMT:=' WITH 152 = "':PYMT.TYPE:'"'
            END
            IF CLIENT.TYPE#'ALL' THEN ; * JMK01
               IF PYMT.TYPE#'ALL' THEN ; *JMK01
                  STMT:=' AND' ; *JMK01
               END ; *JMK01
               BEGIN CASE ; * JMK01
                  CASE CLIENT.TYPE='A' ; * JMK01
                     STMT:=' WITH STATUS # "I"' ; * JMK01
                  CASE CLIENT.TYPE='I' ; * JMK01
                     STMT:=' WITH STATUS = "I"' ; *JMK01
               END CASE ; * JMK01
            END ; * JMK01
            STMT:=' BY NAME.40'
            EXECUTE STMT RTNLIST CLIENT.LIST CAPTURING EXP.RESULTS
            NUM.ITEMS=@SELECTED
            IF NUM.ITEMS+0 = 0 THEN
               PRINT 'No Items Present...'
            END ELSE
               CLIENT.EOF=0
               CLIENT.CNT=0
               LOOP
               READNEXT CLIENT.ID FROM CLIENT.LIST ELSE CLIENT.EOF=1
               UNTIL CLIENT.EOF DO
                  CLIENT.CNT+=1
                  CALL PROG.BAR(20,CLIENT.CNT,NUM.ITEMS,CLIENT.PER)
                  IF CLIENT.ID[1,2]='99' THEN GOTO NEXT.CLIENT
                  READ CLIENT.REC FROM F.CLIENTS,CLIENT.ID THEN
                     CLIENT.NAME=CLIENT.REC<2>
                     SLSMN1=CLIENT.REC<114>
                     READ SLSMN1.REC FROM F.SLSMAN,SLSMN1 ELSE SLSMN1.REC=''
                     SLSMN2=CLIENT.REC<115>
                     READ SLSMN2.REC FROM F.SLSMAN,SLSMN2 ELSE SLSMN2.REC=''
                     PYMT.CODE=CLIENT.REC<152>
                     SLSMN1.NAME=SLSMN1.REC<1>
                     IF SLSMN1.NAME='' THEN SLSMN1.NAME='** Unknown **'
                     SLSMN2.NAME=SLSMN2.REC<1>
                     IF SLSMN2.NAME='' THEN SLSMN2.NAME='** Unknown **'
                     BEGIN CASE
                        CASE PYMT.CODE='C' ; PYMT.DESC='Check'
                        CASE PYMT.CODE='D' ; PYMT.DESC='DDS'
                        CASE PYMT.CODE='W' ; PYMT.DESC='Wire'
                     END CASE
                     EXP.LINE=''
                     EXP.LINE<1,1>=CLIENT.ID
                     EXP.LINE<1,2>=CLIENT.NAME
                     EXP.LINE<1,3>=SLSMN1.NAME
                     EXP.LINE<1,4>=SLSMN2.NAME
                     EXP.LINE<1,5>=PYMT.DESC
                     EXP.REC<-1>=CHANGE(EXP.LINE,@VM,TB)
                  END
NEXT.CLIENT:*
               REPEAT
            END
            WRITE EXP.REC ON F.BILLDATA,EXP.ID
            CALL ELINE('This report is on your "U:\':USER:'" drive called ':EXP.ID)
*
      END CASE
THE.END: *
      CRT @(0,0):@(-1)
      STOP
