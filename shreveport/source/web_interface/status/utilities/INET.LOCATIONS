*
* FREIGHT BILLING PACKAGE
*
* AUTHOR: TAMI SEAGO
* DATE: SEPTEMBER 6, 2000
*
* THIS PROGRAM IS A PROPRIETARY PRODUCT OF AUTOMATED FREIGHT SYSTEMS
* AND MAY NOT BE USED, COPIED OR MODIFIED WITHOUT THE EXPRESS
* WRITTEN CONSENT OF SAME.
*
****************************************************************
* Initialize variables
****************************************************************
      $INCLUDE FILE.DEFS INC_CLIENTS

      OPEN '','LOCATIONS' TO F.LOC ELSE
         CRT "%ERROR:CANNOT OPEN FILE LOCATIONS"
         STOP
      END
      OPEN '','ZIPS.CODES' TO F.ZIPS ELSE
         CALL OPEN.ABORT("ZIPS",PROG.NAME)
      END
      OPEN '','CAN.ZIP.CODES' TO F.CAN.ZIPS ELSE
         CALL OPEN.ABORT("CAN.ZIP.CODES",PROG.NAME)
      END

      EXECUTE 'SSELECT LOCATIONS' PASSLIST CAPTURING RESULTS
      NUM.ITMS=@SELECTED
      IF NUM.ITMS THEN
         XML.REC='<?xml version="1.0" encoding="ISO-8859-1" ?> '
         XML.REC:='<locations>'
         EOF=0
         CNT=0
         LOOP
            READNEXT LOC.ID ELSE EOF=1
         UNTIL EOF DO
            CNT+=1
            READ LOC.REC FROM F.LOC,LOC.ID THEN
               NAME=LOC.REC<1>
               PADDR1=LOC.REC<2,1>
               PADDR2=LOC.REC<2,2>
               PZIP=LOC.REC<3>
               IF PZIP='' THEN
                  PCITY=''
                  PSTATE=''
               END ELSE
                  ZIP.ID=PZIP
                  GOSUB READ.ZIP
                  IF ZIP.DESC # 'ERROR' THEN
                     GOOD.ANS = 1
                     PCITY = ZIP.REC<1>
                     PSTATE = ZIP.REC<2>
                  END
               END
               MADDR1=LOC.REC<4,1>
               MADDR2=LOC.REC<4,2>
               MZIP=LOC.REC<5>
               IF MZIP='' THEN
                  MCITY=''
                  MSTATE=''
               END ELSE
                  ZIP.ID=MZIP
                  GOSUB READ.ZIP
                  IF ZIP.DESC # 'ERROR' THEN
                     GOOD.ANS = 1
                     MCITY = ZIP.REC<1>
                     MSTATE = ZIP.REC<2>
                  END
               END
               VPHONE=LOC.REC<6>
               PH.TYPE=LOC.REC<7>
               FPHONE=LOC.REC<8>
               FX.TYPE=LOC.REC<9>
               XML.REC:='<location>'
               XML.REC:='<pickID>':LOC.ID:'</pickID>'
               XML.REC:='<name>':NAME:'</name>'
               XML.REC:='<physicalAddress>'
               IF PADDR1#'' THEN
                  XML.REC:='<streetAddress1>':PADDR1:'</streetAddress1>'
               END
               IF PADDR2#'' THEN
                  XML.REC:='<streetAddress2>':PADDR2:'</streetAddress2>'
               END
               IF PCITY#'' THEN
                  XML.REC:='<city>':PCITY:'</city>'
               END
               IF PSTATE#'' THEN
                  XML.REC:='<state>':PSTATE:'</state>'
               END
               IF PZIP#'' THEN
                  XML.REC:='<zip>':PZIP:'</zip>'
               END
               XML.REC:='</physicalAddress>'
               XML.REC:='<mailingAddress>'
               IF MADDR1#'' THEN
                  XML.REC:='<streetAddress1>':MADDR1:'</streetAddress1>'
               END
               IF MADDR2#'' THEN
                  XML.REC:='<streetAddress2>':MADDR2:'</streetAddress2>'
               END
               IF MCITY#'' THEN
                  XML.REC:='<city>':MCITY:'</city>'
               END
               IF MSTATE#'' THEN
                  XML.REC:='<state>':MSTATE:'</state>'
               END
               IF MZIP#'' THEN
                  XML.REC:='<zip>':MZIP:'</zip>'
               END
               XML.REC:='</mailingAddress>'
               IF VPHONE#'' THEN
                  XML.REC:='<phone>'
                  NVAL=DCOUNT(VPHONE<1>,@VM)
                  FOR VAL=1 TO NVAL
                     PHONE=VPHONE<1,VAL>
                     DESC=PH.TYPE<1,VAL>
                     IF PHONE#'' THEN
                        PHONE=PHONE'R###-###-####'
                        XML.REC:='<number description="':DESC:'">':PHONE:'</number>'
                     END
                  NEXT VAL
                  XML.REC:='</phone>'
               END
               IF FPHONE#'' THEN
                  XML.REC:='<fax>'
                  NVAL=DCOUNT(FPHONE<1>,@VM)
                  FOR VAL=1 TO NVAL
                     FAX=FPHONE<1,VAL>
                     DESC=FX.TYPE<1,VAL>
                     IF FAX#'' THEN
                        FAX=FAX'R###-###-####'
                        XML.REC:='<number description="':DESC:'">':FAX:'</number>'
                     END
                  NEXT VAL
                  XML.REC:='</fax>'
               END
               XML.REC:='</location>'
            END
         REPEAT
         XML.REC:='</locations>'
      END
      CRT XML.REC
      STOP
*********************************************************************
READ.ZIP: * SUBROUTINE TO RETRIEVE CITY AND STATE
*********************************************************************
      BEGIN CASE
         CASE LEN(ZIP.ID)=5
            READ ZIP.REC FROM F.ZIPS, ZIP.ID ELSE
               ZIP.REC = "~~~ Unknown ~~~"
            END
            ZIP.DESC = ZIP.REC<1> : ' ' : ZIP.REC<2>
         CASE LEN(ZIP.ID)=6
            READ ZIP.REC FROM F.CAN.ZIPS, ZIP.ID ELSE
               ZIP.REC = '~~~ Unknown ~~~'
            END
            ZIP.DESC = ZIP.REC<1>:' ':ZIP.REC<2>
         CASE 1
            CALL ELINE('Invalid entry.')
            ZIP.DESC='ERROR'
      END CASE
      RETURN

