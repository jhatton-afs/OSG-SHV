      SUBROUTINE FB.1.1.ALIAS(CUST.REC,DISPLAY.MODE)
$OPTIONS EXEC.EQ.PERF
 UPL.VIN = 'FBBP' ;  UPL.VIN<2> = 'FB.1.1.ALIAS' ; UPL.VOUT = ''                    ;* NPR_UPL 04/23/2010
 CALL UPD.PROGRAM.LOG(UPL.VIN,UPL.VOUT)                                                           ;* NPR_UPL 04/23/2010
$OPTIONS INFO.ABORT
$OPTIONS HEADER.EJECT
      ERRMSG=''
* PROGRAM TO ENTER CUSTOMER COMMENT LINES
*
* FREIGHT PAYMENT PACKAGE
*
* AUTHOR: BRIAN M. QUEEN
* DATE: JULY 7, 1992
*
* Initialize variables
*
      PROMPT ''
      DISP.REC=CUST.REC<226>
      DISP.ORG=DISP.REC
      CALL GET.USER(USER)
*
* Get company file information
*
      PROG.NAME='FB.1.1.ALIAS'
      PROG.DESC='CUSTOMER ALIAS (DBA) ENTRY'
      PACK.NAME='FREIGHT PAYMENT'
      CALL AFS.SCR.HEAD(CO.ID,FILE.ID,'',PROG.NAME,PROG.DESC,PACK.NAME,CO.NAME,TIME.DATE,1)
*
* First page of codes
*
      PAGE=1
      GOSUB 9000
*
* Enter selection code
*
100:
*
      VAL.COUNT=DCOUNT(DISP.REC<1>,@VM) ;* NPR99
      LAST.PAGE=INT(VAL.COUNT/18)       ;* NPR99
*
      IF DISPLAY.MODE THEN
         HELP="'B'=Back. 'T'=Top, ''=Next page. 'EX'=Exit."
      END ELSE
         HELP="Enter line number. 'B'=Back. 'T'=Top, ''=Next page. 'E'=End-&-Save. 'EX'=Exit."
      END
      CALL GEN.IN(0,22,"Enter line number please. ###",'',Q,0,3,'','',1,-3,22,0,QI,HELP,0,23)
      QI=OCONV(QI,'MCU')
      BEGIN CASE
         CASE QI=''
            PAGE=PAGE+1
            T.START=(PAGE*18)-17
            T.STOP=START+17
            IF DISP.REC<1,T.STOP> = "" THEN
               CALL ELINE("No more pages")
            END ELSE
               GOSUB 9000
            END
         CASE QI='B'
            PAGE=PAGE-1
            IF PAGE=0 THEN
               CALL ELINE("You're on the first page")
               PAGE=1
            END
            GOSUB 9000
         CASE QI='T'
            PAGE=1
            GOSUB 9000
         CASE QI='EX'
**            STOP
            RETURN
         CASE NUM(QI) AND NOT(DISPLAY.MODE)
            SEL=QI
            IF SEL LT START ! SEL GT STOP THEN
               CALL ELINE('Invalid selection.')
               GOTO 100
            END
            GOSUB 8000
         CASE QI='E' AND NOT(DISPLAY.MODE)
            X=DCOUNT(DISP.REC<1>,CHAR(253))
            FOR I=X TO 1 STEP -1
               IF DISP.REC<1,I>='' THEN
                  DISP.REC=DELETE(DISP.REC,1,I)
               END
            NEXT I
*
            CUST.REC<226>=DISP.REC
            RETURN
      END CASE
      GOTO 100
*
*------------------------------------------------------------------------
* INTERNAL SUBROUTINES
*------------------------------------------------------------------------
*
* Edit code
*
8000: ROW=3+REM(SEL,18)
      IF ROW=3 THEN ROW=21
      ALIAS=DISP.REC<1,SEL>
8010: HELP="Enter new Alias. ''=Accept. 'D'=Delete. 'X'=Exit."
      CALL GEN.IN(5,ROW,ALIAS'L#50','',Q,0,50,'','',2,5,ROW,0,QI,HELP,0,23)
*NPR01      QI=OCONV(QI,'MCU')
      QI=OCONV(QI,'MCT')   ;*NPR01
      IF QI='X' THEN
         CRT @(5,ROW):ALIAS'L#50'
         RETURN
      END
      IF QI='' THEN
         Q=ALIAS
         QI=ALIAS
      END
      IF QI='D' THEN
         DISP.REC=DELETE(DISP.REC,1,SEL)
         GOSUB 9000
         RETURN
      END
      ALIAS=QI
      CRT @(5,ROW):ALIAS'L#50'
*
      DISP.REC<1,SEL>=ALIAS
      RETURN
*
* Display array
*
9000: CRT @(0,3):@(-3):"  #":@(5,3):"Alias (dba) Description"
      START=(PAGE*18)-17
      IF START GT 108 THEN
         CALL ELINE("Maximum alliases allowed is 108")
         START=1
         PAGE=1
      END
      STOP=START+17
      FOR I=START TO STOP STEP 1
         ROW=3+REM(I,18)
         IF ROW=3 THEN ROW=21
         ALIAS=DISP.REC<1,I>
         PRINT @(0,ROW):I"R#3":'  ':ALIAS"L#50"
      NEXT I
      RETURN
