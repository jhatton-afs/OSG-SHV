      SUBROUTINE FB.1.1.CHARGES.SVC(CLIENT.ID,CLIENT.REC,DISPLAY.MODE,POS)
$OPTIONS HEADER.EJECT
 UPL.VIN = 'FBBP' ;  UPL.VIN<2> = 'FB.1.1.CHARGES.SVC' ; UPL.VOUT = ''                    ;* NPR_UPL 04/23/2010
 CALL UPD.PROGRAM.LOG(UPL.VIN,UPL.VOUT)                                                           ;* NPR_UPL 04/23/2010
$OPTIONS EXEC.EQ.PERF
$OPTIONS INFO.ABORT
      ERRMSG=''
* PROGRAM FB.1.1.CHARGES.SVC
* PROGRAM TO MODIFY TRANSPORT MODES FOR TRAFFICE SERVICE %
*
* FREIGHT BILLING PACKAGE
*
* AUTHOR: BRIAN M. QUEEN
* DATE: AUGUST 29, 1992
*
* Initialize variables
*
      PROMPT''
      PROG.NAME='FB.1.1.CHARGES.SVC'
      PROG.DESC='EDIT TRAFFIC SERVICE RATES'
      PACK.NAME='FREIGHT BILLING'
      CALL AFS.SCR.HEAD(CO.ID,FILE.ID,'',PROG.NAME,PROG.DESC,PACK.NAME,CO.NAME,TIME.DATE,1)
*
* Open files
*
      OPEN '','FB.TRANSPORT' TO F.FILE ELSE
         EXECUTE 'CREATE-FILE FB.TRANSPORT 1 7' PASSLIST CAPTURING OUTPUT
         OPEN '','FB.TRANSPORT' TO F.FILE ELSE CALL OPEN.ABORT("FB.TRANSPORT",PROG.NAME)
      END
*
* Select codes and build display array
*
      TRANS.ARRAY=''
      EXECUTE 'SSELECT FB.TRANSPORT'
      EOF=0
      CNT=0
      LOOP
         READNEXT ID ELSE EOF=1
      UNTIL EOF DO
         READV DESC FROM F.FILE,ID,1 ELSE DESC='~~~ Not on file ~~~'
         CNT=CNT+1
         TRANS.ARRAY<1,CNT>=ID
         TRANS.ARRAY<2,CNT>=DESC
      REPEAT
      CNT=CNT+1
      TRANS.ARRAY<1,CNT>='A'
      TRANS.ARRAY<2,CNT>='All others'
      DISP.REC=""
      MGMT.SVC.TMODE=CLIENT.REC<177,POS>
      MGMT.SVC.RATE=CLIENT.REC<178,POS>
      NSVM=DCOUNT(MGMT.SVC.TMODE,@SVM)
      FOR SVM=1 TO NSVM
         TMODE=MGMT.SVC.TMODE<1,1,SVM>
         MRATE=MGMT.SVC.RATE<1,1,SVM>
         LOCATE TMODE IN TRANS.ARRAY<1> SETTING TPOS THEN
            DISP.REC<1,SVM>=TMODE
            DISP.REC<2,SVM>=TRANS.ARRAY<2,TPOS>
            DISP.REC<3,SVM>=MRATE
         END
      NEXT SVM
*
* First page of codes
*
      PAGE=1
      GOSUB 9000
*
* Enter selection code
*
      IF DISPLAY.MODE THEN
         CALL ELINE('')
         RETURN
      END ELSE
100:     HELP="Enter line number. 'B'=Back. 'T'=Top, ''=Next page. 'E'=End. 'EX'=Exit."
         CALL GEN.IN(0,22,"Enter line number please. ###",'',Q,0,3,'','',1,-3,22,0,QI,HELP,0,23)
         QI=OCONV(QI,'MCU')
         BEGIN CASE
            CASE QI=''
               PAGE=PAGE+1
               GOSUB 9000
            CASE QI='B'
               PAGE=PAGE-1
               IF PAGE=0 THEN PAGE=1
               GOSUB 9000
            CASE QI='T'
               PAGE=1
               GOSUB 9000
            CASE QI='EX'
               STOP
            CASE NUM(QI)
               SEL=QI
               IF SEL LT START ! SEL GT STOP THEN
                  CALL ELINE('Invalid selection.')
                  GOTO 100
               END
               GOSUB 8000
               GOSUB 9000
            CASE QI='E'
               X=DCOUNT(DISP.REC<1>,CHAR(253))
               FOR I=X TO 1 STEP -1
                  IF DISP.REC<1,I>='' THEN
                     DISP.REC=DELETE(DISP.REC,1,I)
                     DISP.REC=DELETE(DISP.REC,2,I)
                     DISP.REC=DELETE(DISP.REC,3,I)
                  END
               NEXT I
*
               CLIENT.REC<177,POS>=''
               CLIENT.REC<178,POS>=''
               NVAL=DCOUNT(DISP.REC<1>,@VM)
               FOR VAL=1 TO NVAL
                  CLIENT.REC<177,POS,VAL>=DISP.REC<1,VAL>
                  CLIENT.REC<178,POS,VAL>=DISP.REC<3,VAL>
               NEXT VAL
               RETURN
         END CASE
         GOTO 100
      END
*
*------------------------------------------------------------------------
* INTERNAL SUBROUTINES
*------------------------------------------------------------------------
*
* Edit code
*
8000: ROW=3+REM(SEL,18)
      IF ROW=3 THEN ROW=21
      TMODE=DISP.REC<1,SEL>
      DESC=DISP.REC<2,SEL>
      MRATE=DISP.REC<3,SEL>
8010: HELP="Enter Transport Mode code. 'L'=List. 'A'=All. ''=Accept. 'D'=Delete. 'X'=Exit."
      CALL GEN.IN(5,ROW,TMODE'L#4','',Q,0,4,'','',2,-4,ROW,0,QI,HELP,0,23)
      QI=OCONV(QI,'MCU')
      IF QI='X' THEN
         CRT @(5,ROW):TMODE'L#4'
         RETURN
      END
      IF QI='' THEN
         Q=TMODE
      END
      IF QI='L' THEN
         OLD.ROW=ROW
         EXECUTE "SORT FB.TRANSPORT BY MODE MODE 1 (IH"
         CALL ELINE('')
         CALL AFS.SCR.HEAD(CO.ID,FILE.ID,'',PROG.NAME,PROG.DESC,PACK.NAME,CO.NAME,TIME.DATE,1)
         GOSUB 9000
         ROW=OLD.ROW
         GOTO 8010
      END
      IF QI='D' THEN
         DISP.REC=DELETE(DISP.REC,1,SEL)
         DISP.REC=DELETE(DISP.REC,2,SEL)
         DISP.REC=DELETE(DISP.REC,3,SEL)
         RETURN
      END
      LOCATE Q IN DISP.REC<1> SETTING TPOS THEN
         CALL ELINE('Already entered on line ':TPOS:'. Please re-enter.')
         GOTO 8010
      END
      LOCATE Q IN TRANS.ARRAY<1> SETTING TPOS THEN
         TMODE=Q
         DESC=TRANS.ARRAY<2,TPOS>
         CRT @(5,ROW):TMODE'L#4':
         CRT @(10,ROW):DESC'L#25':
8020:*
         HELP="Enter Management Fee %. '<'=Back to code. 'X'=Exit."
         CALL GEN.IN(35,ROW,MRATE,'AMT2',Q,0,10,'','',2,-10,ROW,0,QI,HELP,0,23)
         QI=OCONV(QI,'MCU')
         IF QI='X' THEN
            DISP.REC=DELETE(DISP.REC,1,SEL)
            DISP.REC=DELETE(DISP.REC,2,SEL)
            DISP.REC=DELETE(DISP.REC,3,SEL)
            RETURN
         END
         IF QI='<' THEN GOTO 8010
         IF QI='' THEN Q=MRATE
         IF NOT(NUM(Q)) THEN GOTO 8020
*        IF Q#'' AND Q<100 AND Q#0 THEN
*           CALL ELINE('ERROR : Amount cannot be less than 1.00%.')
*           GOTO 8020
*        END
         IF Q>10000 THEN
            CALL ELINE('ERROR : Amount must not exceed 100%.')
            GOTO 8020
         END
         MRATE=Q
         CRT @(35,ROW):MRATE'R26(#10)':'%'
*
         DISP.REC<1,SEL>=TMODE
         DISP.REC<2,SEL>=DESC
         DISP.REC<3,SEL>=MRATE
* RESORT BY MODE FOR DISPLAY
         OLD.DISP.REC=DISP.REC
         DISP.REC=''
         NVAL=DCOUNT(OLD.DISP.REC<1>,@VM)
         FOR VAL=1 TO NVAL
            TMODE=OLD.DISP.REC<1,VAL>
            DESC=OLD.DISP.REC<2,VAL>
            MRATE=OLD.DISP.REC<3,VAL>
            LOCATE TMODE IN DISP.REC<1> BY "AR" SETTING TPOS ELSE
               INS OLD.DISP.REC<1,VAL> BEFORE DISP.REC<1,TPOS>
               INS OLD.DISP.REC<2,VAL> BEFORE DISP.REC<2,TPOS>
               INS OLD.DISP.REC<3,VAL> BEFORE DISP.REC<3,TPOS>
            END
         NEXT VAL
      END
      RETURN
*
* Display array
*
9000:*
      CRT @(0,3):@(-3):"#""R#3":
      CRT @(5,3):"Code""L#4":
      CRT @(10,3):"Description""L#25":
      CRT @(35,3):"Mgmt Fee %""L#10":
      START=(PAGE*18)-17
      IF START GT 108 THEN
         START=1
         PAGE=1
      END
      STOP=START+17
      FOR I=START TO STOP STEP 1
         ROW=3+REM(I,18)
         IF ROW=3 THEN ROW=21
         TMODE=DISP.REC<1,I>
         DESC=DISP.REC<2,I>
         MRATE=DISP.REC<3,I>
         CRT @(0,ROW):I"R#3":
         CRT @(5,ROW):TMODE"L#4":
         CRT @(10,ROW):DESC"L#25":
         IF MRATE#'' THEN
            CRT @(35,ROW):MRATE"R26(#10)":'%':
         END
      NEXT I
      RETURN
