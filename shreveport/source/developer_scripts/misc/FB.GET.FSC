***********************************************************************
* PROGRAM: FB.GET.FSC
* PROGRAM: TO CREATE A DOWNLOAD FILE TO REVIEW FSC % FOR ALL CARRIERS
*
* THIS PROGRAM IS A PROPRIETARY PRODUCT OF AUTOMATED FREIGHT SYSTEMS
*
* FREIGHT BILLING/EDI PACKAGE
*
* AUTHOR       : J. Michael Kerbow
* DATE CREATED :
* DESCRIPTION  : This program is the front-end for building a delimited
*                file for all the bills paid during the specified dates
*                so data can be worked with under excel.  This program
*                collects the input then calls BUILD.DF.SUB to actually
*                create the tab-delimited file.
***********************************************************************
      PROG.NAME='FB.GET.FSC'
      PROG.DESC='Build File of Fuel Surcharge Percentages by Carrier'
      PACK.NAME='Freight Billing'
* Initialize variables
       UPL.VIN = 'FBBP' ;  UPL.VIN<2> = 'FB.GET.FSC' ; UPL.VOUT = ''                    ;* NPR_UPL 04/23/2010
       CALL UPD.PROGRAM.LOG(UPL.VIN,UPL.VOUT)                                                           ;* NPR_UPL 04/23/2010

      PROMPT''
      CALL CHANNEL(CH.NUM)
      CALL GET.USER(USER)

* Display heading
*
      CALL AFS.SCR.HEAD(CO.ID,FILE.ID,'',PROG.NAME,PROG.DESC,PACK.NAME,CO.NAME,TIME.DATE,1)
      CRT @(0,2):STR('=',79)

10:   ***
      MASK=DATE()'D4/'
      HELP='Enter ending date. [EX]it'
      CALL GEN.IN(0,10,"Enter FSC Date ":MASK,'DATE',Q,0,10,'','',0,-10,10,0,QI,HELP,0,23)
      QI=OCONV(QI,'MCU')
      IF QI='' THEN QI=DATE()'D4'
      BEGIN CASE
         CASE QI='EX' OR QI='EXI' OR QI='EXIT'
            GOTO THE.END
         CASE QI MATCHES '0N'
            BEGIN CASE
               CASE LEN(QI)=4
                  QI=QI[1,2]:'.':QI[3,2]:'.':(DATE()'D4/')[7,4]
               CASE LEN(QI)=6
                  IF QI[5,2]<50 THEN
                     QI=QI[1,2]:'.':QI[3,2]:'.':(DATE()'D4/')[7,2]:QI[5,2]
                  END ELSE
                     QI=QI[1,2]:'.':QI[3,2]:'.':'19':QI[5,2]           ; * ((DATE()-365)'D4/')[7,2]:QI[5,2]
                  END
               CASE LEN(QI)=8
                  QI=QI[1,2]:'.':QI[3,2]:'.':QI[5,4]
               CASE 1
                  CALL ELINE('Use one of the following formats: MMDD or MMDDYY or MMDDYYYY.')
                  GOTO 10
            END CASE
      END CASE
      FSC.DATE=ICONV(QI,'D')
      IF FSC.DATE='' THEN
         CALL ELINE('Invalid date.')
         GOTO 10
      END
      CRT @(15,10):FSC.DATE'D4/'
* Verify selection criteria is correct

VERIFY.SELECTION:***
      HELP='ENTER to continue. [EX]it'
      CALL GEN.IN(0,22,'Enter Selection : ----','',Q,0,4,'','',0,-4,22,1,QI,HELP,0,23)
      QI=OCONV(QI,'MCU')
      BEGIN CASE
         CASE QI='EX' OR QI='EXI' OR QI='EXIT'
            GOTO THE.END
         CASE QI=''
         CASE 1
            GOTO VERIFY.SELECTION
      END CASE


      OPEN '','FB.BC.FSC' TO F.FB.NFSC ELSE ABORT
      OPEN '','FB.TABLES' TO F.TABLES ELSE ABORT
      OPEN '','CARRIERS' TO F.CARRIERS ELSE ABORT
      OPEN '','BILLDATA.':USER TO F.BILLDATA ELSE
         OPEN '','VOC' TO F.VOC ELSE
            CALL ELINE('Unable to open VOC file - Call IS Department.')
            GOTO THE.END
         END
         READ BILLDATA.REC FROM F.VOC,'BILLDATA' ELSE
            CALL ELINE('Unable to open VOC BILLDATA - Call IS Department.')
            GOTO THE.END
         END
         BILLDATA.REC<2>:='\':USER
         WRITE BILLDATA.REC ON F.VOC,'BILLDATA.':USER ELSE
            CALL ELINE('Your user name ':USER:' is not in the BILLDATA folder - Call IS Department.')
            GOTO THE.END
         END
         OPEN '','BILLDATA.':USER TO F.BILLDATA ELSE
            CALL ELINE('Unable to open BILLDATA.':USER:'.')
            GOTO THE.END
         END
      END

      CNTR=3
      STMT='SELECT FB.BC.FSC WITH CLIENT# = "" AND WITH 30 = "ENABLE" AND WITH CARRIER.MODE = "1" AND WITH CARRIER.STATUS = "A"'
      EXECUTE STMT RTNLIST NFSC.LIST CAPTURING NFSC.RESULTS
      OUT.ARRAY=''
      OUT.ARRAY<1>='Report of FSC Percentages and amounts for test shipments of $150.00 by Carrier for ':FSC.DATE'D4/':' run on ':DATE()'D4/'
      OUT.ARRAY<3,1>='Number'
      OUT.ARRAY<3,2>='SCAC'
      OUT.ARRAY<3,3>='Name'
      OUT.ARRAY<3,4>='FSC %'
      OUT.ARRAY<3,5>='FSC Amount'

100:  ***
      READNEXT ID FROM NFSC.LIST ELSE
         FSC.ODATE=FSC.DATE'D4/'
         OUT.ARRAY=CHANGE(OUT.ARRAY,@VM,CHAR(9))
         BILLDATA.ID='FSC_':FSC.ODATE[7,4]:FSC.DATE[1,2]:FSC.ODATE[4,2]:'.XLS'
         WRITE OUT.ARRAY ON F.BILLDATA,BILLDATA.ID

         TEXT='This report is on your "U:\" drive called ':BILLDATA.ID:'.'
         CALL ELINE(TEXT)
         GO THE.END
      END

      BEGIN CASE
         CASE ID='99999' ; GO 100        ; * Test Account
         CASE ID='08519' ; GO 100        ; * AFS Logistics / Air Cargo
         CASE ID='99994' ; GO 100        ; * AFS Logistics / Air Cargo
         CASE 1
      END CASE

      CNTR=CNTR+1
*     PRINT CNTR:' ':ID

      READ DFSC FROM F.FB.NFSC,ID ELSE GO 100
      CARRIER.ID=FIELD(ID,'*',2)
      READ DCARRIER FROM F.CARRIERS,CARRIER.ID ELSE
         DCARRIER=ID:'-Not on file in Carriers'
      END
      CARRIER.NAME=DCARRIER<1>
      CARRIER.ADDR1=DCARRIER<2,1>
      CARRIER.ADDR2=DCARRIER<2,2>
      CARRIER.CITY =DCARRIER<3>
      CARRIER.ST =DCARRIER<4>
      CARRIER.ZIP =DCARRIER<5>
      CARRIER.PHONE=DCARRIER<6>
      CARRIER.SCAC=DCARRIER<12>

      VIN=FSC.DATE
      VIN<2>=5000                        ; * Weight
*     VIN<3>=MILES ; * Dont have miles at this point
      VIN<5>=0
      VIN<6>="15000"                     ; * For a $150.00 Shipment
      VIN<7>="LA"                        ; * Special for Xpedx & AFS
      VIN<8>="TX"                        ; * Special for AFS
      CALL CALC.FSC(VIN,VOUT,DFSC,F.TABLES)
      FSC.PERC=VOUT<3>
      IF FSC.PERC+0#0 THEN
         OUT.ARRAY<CNTR,1>=ID
         OUT.ARRAY<CNTR,2>=CARRIER.SCAC
         OUT.ARRAY<CNTR,3>=CARRIER.NAME
         OUT.ARRAY<CNTR,4>=VOUT<3>'R37'  ; * FSC %
         OUT.ARRAY<CNTR,5>=VOUT<1>'R26'  ; * FSC $
      END ELSE
         CNTR=CNTR-1
      END

      GO 100

THE.END:*
      STOP
   END
