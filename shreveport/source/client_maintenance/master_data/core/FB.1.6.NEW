*********************************************************************
* Program name : FB.1.6
* Author       : David R Friend
* Date written : August 3, 2007
* Library      : FBBP
* COPYRIGHT (c) : AFS, LLC 2016, All Rights Reserved.
*
* modify claims client master
*
* Last updated by afs (nmeddaugh) at 14:36:11 on 03/26/2019.
*
* Start modification history
* 071803 - JMK02 - ADD PHONE NUMBER
* 021904 - JMK03 - ADD FILES FOR DELETE OF MISROUTE BILLS
* 042304 - JMK04 - ADD 'R' OPTION TO RECORD TYPE (V,C,I,R)
* 090106 - JMK05 - UPDATE CONS CODE FOR TARGUS (01478)
* 012307 - JCW01 - USE NEW VEND.CUST.NAMES FILE
* 111607 - JMK06 - T070443 - Create process to update SQL data tables from Universe
* 030708 - NPR01 - CHANGED SELECT ON VEND.CUST.NAME WITH UPPERCASE DESC
* 031819 - NKH01 - ACCU-316 Modified CREATE-FILE file sizes
* End modification history
*****************************************************************************
      $OPTIONS HEADER.EJECT
      $OPTIONS EXEC.EQ.PREF
      $OPTIONS INFO.ABORT
      ERRMSG=''

      PROG.NAME='FB.1.6'
      PROG.DESC='EDIT VENDOR AND CUSTOMER'
      PACK.NAME='FREIGHT BILLING'
      CALL AFS.SCR.HEAD(CO.ID,FILE.ID,'',PROG.NAME,PROG.DESC,PACK.NAME,CO.NAME,TIME.DATE,1)
      UPL.VIN = 'FBBP' ;  UPL.VIN<2> = 'FB.1.6.NEW' ; UPL.VOUT = ''                    ;* NPR_UPL 04/23/2010
      CALL UPD.PROGRAM.LOG(UPL.VIN,UPL.VOUT)                                                           ;* NPR_UPL 04/23/2010

      TESTING=1

      OPEN '','BCUST' TO F.CLIENT ELSE CALL OPEN.ABORT("BCUST",PROG.NAME)
      OPEN '','ZIPS.CODES' TO F.ZIPS ELSE CALL OPEN.ABORT('ZIPS.CODES',PROG.NAME)
      OPEN '','FB.TABLES' TO F.TABLES ELSE CALL OPEN.ABORT('FB.TABLES',PROG.NAME)
      OPEN '','CARRIERS' TO F.CARRIERS ELSE CALL OPEN.ABORT("CARRIERS",PROG.NAME)
      OPEN '','FB.LOGISTICS' TO F.LOGS ELSE CALL OPEN.ABORT("FB.LOGISTICS",PROG.NAME)      ; * JMK03
      OPEN '','FB.CAR.CTRL' TO F.CAR.CTRL ELSE
         CALL OPEN.ABORT("FB.CAR.CTRL",PROG.NAME)
      END

      GOSUB INITIALIZE.VARIABLES

*****************************************************************************
ENTER.CLINUM:* ENTER VENDOR/CUSTOMER NUMBER / NAME
*****************************************************************************
*
      CRT @(0,2):@(-3)
      HELP='Enter the client number - Name for Search - [EX]it'
      CALL GEN.IN(0,3,'Enter Client : #####','',Q,0,20,'','',0,-5,3,0,QI,HELP,0,23)
      QI=OCONV(QI,'MCU')
      CALL GET.USER(USER)
      BEGIN CASE
         CASE QI=''
            STOP
         CASE QI='EX' OR QI='X'
            STOP
         CASE NUM(QI)
            Q=QI'R%5'
         CASE 1
            CALL SOUNDEX.DISPLAY(QI,'BCUST','SDX.CLIENTS,NAME','2,1,3,4',ITEM.LIST)
            CALL AFS.SCR.REFRESH(PROG.NAME,PROG.DESC,PACK.NAME,CO.NAME,TIME.DATE,1)
            IF ITEM.LIST='' ! DCOUNT(ITEM.LIST<1>,@VM) > 1 THEN GOTO ENTER.CLINUM
            IF NOT(NUM(ITEM.LIST<1,1>))THEN GOTO ENTER.CLINUM
            Q=ITEM.LIST<1,1>'R%5'
      END CASE
      CLIENT.ID=Q'R%5'
*****************************************************************************
* READ CLIENT ITEM
*****************************************************************************
      READ CLIENT.REC FROM F.CLIENT,CLIENT.ID ELSE
         CALL ELINE('Client Number ':CLIENT.ID:' not on file!')
         GOTO ENTER.CLINUM
      END
      CLIENT.NAME=CLIENT.REC<2>
      CALL AFS.SCR.REFRESH(PROG.NAME,PROG.DESC,PACK.NAME,CO.NAME,TIME.DATE,1)
      CRT @(0,3):'Enter Client : '
      CBVIN=''
      CBVIN<1>=CLIENT.ID
      CALL CLIENT.BLOCK(CBVIN,CBVOUT,F.CAR.CTRL)
      CRT @(15,3):CBVOUT<1>:CLIENT.ID:' ':CLIENT.NAME:@(-6)
*****************************************************************************
* OPEN VEND.CUST FILE
*****************************************************************************
      OPEN '','VEND.CUST,':CLIENT.ID TO F.VC ELSE
         GOTO ASK.YN
      END

      OPEN '','VEND.CUST.XREF,':CLIENT.ID TO F.VC.XREF ELSE CALL OPEN.ABORT("VEND.CUST.XREF,":CLIENT.ID,PROG.NAME)
      OPEN '','VEND.CUST.ZIPS,':CLIENT.ID TO F.VC.ZIPS ELSE CALL OPEN.ABORT("VEND.CUST.ZIPS,":CLIENT.ID,PROG.NAME)
      OPEN '','VEND.CUST.NAMES,':CLIENT.ID TO F.VCN ELSE CALL OPEN.ABORT("VEND.CUST.NAMES,":CLIENT.ID,PROG.NAME)         ; * JCW01
      OPEN '','FB.BILLS.HIST,':CLIENT.ID TO F.BILLS.HIST ELSE
         CALL OPEN.ABORT('FB.BILLS.HIST,':CLIENT.ID,PROG.NAME)
      END

OPEN.VC:
*****************************************************************************
ASK.SEL:* ASK FOR SELECTION
*************************************************
      ZIP.SEARCH = 0
      CALL AFS.SCR.REFRESH(PROG.NAME,PROG.DESC,PACK.NAME,CO.NAME,TIME.DATE,1)
      CRT @(0,3):'Enter Client : '
      CRT @(15,3):CBVOUT<1>:CLIENT.ID:' ':CLIENT.NAME:@(-6)
      HELP='Enter Customer/Vendor to Search for -[L]ist All, [EX]it, [A]dd, Search by [Z]ip'
      IF CLIENT.ID = '02427'  OR CLIENT.ID = '01862' THEN
      CALL GEN.IN(0,4,'Enter Number : #####','',Q,0,20,'','',0,-6,4,0,QI,HELP,0,23)
    END ELSE
      CALL GEN.IN(0,4,'Enter Number : #####','',Q,0,20,'','',0,-5,4,0,QI,HELP,0,23)   
    END
      QI=OCONV(QI,'MCU')
      BEGIN CASE
         CASE QI='L'
            GOTO LIST
         CASE QI='A'
            VC.REC=''
            GOSUB ASSIGN.VARIABLES
            NEW.VC = 1
            GO ADD.UPDATE.VC
         CASE QI='Z'
            ZIP.SEARCH = 1
            GOTO SEARCH.BY.ZIP
         CASE QI='X'
            CALL AFS.SCR.REFRESH(PROG.NAME,PROG.DESC,PACK.NAME,CO.NAME,TIME.DATE,1)
            GOTO ENTER.CLINUM
         CASE QI='EX'
            STOP
         CASE NUM(QI)
            Q=QI
         CASE 1
            ACTUAL='VEND.CUST,':CLIENT.ID
            SDX='SDX.VEND.CUST,':CLIENT.ID
            PRESENT=1
            OPEN '','SDX.VEND.CUST,':CLIENT.ID ELSE
               PRESENT=0
            END
            IF PRESENT THEN
               CALL SOUNDEX.DISPLAY(QI,ACTUAL,SDX,'1,2,3,4',ITEM.LIST)
            END
            CALL AFS.SCR.REFRESH(PROG.NAME,PROG.DESC,PACK.NAME,CO.NAME,TIME.DATE,1)
            IF ITEM.LIST='' ! DCOUNT(ITEM.LIST<1>,@VM)>1 THEN GOTO ASK.SEL
            IF NOT(NUM(ITEM.LIST<1,1>))THEN GOTO ASK.SEL
            Q=ITEM.LIST<1,1>
            CRT @(0,3):'Enter Client : '
            CRT @(15,3):CLIENT.ID:' ':CLIENT.NAME
            CRT @(0,4):'Enter Number : '
      END CASE
      
      VC.ID=Q'R%5'
      CRT @(15,4):VC.ID
      READ VC.REC FROM F.VC,VC.ID THEN
         GOSUB ASSIGN.VARIABLES
         GO ADD.UPDATE.VC
      END ELSE
         CALL ELINE('ERROR - Cannot read record in VEND.CUST for vendor/customer ':VC.ID)
      END
      GOTO ASK.SEL

*************************************************
LIST:
*************************************************
      EXECUTE 'SORT VEND.CUST,':CLIENT.ID:' BY OWEN 1 2 7'
      CALL AFS.SCR.REFRESH(PROG.NAME,PROG.DESC,PACK.NAME,CO.NAME,TIME.DATE,1)
      CRT @(0,3):'Enter Client : '

      CRT @(15,3):CBVOUT<1>:CLIENT.ID:' ':CLIENT.NAME:@(-6)
      START=1
      GOTO ASK.SEL

*************************************************
ASK.YN:
*************************************************
      CALL AFS.SCR.REFRESH(PROG.NAME,PROG.DESC,PACK.NAME,CO.NAME,TIME.DATE,1)
      CRT @(0,3):'Enter Client : '
      CRT @(15,3):CBVOUT<1>:CLIENT.ID:' ':CLIENT.NAME:@(-6)
      HELP='THIS CLIENT HAS NO VENDOR/CUSTOMER FILE - CREATE THE FILE??? - [EX]it'
      CALL GEN.IN(0,22,'Command: [Y]','',Q,0,20,'','',0,-2,22,0,QI,HELP,0,23)
      QI=OCONV(QI,'MCU')
      BEGIN CASE
         CASE QI='N'
            GOTO ENTER.CLINUM
         CASE QI='X'
            GOTO ENTER.CLINUM
         CASE QI='EX'
            STOP
         CASE 1
            EXECUTE 'CREATE-FILE DATA VEND.CUST,':CLIENT.ID:' 1009,2' PASSLIST CAPTURING OUTPUT    ; * NKH01
            OPEN '','VEND.CUST,':CLIENT.ID TO F.VC ELSE CALL OPEN.ABORT("VEND.CUST,":CLIENT.ID,PROG.NAME)

            EXECUTE 'CREATE-FILE DATA VEND.CUST.XREF,':CLIENT.ID:' 401,1' PASSLIST CAPTURING OUTPUT   ; * NKH01
            OPEN '','VEND.CUST.XREF,':CLIENT.ID TO F.VC.XREF ELSE CALL OPEN.ABORT("VEND.CUST.XREF,":CLIENT.ID,PROG.NAME)

            EXECUTE 'CREATE-FILE DATA VEND.CUST.ZIPS,':CLIENT.ID:' 131,1' PASSLIST CAPTURING OUTPUT   ; * NKH01
            OPEN '','VEND.CUST.ZIPS,':CLIENT.ID TO F.VC.ZIPS ELSE CALL OPEN.ABORT("VEND.CUST.ZIPS,":CLIENT.ID,PROG.NAME)

            EXECUTE 'CREATE-FILE DATA VEND.CUST.NAMES,':CLIENT.ID:' 601,1' PASSLIST CAPTURING OUTPUT     ; * JCW01   NKH01
            OPEN '','VEND.CUST.NAMES,':CLIENT.ID TO F.VCN ELSE CALL OPEN.ABORT("VEND.CUST.NAMES,":CLIENT.ID,PROG.NAME)   ; * JCW01

            EXECUTE 'CREATE-FILE DATA SDX.VCN,':CLIENT.ID:' 139,1' PASSLIST CAPTURING OUTPUT   ; * JCW01  NKH01
            EXECUTE 'CREATE-FILE DATA SDX.VEND.CUST,':CLIENT.ID:' 139,1' PASSLIST CAPTURING OUTPUT       ; * JCW01   NKH01

      END CASE
      GOTO OPEN.VC

*************************************************
SEARCH.BY.ZIP: ***
*************************************************
*
* Get Vend/Cust information and store in array
*
      HELP='Please type in the zip to search for!'
      CALL GEN.IN(0,5,'Zip          : ######','',Q,0,20,'','',0,-6,5,0,QI,HELP,0,23)
      QI=OCONV(QI,'MCU')
      IF QI MATCHES '5N"-"' THEN
         QI=CHANGE(QI,'-','M')
      END
      BEGIN CASE
         CASE QI='' OR QI='X'
            GO ASK.SEL
         CASE QI='EX'
            STOP
         CASE QI MATCHES "5N"
            Q = QI
         CASE QI MATCHES '5N"M"'
            Q = QI
         CASE QI MATCHES '1A1N1A1N1A1N'
            Q = QI
         CASE 1
            CALL ELINE('Invalid Zip Code, please try again.')
            GO SEARCH.BY.ZIP
      END CASE

      VC.ARRAY = ''
      VC.ZIP = Q
      CRT@(0,5):'Zip          : ':VC.ZIP'L#6'
      NUM.ITEMS=0
      READ VC.ZIPS FROM F.VC.ZIPS,VC.ZIP THEN
         NUM.ITEMS=DCOUNT(VC.ZIPS<1>,@VM)
         LAST.PAGE=INT(NUM.ITEMS/9)+1
         FOR X = 1 TO NUM.ITEMS
            VC.ID = VC.ZIPS<1,X>
            READ VC.REC FROM F.VC,VC.ID THEN
               LOCATE VC.REC<1> IN VC.ARRAY<2> BY 'AL' SETTING POS ELSE
                  INS VC.ID BEFORE VC.ARRAY<1,POS>
                  INS VC.REC<1> BEFORE VC.ARRAY<2,POS>
                  INS VC.REC<2> BEFORE VC.ARRAY<3,POS>
               END
            END ELSE
               CALL ELINE('ERROR - Cannot read record in VEND.CUST for vendor/customer ':VC.ID)
            END
         NEXT X
      END ELSE
         CALL ELINE('Invalid Zip Code, please try again.')
         GO SEARCH.BY.ZIP
      END
      VC.ARRAY<1,-1> = 'ADD'
      VC.ARRAY<2,-1> = '<ADD NEW VEND/CUST>'

*************************************************
BEGIN.DISPLAY: **** Display window headings
*************************************************

      CRT @(0,7):@(-3)
      CRT @(0,7):' #       Description              Code'
      CRT @(0,8):@(-3)
      CRT @(0,8):'--- ------------------------- -----------'

      MASK = 'Enter line number. '

      POS = 0
      IF VC.DESC#'' THEN
         LOCATE VC.DESC IN VC.ARRAY<2> BY 'AL' SETTING POS ELSE POS = 0
         MASK = MASK:POS'L#3'
      END ELSE
         MASK = MASK:'###'
      END
      IF POS > 0 THEN
         PAGE=INT(POS/9)
         IF REM(POS,9)>0 THEN PAGE+=1
      END ELSE
         PAGE=1
      END
      GOSUB DISPLAY.PAGE

*************************************************
START.SEL: **** Enter selection of vend/cust
*************************************************
      HELP="Enter LN# [N]=Next [P]revious [T]op [B]ottom [F]irst letter(s) [X]Back [EX]it"
      CALL GEN.IN(0,19,MASK,'',Q,0,5,'','',0,-3,19,0,QI,HELP,0,23)
      IF QI ='' AND POS#0 THEN QI=POS
      QI=OCONV(QI,'MCU')

      BEGIN CASE
         CASE QI='P'
            PAGE=PAGE-1
            IF PAGE=0 THEN PAGE=1
            GOSUB DISPLAY.PAGE
         CASE QI='' OR QI = 'N'
            PAGE=PAGE+1
            GOSUB DISPLAY.PAGE
         CASE QI='X'
            GO SEARCH.BY.ZIP
         CASE QI = 'T'
            PAGE=1
            GOSUB DISPLAY.PAGE
         CASE QI = 'B'
            PAGE = LAST.PAGE
            GOSUB DISPLAY.PAGE
         CASE QI = 'F'
            GOSUB FIND.FIRST
         CASE QI='EX'
            STOP
         CASE NUM(QI)
            IF QI > NUM.ITEMS+1 OR QI < 1 THEN
               CALL ELINE('Invalid line #, please try again')
               GO START.SEL
            END
            IF VC.ARRAY<1,QI> = 'ADD' THEN
               VC.REC=''
               GOSUB ASSIGN.VARIABLES
               NEW.VC=1
               GO ADD.UPDATE.VC
            END ELSE
               SEL = QI
               VC.ID = VC.ARRAY<1,SEL>
               READ VC.REC FROM F.VC,VC.ID ELSE
                  CALL ELINE('ERROR - Cannot read record in VEND.CUST for vendor/customer ':VC.ID)
                  GO START.SEL
               END
               OLD.VC.REC = VC.REC
               GOSUB ASSIGN.VARIABLES
               GO ADD.UPDATE.VC
            END
      END CASE
      GO START.SEL

*************************************************
FIND.FIRST: ***Get Starting Letter(s) in List to Jump to in Display
*************************************************
      HELP="Enter the first letter(s) of the name you are seaching for"
      CALL GEN.IN(0,22,'Enter first letter(s) -------------------------','',Q,0,15,'','',0,-25,22,1,QI,HELP,0,23)
      LOOK=OCONV(QI,'MCU')
      POS=INDEX(@VM:OCONV(VC.ARRAY<2>,'MCU'),@VM:LOOK,1)
      IF POS=0 THEN                      ; * Not Found
         CALL ELINE("A name starting with the letter(s) '":QI:"' was not found")
         GO DISPLAY.PAGE
      END
      VAL=COUNT((@VM:VC.ARRAY<2>)[1,POS],@VM)
      PAGE=INT(VAL/9)
      IF REM(VAL,9)>0 THEN PAGE+=1
      GO DISPLAY.PAGE
      RETURN

*************************************************
ADD.UPDATE.VC: ***
*************************************************
      IF NEW.VC THEN
         DONE=0
         COUNTER.ID = "VC*":CLIENT.ID
         READVU VC.ID FROM F.TABLES,COUNTER.ID,1 ELSE NEXT.VC.ID=1
         LOOP UNTIL DONE = 1 DO
            VC.ID=VC.ID'R%5'
            READ NULL FROM F.VC,VC.ID THEN VC.ID+=1 ELSE DONE=1
         REPEAT
         NEXT.VC.ID=VC.ID+1
         WRITEV NEXT.VC.ID ON F.TABLES,COUNTER.ID,1
      END
      GOSUB CLEAR.WINDOW
      GOSUB DISPLAY.VC
*************************************************
100:
*************************************************
      IF NEW.VC THEN
         GO ENTER.DESC
      END ELSE
         HELP="Enter Line Num. 'CR' or [S]ave [V]iew Loc/Div Misroute Info [X]=Back [EX]it."
         CALL GEN.IN(0,22,'Enter line number. ###','',Q,0,3,'','',0,-3,22,0,QI,HELP,0,23)
         QI=OCONV(QI,'MCU')
         BEGIN CASE
            CASE QI='S' OR QI=''
               GOTO SAVE.VC
            CASE QI='X'
               IF ZIP.SEARCH THEN
                  GO BEGIN.DISPLAY
               END ELSE
                  GO ASK.SEL
               END
            CASE QI='EX'
               STOP
            CASE QI = 'V'

               CALL VIEW.LOCATIONS(F.VC,F.CARRIERS,F.ZIPS,CLIENT.ID,CLIENT.REC,VC.ID,VC.REC,USER,F.BILLS.HIST,F.LOGS)    ; * JMK03
               CALL AFS.SCR.HEAD(CO.ID,FILE.ID,'',PROG.NAME,PROG.DESC,PACK.NAME,CO.NAME,TIME.DATE,1)
               CRT @(0,3):'Enter Client : ':CLIENT.ID:' ':CLIENT.NAME
               CRT @(0,4):'Enter Number : ':VC.ID

               GOSUB DISPLAY.VC
               GO 100
            CASE NUM(QI)
               SEL=QI
               BEGIN CASE
                  CASE SEL = 1
                     GO ENTER.DESC
                  CASE SEL = 2
                     GO ENTER.ADDR1
                  CASE SEL = 3
                     GO ENTER.ADDR2
                  CASE SEL = 4
                     GO ENTER.ZIP
                  CASE SEL = 5
                     GO ENTER.STATE
                  CASE SEL = 6
                     GO ENTER.CITY
                  CASE SEL = 7
                     GO ENTER.CODE
                  CASE SEL = 8
                     GO ENTER.VCI
                  CASE SEL = 9
                     GO ENTER.WAIVED
                  CASE SEL = 10          ; * JMK01
                     GO ENTER.EMAIL      ; * JMK01
                  CASE SEL = 11          ; * JMK02
                     GO ENTER.PHONE      ; * JMK02
                  CASE 1
                     CALL ELINE('Incorrect line number, please try again')
                     GO 100
               END CASE
         END CASE
      END
      RETURN

*************************************************
ENTER.DESC:* Change vendor/customer description
*************************************************
      HELP="Enter description."
      OLD.DESC = VC.DESC
      MASK=VC.DESC'L#30'
      CALL GEN.IN(13,9,MASK,'',Q,0,30,'','',2,-30,9,0,QI,HELP,0,23)
      QI = OCONV(QI,'MCU')
      IF QI='' THEN QI = VC.DESC ; Q = VC.DESC
      BEGIN CASE
         CASE QI='X'
            CRT @(13,9):@(-4)
            NEW.VC=0
            GO BEGIN.DISPLAY
         CASE QI='EX'
            STOP
         CASE INDEX(Q,"'",1)
            CALL ELINE("Error!  Vendor-Customer Descriptions Cannot Contain a Single Quote!")
            GO ENTER.DESC
         CASE QI=''
            CALL ELINE('Please enter a desc, this can not be blank')
            GO ENTER.DESC
         CASE 1
            IF VC.ZIP = '99999' THEN
               XREF.ID = TRIM(QI)
            END ELSE
               XREF.ID = TRIM(QI):'*':VC.ZIP
            END
            IF NEW.VC OR (VC.DESC # OLD.DESC) THEN
               READ XREF.REC FROM F.VC.XREF,XREF.ID THEN
                  CALL ELINE('Vend/Cust ':QI:' already exist under ID: ':XREF.REC<1>)
                  GO ENTER.DESC
               END ELSE
                  VC.DESC=TRIM(Q)
               END
            END ELSE
               VC.DESC = TRIM(Q)
            END
      END CASE

      CRT @(13,9):VC.DESC'L#30'
      IF NEW.VC ELSE GO 100

*************************************************
ENTER.ADDR1: **** Change vendor/customer address 1
*************************************************
      HELP="Enter addr 1."
      MASK=VC.ADDR1'L#30'
      CALL GEN.IN(13,10,MASK,'',Q,0,30,'','',2,-30,10,0,QI,HELP,0,23)
      CRT @(13,10):VC.DESC'L#30'
      QI = OCONV(QI,'MCU')
      IF QI='' THEN QI = VC.ADDR1 ; Q = VC.ADDR1
      BEGIN CASE
         CASE QI='X'
            CRT @(13,11):@(-4)
            IF NEW.VC THEN
               GO ENTER.DESC
            END ELSE
               GO 100
            END
         CASE QI='EX'
            STOP
         CASE QI=''
            CRT @(13,11):@(-4)
            CALL ELINE('Please enter an address, this can not be blank')
            GO ENTER.ADDR1
         CASE 1
            VC.ADDR1=TRIM(Q)
      END CASE
      CRT @(13,10):VC.ADDR1'L#30'
      IF NEW.VC ELSE GO 100

*************************************************
ENTER.ADDR2: **** Change vendor/customer address 2
*************************************************
      HELP="Enter addr 2. [space]=Clear."
      MASK=VC.ADDR2'L#30'
      CALL GEN.IN(13,11,MASK,'',Q,0,30,'','',2,-30,11,0,QI,HELP,0,23)
      QI = OCONV(QI,'MCU')
      IF QI='' THEN QI = VC.ADDR2 ; Q = VC.ADDR2
      BEGIN CASE
         CASE QI='X'
            CRT @(13,11):@(-4)
            IF NEW.VC THEN
               GO ENTER.ADDR1
            END ELSE
               GO 100
            END
         CASE QI='EX'
            STOP
         CASE 1
            VC.ADDR2=TRIM(Q)
      END CASE
      CRT @(13,11):VC.ADDR2'L#30'
      IF NEW.VC ELSE GO 100

*************************************************
ENTER.ZIP: **** Change vendor/customer zip code
*************************************************
      HELP="Enter zip code."
      MASK=VC.ZIP'L#6'
      CALL GEN.IN(13,12,MASK,'',Q,0,6,'','',2,-6,12,0,QI,HELP,0,23)
      QI = OCONV(QI,'MCU')
      IF QI='' THEN QI = VC.ZIP ; Q = VC.ZIP
      IF QI MATCHES '5N"-"' THEN
         QI=CHANGE(QI,'-','M')
         Q=QI
      END
      BEGIN CASE
         CASE QI='X'
            CRT @(13,12)
            IF NEW.VC THEN
               GO ENTER.ADDR2
            END ELSE
               GO 100
            END
         CASE QI='EX'
            STOP
         CASE QI MATCHES "5N" OR QI MATCHES '5N"M"' OR QI MATCHES '1A1N1A1N1A1N'
            VC.ZIP=Q
            READ ZIP.REC FROM F.ZIPS,VC.ZIP THEN
               VC.CITY=ZIP.REC<1>
               VC.STATE=ZIP.REC<2>
            END

         CASE 1
            GO ENTER.ZIP
      END CASE
      GOSUB DISPLAY.VC
      IF NEW.VC ELSE GO 100
*************************************************
ENTER.STATE: ***
*************************************************
      HELP="Enter state code."
      MASK=VC.STATE'L#2'
      CALL GEN.IN(13,13,MASK,'',Q,0,2,'','',2,-2,13,0,QI,HELP,0,23)
      QI = OCONV(QI,'MCU')
      IF QI='' THEN QI = VC.STATE ; Q = VC.STATE
      BEGIN CASE
         CASE QI='X'
            CRT @(13,13):@(-4)
            IF NEW.VC THEN
               GO ENTER.ZIP
            END ELSE
               GO 100
            END
         CASE QI='EX'
            STOP
         CASE 1
            VC.STATE = OCONV(TRIM(Q),'MCU')
      END CASE
      CRT @(13,13):VC.STATE
      IF NEW.VC ELSE GO 100

*************************************************
ENTER.CITY: ***
*************************************************
      HELP="Enter city."
      MASK=VC.CITY'L#30'
      CALL GEN.IN(13,14,MASK,'',Q,0,30,'','',2,-30,14,0,QI,HELP,0,23)
      QI = OCONV(QI,'MCU')
      IF QI='' THEN QI = VC.CITY ; Q = VC.CITY
      BEGIN CASE
         CASE QI='X'
            CRT @(13,14):@(-4)
            IF NEW.VC THEN
               GO ENTER.STATE
            END ELSE
               GO 100
            END
         CASE QI='EX'
            STOP
         CASE 1
            VC.CITY = OCONV(TRIM(Q),'MCT')
      END CASE
      CRT @(13,14):VC.CITY
      IF NEW.VC ELSE GO 100

*************************************************
ENTER.CODE: ***
*************************************************
      IF CLIENT.ID='01478' AND VC.CODE='' THEN     ; * JMK05
         TEST=OCONV(TRIM(VC.DESC),'MCU')           ; * JMK05
         BEGIN CASE                      ; * JMK05
            CASE INDEX(TEST,'BEST BUY',1) ; VC.CODE='59000'  ; * JMK05
            CASE INDEX(TEST,'CDW',1) ; VC.CODE='619800'      ; * JMK05
            CASE INDEX(TEST,'CIRCUIT CITY',1) ; VC.CODE='130000'       ; * JMK05
            CASE INDEX(TEST,'COMPUSA',1) ; VC.CODE='809750'  ; * JMK05
            CASE INDEX(TEST,'COMP USA',1) ; VC.CODE='809750'           ; * JMK05
            CASE INDEX(TEST,'D&H',1) ; VC.CODE='190000'      ; * JMK05
            CASE INDEX(TEST,'D & H',1) ; VC.CODE='190000'    ; * JMK05
            CASE INDEX(TEST,'DELL',1) ; VC.CODE='215030'     ; * JMK05
            CASE INDEX(TEST,"FRY'S",1) ; VC.CODE='306000'    ; * JMK05
            CASE INDEX(TEST,'FRYS',1) ; VC.CODE='306000'     ; * JMK05
            CASE INDEX(TEST,'HP',1) ; VC.CODE='428302'       ; * JMK05
            CASE INDEX(TEST,'HEWLETT',1) ; VC.CODE='428302'  ; * JMK05
            CASE INDEX(TEST,'PACKARD',1) ; VC.CODE='428302'  ; * JMK05
            CASE INDEX(TEST,'IMPRESISTEN',1) ; VC.CODE='428420'        ; * JMK05
            CASE INDEX(TEST,'INGRAM MICRO',1) ; VC.CODE='428350'       ; * JMK05
            CASE INDEX(TEST,'INSIGHT',1) ; VC.CODE='168000'  ; * JMK05
            CASE INDEX(TEST,'LENOVA',1) ; VC.CODE='448501'   ; * JMK05
            CASE INDEX(TEST,'IBM',1) ; VC.CODE='448501'      ; * JMK05
            CASE INDEX(TEST,'NEXT TECHNOLOGIES',1) ; VC.CODE='648000'  ; * JMK05
            CASE INDEX(TEST,'OFFICE DEPOT',1) ; VC.CODE='690100'       ; * JMK05
            CASE INDEX(TEST,'OFFICEMAX',1) ; VC.CODE='691000'          ; * JMK05
            CASE INDEX(TEST,'STAPLES',1) ; VC.CODE='835500'  ; * JMK05
            CASE INDEX(TEST,'SYNNEX',1) ; VC.CODE='850200'   ; * JMK05
            CASE INDEX(TEST,'TARGET',1) ; VC.CODE='854400'   ; * JMK05
            CASE INDEX(TEST,'TARGUS',1) ; VC.CODE='511400'   ; * JMK05
            CASE INDEX(TEST,'TECH DATA',1) ; VC.CODE='855750'          ; * JMK05
            CASE INDEX(TEST,'TOSHIBA',1) ; VC.CODE='875000'  ; * JMK05
            CASE INDEX(TEST,'WAL-MART',1) ; VC.CODE='925000'           ; * JMK05
            CASE INDEX(TEST,'WALMART',1) ; VC.CODE='925000'  ; * JMK05
            CASE INDEX(TEST,'WAL MART',1) ; VC.CODE='925000'           ; * JMK05
            CASE 1 ; VC.CODE='999999'    ; * JMK05
         END CASE                        ; * JMK05
      END                                ; * JMK05
      HELP="Enter code."
      MASK=VC.CODE'L#15'
      CALL GEN.IN(63,9,MASK,'',Q,0,15,'','',2,-15,9,0,QI,HELP,0,23)
      QI = OCONV(QI,'MCU')
      IF QI='' THEN QI = VC.CODE ; Q = VC.CODE
      BEGIN CASE
         CASE QI='X'
            CRT @(63,9):@(-4)
            IF NEW.VC THEN
               GO ENTER.CITY
            END ELSE
               GO 100
            END
         CASE QI='EX'
            STOP
         CASE 1
            VC.CODE=TRIM(Q)
      END CASE
      CRT @(63,9):VC.CODE'L#15'
      IF NEW.VC ELSE GO 100
*************************************************
ENTER.VCI: ***
*************************************************
      HELP="Enter [V]endor, [C]ustomer, [I]ntercompany or [R]eject"    ; * JMK04
      MASK=VC.VCI'L#1'
      CALL GEN.IN(63,10,MASK,'',Q,0,1,'','',2,-1,10,0,QI,HELP,0,23)
      QI = OCONV(QI,'MCU')
      IF QI='' THEN QI = VC.VCI ; Q = VC.VCI
      BEGIN CASE
         CASE QI='X'
            CRT @(63,10):@(-4)
            IF NEW.VC THEN
               GO ENTER.CODE
            END ELSE
               GO 100
            END
         CASE QI='EX'
            STOP
         CASE QI='V' OR QI = 'C' OR QI = 'I' OR QI = 'R'     ; * JMK04
            VC.VCI=QI
         CASE 1
            CALL ELINE('Invalid answer must be V, C, I OR R')
            GO ENTER.VCI
      END CASE
      CRT @(63,10):VC.VCI'L#1'
      IF NEW.VC ELSE GO 100
*************************************************
ENTER.WAIVED: ***
*************************************************
      HELP="Overide (do not process) misroutes. (Y)es or (N)"
      MASK=VC.WAIVED'L#1'
      CALL GEN.IN(63,11,MASK,'',Q,0,1,'','',2,-1,11,0,QI,HELP,0,23)
      QI = OCONV(QI,'MCU')
      IF QI='' THEN QI = VC.WAIVED ; Q = VC.WAIVED
      BEGIN CASE
         CASE QI='X'
            CRT @(63,11):@(-4)
            IF NEW.VC THEN
               GO ENTER.CODE
            END ELSE
               GO 100
            END
         CASE QI='EX'
            STOP
         CASE QI='Y' OR QI=1
            VC.WAIVED='Y'
         CASE QI='N' OR QI=0
            VC.WAIVED='N'
         CASE 1
            CALL ELINE('Invalid answer must be Y or N')
            GO ENTER.WAIVED
      END CASE
      CRT @(63,11):VC.WAIVED'L#1'
      IF NEW.VC THEN
         NEW.VC = 0
      END
      GO 100
*********************************************************************
ENTER.EMAIL: *  GET THE EMAIL ADDRESS FOR THIS VENDOR
*********************************************************************
*DF      HELP='Options:[R]eporting [M]isrouting [X]=Exit'       ; * JMK01
*DF      PRMPT='Enter Reporting or Misrouting Emails? -'        ; * JMK01
*DF      CALL GEN.IN(0,22,PRMPT,'',Q,1,1,'','',2,-1,22,1,QI,HELP,0,23)    ; * JMK01
      ETYPE=OCONV(QI,'MCU')              ; * JMK01
      ETYPE='M'
      VIN='' ; VOUT=''                   ; * JMK01
*DF      BEGIN CASE                         ; * JMK01
*DF         CASE ETYPE='X'                  ; * JMK01
*DF            RETURN                       ; * JMK01
*DF         CASE ETYPE=''                   ; * JMK01
*DF            RETURN                       ; * JMK01
*DF         CASE ETYPE='R'
*DF            HDG.TXT='Reporting Notification'       ; * JMK01
*DF            VOUT<1>=VC.REC<21>           ; * JMK01
*DF            VOUT<2>=VC.REC<22>           ; * JMK01
*DF            VOUT<3>=VC.REC<23>           ; * JMK01
*DF         CASE ETYPE='M'                  ; * JMK01; * JMK03
            HDG.TXT='Misrouting Notification'      ; * JMK01; * JMK03
            FOR ATT=1 TO 18              ; * JMK03
               VOUT<ATT>=VC.REC<23+ATT>  ; * JMK03
            NEXT ATT                     ; * JMK03
*DF         CASE 1                          ; * JMK01
*DF            GOTO ENTER.EMAIL             ; * JMK01
*DF      END CASE                           ; * JMK01
      VIN<1>=PROG.DESC                   ; * JMK01
      VIN<2>=PACK.NAME                   ; * JMK01
      VIN<3>=HDG.TXT                     ; * JMK01
      VIN<4>=0                           ; * JMK01; * JMK02
      VIN<5>=2                           ; * JMK01 PROC LVL 1=TO 2=CC 3=BCC
      IF ETYPE='M' THEN                  ; * JMK03
         VIN<6>='Y'                      ; * JMK03
         VIN<7>='Y'                      ; * JMK03
         VIN<8>='Y'                      ; * JMK03
         VIN<9>='Y'                      ; * JMK03
      END                                ; * JMK03
      CALL FB.EMAIL(VIN,VOUT)            ; * JMK01
      IF VOUT='EXIT' THEN                ; * JMK03
         STOP                            ; * JMK03
      END                                ; * JMK03
      BEGIN CASE                         ; * JMK01
*DF         CASE ETYPE='R' AND VIN<4>=0     ; * JMK01
*DF            VC.REC<21>=VOUT<1>           ; * JMK01
*DF            VC.REC<22>=VOUT<2>           ; * JMK01
*DF            VC.REC<23>=VOUT<3>           ; * JMK01
         CASE ETYPE='M' AND VIN<4>=0     ; * JMK01; * JMK03
            FOR ATT=1 TO 18              ; * JMK03
               VC.REC<23+ATT>=VOUT<ATT>  ; * JMK03
            NEXT ATT                     ; * JMK03
      END CASE                           ; * JMK01
      CALL SCR.REFRESH(PROG.NAME,PROG.DESC,PACK.NAME,CO.NAME,TIME.DATE,1)
      GOSUB DISPLAY.VC
      GO 100

      RETURN
*************************************************
ENTER.PHONE: * Change phone number
*************************************************
      HELP="Enter phone number. [space]=Clear"
      CALL GEN.IN(63,13,VC.PHONE'L#13','',Q,0,13,'','',2,63,13,0,QI,HELP,0,23)
      BEGIN CASE
         CASE QI=' '
            VC.PHONE=''
         CASE QI NE ''
            VC.PHONE=TRIM(Q)
         CASE QI=''
            NULL
         CASE 1
            GOTO ENTER.PHONE
      END CASE
      CRT @(63,13):VC.PHONE'L#13 '
      IF NEW.VC ELSE GO 100
*************************************************
SAVE.VC: ***
*************************************************
      VC.REC<1>=VC.DESC
      VC.REC<2>=VC.CODE
      VC.REC<3,1>=VC.ADDR1
      VC.REC<3,2>=VC.ADDR2
      VC.REC<4>=VC.VCI
      VC.REC<5>=VC.CITY
      VC.REC<6>=VC.STATE
      VC.REC<7>=VC.ZIP
      VC.REC<8>=VC.PHONE                 ; * JMK02
      VC.REC<10>=OCONV(VC.WAIVED,'MCU')
      IF VC.REC = OLD.VC.REC THEN
         GO ASK.SEL
      END
      OLD.VC.ZIP = OLD.VC.REC<7>
      OLD.VC.DESC = OLD.VC.REC<1>
      WRITE VC.REC ON F.VC,VC.ID
      SQLVIN=''                          ; * JMK06
      SQLVIN<1>=USER                     ; * JMK06
      SQLVIN<2>=CLIENT.ID                ; * JMK06
      SQLVIN<3>=VC.ID                    ; * JMK06
      SQLVOUT=''                         ; * JMK06
      CALL UPD.SQL.VC.SUB(SQLVIN,SQLVOUT,VC.REC)   ; * JMK06
      CALL SOUNDEX.EDIT('',VC.DESC,VC.ID,'SDX.VEND.CUST,':CLIENT.ID)
*
* Update Cross Reference Files
*
      UP.DESC=OCONV(TRIM(VC.DESC),'MCU')
      XREF.ID = UP.DESC:'*':VC.ZIP

      OLD.UP.DESC=OCONV(TRIM(OLD.VC.DESC),'MCU')
      OLD.XREF.ID = OLD.UP.DESC:'*':OLD.VC.ZIP

      IF NEW.VC#1 AND OLD.VC.ZIP # VC.ZIP THEN
         READ VC.ZIP.REC FROM F.VC.ZIPS,OLD.VC.ZIP THEN
            LOCATE VC.ID IN VC.ZIP.REC<1,1> SETTING POS THEN
               VC.ZIP.REC = DELETE(VC.ZIP.REC,1,POS)
               WRITE VC.ZIP.REC ON F.VC.ZIPS,OLD.VC.ZIP
            END
         END
         DELETE F.VC.XREF,OLD.XREF.ID
      END

      READ VC.ZIP.REC FROM F.VC.ZIPS,VC.ZIP ELSE VC.ZIP.REC = ''
      LOCATE VC.ID IN VC.ZIP.REC<1> SETTING NULL ELSE
         VC.ZIP.REC<1,-1>=VC.ID
         WRITE VC.ZIP.REC ON F.VC.ZIPS,VC.ZIP
      END

      WRITEV VC.ID ON F.VC.XREF,XREF.ID,1

*
* Update VEND.CUST.NAMES and SDX.VCN
*

      IF NEW.VC OR VC.REC<42> = '' THEN
         SEL.DESC=OCONV(TRIM(VC.DESC),'MCU') ;* NPR01
         STMT = 'SELECT VEND.CUST.NAMES,':CLIENT.ID:' WITH UC.DESC = "':SEL.DESC:'" SAMPLE 1' ;* NPR01
         EXECUTE STMT RTNLIST VCN.LIST CAPTURING MSG
         BEGIN CASE

            CASE @SELECTED < 1
               GOSUB ADD.NEW.VCN
               VCN.REC = ''
               VCN.REC<1> = VC.DESC
               VCN.REC<2> = VC.ID
               WRITE VCN.REC ON F.VCN, VCN.ID
               CALL SOUNDEX.EDIT('',VC.DESC,VCN.ID,'SDX.VCN,':CLIENT.ID)

            CASE @SELECTED = 1
               READNEXT VCN.ID FROM VCN.LIST THEN
                  READ VCN.REC FROM F.VCN, VCN.ID THEN
                     LOCATE VC.ID IN VCN.REC<2> SETTING POS ELSE
                        VCN.REC<2,-1> = VC.ID
                        WRITE VCN.REC TO F.VCN, VCN.ID
                     END
                  END
               END
         END CASE
         VC.REC<42> = VCN.ID
         WRITE VC.REC ON F.VC, VC.ID
      END

      IF NOT(NEW.VC) AND VC.DESC # OLD.VC.REC<1> THEN
         VCN.ID = VC.REC<42>
         READ VCN.REC FROM F.VCN, VCN.ID THEN
            LOCATE VC.ID IN VCN.REC<2> SETTING POS THEN
               VCN.REC = DELETE(VCN.REC,2,POS)
               IF VCN.REC<2> # '' THEN
                  WRITE VCN.REC ON F.VCN, VCN.ID
               END ELSE
                  DELETE F.VCN, VCN.ID
                  CALL SOUNDEX.EDIT(OLD.VC.REC<1>,'',VCN.ID,'SDX.VCN,':CLIENT.ID)
               END
            END
         END

         SEL.DESC=OCONV(TRIM(VC.DESC),'MCU') ;* NPR01
         STMT = 'SELECT VEND.CUST.NAMES,':CLIENT.ID:' WITH UC.DESC = "':SEL.DESC:'" SAMPLE 1' ;* NPR01
         EXECUTE STMT RTNLIST VCN.LIST CAPTURING MSG
         BEGIN CASE

            CASE @SELECTED < 1
               GOSUB ADD.NEW.VCN
               VCN.REC = ''
               VCN.REC<1> = VC.DESC
               VCN.REC<2> = VC.ID
               WRITE VCN.REC TO F.VCN, VCN.ID
               CALL SOUNDEX.EDIT('',VC.DESC,VCN.ID,'SDX.VCN,':CLIENT.ID)

            CASE @SELECTED = 1
               READNEXT VCN.ID FROM VCN.LIST THEN
                  READ VCN.REC FROM F.VCN, VCN.ID THEN
                     LOCATE VC.ID IN VCN.REC<2> SETTING POS ELSE
                        VCN.REC<2,-1> = VC.ID
                        WRITE VCN.REC ON F.VCN, VCN.ID
                     END
                  END
               END
         END CASE
         VC.REC<42> = VCN.ID
         WRITE VC.REC TO F.VC, VC.ID
      END
      GO ASK.SEL

*************************************************
ADD.NEW.VCN: ****  Get new VEND.CUST.NAMES ID
*************************************************
      TABLE.ID = 'VCN*':CLIENT.ID
      READ TABLE.REC FROM F.TABLES, TABLE.ID ELSE
         TABLE.REC = ''
         TABLE.REC<1> = 0
      END
      DONE = 0
      LOOP UNTIL DONE DO
         VCN.ID = TABLE.REC<1>'R%5'
         TABLE.REC<1> += 1
         READ TEST FROM F.VCN, VCN.ID ELSE
            WRITE TABLE.REC ON F.TABLES, TABLE.ID
            DONE = 1
         END
      REPEAT
      RETURN

*************************************************
DISPLAY.PAGE: ***
*************************************************
      START=(PAGE*9)-8
      IF START > NUM.ITEMS+1 THEN RETURN
      STOP=START+8
      FOR SEL=START TO STOP
         GOSUB CALCULATE.LINE
         CRT @(0,LINE):@(-3):SEL'L#3':' ':VC.ARRAY<2,SEL>'L#25':' ':VC.ARRAY<3,SEL>'L#10'
      NEXT SEL
      RETURN

*************************************************
CALCULATE.LINE: ***
*************************************************
      X=REM(SEL,9)
      IF X=0 THEN X=9
      LINE=8+X
      RETURN
*************************************************
EXIT.PROGRAM: * Exit
*************************************************
      STOP
*************************************************
INITIALIZE.VARIABLES: ***
*************************************************
      VC.ID=''
      VC.REC=''
      VC.DESC=''
      VC.CODE=''
      VC.ADDR1=''
      VC.ADDR2=''
      VC.CITY=''
      VC.STATE=''
      VC.ZIP=''
      VC.ZIP.REC=''
      VC.VCI=''
      VC.PHONE=''                        ; * JMK02
      SEL=''
      RETURN
*************************************************
ASSIGN.VARIABLES: ***
*************************************************
      VC.DESC=VC.REC<1>
      VC.CODE=VC.REC<2>
      VC.ADDR1=VC.REC<3,1>
      VC.ADDR2=VC.REC<3,2>
      VC.VCI=VC.REC<4>
      VC.CITY=VC.REC<5>
      VC.STATE=VC.REC<6>
      VC.ZIP=VC.REC<7>
      VC.PHONE=VC.REC<8>                 ; * JMK02
      VC.WAIVED=VC.REC<10>
      OLD.VC.REC=VC.REC
      NEW.VC=0
      RETURN

*************************************************
DISPLAY.VC: **** Display window headings
*************************************************
      CRT @(0,7) :'-------------------Vendor/Customer Information - ID#':VC.ID'R%5':'-----------------------'
      CRT @(0,9) :' 1 Desc......':VC.DESC'L#30'
      CRT @(0,10) :' 2 Addr1.....':VC.ADDR1'L#30'
      CRT @(0,11) :' 3 Addr2.....':VC.ADDR2'L#30'
      CRT @(0,12) :' 4 Zip Code..':VC.ZIP'L#6'
      CRT @(0,13) :' 5 State.....':VC.STATE'L#2'
      CRT @(0,14) :' 6 City......':VC.CITY'L#30'
      CRT @(48,9) :' 7 Code........':VC.CODE'L#20'
      CRT @(48,10):' 8 VCIR........':VC.VCI'L#1'
      CRT @(48,11):' 9 MR Waived...':VC.WAIVED'L#1'
      EMAIL.TMP='No'                     ; * JMK01
*DF      FOR ATT=21 TO 38                   ; * JMK01
      FOR ATT=24 TO 38                   ; * JMK01
         IF VC.REC<ATT>#'' THEN EMAIL.TMP='Yes'    ; * JMK01
      NEXT ATT                           ; * JMK01
      CRT @(48,12):'10 Email Addr..':EMAIL.TMP'L#3'
      CRT @(48,13):'11 Phone Num...':VC.PHONE 'L#13 '        ; * JMK02
      RETURN
*************************************************
CLEAR.WINDOW: ***
*************************************************
      FOR X = 7 TO 21
         CRT @(0,X):@(-4)
      NEXT X
      RETURN

VIEW.MISROUTES: ***
      CRT @(0,0):@(-3)
