      SUBROUTINE FB.2.1.BOL(CLIENT.ID,MAT CLIENT.REC,BOL,QI,OPT)
*
 UPL.VIN = 'FBBP' ;  UPL.VIN<2> = 'FB.2.1.BOL' ; UPL.VOUT = ''                    ;* NPR_UPL 04/23/2010
 CALL UPD.PROGRAM.LOG(UPL.VIN,UPL.VOUT)                                                           ;* NPR_UPL 04/23/2010
* SUBROUTINE TO ENTER BILL OF LADINGS
*
* FREIGHT BILLING PACKAGE
*
* AUTHOR: BRIAN M. QUEEN
* DATE: JUNE 24, 1991
*
* THIS PROGRAM IS A PROPRIETARY PRODUCT OF AUTOMATED FREIGHT SYSTEMS
* AND MAY NOT BE USED, COPIED OR MODIFIED WITHOUT THE EXPRESS
* WRITTEN CONSENT OF SAME.
*
********************************************************************
*
* Last updated by AFS (valerie) at 16:14:43 on 08/24/1994.
*   CHANGED BILL OF LADING NUMBERS TO BE 25 CHARACTERS.
* Last updated by AFS.SOURCE (valerie) at 14:05:23 on 08/04/1994.
*   ADDED CLIENT INSERT TO GO WITH FB.2.1.
*
$INCLUDE PROG.ADMIN INC_OPTIONS
$INCLUDE FILE.DEFS INC_CLIENTS
*
* Initialize variables
*
      PROMPT''
      PROG.NAME='FB.2.1.BOL'
*
* Open files
*
      IF CL.BOL.CRX.FLAG='Y' THEN
        FILE='FB.BOL.CRX,':CLIENT.ID
        OPEN '',FILE TO F.BOL.CRX ELSE CALL OPEN.ABORT(FILE,PROG.NAME)
        OPEN '','CARRIERS' TO F.CARRIERS ELSE
          CALL OPEN.ABORT("CARRIERS",PROG.NAME)
        END
      END
      BEGIN CASE
        CASE OPT='I'
          MODE='EDIT'
          INQUIRE=1
        CASE OPT='E'
          MODE='INPUT'
          INQUIRE=0
        CASE OPT='V'
          MODE='INPUT'
          INQUIRE=0
      END CASE
      LN=0
      CALL FB.CLEAR.WINDOW
*
* Display window headings
*
      CRT @(41,6):"----------- Bill of Ladings ----------"
*
* Start input
*
      PAGE=1
      GOSUB 9000
*
* Enter bill of lading line
*
100   IF MODE='INPUT' THEN
        LN=LN+1
        QI=LN
        IF LN GT STOP THEN
          PAGE=PAGE+1
          GOSUB 9000
        END
      END ELSE
        HELP="Enter Line Num. ''=End. 'B'=Back page. 'F'=Fwd page. 'X'=Back. 'EX'=Exit."
        CALL GEN.IN(41,16,'Enter line number. ###','',Q,0,3,'','',
          0,-3,16,0,QI,HELP,0,23)
      END
      QI=OCONV(QI,'MCU')
      BEGIN CASE
        CASE QI='B'
          PAGE=PAGE-1
          IF PAGE=0 THEN PAGE=1
          GOSUB 9000
        CASE QI='F'
          PAGE=PAGE+1
          GOSUB 9000
        CASE QI=''
          CALL FB.CLEAR.WINDOW
          GOTO 7900
        CASE QI='X'
          CALL FB.CLEAR.WINDOW
          RETURN
        CASE QI='EX'
          STOP
        CASE NUM(QI)
          IF INQUIRE THEN
            CALL ELINE('Can not access detail in inquiry!')
          END ELSE
            SEL=QI
            GOSUB 8000
            IF OPT='V' & QI='' THEN GOTO 7900
          END
      END CASE
      GOTO 100
*
* End of program
*
7900  X=DCOUNT(BOL<1>,CHAR(253))
      FOR I=X TO 1 STEP -1
        IF BOL<1,I>='' THEN BOL=DELETE(BOL,1,I,0)
      NEXT I
      IF OPT='V' THEN
        X=DCOUNT(BOL<1>,CHAR(253))
        FOR I=1 TO X
          IF BOL<2,I> NE '*' THEN
            CALL ELINE('All Bill of Ladings have not been verified.')
            GOTO 100
          END
        NEXT I
        OPT='I'
        PAGE=1
        GOSUB 9000
      END
      BOL=DELETE(BOL,2,0,0)
      RETURN
*-----------------------------------------------------------------------
*
* INTERNAL SUBROUTINES
*
*-----------------------------------------------------------------------
*
* Enter bill of lading
*
8000  IF SEL LT START ! SEL GT STOP THEN
        CALL ELINE('Invalid entry.')
        RETURN
      END
      GOSUB 9100
*
8010  HELP="Enter Bill of Lading ''=Accept 'D'=Delete 'E'=Edit Mode 'X'=Back 'EX'=Exit"
      IF OPT='V' THEN MASK=SPACE(25) ELSE MASK=BOL<1,SEL>'L#25'
      CALL GEN.IN(45,LINE,MASK,'',Q,0,25,'','',2,-25,LINE,0,QI,
        HELP,0,23)
      QI=OCONV(QI,'MCU')
      BEGIN CASE
        CASE QI='' & OPT NE 'V'
          Q=BOL<1,SEL>
          IF Q='' & MODE='INPUT' THEN MODE='EDIT'
        CASE QI='' & OPT EQ 'V'
          RETURN
        CASE QI='EX'
          STOP
        CASE QI='X'
          CRT @(45,LINE):BOL<1,SEL>'L#25'
          MODE='EDIT'
          DATA 'X'
          RETURN
        CASE QI='D' & OPT NE 'V'
          BOL=DELETE(BOL,1,SEL)
          GOSUB 9000
          RETURN
        CASE QI='E'
          CRT @(45,LINE):BOL<1,SEL>
          MODE='EDIT'
          RETURN
      END CASE
*
* Locate Bill of Lading and set verified flag
*
      IF OPT='V' THEN
        X=0
        DONE=0
        VERIFIED=0
        LOOP
          X=X+1
        UNTIL DONE ! VERIFIED DO
          IF BOL<1,X>=QI THEN
            VERIFIED=1
            BOL<2,X>='*'
          END
          IF BOL<1,X>='' THEN DONE=1
        REPEAT
        IF VERIFIED=0 THEN
          CALL ELINE('Invalid Bill of Lading.')
          GOTO 8010
        END
      END ELSE
        BOL<1,SEL>=Q
      END
      CRT @(45,LINE):BOL<1,SEL>
*
* Check for Bill of Lading in Cross Reference file.
*
      IF CL.BOL.CRX.FLAG='Y' AND OPT#'V' THEN
        IF BOL<1,SEL>#'' THEN
          READ X FROM F.BOL.CRX,BOL<1,SEL> THEN
            READV CARRIER.NAME FROM F.CARRIERS,X<2>,1 ELSE
              CARRIER.NAME="~~ Unknown ~~"
            END
            TEXT = 'WARNING - BOL ':BOL<1,SEL>:' entered on Pro '
            TEXT := X<1>:' Carrier ':CARRIER.NAME
            TEXT=TEXT'L#79'
            CALL ELINE(TEXT)
*           LN=LN-1
          END
        END
      END
      RETURN
*
* Print page
*
9000  START=(PAGE*9)-8
      STOP=START+8
      FOR SEL=START TO STOP
        GOSUB 9100
        IF BOL<1,SEL>='' ! OPT='V' THEN
          IF OPT='V' THEN
            CRT @(41,LINE):@(-4):
          END ELSE
            CRT @(41,LINE):SEL'L#3':@(-4):
          END
        END ELSE
          CRT @(41,LINE):SEL'L#3':' ':BOL<1,SEL>'L#25':
        END
      NEXT SEL
      RETURN
*
* Calculate line
*
9100  X=REM(SEL,9)
      IF X=0 THEN X=9
      IF OPT='V' THEN X=1
      LINE=6+X
      RETURN
