      SUBROUTINE CALC.FSC(VIN,VOUT,DFSC,F.TABLES)
*
* 100405 - JMK01 - CORRECT FILE FORMAT OF DFSC RECORDS
* 122807 - JMK02 - C071349 - New AFS Fuel
* 050708 - JMK03 - T081123 - Rawlings / fuel surcharge
* 100608 - JMK04 - T082273 - FSC Benchmarks ; Force FSC Benchmark to "C" when current fuel is not "AFS" or "AFN"
* 121708 - JMK05 - T082703 - Truckload OLA ; benchmark amount incorrect
*
      BILL.DATE=VIN<1>
      WEIGHT=VIN<2>
      MILES=VIN<3>
      CC=VIN<5>
      FREIGHT.AMOUNT=VIN<6>
      ORIG.STATE=VIN<7>                  ; * Special for Xpedx and AFS
      DEST.STATE=VIN<8>                  ; * Special for AFS
****************************************
* Initialize Variables
****************************************
      FSC.AMOUNT=''
      ERROR=''
      MIN.FLAG=0
***************************************
* Variables used for troubleshooting
***************************************
      CALC.FSC.AMT=''
      OVER.AMT=''
      STEPS.OVER=''
      BASE.PERC=''
      OVER.PERC=''
      DELAY.DAYS=''
      SHIFT.DATE=''
      NAT.FUEL.PRICE=''
      NAT.INDEX.NAME=''
      CONV.FUEL.PRICE=''
      CALC.PERC=0
      FORMULA=''
      MODE=''
      BEGIN.EFF.DATE=''
      END.EFF.DATE=''
      BASE.FUEL.PRICE=''
      MAX.SETUP.PRICE=''
      AFS.CONTRACT=''                    ; * JMK04

***************************************
* Check First and Last Effective Date
      FIRST.DATE=DFSC<1,1>
      LAST.DATE=DFSC<2,DCOUNT(DFSC<1>,@VM)>
      IF BILL.DATE < FIRST.DATE THEN     ; * Is bill Before Beginning Date
         ERROR='Bill date is earlier than 1st FSC Effective Date'
         GO 9999
      END
      IF LAST.DATE#'' AND (BILL.DATE > LAST.DATE) THEN
         ERROR='Bill date is later than last FSC Effective Date'
         GO 9999
      END
* Check Actual effective dates
      LOCATE BILL.DATE IN DFSC<1> BY 'AR' SETTING CAR.POS ELSE
         CAR.POS=CAR.POS-1
      END
      IF CAR.POS < 1 THEN
         ERROR='Serious error FSC Carrier Date POS is less than 1, Call Owen'
         GO 9999
      END
      BEGIN.EFF.DATE=DFSC<1,CAR.POS>
      END.EFF.DATE=DFSC<2,CAR.POS>
      IF BILL.DATE < BEGIN.EFF.DATE THEN           ; * Is bill Before Beginning Date
         ERROR='Bill is before Beginning Effective Date'
         GO 9999                         ; * Bill Dated before 1st Setup Date
      END
      IF END.EFF.DATE#'' AND (BILL.DATE > END.EFF.DATE) THEN
         ERROR='Bill is after ending effective date'
         GO 9999                         ; * Date Range "Expired"
      END
      NAT.INDEX.NAME=DFSC<3,CAR.POS>
      IF ORIG.STATE='' THEN STATE.INDEX=0 ELSE
         STATE.INDEX=INDEX('-CA-AZ-NV-OR-WA-',ORIG.STATE,1)
      END
      IF NAT.INDEX.NAME='XP' THEN
         IF STATE.INDEX ELSE
* Maintain XP for California, Arizona, Nevada, Oregon, & Washington
*  otherwise change to US
            NAT.INDEX.NAME='US'
         END
      END
      DELAY.DAYS=DFSC<4,CAR.POS>
      IF DELAY.DAYS='R1' THEN            ; * JMK03
         BILL.ODATE=OCONV(BILL.DATE,'D2-')         ; * JMK03
         OMO=BILL.ODATE[1,2]             ; * JMK03
         ODY=BILL.ODATE[4,2]             ; * JMK03
         OYR=BILL.ODATE[7,2]             ; * JMK03
         IF ODY<15 THEN OMO-=1           ; * JMK03
         IF OMO<1 THEN                   ; * JMK03
            OMO+=12                      ; * JMK03
            OYR-=1                       ; * JMK03
         END                             ; * JMK03
         MON=0                           ; * JMK03
         FOR DY=1 TO 7 UNTIL MON         ; * JMK03
            IF OCONV(ICONV(OMO:'/':DY:'/':OYR,'D'),'DW')=1 THEN        ; * JMK03
               MON=1                     ; * JMK03
               FIRST.MON=ICONV(OMO:'/':DY:'/':OYR,'D')       ; * JMK03
            END                          ; * JMK03
         NEXT DY                         ; * JMK03
         DELAY.DAYS=BILL.DATE-FIRST.MON  ; * JMK03
      END                                ; * JMK03
      SHIFT.DATE=BILL.DATE-DELAY.DAYS
      CALC.METHOD=DFSC<8,CAR.POS>
      ADD.PERC=0
      BEGIN CASE
         CASE NAT.INDEX.NAME='XP'        ; * Special for Xpedx
            TABLE.ID='FSC*CA'
            READ DNAT.INDEX.CA FROM F.TABLES,TABLE.ID ELSE
               ERROR='Serious Error FSC Table ':TABLE.ID:' is not on file'
               GO 9999
            END
*** Get CA National Fuel Price (Based on Shifted Date) ***
            LOCATE SHIFT.DATE IN DNAT.INDEX.CA<1> BY 'DR' SETTING NAT.POS ELSE
* No Shift (-1) necessary, you have the value you want if you use BY 'DR'
            END
            NAT.FUEL.PRICE.CA=DNAT.INDEX.CA<2,NAT.POS>
            TABLE.ID='FSC*WC'
            READ DNAT.INDEX.WC FROM F.TABLES,TABLE.ID ELSE
               ERROR='Serious Error FSC Table ':TABLE.ID:' is not on file'
               GO 9999
            END
*** Get WC National Fuel Price (Based on Shifted Date) ***
            LOCATE SHIFT.DATE IN DNAT.INDEX.WC<1> BY 'DR' SETTING NAT.POS ELSE
* No Shift (-1) necessary, you have the value you want if you use BY 'DR'
            END
            NAT.FUEL.PRICE.WC=DNAT.INDEX.WC<2,NAT.POS>
*  This is the special for Xpedx The average of CA + WC / 2
            NAT.FUEL.PRICE=((NAT.FUEL.PRICE.CA+NAT.FUEL.PRICE.WC)/2)'R0'
         CASE 1
*           IF NAT.INDEX.NAME='AFS' THEN ; * JMK02
            IF NAT.INDEX.NAME='AFS' OR NAT.INDEX.NAME='AFN' THEN       ; * JMK02
               AFS.CONTRACT=NAT.INDEX.NAME         ; * JMK04
**         If ORIG or DEST = CA then add 1.5 percent to the FSC
               IF ORIG.STATE='' THEN ORIG.STATE.INDEX=0 ELSE
                  ORIG.STATE.INDEX=INDEX('-CA-','-':ORIG.STATE:'-',1)
               END
               IF DEST.STATE='' THEN DEST.STATE.INDEX=0 ELSE
                  DEST.STATE.INDEX=INDEX('-CA-','-':DEST.STATE:'-',1)
               END
               IF ORIG.STATE.INDEX+DEST.STATE.INDEX THEN ADD.PERC=1500
               BEGIN CASE                ; * JMK02
                  CASE NAT.INDEX.NAME='AFS'        ; * JMK02
                     AFS.DFSC.ID='AFS*FSC'         ; * JMK02
                  CASE NAT.INDEX.NAME='AFN'        ; * JMK02
                     AFS.DFSC.ID='AFN*FSC'         ; * JMK02
               END CASE                  ; * JMK02
               NAT.INDEX.NAME='US'
*              READ AFS.DFSC FROM F.TABLES,'AFS*FSC' THEN    ; * JMK01 ; * JMK02
               READ AFS.DFSC FROM F.TABLES,AFS.DFSC.ID THEN  ; * JMK01 ; * JMK02
                  DFSC=AFS.DFSC          ; * JMK01
                  LOCATE BILL.DATE IN DFSC<1> BY 'AR' SETTING CAR.POS ELSE       ; * JMK01
                     CAR.POS=CAR.POS-1   ; * JMK01
                  END                    ; * JMK01
               END                       ; * JMK01
            END
            TABLE.ID='FSC*':NAT.INDEX.NAME
            READ DNAT.INDEX FROM F.TABLES,TABLE.ID ELSE
               ERROR='Serious Error FSC Table ':TABLE.ID:' is not on file'
               GO 9999
            END
*** Get National Fuel Price (Based on Shifted Date) ***
            LOCATE SHIFT.DATE IN DNAT.INDEX<1> BY 'DR' SETTING NAT.POS ELSE
* No Shift (-1) necessary, you have the value you want if you use BY 'DR'
            END
            NAT.FUEL.PRICE=DNAT.INDEX<2,NAT.POS>
      END CASE

*** Find out how to calculate (Based on weight) ***
      FORMULA=0
      IF WEIGHT < DFSC<7,CAR.POS> THEN MODE='LTL' ELSE MODE='TL'
      BEGIN CASE
         CASE MODE = 'LTL'
            FSC.MIN=DFSC<5,CAR.POS>
            LOCATE NAT.FUEL.PRICE IN DFSC<9,CAR.POS> BY 'AR' SETTING FP.POS ELSE
               FP.POS-=1
               IF FP.POS < 1 THEN
                  ERROR='Serious Error FP.POS is less than 1'
                  GO 9999
               END
            END
            IF DFSC<13,CAR.POS,FP.POS>=2 THEN
               CONV.FUEL.PRICE=NAT.FUEL.PRICE/10   ; * Place a decimal in the Var
               IF DFSC<14,CAR.POS,FP.POS>='Y' THEN CONV.FUEL.PRICE=CONV.FUEL.PRICE'R0'
               CONV.FUEL.PRICE=FIELD(CONV.FUEL.PRICE,'.',1):"0"
            END ELSE
               CONV.FUEL.PRICE=NAT.FUEL.PRICE
            END
            FSC.PERC=DFSC<10,CAR.POS,FP.POS>
            IF FSC.PERC#'F' THEN         ; * Calculate the Normal Way
               MAX.SETUP.PRICE=DFSC<9,CAR.POS,DCOUNT(DFSC<9,CAR.POS>,@SVM)>
               CALC.PERC=FSC.PERC + ADD.PERC
               FSC.AMOUNT=FREIGHT.AMOUNT*CALC.PERC/100000
            END ELSE
               MAX.SETUP.PRICE=''
               FORMULA=1
*              BASE.FUEL.PRICE=DFSC<17,CAR.POS>    ; * JMK01
               BASE.FUEL.PRICE=DFSC<17,CAR.POS,FP.POS>       ; * JMK01
               OVER.AMT=CONV.FUEL.PRICE-BASE.FUEL.PRICE
               IF OVER.AMT<0 THEN OVER.AMT=0
*          STEPS.OVER=(OVER.AMT/DFSC<18,CAR.POS>)'R0'
*              IF DFSC<18,CAR.POS>+0=0 THEN        ; * JMK01
*              IF DFSC<18,CAR.POS,FP.POS>+0=0 THEN           ; * JMK01
*                 CALL ELINE('CALC.FSC Line 205 - Division by zero call programming')
*              END
*              IF DFSC<18,CAR.POS>=0 THEN          ; * JMK01
               IF DFSC<18,CAR.POS,FP.POS>+0=0 THEN           ; * JMK01
                  STEPS.OVER=0
               END ELSE
*                 STEPS.OVER=INT(OVER.AMT/DFSC<18,CAR.POS>) ; * JMK01
                  STEPS.OVER=INT(OVER.AMT/DFSC<18,CAR.POS,FP.POS>)     ; * JMK01
               END
*              OVER.PERC=DFSC<16,CAR.POS>*STEPS.OVER         ; * JMK01
*              BASE.PERC=DFSC<15,CAR.POS>          ; * JMK01
               OVER.PERC=DFSC<16,CAR.POS,FP.POS>*STEPS.OVER  ; * JMK01
               BASE.PERC=DFSC<15,CAR.POS,FP.POS>   ; * JMK01
               CALC.PERC=BASE.PERC+OVER.PERC+ADD.PERC
               FSC.AMOUNT=FREIGHT.AMOUNT*CALC.PERC/100000
            END
         CASE MODE = 'TL'
            FSC.MIN=DFSC<5,CAR.POS>
            MAX.SETUP.PRICE=DFSC<9,CAR.POS,DCOUNT(DFSC<9,CAR.POS>,@SVM)>
            LOCATE NAT.FUEL.PRICE IN DFSC<9,CAR.POS> BY 'AR' SETTING FP.POS ELSE
               FP.POS-=1
               IF FP.POS < 1 THEN
                  ERROR='Serious Error FP.POS is less than 1'
                  GO 9999
               END
            END
            IF DFSC<13,CAR.POS,FP.POS>=2 THEN
               CONV.FUEL.PRICE=NAT.FUEL.PRICE/10   ; * Place a decimal in the Var
               IF DFSC<14,CAR.POS,FP.POS>='Y' THEN CONV.FUEL.PRICE=CONV.FUEL.PRICE'R0'
               CONV.FUEL.PRICE=FIELD(CONV.FUEL.PRICE,'.',1):"0"
            END ELSE
               CONV.FUEL.PRICE=NAT.FUEL.PRICE
            END
            CALC.METHOD=DFSC<8,CAR.POS>
            BEGIN CASE
               CASE CALC.METHOD='PERC'
                  FSC.PERC=DFSC<11,CAR.POS,FP.POS>
                  IF FSC.PERC#'F' THEN   ; * Calculate the Normal Way
                     CALC.PERC=FSC.PERC+ADD.PERC
                     FSC.AMOUNT=FREIGHT.AMOUNT*CALC.PERC/100000
                  END ELSE
                     FORMULA=1
*                    BASE.FUEL.PRICE=DFSC<27,CAR.POS>        ; * JMK01
                     BASE.FUEL.PRICE=DFSC<27,CAR.POS,FP.POS>           ; * JMK01
                     OVER.AMT=CONV.FUEL.PRICE-BASE.FUEL.PRICE
                     IF OVER.AMT<0 THEN OVER.AMT=0
*                    OVER.PERC=DFSC<26,CAR.POS>*(OVER.AMT/DFSC<28,CAR.POS>)      ; * JMK01
*                    BASE.PERC=DFSC<25,CAR.POS>    ; * JMK01
                     OVER.PERC=DFSC<26,CAR.POS,FP.POS>*(OVER.AMT/DFSC<28,CAR.POS,FP.POS>)  ; * JMK01
                     BASE.PERC=DFSC<25,CAR.POS,FP.POS>       ; * JMK01
                     CALC.PERC=BASE.PERC+OVER.PERC+ADD.PERC
                     FSC.AMOUNT=FREIGHT.AMOUNT*CALC.PERC/100000
                  END
*              CASE CALC.METHOD='PERMILE'          ; * JMK06
               CASE CALC.METHOD='MILES'  ; * JMK06
                  FSC.AMOUNT=MILES*DFSC<12,CAR.POS,FP.POS>/10
            END CASE
      END CASE
      FSC.AMOUNT=FSC.AMOUNT'R0'
*CRT 'SHIFT.DATE: ':OCONV(SHIFT.DATE,'D2/')
*CRT 'NAT.FUEL.PRICE: ':NAT.FUEL.PRICE
*CRT 'CONV.FUEL.PRICE: ':CONV.FUEL.PRICE
*CRT 'CALC.PERC: ':CALC.PERC
*CRT 'OVER.AMT: ':OVER.AMT
*CRT 'CALCULATED FSC.AMOUNT: ':FSC.AMOUNT

      IF FSC.AMOUNT<FSC.MIN THEN
         FSC.AMOUNT=FSC.MIN
         MIN.FLAG=1
*      CRT 'FSC.AMOUNT: ':FSC.AMOUNT
      END
*CRT 'OVER.PERC: ':OVER.PERC
9999: *** End of Program
      VOUT=''
      VOUT<1>=FSC.AMOUNT
      VOUT<2>=MIN.FLAG
      VOUT<3>=CALC.PERC
      VOUT<4>=ERROR
**** The following are used for debugging purposes ***
      VOUT<6>=CALC.FSC.AMT
      VOUT<7>=OVER.AMT
      VOUT<8>=STEPS.OVER
      VOUT<9>=BASE.PERC
      VOUT<10>=OVER.PERC
      VOUT<11>=DELAY.DAYS
      VOUT<12>=SHIFT.DATE
      VOUT<13>=NAT.FUEL.PRICE
      VOUT<14>=CONV.FUEL.PRICE
      VOUT<15>=BEGIN.EFF.DATE
      VOUT<16>=FORMULA                   ; * Switch 0 or 1
      VOUT<17>=MODE
      VOUT<18>=BASE.FUEL.PRICE
      VOUT<19>=NAT.INDEX.NAME
      VOUT<20>=MAX.SETUP.PRICE           ; * Highest Nat Fuel Price in Table
      VOUT<21>=AFS.CONTRACT              ; * JMK04
      RETURN
