      SUBROUTINE FB.BILLS.ACCESSED(VIN,VOUT,CLIENT.REC,CARRIER.REC,BILL.REC,F.ACCESS,F.TABLES)
********************************************************************
* Program name :  FB.BILLS.ACCESSED
* Author       :  Unknown
* Date written :  Unknown
* Library      :  FBBP
$* COPYRIGHT (C) 2016 - AFS LOGISTICS LLC - ALL RIGHTS RESERVED.
*
* Freight bill access history

* Last updated by afs (AFSLOGISTICS\nmeddaugh) at 10:00:19 on 01/16/2020.
*
* Start modification history*
* 041012 - SWB01 - Proj 120 Freight Bill tracking from Mail Room to Processing
* 011620 - NKH01 - ACCU-733 Restructured including FB Bills Access Reasons file
* End modification history      
********************************************************************
*
* Initialize variables
      PROG.NAME='FB.BILLS.ACCESSED'
      PROG.DESC = "Who Accessed Bill"      
      CALL GET.USER.NEW(USER,CH.NUM)
      UPL.VIN = 'FBBP' ; UPL.VIN<2> = PROG.NAME ; UPL.VIN<3> = USER ; UPL.VIN<4> = CH.NUM ; UPL.VOUT = ''
      CALL UPD.PROGRAM.LOG(UPL.VIN,UPL.VOUT)
      CLIENT.ID=VIN<1>                   ; * 5 DIGIT AFS CLIENT NUMBER
      PRO.ID=VIN<2>                      ; * AFS FB.BILLS.HIST RECORD ID CARRIER*PRO*SEQ
      CARRIER.ID=FIELD(PRO.ID,'*',1)     ; * 5 DIGIT AFS CARRIER NUMBER
      USER=VIN<3>                        ; * LOGIN USER
      TYPE=VIN<4>                        ; * LOGIN USER DEPARTMENT OR "IN" FOR INTERNET
      ACC.DATE=VIN<5>                    ; * DATE OF INQUIRY
      ACC.TIME=VIN<6>                    ; * TIME OF INQUIRY
      NOTIFY=VIN<7>                      ; * NOTIFY IF "FP" ACCESS MORE THAN 3 TIMES (1 TO NOTIFY, 0 TO BUILD FILE)
      PROG.NAME=VIN<8>                   ; * PROGRAM NAME OF TOP LEVEL CALLING PROGRAM
      PROG.DESC=VIN<9>                   ; * PROGRAM DESC OF TOP LEVEL CALLING PROGRAM
      PACK.NAME=VIN<10>                  ; * PACKAGE NAME OF TOP LEVEL CALLING PROGRAM
      CO.NAME=VIN<11>                    ; * COMPANY NAME FROM TOP LEVEL CALLING PROGRAM
      TOUCHES.ONLY=VIN<12>               ; * Only display those who touched the Freight Bill.
      DASH=STR('-',40)
      PAGE.SIZE=13                       ; * Number of lines per page
      START.Y=7                          ; * Starting y-position on the screen minus one
      LAST.EDIT.MASK=1                   ; * Last item edited or deleted
      WIDTH=80                           ; * Print width for printing table of includes/excludes
      CTR=INT(WIDTH/2)                   ; * Center of the table width

      ACCESS.ID=PRO.ID
      READ ACCESS.REC FROM F.ACCESS,ACCESS.ID ELSE ACCESS.REC=''
      ACCESS.ORG=ACCESS.REC

* Open Files
      EDI.REC = ""
      EDI.ID = BILL.REC<88>
      EDI.HIST.ID = FIELD(EDI.ID,"*",2,99)
      IF EDI.HIST.ID NE '' THEN
         OPEN '','EDI.BILLS.HIST,':CLIENT.ID TO F.EDI.BHIST THEN
            READ EDI.HIST.REC FROM F.EDI.BHIST, EDI.ID ELSE EDI.HIST.REC = ""
         END
      END

      OPEN '','BCTRL' TO F.BCTRL ELSE
         CALL OPEN.ABORT('BCTRL0','FB.BILLS.ACCESSED')   
         RETURN
      END
      
      OPEN '','FB.BILLS.ACCESS.REASON' TO FB.BILLS.ACCESS.REASON ELSE
         CALL OPEN.ABORT('FB.BILLS.ACCESS.REASON','FB.BILLS.ACCESSED')  
         RETURN
      END

*****************************************************************
* MAIN PROCESSING
*****************************************************************
      GOSUB SETUP.DATA
      GOSUB DISPLAY.HEADER
      N.DET = DCOUNT(NTEXT<1>,@VM)
      NUM.PAGES=INT(N.DET/PAGE.SIZE)
      IF REM(N.DET,PAGE.SIZE) THEN NUM.PAGES+=1
      PAGE.NUMBER=1
      GOSUB DISPLAY.PAGE
      PAGE.NUMBER=1
      GOSUB DISPLAY.DETAIL

      RETURN
      
*****************************************************************
DISPLAY.HEADER:
*****************************************************************
      CRT @(-1)
      CALL AFS.SCR.HEAD(CO.ID,FILE.ID,PERIOD,PROG.NAME,PROG.DESC,PACK.NAME,CO.NAME,TIME.DATE,1)
      SCREEN.DSP = ''

      REJ.TYPE = BILL.REC<66>
      REJ.BY = BILL.REC<60,1,1>
      REJ.DATE = BILL.REC<60,1,2>

      BILL.TYPE = "DATA ENTRY"
      IF EDI.ID NE "" THEN BILL.TYPE = "EDI"

      SCREEN.DSP := @(0 , 3):'Client  : ' : CLIENT.ID : " " : CLIENT.REC<2> : @(50,3) :'Pro #   : ' : FIELD(PRO.ID,"*",2)
      SCREEN.DSP := @(0 , 4):'Carrier : ' : CARRIER.ID : " " : CARRIER.REC<1> : @(50,4) :'Type    : ' : BILL.TYPE
      SCREEN.DSP := @(0 , 5):STR("=",79)
      SCREEN.DSP := @(0 , 6) : 'Name''L#18 ' : ' ' : 'Action''L#22 ':'Date''R#10   ':'Time''R#10' : 'Duration' 'R#13'
      SCREEN.DSP := @(0 , 7) : DASH'L#18 ' : ' ' : DASH'L#22 ':DASH'R#10   ':DASH'R#10' : ' ' : DASH 'R#12'

      CRT SCREEN.DSP

      RETURN

*****************************************************************
DISPLAY.DETAIL:
*****************************************************************
      CRT @(0,22):'Enter Option : --'
      HELP='[B]ack Page [F]orward Page [T]op Page [L]ast Page, [CR] to continue or [EX]it'
      CALL GEN.IN(15,22,'','',Q,0,2,'','',2,15,22,0,QI,HELP,0,23)
      QI = OCONV(QI,"MCU")

      BEGIN CASE
         CASE QI='L'                     ; * Last Page
            IF NUM.PAGES#0 AND PAGE.NUMBER#NUM.PAGES THEN
               PAGE.NUMBER=NUM.PAGES
               GOSUB DISPLAY.PAGE
            END ELSE
               CALL ELINE('** LAST PAGE **')
            END
         CASE QI='T'                     ; * Top Page
            IF NUM.PAGES#0 AND PAGE.NUMBER#1 THEN
               PAGE.NUMBER=1
               GOSUB DISPLAY.PAGE
            END ELSE
               CALL ELINE('** FIRST PAGE **')
            END
         CASE QI='F'                     ; * Forward a page
            IF NUM.PAGES#0 AND PAGE.NUMBER<NUM.PAGES THEN
               PAGE.NUMBER+=1
               GOSUB DISPLAY.PAGE
            END ELSE
               CALL ELINE('** LAST PAGE **')
            END
         CASE QI='B'                     ; * Backward a page
            IF NUM.PAGES#0 AND PAGE.NUMBER>1 THEN
               PAGE.NUMBER-=1
               GOSUB DISPLAY.PAGE
            END ELSE
               CALL ELINE('** FIRST PAGE **')
            END
         CASE QI=''
            RETURN                       ; * To calling program
         CASE QI='EX'
            STOP
      END CASE

      GOTO DISPLAY.DETAIL

      RETURN

*****************************************************************
DISPLAY.PAGE:
*****************************************************************
      STR.OUT='Page ':PAGE.NUMBER:' of ':NUM.PAGES:' '
      STR.OUT=SPACE(80-LEN(STR.OUT)):STR.OUT
      CRT @(0,5): STR.OUT
      Y=1                                ; * Screen position
      IF PAGE.NUMBER=0 THEN
         LINE.NUM=1
      END ELSE
         LINE.NUM=(PAGE.NUMBER-1)*PAGE.SIZE+1
      END
      LOOP WHILE Y<=PAGE.SIZE
         BEGIN CASE
            CASE LINE.NUM>N.DET
               * Print nothing - actually clears line
               SCREEN.LINE = ""
            CASE 1
               SCREEN.LINE = NTEXT<1,LINE.NUM>
         END CASE
         CRT @(0,START.Y+Y):SCREEN.LINE:@(-4)      ; * Draw screen line
         Y+=1                                      ; * Inc screen position
         LINE.NUM+=1
      REPEAT

      RETURN

*****************************************************************
SETUP.DATA:
*****************************************************************
      ACC.CNT=0
      ACCESS.LOG=''
      NTEXT = ''
      NVAL=DCOUNT(ACCESS.REC<1>,@VM)
      FOR VAL=1 TO NVAL
         LOG.DATE=FIELD(ACCESS.REC<1,VAL>,'*',1)
         LOG.TYPE=FIELD(ACCESS.REC<1,VAL>,'*',2)
         BEGIN CASE
            CASE LOG.TYPE='AC' ; LOG.SRC='Accounting'
            CASE LOG.TYPE='AU' ; LOG.SRC='Audit'
            CASE LOG.TYPE='FP' ; LOG.SRC='Freight Payment'
            CASE LOG.TYPE='IL' ; LOG.SRC='International Logistics'
            CASE LOG.TYPE='IN' ; LOG.SRC='Internet'
            CASE LOG.TYPE='IS' ; LOG.SRC='Information Systems'
            CASE LOG.TYPE='LG' ; LOG.SRC='Logistics'
            CASE LOG.TYPE='LT' ; LOG.SRC='LTL Logistics'
            CASE LOG.TYPE='SA' ; LOG.SRC='Sales'
            CASE LOG.TYPE='TP' ; LOG.SRC='Transportation'
            CASE LOG.TYPE='TL' ; LOG.SRC='Transportation Logistics'
            CASE 1 ; LOG.SRC=LOG.TYPE
         END CASE
         ACC.CNT+=1                      
         NSVAL=DCOUNT(ACCESS.REC<2,VAL>,@SVM)
         FOR SVAL=1 TO NSVAL
            LOG.USER=ACCESS.REC<2,VAL,SVAL>
            LOG.ACTION = ACCESS.REC<4,VAL,SVAL>
            LOG.ACTION = FIELD(LOG.ACTION,"*",1) ; USER.TYPE = FIELD(LOG.ACTION,"*",2)

            READV ACCESS.REASON FROM FB.BILLS.ACCESS.REASON, LOG.ACTION, 1 THEN
               LOG.REASON = ACCESS.REASON
            END ELSE
               LOG.REASON = 'Unknown'
            END
            IF LOG.ACTION[1, 7] = "CLI.CHG" THEN
               FROM.CLIENT.ID = FIELD(LOG.ACTION, '.', 3)       
               LOG.REASON = 'Moved fm ':FROM.CLIENT.ID   
            END
            IF LOG.TYPE='IN' THEN
               ACCESS.REC<2,VAL,SVAL>='SV.CABS'
               LOG.USER=ACCESS.REC<2,VAL,SVAL>
               LOG.REASON = "Web Inquiry"
               LOG.USER.GRP = "IN"
               LOG.USER.DEPT = "IN"
            END ELSE
               READ USER.REC FROM F.BCTRL, LOG.USER ELSE USER.REC = ""
               LOG.USER.GRP = USER.REC<14>
               LOG.USER.DEPT = USER.REC<15>
               IF LOG.USER.GRP = "F" THEN LOG.USER.GRP = "FP"
               IF LOG.USER.GRP = "A" THEN LOG.USER.GRP = "AU"
            END
            LOG.TIME=ACCESS.REC<3,VAL,SVAL>
            SORT.TIME = FIELD(LOG.TIME,".",1)
            LOG.SORT = LOG.DATE "R%5" :SORT.TIME "R%5"
            LOCATE LOG.SORT IN ACCESS.LOG<1> BY 'DR' SETTING LPOS THEN NULL ELSE NULL
            INS LOG.SORT BEFORE ACCESS.LOG<1,LPOS>
            INS LOG.USER BEFORE ACCESS.LOG<2,LPOS>
            INS LOG.USER.GRP BEFORE ACCESS.LOG<3,LPOS>
            INS LOG.USER.DEPT BEFORE ACCESS.LOG<4,LPOS>
            INS LOG.REASON BEFORE ACCESS.LOG<5,LPOS>
            INS USER.TYPE BEFORE ACCESS.LOG<6,LPOS>
         NEXT SVAL
      NEXT VAL

      NVAL=DCOUNT(ACCESS.LOG<1>,@VM)
      FOR VAL=1 TO NVAL
         LOG.SORT = ACCESS.LOG<1,VAL>
         LOG.USER = ACCESS.LOG<2,VAL>
         LOG.USER.GRP = ACCESS.LOG<3,VAL>
         LOG.USER.DEPT = ACCESS.LOG<4,VAL>
         LOG.ACTION = ACCESS.LOG<5,VAL>
         LOG.USER.TYPE = ACCESS.LOG<6,VAL>
         LOG.DATE = LOG.SORT[1,5]
         LOG.TIME = LOG.SORT[6,5]

         DUR.STR = ""
         IF VAL > 1 THEN
            LAST.LOG.SORT = ACCESS.LOG<1,VAL-1>
            LAST.LOG.DATE = LAST.LOG.SORT[1,5]
            LAST.LOG.TIME = LAST.LOG.SORT[6,5]
            DAYS.PASSED = LAST.LOG.DATE - LOG.DATE
            TIME.PASSED = LAST.LOG.TIME - LOG.TIME
            IF DAYS.PASSED GT 0 THEN
               DUR.STR = DAYS.PASSED "R#6" : ' Days'
            END ELSE
               DUR.STR = OCONV(TIME.PASSED,"MT") "R#6" : '  hrs'
            END
         END

         IF LOG.USER.GRP='IN' THEN
            LOG.NAME='From WebSite' "L#15"
            LOG.ACTION = "Inquiry"
         END ELSE
            LOG.NAME=OCONV(LOG.USER,'TBCTRL;X;;13')
            IF LOG.USER='SV.UVAUTO' THEN           
               LOG.NAME='UV Auto'       
            END                         
            LOG.NAME = LOG.NAME[1,15] "L#15"
         END
         IF TRIM(LOG.NAME) = '' THEN LOG.NAME = LOG.USER
         LOG.NAME = LOG.NAME"L#15":"-":LOG.USER.GRP"L#2"

         IF LOG.USER#'' THEN
            NTEXT<1,-1>=LOG.NAME[1,18] 'L#18 ': " " : LOG.ACTION[1,22] 'L#22 ':OCONV(LOG.DATE,'D4/')'R#10   ':OCONV(LOG.TIME,'MTHS')'R#10' : ' ':DUR.STR 'R#12'
         END
      NEXT VAL

      RETURN
      
