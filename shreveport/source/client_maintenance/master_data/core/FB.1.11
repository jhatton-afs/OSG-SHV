*******************************************************
* Program name : FB.1.11
* Author       : BRIAN M. QUEEN
* Date written : August 2, 1993
* Library      : FBBP
* COPYRIGHT (C) 2016 - AFS LOGISTICS LLC - ALL RIGHTS RESERVED.
*
* PROGRAM TO MODIFY VENDOR AND CUSTOMER ITEMS
*
* Last updated by afs (AFSLOGISTICS\nmeddaugh) at 09:22:51 on 03/22/2019.
*
* Start modification history
* 03/22/19 : NKH01 : ACCU-316 Modified CREATE-FILE file sizes
* End modification history
*
*******************************************************
$OPTIONS HEADER.EJECT
      ERRMSG=''
*
$OPTIONS EXEC.EQ.PERF
$OPTIONS INFO.ABORT
*
* Initialize variables
*
      PROG.NAME='FB.1.11'
      PROG.DESC='EDIT APPROVED CARRIERS'
      PACK.NAME='FREIGHT BILLING'
      CALL AFS.SCR.HEAD(CO.ID,FILE.ID,'',PROG.NAME,PROG.DESC,PACK.NAME,CO.NAME,TIME.DATE,1)
       UPL.VIN = 'FBBP' ;  UPL.VIN<2> = 'FB.1.11' ; UPL.VOUT = ''                    ;* NPR_UPL 04/23/2010
       CALL UPD.PROGRAM.LOG(UPL.VIN,UPL.VOUT)                                                           ;* NPR_UPL 04/23/2010
      ADD.FLG=0
*
* Open files
*
      OPEN '','BCUST' TO F.CLIENT ELSE CALL OPEN.ABORT("BCUST",PROG.NAME)
      OPEN '','CARRIERS' TO F.CARRIERS ELSE CALL OPEN.ABORT("CARRIERS",PROG.NAME)
*
* Enter client number
*
50:   CRT @(0,2):@(-3)
      HELP='Enter the client number. Name for search.'
      CALL GEN.IN(0,2,'Enter Client Number #####','',Q,0,20,'','',0,-5,2,0,QI,HELP,0,23)
      QI=OCONV(QI,'MCU')
      BEGIN CASE
         CASE QI=''
            STOP
         CASE NUM(QI)
            Q=QI'R%5'
         CASE 1
            CALL SOUNDEX.DISPLAY(QI,'BCUST','SDX.CLIENTS,NAME',2,ITEM.LIST)
            CALL AFS.SCR.REFRESH(PROG.NAME,PROG.DESC,PACK.NAME,CO.NAME,TIME.DATE,1)
            IF ITEM.LIST='' ! DCOUNT(ITEM.LIST<1>,@VM) > 1 THEN GOTO 50
            IF NOT(NUM(ITEM.LIST<1,1>)) THEN GOTO 50
            Q=ITEM.LIST<1,1>'R%5'
      END CASE
      CLIENT.ID=Q'R%5'
*
* Read client item
*
      READ CLIENT.REC FROM F.CLIENT,CLIENT.ID ELSE
         CALL ELINE('Client number ':CLIENT.ID:' not on file.')
         GOTO 50
      END
      CLIENT.NAME=CLIENT.REC<2>
      CRT @(20,2):CLIENT.ID:' ':CLIENT.NAME
*
      OPEN '','FB.APRVD.CARRS,':CLIENT.ID TO F.APRVD.CARRS ELSE
         EXECUTE 'CREATE-FILE DATA FB.APRVD.CARRS,':CLIENT.ID:' 7,1' PASSLIST CAPTURING OUTPUT     ; * NKH01
         OPEN '','FB.APRVD.CARRS,':CLIENT.ID TO F.APRVD.CARRS ELSE CALL OPEN.ABORT("FB.APRVD.CARRS,":CLIENT.ID,PROG.NAME)
      END
*
* Select approved carriers file and build display array
*
      AC.REC=''
      ATTR=0
      EXECUTE "SSELECT FB.APRVD.CARRS,":CLIENT.ID:" BY CARRIER.NAME" PASSLIST CAPTURING OUTPUT
      LOOP
         READNEXT ID ELSE ID="**"
      UNTIL ID="**" DO
         READ APRVD.REC FROM F.APRVD.CARRS,ID THEN
            READV CARRIER.NAME FROM F.CARRIERS,ID,1 THEN
               ATTR=ATTR+1
               AC.REC<ATTR,1>=ID
               AC.REC<ATTR,2>=CARRIER.NAME
               AC.REC<ATTR,3>=APRVD.REC<1>
               AC.REC<ATTR,4>=APRVD.REC<2>
            END
         END
      REPEAT
*
* Display first page of approved carriers
*
      PAGE=1
      GOSUB 9000
*
* Enter selection code
*
100:  HELP="Line number. [B]=Back. [T]=Top, [cr]=Next page. [E]=End. [EX]=Exit. [L]=Last."
      CALL GEN.IN(0,22,"Enter line number please. #####",'',Q,0,5,'','',1,-5,22,0,QI,HELP,0,23)
      QI=OCONV(QI,'MCU')
      BEGIN CASE
         CASE QI=''
            PAGE=PAGE+1
            GOSUB 9000
         CASE QI='B'
            PAGE=PAGE-1
            IF PAGE=0 THEN PAGE=1
            GOSUB 9000
         CASE QI EQ 'L'
            MX = DCOUNT(AC.REC,CHAR(254))
            MX = INT(MX/18)
            PAGE = MX
            GOSUB 9000
         CASE QI='T'
            PAGE=1
            GOSUB 9000
         CASE QI='EX'
            STOP
         CASE NUM(QI)
            SEL=QI
            IF SEL LT START ! SEL GT STOP THEN
               CALL ELINE('Invalid selection.')
               GOTO 100
            END
            GOSUB 8000
         CASE QI='E'
            CLEARFILE F.APRVD.CARRS
            X=DCOUNT(AC.REC,CHAR(254))
            FOR I=1 TO X
               IF AC.REC<I> NE '' THEN
                  CARRIER.ID=AC.REC<I,1>
                  TEMP=''
                  TEMP<1>=AC.REC<I,3>
                  TEMP<2>=AC.REC<I,4>
                  WRITE TEMP ON F.APRVD.CARRS,CARRIER.ID
               END
            NEXT I
*
            CRT @(0,22):@(-3)
            HELP="Enter Y to print listing"
            CALL GEN.IN(0,22,'Do you want a listing? N','',Q,0,1,'','',1,-1,22,1,QI,HELP,0,23)
            QI=OCONV(QI,'MCU')
            BEGIN CASE
               CASE QI='' ! QI='N'
                  STOP
               CASE QI='Y'
                  CALL PAGE.HEAD('FB.1.11',CLIENT.NAME,'','Approved Carriers Listing',DATE(),132,HED)
                  PREC='SSELECT FB.APRVD.CARRS,':CLIENT.ID:' BY CARRIER.NAME'
                  EXECUTE PREC PASSLIST CAPTURING OUTPUT
                  PRINTER ON
                  HED=HED:'Carr# Carrier Name.................. From Date   To Date'
                  HEADING HED
                  LOOP
                     READNEXT ID ELSE ID='**'
                  UNTIL ID='**' DO
                     READ REC FROM F.APRVD.CARRS,ID THEN
                        READV CARRIER.NAME FROM F.CARRIERS,ID,1 ELSE CARRIER.NAME='~~ Unknown ""'
                        FROM.DATE=REC<1>
                        IF FROM.DATE='' THEN FROM.DATE=SPACE(8) ELSE FROM.DATE=FROM.DATE'D2/'
                        TO.DATE=REC<2>
                        IF TO.DATE='' THEN TO.DATE=SPACE(8) ELSE TO.DATE=TO.DATE'D2/'
                        PRINT ID'R#5':' ':CARRIER.NAME'L#30':'  ':FROM.DATE:'  ':TO.DATE
                     END
                  REPEAT
                  PRINTER OFF
                  PRINTER CLOSE
                  STOP
            END CASE
      END CASE
      GOTO 100
*------------------------------------------------------------------------
*
* INTERNAL SUBROUTINES
*
*------------------------------------------------------------------------
*
* Edit approved carrier code
*
8000: ROW=3+REM(SEL,18)
      IF ROW=3 THEN ROW=21
      CARRIER.ID=AC.REC<SEL,1>
      CARRIER.NAME=AC.REC<SEL,2>
      FROM.DATE=AC.REC<SEL,3>
      IF FROM.DATE NE '' THEN FROM.DATE=FROM.DATE'D2/' ELSE FROM.DATE='        '
      TO.DATE=AC.REC<SEL,4>
      IF TO.DATE NE '' THEN TO.DATE=TO.DATE'D2/' ELSE TO.DATE='        '
*
* Get Carrier ID and Carrier Name
*
8010: HELP="Enter carrier ID. Name for search. [cr]=Accept. [D]=Delete. [X]=Exit."
      CALL GEN.IN(6,ROW,CARRIER.ID'L#5','',Q,0,20,'','',2,-5,ROW,0,QI,HELP,0,23)
      QI=OCONV(QI,'MCU')
      BEGIN CASE
         CASE QI=''
            Q=CARRIER.ID
         CASE QI='X'
            CRT @(6,ROW):CARRIER.ID'L#5':' ':CARRIER.NAME'L#30':'  ':FROM.DATE:' ':TO.DATE
            RETURN
         CASE QI='D'
            AC.REC=DELETE(AC.REC,SEL)
            GOSUB 9000
            RETURN
         CASE ALPHA(QI)
            CALL SOUNDEX.DISPLAY(Q,'CARRIERS','SDX.CARRIER,NAME','1,3,4',QX)
            CALL SCR.REFRESH(PROG.NAME,PROG.DESC,PACK.NAME,CO.NAME,TIME.DATE,1)
            GOSUB 9000
            CT=DCOUNT(QX<1>,@VM)
            IF CT=1 THEN Q=QX<1,1> ELSE GOTO 8010
      END CASE
      CARRIER.ID=Q'R%5'
      CRT @(6,ROW):CARRIER.ID'L#5'
      READV CARRIER.NAME FROM F.CARRIERS,CARRIER.ID,1 ELSE CARRIER.NAME='Unknown'
      CRT @(12,ROW):CARRIER.NAME'L#30'
*
* Get from date
*
8020: HELP="Enter approval begin date. [cr]=Accept. [space]=Clear. [X]=Back to Carrier."
      IF FROM.DATE='' THEN DVAR=SPACE(8) ELSE DVAR=FROM.DATE'D2/'
      CALL GEN.IN(44,ROW,DVAR,'DATE',Q,0,8,'','',1,-8,ROW,0,QI,HELP,0,23)
      QI=OCONV(QI,'MCU')
      BEGIN CASE
         CASE QI=''
            IF FROM.DATE='        ' THEN FROM.DATE=''
            Q=FROM.DATE
         CASE QI=' '
            Q=''
         CASE QI='X'
            CRT @(44,ROW):DVAR
            GOTO 8010
      END CASE
      FROM.DATE=Q
      IF FROM.DATE='' THEN DVAR='        ' ELSE DVAR=FROM.DATE'D2/'
      CRT @(44,ROW):DVAR'L#8'
*
* Get to date
*
8030: HELP="Enter approval end date. [cr]=Accept. [space]=Clear. [X]=Back to begin date."
      IF TO.DATE='' THEN DVAR=SPACE(8) ELSE DVAR=TO.DATE'D2/'
      CALL GEN.IN(53,ROW,DVAR,'DATE',Q,0,8,'','',1,-8,ROW,0,QI,HELP,0,23)
      QI=OCONV(QI,'MCU')
      BEGIN CASE
         CASE QI=''
            IF TO.DATE='        ' THEN TO.DATE=''
            Q=TO.DATE
         CASE QI=' '
            Q=''
         CASE QI='X'
            CRT @(53,ROW):DVAR
            GOTO 8020
      END CASE
      TO.DATE=Q
      IF TO.DATE='' THEN DVAR='        ' ELSE DVAR=TO.DATE'D2/'
      CRT @(53,ROW):DVAR'L#8'
*
* Update array
*
      AC.REC<SEL,1>=CARRIER.ID
      AC.REC<SEL,2>=CARRIER.NAME
      AC.REC<SEL,3>=FROM.DATE
      AC.REC<SEL,4>=TO.DATE
      RETURN
*
* Display vendor array
*
9000: CRT @(0,3):@(-3):"   #":@(6,3):"Code  Carrier Name.................. From Date  To Date"
      START=(PAGE*18)-17
      IF START GT 9990 THEN
         START=1
         PAGE=1
      END
      STOP=START+17
      FOR I=START TO STOP STEP 1
         D.ROW=3+REM(I,18)
         IF D.ROW=3 THEN D.ROW=21
         CARRIER.ID=AC.REC<I,1>
         CARRIER.NAME=AC.REC<I,2>
         FROM.DATE=AC.REC<I,3>
         IF FROM.DATE NE '' THEN FROM.DATE=FROM.DATE'D2/' ELSE FROM.DATE='        '
         TO.DATE=AC.REC<I,4>
         IF TO.DATE NE '' THEN TO.DATE=TO.DATE'D2/' ELSE TO.DATE='        '
         PRINT @(0,D.ROW):I"R#4":'  ':CARRIER.ID"L#5":' ':CARRIER.NAME"L#30":'  ':FROM.DATE:' ':TO.DATE
      NEXT I
      RETURN
