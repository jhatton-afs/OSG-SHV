$OPTIONS HEADER.EJECT
ERRMSG=''
*
$OPTIONS EXEC.EQ.PERF
$OPTIONS INFO.ABORT
*
* PROGRAM: FB.4.4
* PROGRAM TO REBUILD CARRIER SOUNDEX FILES
************************************************
* 05/08/06 : JCW01 : CHANGES TO '@SELECTED', 'CLEARSELECT', AND 'STMT<X>'
************************************************
*
*  OPEN FILES
*
      OPEN '','CARRIERS' TO F.CARRIERS ELSE CALL OPEN.ABORT("CARRIERS","FB.4.4")
      OPEN '','SDX.CARRIER,NAME' TO F.SDX.NAME ELSE CALL OPEN.ABORT("SDX.CARRIERS,NAME","FB.4.4")
      CALL AFS.SCR.HEAD(CO.ID,FILE.ID,'',"FB.4.4","Rebuild Carrier Soundex File","Freight Billing",CO.NAME,TIME.DATE,1)
*
*  SELECT CARRIER FILE
*
      TEXT='Selecting Carrier File.'
      CALL CENTER(TEXT,80)
      CRT @(0,10):TEXT
*
      EXECUTE 'SSELECT CARRIERS' PASSLIST CAPTURING OUTPUT
      NUM.ITMS = @SELECTED
      COUNTER=0
      IF @SELECTED < 1 THEN ; * JCW01
        TEXT='ERROR - No carriers on file.'
        CALL CENTER(TEXT,80)
        CRT @(0,10):TEXT
        CRT @(0,22):'Press any key to continue.':
        INPUT ACK,1:
        STOP
      END
*
*  REBUILD SOUNDEX FILE
*
      CLEARFILE F.SDX.NAME
      LOOP
        READNEXT CARRIER.ID ELSE CARRIER.ID=''
        COUNTER=COUNTER+1
        CRT @(0,19):CARRIER.ID:@(-4):
      UNTIL CARRIER.ID='' DO
        READ CARRIER.REC FROM F.CARRIERS,CARRIER.ID THEN
          NAME=CARRIER.REC<1>
          CALL SOUNDEX.EDIT('',NAME,CARRIER.ID,'SDX.CARRIER,NAME')
          CALL PROG.BAR(21,COUNTER,NUM.ITMS,PER)
        END
      REPEAT
      STOP
