      SUBROUTINE FB.NOTIFY.SUB(VIN,VOUT,F.PN,F.PN.HIST)
***********************************************************************
* Program name :  FB.NOTIFY.SUB
* Author       :
* Date written :
* Library      :  FBBP
$* COPYRIGHT (C) 2019 - AFS LOGISTICS LLC - ALL RIGHTS RESERVED.
*
* PRINT MISROUTE LETTERS
* Last updated by afs (AFSLOGISTICS\gbino) at 10:36:43 on 02/19/2020.
*
* Mod nn  mm/dd/yy, init, req #, description
* Start modification history
* 021920 - GRB01 - ACCU-480 Automated processing
* End modification history
***********************************************************************

      VOUT=''
****      CALL GET.USER.NEW(USER,CH.NUM)
      UPL.VIN = 'FBBP' ; UPL.VIN<2> = 'FB.NOTIFY.SUB' ; UPL.VOUT = ''
****      UPL.VIN<3> = USER
****      UPL.VIN<4> = CH.NUM
****      CALL UPD.PROGRAM.LOG(UPL.VIN,UPL.VOUT)
      FROM=VIN<1>
      CLIENT=VIN<2>
      CLIENT.NAME=VIN<3>
      PDATE=VIN<4>
      USER=VIN<5>
      IF FROM='FB.2.3.NW' THEN FROM.PROCESS=1 ELSE FROM.PROCESS=0
      IF FROM='FB.3.1.NEW' THEN FROM.REPORTS=1 ELSE FROM.REPORTS=0
      OPR.ACT=''
      PN.BCC=''
      READ PN.REC FROM F.PN,CLIENT THEN
         PN.ORG=PN.REC
         NVAL=DCOUNT(PN.REC<1>,@VM)
         FOR VAL=1 TO NVAL
            PN.BDATE=PN.REC<2,VAL>
            PN.EDATE=PN.REC<3,VAL>
            IF PN.EDATE='' THEN PN.EDATE=DATE()+1
            IF PDATE-PN.EDATE>7 THEN
               HIST.ID=CLIENT:'*':PN.BDATE:'*':PN.EDATE
               READ HIST.REC FROM F.PN.HIST,HIST.ID ELSE HIST.REC=''
               HIST.REC<1,-1>=PN.REC<4,VAL>
               HIST.REC<2,-1>=PN.REC<5,VAL>
               HIST.REC<3,-1>=PN.REC<6,VAL>
               HIST.REC<4,-1>=PN.REC<7,VAL>
               HIST.REC<5,-1>=PN.REC<8,VAL>
               WRITE HIST.REC ON F.PN.HIST,HIST.ID
               FOR ATT=1 TO 8
                  DEL PN.REC<ATT,VAL>
               NEXT ATT
               VAL=VAL-1
               NVAL=NVAL-1
               GO SKIP.NOTIFY
            END
            PN.STS=PN.REC<4,VAL>
            IF PDATE>=PN.BDATE AND PDATE<=PN.EDATE AND PN.STS='Y' THEN
               IF FROM.PROCESS THEN
                  IF PN.REC<5,VAL>='Y' THEN
                     CRT @(0,21):PN.REC<8,VAL>
                     HELP="X=Change Client. EX=Exit. [YES]=Continue."
                     CALL GEN.IN(0,22,'Process Notify. ---','',Q,0,3,'','',0,-3,22,1,QI,HELP,0,23)
                     QI=OCONV(QI,'MCU')
                     BEGIN CASE
                        CASE QI='X'
                           IF OPR.ACT='' OR OPR.ACT='YES' THEN OPR.ACT='X'
                        CASE QI='EX'
                           IF OPR.ACT='' OR OPR.ACT='YES' OR OPR.ACT='X' THEN OPR.ACT='EX'
                        CASE QI='YES'
                           IF OPR.ACT='' THEN OPR.ACT='YES'
                     END CASE
                     IF @TTY # "phantom" THEN      ; * GRB01
                        CRT @(0,21):@(-4):
                     END
                  END
NOTIFY.EMAIL:     *
                  SUBJ='Process Notify Alert'
                  TEXT=USER:' ran the process program on ':DATE()'D2/':' at ':OCONV(TIME(),'MTHS'):' for client '
                  TEXT:=CLIENT:' - ':CLIENT.NAME:', which contained a process notify alert of: '
                  TEXT<1,-1>=PN.REC<8,VAL>
                  TEXT<1,-1>=' '
                  TEXT<1,-1>='This alert can be changed at menu path 1.4.6 Enter/Update Process Notify'
                  TEXT<1,-1>=' '
                  IF PN.REC<5,VAL>='Y' THEN
                     IF OPR.ACT='YES' THEN
                        TEXT<1,-1>=USER:' chose to continue with the process program.'
                     END ELSE
                        TEXT<1,-1>=USER:' chose to abort the process program.'
                     END
                     TEXT<1,-1>=' '
                  END
                  TEXT<1,-1>='(FB.NOTIFY.SUB, Label NOTIFY.EMAIL:)'
                  TEXT<1,-1>=' '
                  MVIN=''
                  MVIN<1>=CHANGE(PN.REC<7,VAL>,@SVM,@VM)

                  MVIN<4>='AFS'
                  MVIN<5>=SUBJ
                  MVIN<6>=TEXT
                  CALL SENDMAIL(MVIN,MVOUT)
               END
               IF FROM.REPORTS THEN
                  NSVAL=DCOUNT(PN.REC<7,VAL>,@SVM)
                  IF PN.REC<6,VAL>='Y' THEN
                     FOR SVAL=1 TO NSVAL
                        EADDR=OCONV(PN.REC<7,VAL,SVAL>,'MCL')
                        LOCATE EADDR IN PN.BCC<1> SETTING EPOS ELSE
                           PN.BCC<1,-1>=EADDR
                        END
                     NEXT SVAL
                  END
               END
            END
SKIP.NOTIFY:*
         NEXT VAL
         IF PN.REC#PN.ORG THEN
            WRITE PN.REC ON F.PN,CLIENT
         END
      END
9999: * TIME TO RETURN BACK TO HOST PROGRAM
      IF FROM.PROCESS THEN
         VOUT<1>=OPR.ACT
      END
      IF FROM.REPORTS THEN
         VOUT<2>=PN.BCC
      END
      RETURN
   END
