*************************************************************************
* Program : FB.11.17.1
*           Import Carrier Service data from Non-SMC Carriers
* Author  : JMK
* Date    : October 2007
*
* Copyright (c) 2002 by AFS Logistics, LLC. - All Rights Reserved
*************************************************************************
* Format of CARRIER.SVC Text file            Format of CARRIER.SVC
* Tab delimited with 6 possible fields       ITEM ID = CARRIER*CLIENT*ORIGIN ZIP (OR ALL)
* ----------------------------------------------------------------------
* (1) AFS Carrier #                          REC< 1> Dest ZipCode (3 or 5/6) multivalued
* (2) Origin ZipCode (3 or 5/6)              REC< 2> Direct/Indirect (D/I) multivalued
* (3) Dest ZipCode   (3 or 5/6)              REC< 3> Service Days multivalued
* (4) Direct/Indirect (D/I)
* (5) Service Days
*************************************************************************
* 122908 JMK01 C082744 - Beaver Express Points list ; update for 1/2009
* 042314 - JMK02 - SysAid#9449 - Adjust authorization list

$INCLUDE PROG.ADMIN INC_OPTIONS

* Initialize general variables
      PROMPT''
      CALL GET.USER.NEW(USER.NAME,CH.NUM)
   ***   CALL CHANNEL(CH.NUM)
      PROG.NAME='FB.11.17.1'
      PROG.DESC='Import Carrier Service Data'
      PACK.NAME='Freight Payment'
      MASK.CHAR='-'
      UPL.VIN = 'FBBP' ; UPL.VIN<2> = 'FB.11.17.1' ; UPL.VOUT = ''     ; * NPR_UPL 04/23/2010
      CALL UPD.PROGRAM.LOG(UPL.VIN,UPL.VOUT)       ; * NPR_UPL 04/23/2010
      TAB=CHAR(9)
      ESC=CHAR(27)
      OPEN '','OPERATOR' TO F.OP ELSE CALL OPEN.ABORT("OPERATOR",PROG.NAME)      ; * GRB01
* Write screen header
      CALL AFS.SCR.HEAD(CO.ID,FILE.ID,'',PROG.NAME,PROG.DESC,PACK.NAME,CO.NAME,TIME.DATE,1)
      CRT @(0,2):STR('=',79)

* Restrict program usage
      VALID.USER=1
      READV OP.FLG FROM F.OP,USER.NAME,44 ELSE
         OP.FLG = "N"
      END
      IF OP.FLG = "Y" THEN
         ** BEGIN CASE
         ****      CASE USER.NAME='BRIAN'
         ****      CASE USER.NAME='DAVID'
         ****      CASE USER.NAME='RBOEHNLEIN'     ; * Ticket 5457 - DRF 03/18/13
         ****      CASE USER.NAME='SSELMAN'        ; * Added 05-13-2011 per Brian Jeppson ... BOH
         ****      CASE USER.NAME='SWINSTEAD'      ; * Added 05-13-2011 per Brian Jeppson ... BOH
         ***      CASE 1
         **       VALID.USER=0
         **    END CASE
         **    IF NOT(VALID.USER) THEN
      END ELSE
         TEMP='**  This is a restricted access application  **'
         CALL CENTER(TEMP,80)
         CRT @(0,12):TEMP
         GOTO THE.END
      END
      CRT @(0,12):'This spreadsheet should be saved on your "U:\" drive as'
      CRT @(0,13):'a tab delimited text file called "CARRIER.SERVICE.TXT"'
      CRT @(0,15):'This spreadsheet needs to be formatted correctly to import:'
      CRT @(0,16):'Column A should contain the AFS Carrier number.'
      CRT @(0,17):'Column B should contain the Origin Zip Code.'
      CRT @(0,18):'Column C should contain the Destination Zip Code.'
      CRT @(0,19):'Column D should contain D for direct or I for indirect service.'
      CRT @(0,20):'Column E should contain the number of service days.'

100:  ***
      LN='Are you ready to import Carrier service file? '
      MASK='N-'
      HELP='[Y]=Yes [N]=No [EX]=Exit'
      CALL GEN.IN(0,22,LN:MASK,'',Q,0,2,'','',2,-2,22,1,QI,HELP,0,23)
      QI=OCONV(QI,'MCU')
      BEGIN CASE
         CASE QI='EX' OR QI='N' OR QI=''
            GOTO THE.END
         CASE QI='Y'
            NULL
         CASE 1
            GOTO 100
      END CASE

* Open other files
      OPEN '','BILLDATA.':USER.NAME TO F.BILLDATA ELSE
         OPEN '','VOC' TO F.VOC ELSE
            CALL ELINE('Unable to open VOC file - Call IS Department.')
            GOTO THE.END
         END
         READ BILLDATA.REC FROM F.VOC,'BILLDATA' ELSE
            CALL ELINE('Unable to open VOC BILLDATA.')
            GOTO THE.END
         END
         BILLDATA.REC<2>:='\':USER.NAME
         WRITE BILLDATA.REC ON F.VOC,'BILLDATA.':USER.NAME ELSE
            CALL ELINE('Your user name ':USER.NAME:' is not in the BILLDATA folder - Call IS Department.')
            GOTO THE.END
         END
         OPEN '','BILLDATA.':USER.NAME TO F.BILLDATA ELSE
            CALL ELINE('Unable to open BILLDATA.':USER.NAME:'.')
            GOTO THE.END
         END
      END
      OPEN '','CARRIER.SVC' TO F.CS ELSE
         CALL ELINE('Unable to open CARRIER.SVC.')
      END
      OPEN '','FB.TABLES' TO F.TABLES ELSE
         CALL ELINE('Unable to open FB.TABLES.')
      END
      OPEN '','CARRIERS' TO F.CARRIERS ELSE        ; * JMK01
         CALL ELINE('Unable to open CARRIERS.')    ; * JMK01
      END                                ; * JMK01
      READU NSMC.LIST FROM F.TABLES,'NSMC.CARRIERS' ELSE NSMC.LIST=''
      NSMC.LIST.ORG=NSMC.LIST

* Read in the existing Carrier Service text file
      READ CS.IMPORT FROM F.BILLDATA,'CARRIER.SERVICE.TXT' ELSE
         CALL ELINE('CARRIER.SERVICE.TXT does not exist.')
         GOTO THE.END
      END
      CS.LAST.IMPORT=CS.IMPORT           ; * Save file for writing as "last"
      CS.IMPORT=CHANGE(CS.IMPORT,CHAR(12),'')
      CS.IMPORT=CHANGE(CS.IMPORT,CHAR(9),@VM)

* Status lines
      NATT=DCOUNT(CS.IMPORT,@AM)
      CRT @(0,5):'Total items in import file = ':NATT:@(-4)
      CRT @(0,7):'Total Items processed'
      CRT @(0,8):'---------------------'

* Set counters for processing
      ALREADY.THERE=0
      ALREADY.THERE.BOTH.MATCH=0
      ALREADY.THERE.AMOUNTS.MATCH=0
      ALREADY.THERE.DATES.MATCH=0
      ALREADY.THERE.NEITHER.MATCH=0

      ALREADY.THERE.NULL=0
      NOT.THERE=0

      RESULTS.ARRAY=''
      ERROR.ARRAY=''
      PERC=0
      PREV.CLIENT.NUMBER=''
      PROC.DATE=DATE()
      PROC.TIME=TIME()
      CS.TOT=0                           ; * JMK01
      ERRORS=0                           ; * JMK02
* Prepare the file for processing
      FOR ATT=1 TO NATT
         CRT @(0,9):ATT'R#21 ':'Items Submitted...'          ; * JMK01
         CALL PROG.BAR(11,ATT,NATT,PERC)
         CS.LINE=CS.IMPORT<ATT>
         CS.CARRIER=CS.LINE<1,1>'R%5'    ; * AFS Carrier Number

         READ CARRIER.REC FROM F.CARRIERS,CS.CARRIER ELSE    ; * JMK02
            CALL ELINE(CS.CARRIER:' is not a valid carrier. Skipping row ':ATT)  ; * JMK02
            ERRORS+=1                    ; * JMK02
            GOTO NEXT.ATT                ; * JMK02
         END                             ; * JMK02
         CARRIER.SCAC=CARRIER.REC<12>    ; * JMK01
         CARRIER.TYPE='A'                ; * JMK01
         IF CARRIER.REC<44>='6' THEN     ; * JMK01
            CARRIER.TYPE='B'             ; * JMK01
         END                             ; * JMK01
         CS.ORIG=CS.LINE<1,2>            ; * Origin ZipCode 3 or 5
         CS.DEST=CS.LINE<1,3>            ; * Destination ZipCode 3 or 5
         CS.DI=CS.LINE<1,4>              ; * (D)irect or (I)ndirect code
         CS.DAYS=CS.LINE<1,5>            ; * Number of service days

         IF CS.CARRIER#'' AND CS.ORIG#'' THEN      ; * JMK01
            CS.ID=CS.CARRIER:'*':CS.ORIG
            READ CS.REC FROM F.CS,CS.ID ELSE CS.REC=''
            CS.ORG=CS.REC
            LOCATE CS.DEST IN CS.REC<1> BY 'AL' SETTING DPOS ELSE
               FOR FLD=1 TO 3
                  INS '' BEFORE CS.REC<FLD,DPOS>
               NEXT FLD
            END
            CS.REC<1,DPOS>=CS.DEST
            CS.REC<2,DPOS>=CS.DI
            CS.REC<3,DPOS>=CS.DAYS
            LOCATE (USER.NAME:'*':PROC.DATE:'*':PROC.TIME) IN CS.REC<14> BY 'AL' SETTING UPOS ELSE
               FOR FLD=11 TO 14
                  INS '' BEFORE CS.REC<FLD,UPOS>
               NEXT FLD
            END
            CS.REC<11,UPOS>=USER.NAME
            CS.REC<12,UPOS>=PROC.TIME
            CS.REC<13,UPOS>=PROC.DATE
            CS.REC<14,UPOS>=USER.NAME:'*':PROC.DATE:'*':PROC.TIME
            IF CS.REC#CS.ORG THEN
               CS.TOT+=1                 ; * JMK01
               CRT @(0,10):CS.TOT'R#21 ':'Non-SMC Items Added/Updated...'        ; * JMK01
               WRITE CS.REC ON F.CS,CS.ID
               LOCATE CS.CARRIER IN NSMC.LIST BY 'AR' SETTING CPOS ELSE
                  INS CS.CARRIER BEFORE NSMC.LIST<CPOS>
               END
            END
         END
NEXT.ATT:*
      NEXT ATT

* Update files
      WRITE CS.LAST.IMPORT ON F.BILLDATA,'CARRIER.SERVICE-LAST.TXT' THEN
         WRITE CS.LAST.IMPORT ON F.BILLDATA,'CARRIER.SERVICE-':TIMEDATE():'.TXT'
         DELETE F.BILLDATA,'CARRIER.SERVICE.TXT'   ; * Delete the original CARRIER.SERVICE text file
         IF NSMC.LIST#NSMC.LIST.ORG THEN
            WRITE NSMC.LIST ON F.TABLES,'NSMC.CARRIERS'
         END
      END ELSE
         CALL ELINE('Could not write CARRIER.SERVICE-LAST.TXT. Not deleting CARRIER.SERVICE.TXT. Call Help Desk.')
      END

*************************************************************************

THE.END:***
* End of program
      CRT @(0,23):
   END
