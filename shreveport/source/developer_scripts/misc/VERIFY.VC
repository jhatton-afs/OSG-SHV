* FREIGHT BILLING PACKAGE
*---- PROGRAM: VERIFY.VC
* AUTHOR: JAY WEBERT
* DATE: 12/01/06
* DESCRIPTION: VERIFY DESCRIPTION VALUES IN VEND.CUST AGAINST VEND.CUST.NAMES FILE
*---- THIS PROGRAM IS A PROPRIETARY PRODUCT OF AUTOMATED FREIGHT SYSTEMS
* AND MAY NOT BE USED, COPIED OR MODIFIED WITHOUT THE EXPRESS
* WRITTEN CONSENT OF SAME.
*----
      CLIENT.ID = ''
      PROG.NAME = 'VERIFY.VC'
      OPEN 'CLIENTS' TO F.CLIENTS ELSE
        CALL OPEN.ABORT('CLIENTS',PROG.NAME)
      END
      ******************************
      *** CHECK FOR SPECIFIED CLIENT
      ******************************
      CLIENT.ID = FIELD(@SENTENCE,' ',2)
      IF CLIENT.ID NE '' THEN
        READ TEST FROM F.CLIENTS, CLIENT.ID ELSE
          CRT CLIENT.ID:' is not a valid client number; program stopped.'
          STOP
        END
      END
      ********************
      *** SELECT CLIENT ID'S
      ********************
      IF CLIENT.ID = '' THEN
        STMT = 'SELECT VOC "VEND.CUST" *A7'
        EXECUTE STMT RTNLIST CAPTURING MSG
        IF @SELECTED < 1 THEN
          CRT 'NO CLIENTS SELECTED.'
          STOP
        END
        STMT = 'SELECT CLIENTS'
        EXECUTE STMT PASSLIST RTNLIST CLIENT.LIST CAPTURING MSG
        IF @SELECTED < 1 THEN
          CRT 'NO CLIENT RECORDS SELECTED.'
          STOP
        END
      END ELSE
        STMT = 'SELECT CLIENTS ':CLIENT.ID
        EXECUTE STMT RTNLIST CLIENT.LIST CAPTURING MSG
        IF @SELECTED < 1 THEN
          CRT 'NO CLIENT RECORDS SELECTED.'
          STOP
        END
      END
      ********************
      *** LOOP THROUGH ALL CLIENTS
      ********************
      DONE1 = 0
      LOOP UNTIL DONE1 DO
        READNEXT CLIENT.ID FROM CLIENT.LIST THEN
          CRT CLIENT.ID
          OPEN 'VEND.CUST,':CLIENT.ID TO F.VC ELSE
            CRT 'Could not open VEND.CUST,':CLIENT.ID
            GOTO NEXT.CLIENT
          END
          OPEN 'VEND.CUST.NAMES,':CLIENT.ID TO F.VCN ELSE
            CRT 'Could not open VEND.CUST.NAMES,':CLIENT.ID
            GOTO NEXT.CLIENT
          END
          STMT = 'SELECT VEND.CUST,':CLIENT.ID
          EXECUTE STMT RTNLIST VC.LIST CAPTURING MSG
          IF @SELECTED > 0 THEN
            DONE2 = 0
            LOOP UNTIL DONE2 DO
              READNEXT VC.ID FROM VC.LIST THEN
                READ VC.REC FROM F.VC, VC.ID ELSE
                  CRT 'COULD NOT READ VEND.CUST,':CLIENT.ID:' RECORD ':VC.ID
                  GOTO NEXT.VC.REC
                END
                VCN.ID = VC.REC<42>
                IF VCN.ID = '' THEN
                  CRT 'VEND.CUST,':CLIENT.ID:' RECORD ':VC.ID:' HAS NO VCN.ID'
                  GOTO NEXT.VC.REC
                END
                READ VCN.REC FROM F.VCN, VCN.ID ELSE
                  CRT 'COULD NOT READ VEND.CUST.NAMES,':CLIENT.ID:' RECORD ':VCN.ID
                  GOTO NEXT.VC.REC
                END
                IF VC.REC<1> # VCN.REC<1> THEN
                  CRT 'MISMATCH FOR CLIENT ':CLIENT.ID:' ':VC.ID
                END
              END ELSE
                DONE2 = 1
              END
NEXT.VC.REC:
            REPEAT
          END
        END ELSE
          DONE1 = 1
        END
NEXT.CLIENT:
      REPEAT
