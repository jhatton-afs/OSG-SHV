 SUBROUTINE AFS.PAGE.HEAD(ID,CONAME.1,CONAME.2,TITLE,ODATE,WIDTH,OUTPUT)
*
 UPL.VIN = 'FBBP' ;  UPL.VIN<2> = 'AFS.PAGE.HEAD' ; UPL.VOUT = ''                    ;* NPR_UPL 04/22/2010
 CALL UPD.PROGRAM.LOG(UPL.VIN,UPL.VOUT)                                                           ;* NPR_UPL 04/22/2010
* SUBROUTINE FOR GENERATING STANDARD VCI REPORT HEADINGS
*
* GENERAL PROGRAMS PACKAGE
*
* AUTHOR: MICHAEL F. REYNOLDS
* DATE: JANUARY 30, 1991
*
* THIS PROGRAM IS A PROPRIETARY PRODUCT OF AUTOMATED FREIGHT SYSTEMS
* AND MAY NOT BE USED, COPIED OR MODIFIED WITHOUT THE
* EXPRESS WRITTEN CONSENT OF SAME.
*
* VARIABLE DESCRIPTIONS
*
* ID - The report ID found in the upper left corner of
* line one.
* CONAME.1 - The company ID located centered on the first line.
* CONAME.2 - The second line of the company name.
* TITLE - The title of the report, centered on the second line.
* ODATE - Output date string if passed.
* WIDTH - The width of the report page.
* OUTPUT - The complete formated heading string.
*
**********************************************************************
*
* Last updated by AFS.SOURCE (valerie) at 11:28:07 on 06/17/1994.
*   UPDATED FOR UNIVERSE AND ADDED TIME AND DATE TO REPORT HEADINGS
*
$INCLUDE PROG.ADMIN INC_OPTIONS
*
* INITALIZE VARIABLES
*
      PROMPT ''
      ERRMSG=''
*
* GENERATE HEADING
*
      IF LEN(ODATE)=4 THEN
        CALL FULL.DATE(ODATE,TEMP)
      END ELSE
        TEMP=ODATE
      END
*
      LENGTH.ID = LEN(ID)
      LENGTH.CONAME.1 = LEN(CONAME.1)
      S1 = INT((WIDTH-LENGTH.CONAME.1)/2)-LENGTH.ID
      S2 = WIDTH - (S1+LENGTH.CONAME.1+LENGTH.ID+10)
      OUTPUT = ID:SPACE(S1):CONAME.1:SPACE(S2):"Page: 'PLT'"
      SKIP.LEN = LEN( TIMEDATE() ) + 3  ;*  TO MATCH 'C'
      IF CONAME.2 # "" THEN
        HEAD.GAP = INT((WIDTH - LEN(CONAME.2))/2) - SKIP.LEN
        OUTPUT := SPACE(HEAD.GAP) : CONAME.2:"'L'"
        SKIP.LEN = 0
      END
      BEGIN CASE
        CASE TITLE#'' & TEMP#''
          HEAD.GAP = INT((WIDTH - LEN(TITLE))/2) - SKIP.LEN
          OUTPUT := SPACE(HEAD.GAP) : TITLE:"'LC'":TEMP:"'L'"
        CASE TITLE#''
          HEAD.GAP = INT((WIDTH - LEN(TITLE))/2) - SKIP.LEN
          OUTPUT := SPACE(HEAD.GAP) : TITLE:"'L'"
        CASE TEMP#''
          HEAD.GAP = INT((WIDTH - LEN(TEMP))/2) - SKIP.LEN
          OUTPUT := SPACE(HEAD.GAP) : TEMP:"'L'"
      END CASE
*
      RETURN
*
***********************************************************************
*
   END
