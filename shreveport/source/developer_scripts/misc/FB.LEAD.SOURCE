* PROGRAM FB.LEAD.SOURCE
* PROGRAM TO MODIFY FREIGHT BILL LINE ITEM CODES
*
* FREIGHT BILLING PACKAGE
*
* AUTHOR : MIKE KERBOW
* DATE   : December 3 2013
*
* Initialize variables
*
      $OPTIONS HEADER.EJECT
      $OPTIONS EXEC.EQ.PERF
      $OPTIONS INFO.ABORT
      ERRMSG=''
      PROMPT''
      PROG.NAME='FB.LEAD.SOURCE'
      PROG.DESC='CLIENT LEAD SOURCE CODE MAINTENANCE'
      PACK.NAME='FREIGHT BILLING'
      CALL AFS.SCR.HEAD(CO.ID,FILE.ID,'',PROG.NAME,PROG.DESC,PACK.NAME,CO.NAME,TIME.DATE,1)
      UPL.VIN = 'FBBP' ;  UPL.VIN<2> = 'FB.LEAD.SOURCE' ; UPL.VOUT = ''
      CALL UPD.PROGRAM.LOG(UPL.VIN,UPL.VOUT)
      ESC=CHAR(27)
      PG.ROWS=10
      PG.ROWS2=5
      SCROLL=0
      DASH=STR('=',132)

      CALL GET.USER(USER.NAME)
      BEGIN CASE
*
* Authorized users
*
         CASE USER.NAME = 'DAVID'
         CASE USER.NAME = 'MKERBOW'
         CASE USER.NAME = 'PBASS'           ; * PKB01
         CASE USER.NAME = 'MYRA'
         CASE USER.NAME = 'MARY'
*
* Restrict everyone else
*
         CASE 1
            SUBJ="Unauthorized Program Access by ":USER.NAME
            TEXT=''
            TEXT<1,-1>=USER.NAME:' ':DATE()'D2/':' ':OCONV(TIME(),'MTHS'):' ':PROG.NAME
            TEXT<1,-1>='User ':USER.NAME:' tried to access ':PROG.DESC
            TEXT<1,-1>="(This user's name was not included in the program to allow access)"
            MVIN=''
            MVIN<2>=''
            MVIN<4>='AFS'
            MVIN<5>=SUBJ
            MVIN<6>=TEXT
*           CALL SENDMAIL(MVIN,MVOUT)

            CALL FB.PRINT.CENT(1,80,12,'Restricted Access Application...')
            CALL FB.PRINT.CENT(1,80,14,'Access Denied! Contact the IS Help Desk for Access')
            CALL ELINE('')
            STOP
      END CASE

*
* Open files
*
      OPEN '','FB.TABLES' TO F.TABLES ELSE CALL OPEN.ABORT('FB.TABLES',PROG.NAME)
*
* Read Code Table from File
*
      READ DISP.REC FROM F.TABLES,'FB.LEAD.SOURCES' ELSE DISP.REC=''
*
* First page of codes
*
      PAGE=1
      PAGE2=1
      SCROLL=1
      SEL=''
      SEL2=''
      GOSUB 9000
*
* Enter selection code
*
100:  HELP="Enter line number. 'B'=Back. 'T'=Top, ''=Next page. 'E'=End-&-Save. 'EX'=Exit."
      CALL GEN.IN(0,22,"Enter line number please. ###",'',Q,0,3,'','',1,-3,22,0,QI,HELP,0,23)
      QI=OCONV(QI,'MCU')
      BEGIN CASE
         CASE QI=''
            PAGE=PAGE+1
            GOSUB 9000
         CASE QI='B'
            PAGE=PAGE-1
            IF PAGE=0 THEN PAGE=1
            GOSUB 9000
         CASE QI='T'
            PAGE=1
            GOSUB 9000
         CASE QI='P'                     ; * JMK01
            GOSUB 9500                   ; * JMK01
         CASE QI='EX'
            STOP
         CASE NUM(QI)
            SEL=QI
            IF SEL LT START ! SEL GT STOP THEN
               CALL ELINE('Invalid selection.')
               GOTO 100
            END
            GOSUB 9000
            GOSUB 8000
         CASE QI='E'
            X=DCOUNT(DISP.REC<1>,CHAR(253))
            FOR I=X TO 1 STEP -1
               IF DISP.REC<1,I>='' THEN
                  DEL DISP.REC<1,I>
                  DEL DISP.REC<2,I>
                  DEL DISP.REC<3,I>
               END
            NEXT I
*
            WRITE DISP.REC ON F.TABLES,'FB.LEAD.SOURCES'
            STOP
      END CASE
      GOTO 100
*
*------------------------------------------------------------------------
* INTERNAL SUBROUTINES
*------------------------------------------------------------------------
*
* Edit code
*
8000: ROW=3+REM(SEL,PG.ROWS)
      IF ROW=3 THEN ROW=PG.ROWS+3
      CODE=DISP.REC<1,SEL>
      DESC=DISP.REC<2,SEL>
      CODE.HOLD = CODE

8010: HELP="Enter new code. ''=Accept. 'D'=Delete. 'X'=Back. 'EX'=Exit"    ;* NPR01
      CALL GEN.IN(5,ROW,CODE'L#4','',Q,0,4,'','',2,-4,ROW,0,QI,HELP,0,23)
      QI=OCONV(QI,'MCU')
      IF QI='X' THEN
         CRT @(5,ROW):CODE'L#4'
         RETURN
      END

      IF QI='EX' THEN STOP

      IF QI='' THEN
         IF CODE.HOLD NE "" THEN
            Q=CODE
            QI=CODE
         END ELSE
            CALL ELINE("Invalid response - Enter a code")
            GOTO 8010
         END
      END
      IF QI='D' THEN
         FOR X = 1 TO 3
            DISP.REC=DELETE(DISP.REC,X,SEL)
         NEXT X
         GOSUB 9000
         RETURN
      END

      LOCATE QI IN DISP.REC<1> SETTING POS THEN
         IF POS NE SEL THEN
            CALL ELINE("Duplicate code ":QI:" See line ":POS)
            GOTO 8010
         END
      END

      CODE=QI
      CRT @(5,ROW):CODE'L#4'
      CODE.HOLD = CODE
*
8020: HELP="Enter code description. '<'=Back to code. 'X'=Back. 'EX'=Exit"
      CALL GEN.IN(11,ROW,DESC'L#30','',Q,0,30,'','',2,-30,ROW,0,QI,HELP,0,23)
      QI=OCONV(QI,'MCU')
      BEGIN CASE
         CASE QI='X' ! QI='<'
            CRT @(11,ROW):DESC'L#30'
            GOTO 8010
         CASE QI='EX'
            STOP
         CASE QI=''
            Q=DESC
      END CASE
      DESC=Q
      CRT @(11,ROW):DESC'L#30'
      DISP.REC<1,SEL>=CODE
      DISP.REC<2,SEL>=DESC
*
* Edit Code Cagetories
*
      PAGE2=1
      SCROLL=2
      GOSUB 9000
*
* Enter selection code
*
8100: HELP="Enter line number. 'B'=Back. 'T'=Top, ''=Next page. 'E'=End-&-Save. 'EX'=Exit."
      CALL GEN.IN(0,22,"Enter line number please. ###",'',Q,0,3,'','',1,-3,22,0,QI,HELP,0,23)
      QI=OCONV(QI,'MCU')
      BEGIN CASE
         CASE QI=''
            PAGE2=PAGE2+1
            GOSUB 9000
         CASE QI='B'
            PAGE2=PAGE2-1
            IF PAGE2=0 THEN PAGE2=1
            GOSUB 9000
         CASE QI='T'
            PAGE2=1
            GOSUB 9000
         CASE QI='EX'
            STOP
         CASE NUM(QI)
            SEL2=QI
            IF SEL2 LT START2 ! SEL2 GT STOP2 THEN
               CALL ELINE('Invalid selection.')
               GOTO 8100
            END
            GOSUB 9000
            GOSUB 8200
         CASE QI='E'
            X=DCOUNT(DISP.REC<3,SEL2>,@SVM)
            FOR I=X TO 1 STEP -1
               IF DISP.REC<3,SEL2,I>='' THEN
                  DEL DISP.REC<3,SEL2,I>
                  DEL DISP.REC<4,SEL2,I>
               END
            NEXT I
            SEL2=''
            SCROLL=1
            GOSUB 9000
            RETURN
      END CASE
      GOTO 8100
8200: ROW2=15+REM(SEL,PG.ROWS)
      IF ROW2=15 THEN ROW2=PG.ROWS+15
      DESC2=DISP.REC<3,SEL,SEL2>
*
8210: HELP="Enter code description. '<'=Back to code. 'X'=Back. 'EX'=Exit"
      CALL GEN.IN(5,ROW2,DESC2'L#30','',Q,0,30,'','',2,-30,ROW2,0,QI,HELP,0,23)
      QI=OCONV(QI,'MCU')
      BEGIN CASE
         CASE QI='X' ! QI='<'
            CRT @(11,ROW2):DESC2'L#30'
            RETURN
         CASE QI='EX'
            STOP
         CASE QI=''
            Q=DESC2
      END CASE
      DESC2=Q
      CRT @(5,ROW2):DESC2'L#30'
      DISP.REC<3,SEL,SEL2>=DESC2
      RETURN
*
* Display lead source array
*
9000:
      CRT @(0,2):DASH 'L#79'
      CRT @(0,3):@(-3):"  #":@(5,3):"Code  Description"
      CRT @(0,PG.ROWS+4):DASH 'L#79'
      CRT @(0,21):DASH 'L#79'
      BEGIN CASE
         CASE SCROLL=1
            START=(PAGE*PG.ROWS)-(PG.ROWS-1)
            IF DISP.REC<1,START-1>='' THEN
               START=1
               PAGE=1
            END
            STOP=START+(PG.ROWS-1)
            FOR I=START TO STOP STEP 1
               ROW=3+REM(I,PG.ROWS)
               IF ROW=3 THEN ROW=PG.ROWS+3
               CODE=DISP.REC<1,I>
               DESC=DISP.REC<2,I>
               CRT @(0,ROW):I"R#3":'  ':CODE"L#4":'  ':DESC"L#30"
            NEXT I
            START2=(PAGE2*PG.ROWS2)-(PG.ROWS2-1)
            IF DISP.REC<3,START2-1>='' THEN
               START=1
               PAGE=1
            END
            STOP2=START2+(PG.ROWS2-1)
            FOR I=START2 TO STOP2 STEP 1
               ROW2=15+REM(I,PG.ROWS2)
               IF ROW2=15 THEN ROW2=PG.ROWS2+15
               DESC2=''
               IF SEL#'' THEN
                  DESC2=DISP.REC<3,SEL,I>
                  CRT @(0,15):"  #":@(5,15):"Source Agent Description"
               END
               CRT @(0,ROW2):''"R#3":'  ':DESC2"L#30"
            NEXT I
         CASE SCROLL=2
            START=(PAGE*PG.ROWS)-(PG.ROWS-1)
            IF DISP.REC<1,START-1>='' THEN
               START=1
               PAGE=1
            END
            STOP=START+(PG.ROWS-1)
            FOR I=START TO STOP STEP 1
               ROW=3+REM(I,PG.ROWS)
               IF ROW=3 THEN ROW=PG.ROWS+3
               CODE=DISP.REC<1,I>
               DESC=DISP.REC<2,I>
               CRT @(0,ROW):'     ':CODE"L#4":'  ':DESC"L#30"
            NEXT I
            CRT @(0,15):"  #":@(5,15):"Description"
            START2=(PAGE2*PG.ROWS2)-(PG.ROWS2-1)
            IF DISP.REC<3,START2-1>='' THEN
               START=1
               PAGE=1
            END
            STOP2=START2+(PG.ROWS2-1)
            FOR I=START2 TO STOP2 STEP 1
               ROW2=15+REM(I,PG.ROWS2)
               IF ROW2=15 THEN ROW2=PG.ROWS2+15
               DESC2=DISP.REC<3,SEL,I>
               CRT @(0,ROW2):I"R#3":'  ':DESC2"L#30"
            NEXT I
      END CASE
      RETURN

*
9500: * Print report
*
      EXECUTE 'TERM ,,,,,,218,75'
      PRINTER ON
      LASER = ''
      LASER<1>= 0
      LASER<2>= 8
      LASER<3>= 84
      LASER<4>= 16.7
      LASER<5>= 0
      LASER<6>= 0
      LASER<7>= 1
      LASER<8>= 0
      MARGIN = 0
      PRINT ESC:"E"
      PRINT ESC:"&l":LASER<1>:"O":
      PRINT ESC:"&l":LASER<2>:"D":
      PRINT ESC:"&l":LASER<3>:"F":
      PRINT ESC:"(s":LASER<4>:"h":
      PRINT LASER<5>:"p":
      PRINT LASER<6>:"s":
      PRINT "0T":
      PRINT ESC:"&l":LASER<7>:"H":
      PRINT ESC:"&a":MARGIN:"L":
      PRINT ESC:'"g':LASER<8>:'F':

      HED=''
      CALL PAGE.HEAD(PROG.NAME,'AFS Logistics, LLC',PROG.DESC,'','',132,HED)
      HEADING HED
      PRINT '#'"R#3":'  ':'Code'"L#4":'  ':'Description'"L#30"
      NVAL=DCOUNT(DISP.REC<1>,@VM)
      FOR VAL=1 TO NVAL
         CODE=DISP.REC<1,VAL>
         DESC=DISP.REC<2,VAL>
         PRINT VAL"R#3":'  ':CODE"L#4":'  ':DESC"L#30"
      NEXT VAL
      PRINTER OFF
      HUSH ON
      PRINTER CLOSE
      HUSH OFF
      EXECUTE 'TERM ,,,,,,132,60'
      RETURN
