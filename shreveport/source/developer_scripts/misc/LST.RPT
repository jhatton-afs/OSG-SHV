***********************************************************************
* Program name : LST.RPT
* Author       : UNKNOWN
* Date written : UNKNOWN
* Library      : FBBP
* COPYRIGHT (C) 2019 - AFS LOGISTICS LLC - ALL RIGHTS RESERVED.
*
* * PENDING AUDIT REPORT
*
* Last updated by afs (AFSLOGISTICS\gbino) at 10:24:59 on 03/12/2019.
*
* Start modification history

* End modification history
*
************************************************************************

      TB=CHAR(9)
      ESC=CHAR(27)
      PROG.NAME='LS.RPT'
      PROG.DESC='Pending Audit Report'
      PACK.NAME='Freight Billing'
      CO.NAME='AFS Logistics, LLC'
      CALL GET.USER.NEW(USER,CH.NUM)
      UPL.VIN = 'FBBP' ;  UPL.VIN<2> = 'LS.RPT' ; UPL.VOUT = ''     
      UPL.VIN<3> USER
      UPL.VIN<4> = CH.NUM              
      CALL UPD.PROGRAM.LOG(UPL.VIN,UPL.VOUT)                                                          
      OUT.ACT='P'
      PROC.DATE=DATE()
      TODAY=DATE()
      DOW.VAR=0
      DIM CLIENT.REC(250),BILL.REC(50)
      MAT CLIENT.REC = ""; MAT BILL.REC = ""
      TODAY.DOW=OCONV(TODAY,'DW')
      IF TODAY.DOW='5' THEN
         DOW.VAR=4
      END ELSE
         DOW.VAR=2
      END
      
      OPEN 'BCTRL' TO F.BCTRL ELSE
         CALL OPEN.ABORT('BCTRL',PROG.NAME)
      END
      OPEN 'CLIENTS' TO F.CLIENTS ELSE
         CALL OPEN.ABORT('CLIENTS',PROG.NAME)
      END
      OPEN 'FB.TABLES' TO F.TABLES ELSE
         CALL OPEN.ABORT('FB.TABLES',PROG.NAME)
      END
      OPEN 'UPS.BM' TO F.UPS.BM ELSE
         CALL OPEN.ABORT('UPS.BM',PROG.NAME)
      END
      OPEN '','FB.BANKS' TO F.FB.BANKS ELSE        ; * JMK128
         CALL OPEN.ABORT('FB.BANKS',PROG.NAME)     ; * JMK128
      END                                ; * JMK128
      OPEN '','FB.BANK.CTRL' TO F.BANK.CTRL ELSE        ; * JMK128
         CALL OPEN.ABORT('FB.BANK.CTRL',PROG.NAME)     ; * JMK128
      END                                ; * JMK128
      OPEN '','FP.PENDING' TO F.FPPEND ELSE
         CALL OPEN.ABORT('FP.PENDING',PROG.NAME)
      END
      OPEN '','PCL.BILLING.XREF' TO F.PCL.BILLING.XREF ELSE
         CALL OPEN.ABORT('PCL.BILLING.XREF',PROG.NAME)
      END
      CLEARFILE F.FPPEND
      READ BCTRL.REC FROM F.BCTRL,USER ELSE BCTRL.REC=''
      USER.EMAIL=BCTRL.REC<20>
      CALL AFS.SCR.HEAD(CO.ID,FILE.ID,PERIOD,PROG.NAME,PROG.DESC,PACK.NAME,CO.NAME,TIME.DATE,1)
100: *
      CRT @(5,7):'Process Date :':
      IF PROC.DATE # '' THEN
         MASK = PROC.DATE'D2.'
      END ELSE
         MASK = 'MM/DD/YY'
      END
      HELP = "Enter Process Date. [T]omorrow [EX]it "
      CALL GEN.IN(20,7,MASK,'DATE',Q,0,8,'','',2,-8,7,0,QI,HELP,0,23)
      QI = OCONV(QI,'MCU')
      IF QI = '' THEN
         QI = OCONV(PROC.DATE,'D2.')
         Q = PROC.DATE
      END
      BEGIN CASE
         CASE QI = 'T'
            Q = DATE()+DOW.VAR
         CASE QI = 'EX'
            STOP
         CASE NOT(NUM(Q)) OR Q GT DATE()+DOW.VAR ! Q = ''
            CALL ELINE('ERROR - Invalid Date.')
            GOTO 100
      END CASE
      IF Q < DATE()-730 THEN
         CALL ELINE("Warning! Bill was shipped more than 2 years ago!")
      END
      IF Q > DATE()+DOW.VAR THEN
         CALL ELINE("Error Process date can't be beyond tomorrow!")
         GOTO 100
      END
      PROC.DATE = Q
      IF PROC.DATE='' THEN
         CALL ELINE('ERROR - Invalid Date.')
         GOTO 100
      END
      CRT @(20,7):PROC.DATE'D4/'
      PROC.DOW=OCONV(PROC.DATE,'DW')

200: *
      CRT @(5,9):'Report Type  :':
      MASK = 'Ready to Process'
      HELP = "1=Ready to Process, 2=Pending Audit, 3=Already Processed, 4=All [EX]it "
      CALL GEN.IN(20,9,MASK,'',Q,0,20,'','',2,20,9,0,QI,HELP,0,23)
      QI = OCONV(QI,'MCU')
      IF QI = '' THEN QI = 1
      BEGIN CASE
         CASE QI = 'EX'
            STOP
         CASE QI = '1'
         CASE QI = '2'
         CASE QI = '3'
         CASE QI = '4'
         CASE 1
            CALL ELINE('ERROR - Invalid Report Type.')
            GOTO 200
      END CASE
      RPT.TYPE = QI
      BEGIN CASE
         CASE RPT.TYPE=1 ; RPT.DESC='Ready to Process'
         CASE RPT.TYPE=2 ; RPT.DESC='Pending Audit'
         CASE RPT.TYPE=3 ; RPT.DESC='Already Processed'
         CASE RPT.TYPE=4 ; RPT.DESC='All'
      END CASE
      CRT @(20,9):RPT.DESC:@(-4)

300: *
      CRT @(5,11):'Output Action:':
      MASK = 'Print'
      HELP = "Output to [P]rinter, [E]mail, [F]ile, [EX]it "
      CALL GEN.IN(20,11,MASK,'',Q,0,5,'','',5,20,11,0,QI,HELP,0,23)
      QI = OCONV(QI,'MCU')
      IF QI = '' THEN QI = 'P'
      BEGIN CASE
         CASE QI = 'P' ; CRT @(20,11):'Print'
         CASE QI = 'E' ; CRT @(20,11):'Email'
         CASE QI = 'F' ; CRT @(20,11):'File '
            OPEN '','VOC' TO F.VOC ELSE
               CALL ELINE('Unable to open VOC file - Call IS Department.')
               GOTO THE.END
            END
            READ BILLDATA.REC FROM F.VOC,'BILLDATA' ELSE
               CALL ELINE('Unable to open VOC BILLDATA - Call IS Department.')
               GOTO THE.END
            END
            BILLDATA.REC<2>:='\':USER
            WRITE BILLDATA.REC ON F.VOC,'BILLDATA.':USER ELSE
               CALL ELINE('Your user name ':USER:' is not in the BILLDATA folder - Call IS Department.')
               GOTO THE.END
            END
            OPEN '','BILLDATA.':USER TO F.BILLDATA ELSE
               CALL ELINE('Unable to open BILLDATA.':USER:'.')
               GOTO THE.END
            END
         CASE QI = 'EX' OR QI = 'X'
            STOP
      END CASE
      OUT.ACT = QI

      RPT.OUTPUT=''                  ; * JMK60
      EMAIL.TEXT=''
      RPT.OUTPUT<1>=\LS.RPT FP\:RPT.DESC:\ Report for \:PROC.DATE 'D2/'       ; * JMK60
      RPT.OUTPUT<3,1>=\Client #\      ; * JMK60
      RPT.OUTPUT<3,2>=\Client Name\   ; * JMK60

       RPT.OUTPUT<3,3>='Verif'
      RPT.OUTPUT<3,4>=\Process Block\
      RPT.OUTPUT<3,5>=\Freight Bill Status\   ; * JMK60
      EMAIL.TEXT<1,-1>=\LS.RPT FP \:RPT.DESC:\ Report for \:PROC.DATE 'D2/'
      EMAIL.TEXT<1,-1>=' '
      EMAIL.TEXT<1,-1>=\Client #\'L#10 ':\Client Name\'L#30 ':\Verif \:\Process Block\'L#40 ':\Freight Bill Status\'L#40 '

*
* SELECT CLIENT FILE
*
      TEXT='Selecting Clients.'
      CALL CENTER(TEXT,80)
      CRT @(0,12):TEXT
      PROC.DATE=DATE()
*
      RPT.RDY=''
      RPT.PEND=''
      RPT.PROC=''
      EMAIL.RDY=''
      EMAIL.PEND=''
      EMAIL.PROC=''
      CLIENT.OUTPUT=''
      CLIENT.STMT='SSELECT CLIENTS WITH @ID NE "99]" AND WITH STATUS EQ "A" AND WITH 151 = "':PROC.DOW:'" BY NAME'

      EXECUTE CLIENT.STMT RTNLIST CLIENT.LIST CAPTURING CLIENT.OUTPUT
      CLIENT.ITEMS = @SELECTED
      IF CLIENT.ITEMS < 1 THEN
         CALL ELINE('ERROR - No clients on file.')
         STOP
      END
      CLIENT.EOF=0
      CLIENT.CNT=0
      CLIENT.PER=0
      LOOP
         READNEXT CLIENT.ID FROM CLIENT.LIST ELSE CLIENT.EOF=1
      UNTIL CLIENT.EOF DO
         CLIENT.CNT+=1
         IF CLIENT.ID='09998' THEN GO SKIP.CLIENT
         PCL.BILLING.XREF.ID=CLIENT.ID:'*':PROC.DATE
         MATREAD CLIENT.REC FROM F.CLIENTS,CLIENT.ID ELSE GO SKIP.CLIENT
         CLIENT.NAME=CLIENT.REC(2)
         CLIENT.STATUS=CLIENT.REC(30)
         IF CLIENT.STATUS#'A' THEN GO SKIP.CLIENT
         CLIENT.DOW=CLIENT.REC(151)
         IF PROC.DOW#CLIENT.DOW THEN GO SKIP.CLIENT
         READ PCL.TEST FROM F.PCL.BILLING.XREF,PCL.BILLING.XREF.ID ELSE PCL.TEST=''
         CRT @(0,14):CLIENT.ID:" ":CLIENT.NAME:" - Now selecting freight bills":@(-3):
         CALL PROG.BAR(16,CLIENT.CNT,CLIENT.ITEMS,CLIENT.PER)
         OPEN '','FB.BILLS,':CLIENT.ID TO F.BILLS ELSE GO SKIP.CLIENT
         FB.TYPE.F=''
         FB.TYPE.V=''
         FB.TYPE.N=''
         CLIENT.BANK=CLIENT.REC(81)
         READ CLIENT.BANK.REC FROM F.FB.BANKS,'BANK*':CLIENT.BANK ELSE CLIENT.BANK.REC=''
         CLIENT.FB.TYPE=CLIENT.BANK.REC<6>
         
         READ BANK.CTRL.REC FROM F.BANK.CTRL,CLIENT.ID ELSE BANK.CTRL.REC=''
         BLOCK.REASON=''
*
* CHECK FOR ANY CLIENT SPECIFIC BLOCKS
*
         CRT @(0,20):'Checking for Client blocks...':@(-4)
         IF BLOCK.REASON='' THEN
            READ BLOCK.REC FROM F.TABLES,'NO-PROCESS*':CLIENT.ID ELSE BLOCK.REC=''
            BLOCK.REASON=BLOCK.REC<1>
         END
*
* CHECK FOR ANY CLIENT MISSING UPS BILLS
*
         IF BLOCK.REASON='' THEN
            CRT @(0,20):'Checking for missing UPS E-Bill blocks...':@(-4)
            EBILL.CLIENT=0
            READ DUMMY FROM F.UPS.BM,CLIENT.ID THEN
               EBILL.CLIENT=1
            END
            READ DUMMY FROM F.UPS.BM,CLIENT.ID:'-I' THEN
               EBILL.CLIENT=1
            END
            READ DUMMY FROM F.UPS.BM,CLIENT.ID:'-O' THEN
               EBILL.CLIENT=1
            END
            IF CLIENT.ID=01071 THEN EBILL.CLIENT=0
            IF CLIENT.ID=01448 THEN EBILL.CLIENT=0
            IF CLIENT.ID=01653 THEN EBILL.CLIENT=0
            IF CLIENT.ID=00972 THEN EBILL.CLIENT=0
            IF EBILL.CLIENT THEN
               EBILL.DATE=DATE()-(MOD(DATE(),7)+1)
               EBILL.DATE=OCONV(EBILL.DATE,'D2/')
               EBILL.DATE=EBILL.DATE[1,2]:EBILL.DATE[4,2]:EBILL.DATE[7,2]
               EBILL.STMT = 'SSELECT FB.BILLS,':CLIENT.ID:' BY 30'
               EBILL.STMT:= ' WITH VERSION = "0" AND WITH 39 = "O"'
               EBILL.STMT:=' AND WITH CARRIER# = "00041"'
               EBILL.STMT:=' AND WITH @ID = "[':EBILL.DATE:'-]" '
               EBILL.STMT:= ' SAMPLE 1'
               EXECUTE EBILL.STMT RTNLIST EBILL.LIST CAPTURING EBILL.OUTPUT
               IF @SELECTED < 1 THEN
                  BLOCK.REASON='The Ebill Is Not On File For This Week'
               END
            END
         END
*
* CHECK FOR FEDEX BLOCK
*
         IF BLOCK.REASON='' THEN
            CRT @(0,20):'Checking for FedEx Ground blocks...':@(-4)
            READ NO.PROCESS FROM F.TABLES,'NO-PROCESS*FEDEX.GROUND' THEN
               IF MOD(DATE(),7) > 4 THEN
                  FEDEX.STMT='SELECT EDI.CAR.ACNT.XREF WITH CARRIER# = "01075" AND WITH 1 = "':CLIENT.ID:'"'
                  EXECUTE FEDEX.STMT RTNLIST FEDEX.LIST CAPTURING FEDEX.OUTPUT
                  IF @SELECTED> 0 THEN
                     BLOCK.REASON=NO.PROCESS<1>
                  END
               END
            END
         END
         IF BLOCK.REASON='' THEN
            IF PCL.TEST#'' AND CLIENT.REC(179)='' THEN GO SKIP.CLIENT
         END

         IF CLIENT.REC(82)>=PROC.DATE THEN
            IF RPT.TYPE='3' OR RPT.TYPE='4' THEN
               RPT.LINE=''
               BEGIN CASE
                  CASE BLOCK.REASON#''
                     RPT.LINE<1,1>=CLIENT.ID:' B'
                  CASE CLIENT.REC(179) # ''
                     RPT.LINE<1,1>=CLIENT.ID:' T'
                  CASE 1
                     RPT.LINE<1,1>=CLIENT.ID
               END CASE
               RPT.LINE<1,2>=CLIENT.NAME

               RPT.LINE<1,4>=BLOCK.REASON
               RPT.LINE<1,5>=\Client has already been processed.\
               RPT.PROC<-1>=RPT.LINE
               EMAIL.PROC<1,-1>=RPT.LINE<1,1>'L#10 ':RPT.LINE<1,2>'L#30 ':RPT.LINE<1,3>'L#5 ':RPT.LINE<1,5>'L#50'
            END
         END ELSE
            BILL.STMT='SELECT FB.BILLS,':CLIENT.ID:' AND WITH STATUS # "R" '

            BILL.STMT:=' AND WITH VERSION EQ "0"'
            EXECUTE BILL.STMT RTNLIST BILL.LIST CAPTURING OUTPUT
            BILL.TCNT=@SELECTED
            BILL.EOF=0
            BILL.CNT=0
            PEND.CNT=0
            UNVERIF.CNT=0
            VERIF.ALL=1
            LOOP
               READNEXT BILL.ID FROM BILL.LIST ELSE BILL.EOF=1
            UNTIL BILL.EOF DO
               BILL.CNT+=1
               MATREAD BILL.REC FROM F.BILLS,BILL.ID THEN
                  BILL.STATUS=BILL.REC(39)
                  IF BILL.REC(39)='' THEN
                     VERIF.ALL=0
                     UNVERIF.CNT+=1
                  END
                  IF BILL.STATUS#'O' THEN
                     PEND.CNT+=1
                  END
                  BILL.BANK=BILL.REC(40)
                  IF BILL.BANK='' THEN
                     BILL.BANK=CLIENT.BANK
                     CARRIER.ID=FIELD(BILL.ID,'*',1)
                     LOCATE CARRIER.ID IN BANK.CTRL.REC<1> SETTING CPOS THEN
                        CARRIER.BANK=BANK.CTRL.REC<10,CPOS>
                        CARRIER.OVERRIDE=1
                        IF BANK.CTRL.REC<2,CPOS>#'' THEN
                           IF BILL.REC(1) < BANK.CTRL.REC<2,CPOS> THEN
                              CARRIER.OVERRIDE=0
                           END
                        END
                        IF BANK.CTRL.REC<3,CPOS>#'' THEN
                           IF BILL.REC(1) > BANK.CTRL.REC<3,CPOS> THEN
                              CARRIER.OVERRIDE=0
                           END
                        END
                        IF CARRIER.OVERRIDE THEN
                           BILL.BANK=CARRIER.BANK
                        END
                     END
                  END
                  READ BILL.BANK.REC FROM F.FB.BANKS,'BANK*':BILL.BANK ELSE BILL.BANK.REC=''
                  BILL.FB.TYPE=BILL.BANK.REC<6>
                  BEGIN CASE
                     CASE BILL.FB.TYPE='F' ; FB.TYPE.F='X'
                     CASE BILL.FB.TYPE='V' ; FB.TYPE.V='X'
                     CASE BILL.FB.TYPE='N' ; FB.TYPE.N='X'
                  END CASE
               END
            REPEAT

            IF VERIF.ALL THEN TYPE.VERIF.ALL='  X' ELSE TYPE.VERIF.ALL='   '
            IF PEND.CNT#0 THEN
               IF RPT.TYPE='2' OR RPT.TYPE='4' THEN
                  RPT.LINE=''
                  BEGIN CASE
                     CASE BLOCK.REASON#''
                        RPT.LINE<1,1>=CLIENT.ID:' B'
                     CASE CLIENT.REC(179)#''
                        RPT.LINE<1,1>=CLIENT.ID:' T'
                     CASE BILL.TCNT=0
                        RPT.LINE<1,1>=CLIENT.ID:' NB'
                     CASE 1
                        RPT.LINE<1,1>=CLIENT.ID
                  END CASE
                  RPT.LINE<1,2>=CLIENT.NAME
                 RPT.LINE<1,3>=TYPE.VERIF.ALL
                  RPT.LINE<1,4>=BLOCK.REASON
                  RPT.LINE<1,5>=UNVERIF.CNT'R#5':\ unverified freight bills.\
                  RPT.PEND<-1>=RPT.LINE
                  EMAIL.PEND<1,-1>=RPT.LINE<1,1>'L#10 ':RPT.LINE<1,2>'L#30 ':RPT.LINE<1,3>'L#5 ':RPT.LINE<1,5>'L#50'
               END
            END ELSE
               IF RPT.TYPE='1' OR RPT.TYPE='4' THEN ; * JMKXXX
                  RPT.LINE=''
                  BEGIN CASE
                     CASE BLOCK.REASON#''
                        RPT.LINE<1,1>=CLIENT.ID:' B'
                     CASE CLIENT.REC(179) # ''
                        RPT.LINE<1,1>=CLIENT.ID:' T'
                     CASE BILL.TCNT=0
                        RPT.LINE<1,1>=CLIENT.ID:' NB'
                     CASE 1
                        RPT.LINE<1,1>=CLIENT.ID
                  END CASE
                  RPT.LINE<1,2>=CLIENT.NAME
                  RPT.LINE<1,3>=TYPE.VERIF.ALL
                  RPT.LINE<1,4>=BLOCK.REASON
                  IF CLIENT.REC(179)#'' THEN
                     RPT.LINE<1,5>=\*** \:CLIENT.REC(242)'L#33':\ ***\
                  END ELSE
                     RPT.LINE<1,5>="All pro's have been verified and audited."
                  END
                  RPT.RDY<-1>=RPT.LINE
                  EMAIL.RDY<1,-1>=RPT.LINE<1,1>'L#10 ':RPT.LINE<1,2>'L#30 ':RPT.LINE<1,3>'L#5 ':RPT.LINE<1,4>'L#40 ':RPT.LINE<1,5>'L#40'
                  IF RPT.TYPE='1' AND BILL.TCNT>0 THEN       ; * JMKXXX
                   START.DT.RANGE = CLIENT.REC<82>+1
                   IF START.DT.RANGE GT DATE() - 10 THEN
                     IF BLOCK.REASON='' AND CLIENT.REC(179)='' THEN
                        WRITE '' ON F.FPPEND,CLIENT.ID
                     END
                   END
                  END
               END
            END
         END
SKIP.CLIENT: ***
      REPEAT
      EMAIL.TEXT<1,-1>=EMAIL.RDY
      EMAIL.TEXT<1,-1>=EMAIL.PEND
      EMAIL.TEXT<1,-1>=EMAIL.PROC
      RPT.OUTPUT<-1>=RPT.RDY
      RPT.OUTPUT<-1>=RPT.PEND
      RPT.OUTPUT<-1>=RPT.PROC
      RPT.OUTPUT=CHANGE(RPT.OUTPUT,@VM,TB)
      RECORD.NAME='ALL-'
      D=PROC.DATE'D2/'
      RECORD.NAME:=D[1,2]:D[4,2]
      RECORD.NAME:='-LS.XLS'
      IF OUT.ACT='F' THEN
         WRITE RPT.OUTPUT ON F.BILLDATA,RECORD.NAME
      END
      EMAIL.IN=''
      EMAIL.OUT=''
      EMAIL.IN<1>=USER.EMAIL
      
      EMAIL.IN<4>='Reports@afs.net'
      EMAIL.IN<5>='LS.RPT FP ':RPT.DESC:' Report'
      EMAIL.IN<6>=EMAIL.TEXT
      IF OUT.ACT='E' THEN
         CALL SENDMAIL(EMAIL.IN,EMAIL.OUT)
      END
      IF OUT.ACT='P' THEN
         PRINTER ON
         WIDTH=132
         LENGTH=80
         LASER=''
         LASER<1>=0                         ; * Portrait
         LASER<2>=8                         ; * Lines per inch
         LASER<3>=80                        ; * Page length
         LASER<4>=16.7                      ; * Characters per inch
         LASER<5>=0                         ; * Set spacing (0=fixed,1=proprotional)
         LASER<6>=0                         ; * Italics (0=off,1=on)
         LASER<7>=1                         ; * Bin selection
         LASER<8>=0                         ; * Paper output
         LASER<9>=0                         ; * Font number
         LENGTH=LASER<3>-1
         EXECUTE 'TERM ,,,,,,':WIDTH:',':LENGTH
         MARGIN=0
         LASER.STR=''
         LASER.STR=ESC:"E"                  ; * Reset printer
         LASER.STR:=ESC:"&l":LASER<1>:"O"   ; * Set orientation
         LASER.STR:=ESC:"&l":LASER<2>:"D"   ; * Lines per inch
         LASER.STR:=ESC:"&l":LASER<3>:"F"   ; * Page length
         LASER.STR:=ESC:"(s":LASER<4>:"h"   ; * Char per Inch (pitch)
         LASER.STR:=LASER<5>:"p"            ; * Set spacing
         LASER.STR:=LASER<6>:"s"            ; * Italics off
         LASER.STR:="0b"                    ; * Stroke weight
         LASER.STR:="8.50v"                 ; * Point size
         LASER.STR:=LASER<9>:"T"            ; * Font
         LASER.STR:=ESC:"(8U"               ; * Select symbol set
         LASER.STR:=ESC:"&l":LASER<7>:"H"   ; * Bin selection
         LASER.STR:=ESC:"&a":MARGIN:"L"     ; * Set left margin
         LASER.STR:=ESC:'"g':LASER<8>:'F'   ; * Paper output
         PRINT LASER.STR:

         TITLE.1=RPT.OUTPUT<1>
         TITLE.2=''
         ODATE=OCONV(PROC.DATE,'D2/')
         CALL PAGE.HEAD(PROG.NAME,CO.NAME,TITLE.1,TITLE.2,ODATE,WIDTH,HED)       ; * JMK95
         HEADING HED:"'L'"
         NVAL=DCOUNT(EMAIL.TEXT,@VM)
         FOR VAL=3 TO NVAL
            PRINT EMAIL.TEXT<1,VAL>
         NEXT VAL
         PRINTER OFF
         PRINTER CLOSE
      END
      BEGIN CASE
         CASE QI = 'P'
            OUT.TXT='Report has been sent to your printer.'
         CASE QI = 'E'
            OUT.TXT='Report "LS.RPT FP ':RPT.DESC:' Report" has been sent to your email inbox.'
         CASE QI = 'F'
            OUT.TXT='Report ':RECORD.NAME:' has been sent to your "U:\" drive.'
      END CASE
      CALL ELINE(OUT.TXT)
THE.END: *
      END
