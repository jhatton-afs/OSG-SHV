$OPTIONS HEADER.EJECT
$OPTIONS EXEC.EQ.PERF
$OPTIONS INFO.ABORT
      ERRMSG=''
* PROGRAM FB.11.10
* PROGRAM TO MODIFY FREIGHT BILL UPS BILL TRANSLATION CODES
*
* FREIGHT BILLING PACKAGE
*
* AUTHOR: OWEN HOLMAN
* DATE: 08-2005
*
* Modifications
*
* 080312 - RLM01 - Sysaid #3073 - Replace former employees' email addresses with those of current employees
*
* Initialize variables
*
      PROMPT''
      PROG.NAME='FB.11.10'
      PROG.DESC='UPS BILL TRANSLATION CODE MAINTENANCE'
      PACK.NAME='FREIGHT BILLING'
      CALL AFS.SCR.HEAD(CO.ID,FILE.ID,'',PROG.NAME,PROG.DESC,PACK.NAME,CO.NAME,TIME.DATE,1)

      CALL GET.USER(USER.NAME)
      BEGIN CASE
            *
            * Authorized users
            *
         CASE USER.NAME = 'BRIAN'
         CASE USER.NAME = 'KAREN'
         CASE USER.NAME = 'OWEN'
         CASE USER.NAME = 'BRIANP'
         CASE USER.NAME = 'JDARWIN'
         CASE USER.NAME = 'NRATH'
         CASE USER.NAME = 'RRANEY'
         CASE USER.NAME = 'MKERBOW'
         CASE USER.NAME = 'DAVID'
            *
            * Restrict everyone else
            *
         CASE 1
            SUBJ="Unauthorized Program Access by ":USER.NAME
            TEXT=''
            TEXT<1,-1>=USER.NAME:' ':DATE()'D2/':' ':OCONV(TIME(),'MTHS'):' ':PROG.NAME
            TEXT<1,-1>='User ':USER.NAME:' tried to access ':PROG.DESC
            TEXT<1,-1>="(This user's name was not included in the program to allow access)"
            MVIN=''
*           MVIN<1>='Owen@afs.net':@VM:'SMatthews@afs.net'  ; * RLM01
            MVIN<1>='Bfranklin@afs.net':@VM:'SMatthews@afs.net'  ; * RLM01
            MVIN<2>=''
            MVIN<4>='AFS'
            MVIN<5>=SUBJ
            MVIN<6>=TEXT
            CALL SENDMAIL(MVIN,MVOUT)

            CALL FB.PRINT.CENT(1,80,12,'Restricted Access Application...')
            CALL FB.PRINT.CENT(1,80,14,'Access Denied! See CHRIS for Access')
            CALL ELINE('')
            STOP
      END CASE

*
* Open files
*
      OPEN '','FB.TABLES' TO F.TABLES ELSE CALL OPEN.ABORT('FB.TABLES',PROG.NAME)
*
* Read Code Table from File
*
      READ DISP.REC FROM F.TABLES,'UPSBILL.CODES' ELSE DISP.REC=''
*
* First page of codes
*
      PAGE=1
      GOSUB 9000
*
* Enter selection code
*
100:  HELP="Enter line number. 'B'=Back. 'T'=Top, ''=Next page. 'E'=End-&-Save. 'EX'=Exit."
      CALL GEN.IN(0,22,"Enter line number please. ###",'',Q,0,3,'','',1,-3,22,0,QI,HELP,0,23)
      QI=OCONV(QI,'MCU')
      BEGIN CASE
         CASE QI=''
            PAGE=PAGE+1
            GOSUB 9000
         CASE QI='B'
            PAGE=PAGE-1
            IF PAGE=0 THEN PAGE=1
            GOSUB 9000
         CASE QI='T'
            PAGE=1
            GOSUB 9000
         CASE QI='EX'
            STOP
         CASE NUM(QI)
            SEL=QI
            IF SEL LT START ! SEL GT STOP THEN
               CALL ELINE('Invalid selection.')
               GOTO 100
            END
            GOSUB 8000
         CASE QI='E'
            X=DCOUNT(DISP.REC<1>,CHAR(253))
            FOR I=X TO 1 STEP -1
               IF DISP.REC<1,I>='' THEN
                  DISP.REC=DELETE(DISP.REC,1,I)
                  DISP.REC=DELETE(DISP.REC,2,I)
                  DISP.REC=DELETE(DISP.REC,3,I)
               END
            NEXT I
*
            VIN=''
            VIN<1>=1
            VIN<2>='L'
            AIN=DISP.REC
            CALL SORT.ARRAY(VIN,VOUT,AIN,AOUT)
            DISP.REC=AOUT
            WRITE DISP.REC ON F.TABLES,'UPSBILL.CODES'
            STOP
      END CASE
      GOTO 100
*
*------------------------------------------------------------------------
* INTERNAL SUBROUTINES
*------------------------------------------------------------------------
*
* Edit code
*
8000: ROW=3+REM(SEL,18)
      IF ROW=3 THEN ROW=21
      CODE=DISP.REC<1,SEL>
      DESC=DISP.REC<2,SEL>
      TYPE=DISP.REC<3,SEL>
8010: HELP="Enter new code. ''=Accept. 'D'=Delete. 'X'=Exit."
      CALL GEN.IN(5,ROW,CODE'L#4','',Q,0,4,'','',2,-4,ROW,0,QI,HELP,0,23)
      QI=OCONV(QI,'MCU')
      IF QI='X' THEN
         CRT @(5,ROW):CODE'L#4'
         RETURN
      END
      IF QI='' THEN
         Q=CODE
         QI=CODE
      END
      IF QI='D' THEN
         FOR X = 1 TO 3
            DISP.REC=DELETE(DISP.REC,X,SEL)
         NEXT X
         GOSUB 9000
         RETURN
      END
      CODE=QI
      CRT @(5,ROW):CODE'L#4'
*
8020: HELP="Enter code description. '<'=Back to code. 'X'=Exit."
      CALL GEN.IN(11,ROW,DESC'L#30','',Q,0,30,'','',2,-30,ROW,0,QI,HELP,0,23)
      QI=OCONV(QI,'MCU')
      BEGIN CASE
         CASE QI='X' ! QI='<'
            CRT @(11,ROW):DESC'L#30'
            GOTO 8010
         CASE QI=''
            Q=DESC
      END CASE
      DESC=Q
      CRT @(11,ROW):DESC'L#30'
8030: HELP="[I]nbound, [O]utbound, [T]hird party, [S]plit"
      CALL GEN.IN(43,ROW,TYPE'L#1','',Q,0,1,'','',2,-1,ROW,0,QI,HELP,0,23)
      QI=OCONV(QI,'MCU')
      BEGIN CASE
         CASE QI='X' ! QI='<'
            CRT @(43,ROW):TYPE'L#1'
            GOTO 8020
         CASE QI='' AND (TYPE='I' OR TYPE='O' OR TYPE='T' OR TYPE'S')
            Q=TYPE
         CASE QI='I' OR QI='O' OR QI='T' OR QI='S'
            NULL
         CASE 1
            CALL ELINE("Must be I, O, T, or S")
            GO 8030
      END CASE
      TYPE=OCONV(Q,'MCU')
      CRT @(43,ROW):TYPE'L#1'
      DISP.REC<1,SEL>=CODE
      DISP.REC<2,SEL>=DESC
      DISP.REC<3,SEL>=TYPE
      RETURN
*
* Display array
*
9000: CRT @(0,3):@(-3):"  #":@(5,3):"Code  Description                     Dir"
      START=(PAGE*18)-17
      IF START GT 108 THEN
         START=1
         PAGE=1
      END
      STOP=START+17
      FOR I=START TO STOP STEP 1
         ROW=3+REM(I,18)
         IF ROW=3 THEN ROW=21
         CODE=DISP.REC<1,I>
         DESC=DISP.REC<2,I>
         TYPE=DISP.REC<3,I>
         PRINT @(0,ROW):I"R#3":'  ':CODE"L#4":'  ':DESC"L#30":'  ':TYPE"L#1"
      NEXT I
      RETURN
