      SUBROUTINE FB.2.1.CSZ(VIN,VOUT,ROW,ZIP,HELP,ZIP.LEN,BACK.FLAG,CITY,STATE)
*******************************************************************************
* SUBROUTINE TO RETRIEVE AND MODIFY CITY AND STATE DESCRIPTIONS
*
* FREIGHT BILLING PACKAGE
*
* AUTHOR: BRIAN M. QUEEN
* DATE: January 14, 1993
*
* Enter zip
*
* 091702 - JMK01 - ADD CANADA TO ZIP FILE READ
* 123002 - JMK02 - DO NOT CONTINUE IF BAD ZIP
* 081909 - NPR01 - T090936 - Country Code prompting added
*******************************************************************************
      $OPTIONS EXEC.EQ.PERF
      $OPTIONS INFO.ABORT
      $OPTIONS HEADER.EJECT
      ERRMSG=''
      CC=VIN<1>
      COL=VIN<2>
      LAST.ZIP=VIN<3>
      LAST.CITY=VIN<4>
      LAST.STATE=VIN<5>
      COUNTRY.CODE = VIN<7>  ;* NPR01
      CLIENT.ID    = VIN<8>  ;* NPR01
      CARRIER.ID   = VIN<9>  ;* NPR01

      IF COL='' OR NOT(NUM(COL)) THEN COL=5
      IF ZIP.LEN='C' THEN
         CANADIAN=1
         MAX.LEN=6
      END ELSE
         CANADIAN=0
         MAX.LEN=ZIP.LEN
      END
100:  MASK=ZIP:STR('#',MAX.LEN-LEN(ZIP))
      IF ZIP='' THEN REQ=1 ELSE REQ=0
*     CALL GEN.IN(COL,ROW,MASK,'',Q,REQ,MAX.LEN,'R','0',2,COL,ROW,0,QI,HELP,0,23)
*     CC+=LEN(QI)+1
      CALL GEN.IN(COL,ROW,MASK,'',Q,REQ,MAX.LEN,'','0',2,COL,ROW,0,QI,HELP,0,23)
      CC+=LEN(QI)+1
      QI=OCONV(QI,'MCU')
      Q =OCONV(Q,'MCU')
      IF QI MATCHES '5N"-"' THEN
         QI=CHANGE(QI,'-','M')
         Q=QI
         CRT @(COL,ROW):QI'L#6'
      END
      BEGIN CASE
         CASE QI='X'
            CRT @(COL,ROW):ZIP'L#6'
            BACK.FLAG=1
            GO 900
         CASE QI='EX'
            STOP
         CASE QI='L' AND LAST.ZIP#''
            ZIP=LAST.ZIP
            CITY=LAST.CITY
            STATE=LAST.STATE
            GO 800
         CASE QI='S'
            HELP="Enter city and state for zip code search. Separate with comma(,)."
            CALL GEN.IN(COL,ROW,STR('-',30),'',Q,0,30,'','',1,-30,ROW,0,QI,HELP,0,23)
            CC+=LEN(QI)
            VIN=CC
            CALL ZIPS.DISPLAY.NEW(VIN,VOUT,Q,'ZIPS.CODES','ZIPS.CITY','1,2,3',ITEM.LIST)
            HELP='S'
            CC=VOUT<1>
            BEGIN CASE
               CASE DCOUNT(ITEM.LIST<1>,@VM) GT 1 OR ITEM.LIST=''
                  GOTO 100
               CASE ITEM.LIST NE ''
                  DATA ITEM.LIST<1,1>
                  GO 100
            END CASE
         CASE QI=''
            Q=ZIP
         CASE CANADIAN AND QI MATCHES '1A1N1A1N1A1N'
            STATE=''
            ZIP.VAR=QI
            CALL FB.READ.ZIP(ZIP.VAR,CITY,STATE)
            IF STATE='' THEN             ; * JMK02
               BACK.FLAG=1               ; * JMK02
               GOTO 100                  ; * JMK02
            END                          ; * JMK02
         CASE CANADIAN AND QI MATCHES '5N"M"'
            STATE=''
            ZIP.VAR=QI
            CALL FB.READ.ZIP(ZIP.VAR,CITY,STATE)
            IF STATE='' THEN             ; * JMK02
               BACK.FLAG=1               ; * JMK02
               GOTO 100                  ; * JMK02
            END                          ; * JMK02
         CASE CANADIAN AND QI MATCHES '1A1N1A'
            STATE=''
            ZIP.VAR=QI
            CALL FB.READ.ZIP(ZIP.VAR,CITY,STATE)
            IF STATE='' THEN             ; * JMK02
               BACK.FLAG=1               ; * JMK02
               GOTO 100                  ; * JMK02
            END                          ; * JMK02
         CASE CANADIAN AND LEN(QI) NE 5
            CALL ELINE('Invalid entry.')
            CC+=1
            GOTO 100
         CASE NOT(CANADIAN) AND LEN(QI) NE MAX.LEN
            CALL ELINE('Invalid entry.')
            CC+=1
            GOTO 100
         CASE NUM(QI)
            BACK.FLAG=0
            ZIP.VAR=QI
            CALL FB.READ.ZIP(ZIP.VAR,CITY,STATE)
            IF STATE='' THEN
               BACK.FLAG=1
               DEST.ZIP=QI
               GOTO 100
            END
         CASE 1
            GOTO 100
      END CASE
      ZIP=Q
      IF ZIP='99999' THEN BACK.FLAG=1    ; * Force Entry of "City & State"
      IF CITY NE '' THEN DESC=CITY:', ':STATE ELSE DESC=STATE
      IF CANADIAN THEN
         CRT @(COL,ROW):ZIP'L#6':' ':DESC'L#24'
      END ELSE
         CRT @(COL,ROW):ZIP'L#5':' ':DESC'L#24'
      END
*
200:  IF BACK.FLAG THEN
         BACK.FLAG=0
         BEGIN CASE
            CASE MAX.LEN=5 OR MAX.LEN=6
210:           HELP="Enter different city, state. ''=Accept. 'X'=Back. 'EX'=Stop."
               MASK=CITY:', ':STATE
               CALL GEN.IN(11,ROW,MASK'L#24','',Q,0,24,'','',2,-24,ROW,0,QI,HELP,0,23)
               CC+=LEN(QI)+1
               QI=OCONV(QI,'MCU')
               BEGIN CASE
                  CASE QI='X'
                     CRT @(11,ROW):SPACE(24)
                     GOTO 100
                  CASE QI='EX'
                     STOP
                  CASE QI=''
                     Q=MASK
               END CASE
               X=0
               X=INDEX(Q,',',1)
               IF X THEN
                  CITY=FIELD(Q,',',1)
                  CITY=OCONV(CITY,'MCT')
                  STATE=FIELD(Q,',',2)
                  STATE=OCONV(STATE,'MCU')
               END ELSE
                  CALL ELINE('Invalid entry.')
                  CC+=1
                  GOTO 210
               END
*
            CASE MAX.LEN=3
               HELP="Enter different state. ''=Accept. 'X'=Back. 'EX'=Stop."
               CALL GEN.IN(11,ROW,STATE'L#2','',Q,0,2,'','',2,-2,ROW,0,QI,HELP,0,23)
               CC+=LEN(QI)+1
               QI=OCONV(QI,'MCU')
               BEGIN CASE
                  CASE QI='X'
                     GOTO 100
                  CASE QI='EX'
                     STOP
                  CASE QI=''
                     Q=STATE
               END CASE
         END CASE
      END
800: ***

*NPR01  Start changes
*
      IF ZIP = "99999" THEN
         IF CARRIER.ID='00041' OR CARRIER.ID='12810' OR CARRIER.ID='03042' OR CARRIER.ID='01075' THEN
            NULL
         END ELSE
***            IF CLIENT.ID = "01078" THEN      ;* For APL West for now
            CVIN = STATE   ;  CVOUT = ''
            CALL GET.COUNTRY(CVIN,CVOUT)
            COUNTRY.CODE = CVOUT<1>
            STATE        = CVOUT<2>
            ERRX         = CVOUT<3>
            IF ERRX NE "" THEN
               CALL ELINE(ERRX)
               CC+=1
               GOTO 210
            END

            IF ERRX = "X" THEN
               BACK.FLAG=1
               GO 900
            END
***            END
         END
      END
*
*NPR01 End Changes

      IF CITY NE '' THEN DESC=CITY:', ':STATE ELSE DESC=STATE
      IF CANADIAN THEN
         CRT @(COL,ROW):ZIP'L#6':' ':DESC'L#24'
      END ELSE
         CRT @(COL,ROW):ZIP'L#5':' ':DESC'L#24'
      END
      CITY=TRIM(CITY)
      STATE=TRIM(STATE)
900: ***
      VOUT=''
      VOUT<1>=CC
      VOUT<2>=COUNTRY.CODE   ;* NPR01
      RETURN
