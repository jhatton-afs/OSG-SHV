****************************************************
 * Program name : FB.4.12.9
 * Author       : N.C.G. Pratt
 * Date written : May 30, 2008
 * Library      : FBBP
 * COPYRIGHT (C) 2016 - AFS LOGISTICS LLC - ALL RIGHTS RESERVED.
 *
 * KEYER  BY HOUR ANALYSIS SPREADSHEET
 *
 * Last updated by AFS (gbino) at 07:57:18 on 06/29/2016.
 *
 * Start modification history
* 022117 - GRB01 - SysAid 30601 - Add audit group  
 
 * End modification history
 *
 *******************************************************    
*
      $OPTIONS HEADER.EJECT
      ERRMSG=''
*
      $OPTIONS EXEC.EQ.PERF
      $OPTIONS INFO.ABORT
      CALL GET.USER.NEW(USER.NAME,CH.NUM)
   ***   CALL CHANNEL(CH.NUM)
      PROG.NAME='FB.4.12.9'
      PROG.DESC='KEYER  BY HOUR ANALYSIS SPREADSHEET(XLS)'
      PACK.NAME='FREIGHT BILLING'
      CO.NAME='AFS Logistics, LLC'
      UPL.VIN = 'FBBP' ;  UPL.VIN<2> = 'FB.4.12.7' ; UPL.VOUT = ''                    ;* NPR_UPL 04/23/2010
      CALL UPD.PROGRAM.LOG(UPL.VIN,UPL.VOUT)                                                           ;* NPR_UPL 04/23/2010
*
* Open files
*
      OPEN '','CLIENTS' TO F.CLIENT ELSE CALL OPEN.ABORT("CLIENTS",PROG.NAME)
      OPEN '','BCTRL' TO F.BCTRL ELSE CALL OPEN.ABORT("BCTRL",PROG.NAME)
      OPEN '','LOCATIONS' TO F.LOCATIONS ELSE CALL OPEN.ABORT("LOCATIONS",PROG.NAME)
      OPEN '','TC.WEEKLY' TO F.TC.WEEKLY ELSE CALL OPEN.ABORT("TC.WEEKLY",PROG.NAME)
*
      OPEN '','EMP.HR.PERF' TO F.EMP.HR.PER ELSE CALL OPEN.ABORT("EMP.HR.PERF",PROG.NAME)
*
      OPEN '','VOC' TO F.VOC ELSE
         CALL ELINE('UNABLE TO OPEN VOC FILE')
         STOP
      END
*
      READ BILLDATA.REC FROM F.VOC,"BILLDATA" ELSE
         CALL ELINE('UNABLE TO OPEN "VOC BILLDATA"')
         STOP
      END
*
      BILLDATA.REC<2>:='\':USER.NAME
      WRITE BILLDATA.REC ON F.VOC,"BILLDATA.":USER.NAME ELSE
         CALL ELINE("YOUR USER NAME '":USER.NAME:"' IS NOT IN THE BILLDATA FOLDER - PLEASE CALL THE HELPDESK")
         STOP
      END
      OPEN '','BILLDATA.':USER.NAME TO F.BILLDATA ELSE
         CALL ELINE('UNABLE TO OPEN "BILLDATA.":USER')
         STOP
      END

      OPEN '','FBWORK':CH.NUM TO F.WORK ELSE
         EXECUTE 'CREATE-FILE FBWORK':CH.NUM:' 1 1001' PASSLIST CAPTURING OUTPUT
         OPEN '','FBWORK':CH.NUM TO F.WORK ELSE
            CALL OPEN.ABORT('FBWORK':CH.NUM,PROG.NAME)
         END
      END
      OPEN 'DICT','FBWORK':CH.NUM TO F.DICT.WORK ELSE
         CALL OPEN.ABORT('DICT FBWORK':CH.NUM,PROG.NAME)
      END
*
***********************************************************************
* Clear work file
*
      CLEARFILE F.WORK
*
      COL.HEADERS = "ABCDEFGHIJKLMNOPQRSTUVWXYZ"  ;* EXCEL Column headers
*
      HLINE1 = "Freight Bills Keyed "       ;* 05-17-2008 - 05-23-2008
*
      HLINE2    = ""
      COL.NO = 1
      HLINE2<1,COL.NO> = "Name"                ; COL.NO += 1
      HLINE2<1,COL.NO> = "Date"                ; COL.NO += 1
      HLINE2<1,COL.NO> = "Hour"                ; COL.NO += 1
      LOC.COL = COL.NO
      HLINE2<1,COL.NO> = "Loc."                ; COL.NO += 1
      HLINE2<1,COL.NO> = "Dept."               ; COL.NO += 1

      HLINE2<1,COL.NO> = "Chars. Keyed"        ; COL.NO += 1
      HLINE2<1,COL.NO> = "Chars. Verified"     ; COL.NO += 1
      TOT.CHARS.COL = COL.NO
      HLINE2<1,COL.NO> = "Total Chars"         ; COL.NO += 1

      BILLS.KEYED.COL = COL.NO
      HLINE2<1,COL.NO> = "Bills Keyed"         ; COL.NO += 1
      HLINE2<1,COL.NO> = "Bills Verified"      ; COL.NO += 1
      TOT.BILLS.COL = COL.NO
      HLINE2<1,COL.NO> = "Total Bills"         ; COL.NO += 1
      HLINE2<1,COL.NO> = "First Bill"         ; COL.NO += 1
      HLINE2<1,COL.NO> = "Last Bill"         ; COL.NO += 1


      TODAY = DATE()
      SDATE = TODAY
      EDATE = TODAY + 6
*
      THIS.FRIDAY = ""
      DAY.STR = "MTWTFSS"
*
      FOR DX = SDATE TO EDATE
         W.DAY        = OCONV(DX,"DD")
         W.DAY.ALPHA  = OCONV(DX,"DWA")
         W.DOW        = OCONV(DX,"DW")
         W.MTH        = OCONV(DX,"DM")
         W.MTH.ALPHA  = OCONV(DX,"DMA")
         W.YEAR       = OCONV(DX,"DY")
*
         IF W.DOW = "5" THEN THIS.FRIDAY = DX

      NEXT DX
*
      LAST.SATURDAY = THIS.FRIDAY - 6
*
      CALL SCR.REFRESH(PROG.NAME,PROG.DESC,PACK.NAME,CO.NAME,TIME.DATE,1)

********************************************************************
ENTER.BEGIN.DATE: ***
*********************************************************************
      CRT @(0,3):" Last Saturday  ":OCONV(LAST.SATURDAY,"D2/")
      CRT @(0,5):" This Friday    ":OCONV(THIS.FRIDAY,"D2/")
*
      BEG.DATE = LAST.SATURDAY
      HELP="Enter from date. nn=Day. nn/nn=Month & Day. [T]oday, [Y]esterday EX=Exit."
*
      LN='Enter the Beginning date  : ':BEG.DATE'D2/'
      CALL GEN.IN(0,7,LN,'DATE',Q,0,8,'','',0,-8,7,0,QI,HELP,0,23)
      QI=OCONV(QI,'MCU')
      BEGIN CASE
         CASE QI="T"
            Q=TODAY
         CASE QI = "Y"
            Q=TODAY-1
         CASE QI='EX'
            STOP
      END CASE
      BEG.DATE=Q
      CRT @(28,7):BEG.DATE'D2/'

*********************************************************************
ENTER.END.DATE: ***
*********************************************************************
      IF BEG.DATE = LAST.SATURDAY THEN
         END.DATE = THIS.FRIDAY
      END ELSE
         END.DATE = BEG.DATE + 6
      END
*
      HELP="Enter End date. nn=Day. nn/nn=Month & Day. [T]oday, [Y]esterday X=Back EX=Exit."
*
      LN='Enter the Ending Date     : ':END.DATE'D2/'
      CALL GEN.IN(0,9,LN,'DATE',Q,0,9,'','',0,-8,9,0,QI,HELP,0,23)
      QI=OCONV(QI,'MCU')
      BEGIN CASE
         CASE QI="T"
            Q=TODAY
         CASE QI = "Y"
            Q=TODAY-1
         CASE QI='X'
            CRT @(0,9):@(-3):
            GOTO ENTER.BEGIN.DATE
         CASE QI='EX'
            STOP
      END CASE
      END.DATE=Q

      IF BEG.DATE GT END.DATE THEN
         CALL ELINE("End Date may not be before the Start Date - RETURN to re-enter")
         CRT @(28,9):SPACE(10)
         GOTO ENTER.BEGIN.DATE
      END

      IF END.DATE GT TODAY THEN
         CALL ELINE("Future Date Invalid - RETURN to re-enter")
         CRT @(28,9):SPACE(10)
         GOTO ENTER.END.DATE
      END

      DAYS=END.DATE-BEG.DATE+1
      CRT @(28,9):END.DATE'D2/'
*
      STMT = 'SSELECT EMP.HR.PERF WITH DATE GE "':OCONV(BEG.DATE,'D2/'):'"'
      STMT := ' AND WITH DATE LE "':OCONV(END.DATE,'D2/'):'" BY USER BY DATE BY HOUR'
*
      CRT @(10,12) : " Now selecting Records ":@(-4)
      EXECUTE STMT RTNLIST EMP.LIST CAPTURING OUTPUT
*
      EMP.ITEMS = @SELECTED
      IF EMP.ITEMS LE 0 THEN
         CALL ELINE('ERROR - No RECORDS on file.')
         STOP
      END
*
      CRT @(10,14) : " Now processing selected Records ":@(-4)
*
      COUNTER.1=0
      PER.1=0
      HLINE1 := " ":BEG.DATE 'D2/':" - ":END.DATE 'D2/'
      XLS.ARRAY = HLINE1
      XLS.ARRAY<2> = HLINE2
      XLS.ARRAY<3> = ''
*
      LOOP
         READNEXT ID.EMP.HR.PERF FROM EMP.LIST ELSE ID.EMP.HR.PERF='DONE'
      UNTIL ID.EMP.HR.PERF='DONE' DO
         COUNTER.1=COUNTER.1+1
         READ HR.PERF.REC FROM F.EMP.HR.PER, ID.EMP.HR.PERF THEN
            HR.USER = FIELD(ID.EMP.HR.PERF,"*",1)
            IF HR.USER = "EDI" THEN CONTINUE  ;* SKIP
            W.PREFIX = FIELD(HR.USER,":",1)
            IF W.PREFIX = "EDI" THEN EDI.FLAG = "1" ELSE EDI.FLAG = "0"
            STP = LEN(HR.USER)-4
            W.SUFFIX = HR.USER[STP,5]
            IF W.SUFFIX = "AUDIT" THEN  ;* Currently Myra or Mary have logons ending in AUDIT
               HR.USER = HR.USER[1,STP-1]
            END


            DLINE=''
            DLINE<1,1>=HR.USER
            HR.DATE = FIELD(ID.EMP.HR.PERF,"*",2)
            HR.HOUR = FIELD(ID.EMP.HR.PERF,"*",3)
            HR.BILLS.ENTERED  = HR.PERF.REC<1>
            HR.BILLS.VERIFIED = HR.PERF.REC<2>
            HR.BILLS.TOT      = HR.BILLS.ENTERED + HR.BILLS.VERIFIED
            HR.CHARS.ENTERED  = HR.PERF.REC<8>
            HR.CHARS.VERIFIED = HR.PERF.REC<9>
            HR.CHARS.TOT      = HR.CHARS.ENTERED + HR.CHARS.VERIFIED
            IF EDI.FLAG THEN HR.USER = FIELD(HR.USER,":",2)
*
            READ CTRL.REC FROM F.BCTRL, HR.USER ELSE CTRL.REC = ""
            DEPT   = CTRL.REC<15>
*
            IF DEPT = "FP" OR DEPT = "LG" OR DEPT = "AU" THEN  ; * GRB01
               NULL
            END ELSE
               CONTINUE
            END
*
            HR.LOC = CTRL.REC<23>
*
            USER.NAME = CTRL.REC<13>
*
            IF HR.LOC = "" THEN HR.LOC = "99999"  ;* Dummy number
            IF EDI.FLAG THEN
               BILL.TYPE = "EDI"
            END ELSE
               BILL.TYPE = "PAPER"
            END
            DLINE<1,2>=OCONV(HR.DATE,'D2/')
            DLINE<1,3>=OCONV(HR.HOUR,'MTH')
            DLINE<1,4>=HR.LOC
            DLINE<1,5>=DEPT
            DLINE<1,6>=HR.CHARS.ENTERED
            DLINE<1,7>=HR.CHARS.VERIFIED
            DLINE<1,8>=HR.CHARS.TOT
            DLINE<1,9>=HR.BILLS.ENTERED
            DLINE<1,10>=HR.BILLS.VERIFIED
            DLINE<1,11>=HR.BILLS.TOT
            DLINE<1,12>=OCONV(HR.PERF.REC<10>,'MTH')
            DLINE<1,13>=OCONV(HR.PERF.REC<11>,'MTH')
            XLS.ARRAY<-1>=DLINE
         END
         CALL PROG.BAR(20,COUNTER.1,EMP.ITEMS,PER.1)
      REPEAT
*
      CONVERT @VM TO CHAR(9) IN XLS.ARRAY
*
      W.BEG.DATE = OCONV(BEG.DATE,"D2/")
      CONVERT "/" TO "" IN W.BEG.DATE
      W.END.DATE = OCONV(END.DATE,"D2/")
      CONVERT "/" TO "" IN W.END.DATE
      ID.XLS = "FB_HR_KEYED_":W.BEG.DATE:"_":W.END.DATE:".XLS"
      WRITE XLS.ARRAY ON F.BILLDATA, ID.XLS
*
*
      CALL ELINE("File ":ID.XLS:" may be found in your own U: drive directory")
*
      STOP
