************************************************************************
* PROGRAM: FB.BUILD.TIMELINE.SUMMARY - Program to rebuild
*     FB.TIMELINE.SUMMARY for ALL Clients. Since there are 2 of the
*     Summary record ids are for ALL Clients, rebuilding
*     the file for specific Clients is not practical. Likewise, using
*     the Processed Cross Reference isn't practical because there is
*     data for Freight Bills that have been entered, but not processed.
*
* PROGRAMMER: Steve Barbee
*
************************************************************************
*
* PARAMETERS:
*
* None
*
*
************************************************************************
*
* EDIT HISTORY:
*
*    NAME :   DATE:     : DESCRIPTION
*     SWB : 04/12/12    : Started new program
*
************************************************************************
      PROG.NAME = 'FB.BUILD.TIMELINE.SUMMARY'
      CALL CHANNEL(CH.NUM)
      OPEN 'CLIENTS' TO F.CLIENTS ELSE
         CALL OPEN.ABORT('CLIENTS', PROG.NAME)
      END

      OPEN 'FB.TIMELINE.SUMMARY' TO F.TIMELINE.SUMMARY ELSE
         CALL OPEN.ABORT('TIMELINE.SUMMARY', PROG.NAME)
      END

      *
* SELECT CLIENT FILE
*
      TEXT='Selecting Clients.'
      CALL CENTER(TEXT,80)
      CRT @(0,10):TEXT
*
      STMT="SELECT VOC 'FB.BILLS.HIST' *A7"
      PRINT STMT
      ******************************************************************
      * Select of First List
      ******************************************************************
      EXECUTE STMT RTNLIST FIRST.LIST CAPTURING OUTPUT
      FIRST.ITEMS = @SELECTED
      IF @SELECTED < 1 THEN              ; * JCW01
         CALL ELINE('ERROR - No clients on file.')
         STOP
      END
      COUNTER.1=0
      PER=0
      EXECUTE 'CLEAR.FILE DATA FB.TIMELINE.SUMMARY'
      LOOP
         READNEXT FIRST.LIST.ID FROM FIRST.LIST ELSE FIRST.LIST.ID='DONE'
      UNTIL FIRST.LIST.ID='DONE' DO
         COUNTER.1=COUNTER.1+1
         READV CLIENT.NAME FROM F.CLIENTS,FIRST.LIST.ID,2 ELSE
            CLIENT.NAME='*** Not On File ***'
         END
         CRT @(0,17):FIRST.LIST.ID:" ":CLIENT.NAME:" - Now selecting freight bills":@(-4):
         CALL PROG.BAR(20,COUNTER.1,FIRST.ITEMS,PER.1)
         OPEN '','FB.BILLS.HIST,':FIRST.LIST.ID TO F.BILLS.HIST THEN
            STMT='SELECT FB.BILLS.HIST,':FIRST.LIST.ID
            STMT:=' WITH VERSION = "0"'
            STMT := ' AND WITH ORIG.ENTERED.DATE # "" '
            EXECUTE STMT RTNLIST SECOND.LIST CAPTURING OUTPUT
            SECOND.ITEMS = @SELECTED
            CRT @(0,17):FIRST.LIST.ID:" ":CLIENT.NAME:" - Now processing ":SECOND.ITEMS:" Freight Bills":@(-4):
            COUNTER.2=0
            LOOP
               DONE=0
               READNEXT PRO.ID FROM SECOND.LIST ELSE DONE=1
            UNTIL DONE=1 DO
               COUNTER.2=COUNTER.2+1
               PRINT @(0,23):COUNTER.2'L%6':PRO.ID:
               IF SECOND.ITEMS>0 THEN CALL PROG.BAR(22,COUNTER.2,SECOND.ITEMS,PER.2)
               READ DBILL FROM F.BILLS.HIST,PRO.ID THEN
                  MR.DATE = DBILL<246>
                  IF MR.DATE # '' THEN
                     TIMELINE.TRANS.TYPE = 'RFN'
                     ORIG.ENTERED.DATE = DBILL<247>
                     MR.TO.ENTERED.DAYS = DBILL<248>
                     PROCESSED.DATE = DBILL<37>
                     IF PROCESSED.DATE # '' THEN
                        MR.TO.PROCESSED.DAYS = DBILL<249>
                        TIMELINE.TRANS.TYPE = 'RPN'
                     END ELSE
                        MR.TO.PROCESSED.DAYS = ''
                     END
                     GOSUB UPDATE.TIMELINE.SUMMARY
                  END
               END
SKIP.BILL:***
            REPEAT
         END
SKIP.FIRST:***
      REPEAT
      STOP

************************
UPDATE.TIMELINE.SUMMARY:
************************

      TR.VIN = DBILL<53>
      TR.VIN<2> = FIRST.LIST.ID
      TR.VIN<3> = FIELD(PRO.ID, '*', 1)
      TR.VIN<4> = MR.DATE
      TR.VIN<5> = ORIG.ENTERED.DATE
      TR.VIN<6> = MR.TO.ENTERED.DAYS
      TR.VIN<7> = PROCESSED.DATE
      TR.VIN<8> = MR.TO.PROCESSED.DAYS
      TR.VIN<9> = PRO.ID
      TR.VIN<10> = TIMELINE.TRANS.TYPE

      CALL FB.UPD.TIMELINE.SUMMARY(TR.VIN, TR.VOUT, F.TIMELINE.SUMMARY)

      RETURN

9999:* EXIT PROGRAM
      STOP

   END
