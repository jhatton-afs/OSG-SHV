SUBROUTINE BUILD.DF.DOUGLAS.BATTERY.SUB(F.CLIENTS,F.CARRIERS,F.FILE,VIN,VOUT)
***********************************************************************
 UPL.VIN = 'FBBP' ;  UPL.VIN<2> = 'BUILD.DF.DOUGLAS.BATTERY.SUB' ; UPL.VOUT = ''                    ;* NPR_UPL 04/22/2010
 CALL UPD.PROGRAM.LOG(UPL.VIN,UPL.VOUT)                                                           ;* NPR_UPL 04/22/2010
* Build a spread sheet for Douglas Battery reports
* Copyright (c) AFS Logistics, LLC., 2001
*
* AUTHOR : David R Friend
* Date   : 8-28-02
***********************************************************************
*
* VIN<1>=CLIENT.ID
* VIN<2>=BEG.DATE
* VIN<3>=END.DATE
* VIN<4>=FROM.FB.3.1.NEW
*
*

$INCLUDE PROG.ADMIN INC_OPTIONS

*
***   Initialize general variables
*
      CLIENT.ID=VIN<1>
      BEG.DATE=VIN<2>
      END.DATE=VIN<3>
      WHERE.FROM=VIN<4>
      TAB=CHAR(9)



      CLIENT=CLIENT.ID
SELECT.BILLS:
      READV CLIENT.NAME FROM F.CLIENTS,CLIENT,2 ELSE
         CLIENT.NAME='*** Not On File ***'
      END
      CRT @(0,15):CLIENT:' ':CLIENT.NAME:'  - Selecting Freight Bills':@(-4):
      OPEN '','FB.CLIENTS.CUSTS,':CLIENT.ID TO F.CLIENTS.CUST ELSE
        F.CLIENTS.CUST=''
      END
      OPEN '','FB.BILLS.HIST,':CLIENT TO F.BILLS THEN
         STMT='SELECT FB.BILLS.HIST,':CLIENT
         STMT:=' WITH VERSION = "0"'                        ;* Exclude duplicates
         STMT:=' AND WITH 37 GE "':OCONV(BEG.DATE,'D2/'):'"';* Within the dates
         STMT:=' AND WITH 37 LE "':OCONV(END.DATE,'D2/'):'"';* Within the dates
         STMT:=' AND WITH 39 # "R"'                         ;* Exclude rejects
         STMT:=' AND WITH 136'
         EXECUTE STMT RTNLIST BILLS.LIST CAPTURING OUTPUT
         NUM.BILLS=@SELECTED
         IF NUM.BILLS=0 THEN
*DF             CALL ELINE('No bills on file with these criteria.')
             REPORT=''
             REPORT<4>='No Data present'
             GOSUB BUILD.FILE
             GOTO END.PROGRAM
         END
         CRT @(0,18):CLIENT:' ':CLIENT.NAME:' - Processing ':NUM.BILLS:' Freight Bills':@(-4):
         COUNTER.2=0; PER.2=0
         REPORT=''
         ATT.NUM=4
         LOOP
            READNEXT PRO.ID FROM BILLS.LIST ELSE PRO.ID='DATA.ENTRY.IS.FINISHED'
        UNTIL PRO.ID='DATA.ENTRY.IS.FINISHED' DO
            COUNTER.2=COUNTER.2+1
            CALL PROG.BAR(20,COUNTER.2,NUM.BILLS,PER.2)
            READ BILL.REC FROM F.BILLS,PRO.ID THEN
               CARRIER.ID=FIELD(PRO.ID,'*',1)
               READV CARRIER.NAME FROM F.CARRIERS,CARRIER.ID,1 ELSE
                 CARRIER.NAME='Unknown Carrier'
               END
               CARRIER.NAME=' ':CARRIER.NAME
               PRO.ID=FIELD(PRO.ID,'*',2)
               TIMES = DCOUNT(BILL.REC<136>,@VM)
               FOR I = 1 TO TIMES
                 REPORT<ATT.NUM,1>=CARRIER.ID
                 REPORT<ATT.NUM,2>=CARRIER.NAME
                 REPORT<ATT.NUM,3>=PRO.ID
                 REPORT<ATT.NUM,4>=BILL.REC<27>
                 REPORT<ATT.NUM,5>=BILL.REC<25>
                 REPORT<ATT.NUM,6>=BILL.REC<3>
                 REPORT<ATT.NUM,7>=BILL.REC<136,I>
                 READV CLIENT.CUST.NAME FROM F.CLIENTS.CUST,BILL.REC<136,I>,1 ELSE CLIENT.CUST.NAME='Not On File'
                 REPORT<ATT.NUM,8>=CLIENT.CUST.NAME
                 REPORT<ATT.NUM,9>=BILL.REC<137,I>
                 REPORT<ATT.NUM,10>=BILL.REC<138,I>
                 REPORT<ATT.NUM,11>=BILL.REC<139,I>
                 REPORT<ATT.NUM,12>=BILL.REC<140,I>
                 REPORT<ATT.NUM,13>=BILL.REC<141,I>'MR26'
                 ATT.NUM=ATT.NUM+1
               NEXT I
            END; * READ BILL.REC FROM F.BILLS,PRO.ID
SKIP.BILL:***
         REPEAT
      END ELSE
         CRT @(0,23):'Could not open FB.BILLS.HIST,':CLIENT:'. Skipping this file.':
      END; * OPEN '','FB.BILLS.HIST,':CLIENT TO F.BILLS

*
***   Read work file and print data to an output file
*
      GOSUB BUILD.FILE
      GOTO END.PROGRAM
BUILD.FILE:***
      TEXT='Client Customer Report for Client: ':CLIENT.ID :' ':CLIENT.NAME
      TEXT=TEXT:' from ':BEG.DATE'D4/':' to ':END.DATE'D4/'
      REPORT<1,1>=TEXT
      REPORT<2, 1>=' '
      REPORT<3, 1>='Carrier #'
      REPORT<3, 2>='Carrier Name'
      REPORT<3, 3>='Pro'
      REPORT<3, 4>='Orig City'
      REPORT<3, 5>='Orig State'
      REPORT<3, 6>='Orig Zip'
      REPORT<3, 7>='Client Cust #'
      REPORT<3, 8>='Client Cust Name'
      REPORT<3, 9>='Dest City'
      REPORT<3,10>='Dest State'
      REPORT<3,11>='Dest Zip'
      REPORT<3,12>='Weight'
      REPORT<3,13>='Charges'
      REPORT=CHANGE(REPORT,@VM,TAB)
      FILE.ID=CLIENT.ID:'-'
      D=BEG.DATE'D2/'
      FILE.ID:=D[7,2]:D[1,2]:D[4,2]:'-'
      D=END.DATE'D2/'
      FILE.ID:=D[7,2]:D[1,2]:D[4,2]:'-'
      FILE.ID:='CC.XLS'
      WRITE REPORT ON F.FILE,FILE.ID ELSE
         CALL ELINE(FILE.ID:' could not be written.')
      END
      VOUT=''
      VOUT<1>=FILE.ID
      RETURN

END.PROGRAM:***
*     End of program
      RETURN
