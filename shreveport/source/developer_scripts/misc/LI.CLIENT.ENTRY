$OPTIONS HEADER.EJECT
$OPTIONS EXEC.EQ.PERF
$OPTIONS INFO.ABORT
ERRMSG=''
* PROGRAM LI.CLIENT.ENTRY
* PROGRAM TO CREATE OR MODIFY CLIENTS FOR LOGISTICS INVOICING
*
* FREIGHT BILLING PACKAGE
*
* AUTHOR: VALERIE DOTY
* DATE:   JANUARY 21, 1994
*
***********************************************************************
*
* Initialize variables
*
      PROMPT''
      ADD.FLAG = 0
      PROG.NAME = 'LI.CLIENT.ENTRY'
      PROG.DESC = 'CLIENT MAINTENANCE'
      PACK.NAME = 'LOGISTICS INVOICING'
*
       UPL.VIN = 'FBBP' ;  UPL.VIN<2> = 'LI.CLIENT.ENTRY' ; UPL.VOUT = ''                    ;* NPR_UPL 04/23/2010
       CALL UPD.PROGRAM.LOG(UPL.VIN,UPL.VOUT)                                                           ;* NPR_UPL 04/23/2010
* Open files
*
      OPEN '', 'LI.CLIENTS' TO LI.CLIENTS ELSE
        CALL OPEN.ABORT("LI.CLIENTS", PROG.NAME)
      END
      OPEN '', 'ZIPS.CODES' TO ZIPS ELSE CALL OPEN.ABORT("ZIPS", PROG.NAME)
*
* Display page heading
*
      CALL AFS.SCR.HEAD(CO.ID, FILE.ID, PERIOD, PROG.NAME, PROG.DESC, PACK.NAME, CO.NAME, TIME.DATE, 1)
*
***********************************************************************
* Enter client number,  if not on file then create if desired
***********************************************************************
*
100   CRT @(0, 2):@(-3)
      ADD.FLAG = 0
      HELP = "Enter client number. [N] = New. [cr] = Exit."
      CALL GEN.IN(0, 2, 'Enter Client Number #####', '', Q, 0, 20, '', '', 0, -5, 2, 0, QI, HELP, 0, 23)
      QI = OCONV(QI, 'MCU')
      BEGIN CASE
        CASE QI = ''
          STOP
        CASE QI = 'N'
          ID = 100
          LOOP
            ID = ID+1
            READ XX FROM LI.CLIENTS, ID ELSE XX = '**'
          UNTIL XX = '**' DO REPEAT
          Q = ID
        CASE NUM(QI)
          Q = QI
        CASE 1
          GOTO 100
      END CASE
*
* Read client item
*
      CLIENT.ID = Q
      READU CLIENT.REC FROM LI.CLIENTS, CLIENT.ID ELSE
        HELP = "Enter either Y for Yes or N for No."
        LN = 'Client number ':Q:' not on file. Do you want to create.? N'
        CALL GEN.IN(0,22,LN,'',ACK,0,1,'','',1,-1,22,1,QI,HELP,0,23)
        QI = OCONV(QI, 'MCU')
        IF QI = 'Y' THEN
          CALL AFS.SCR.REFRESH(PROG.NAME, PROG.DESC, PACK.NAME, CO.NAME, TIME.DATE, 1)
        END ELSE
          RELEASE
          GOTO 100
        END
        CLIENT.REC = ''
        ADD.FLAG = 1
      END
      OLD.CLIENT.REC = CLIENT.REC
*
* Display client data
*
      TEXT = 'Client number ':CLIENT.ID
      CALL CENTER(TEXT, 80)
      CRT @(0,2) : TEXT
      CRT @(0,3) : @(-4):
      CRT @(0,4) : ' 1.  Client Name   : ' : CLIENT.REC<1>
      CRT @(0,5) : ' 2.  Address Line 1: ' : CLIENT.REC<2>
      CRT @(0,6) : ' 3.  Address Line 2: ' : CLIENT.REC<3>
      CRT @(0,7) : ' 4.  City          : ' : CLIENT.REC<4>
      CRT @(0,8) : ' 5.  State         : ' : CLIENT.REC<5>
      CRT @(0,9) : ' 6.  Zip           : ' : CLIENT.REC<6>
      IF ADD.FLAG THEN
        GOSUB 1000
        GOSUB 2000
        GOSUB 3000
        GOSUB 4000
        GOSUB 5000
        GOSUB 6000
      END
*
* Get selection from operator
*
200   HELP = "Enter number of information to change. (1 to 6),  [D] = Delete"
      CALL GEN.IN(0,22,'Enter Selection Number ##','',Q,0,2,'','',1,-2,22,1,QI,HELP,0,23)
      QI = OCONV(QI, 'MCU')
      IF QI = '' THEN GOTO 99000
      IF QI = 'D' THEN
        HELP = 'Enter Y to delete or N not to delete.'
        CALL GEN.IN(0,22,'Are you sure? N','',Q,0,1,'','',1,-1,22,1,QI,HELP,0,23)
        QI = OCONV(QI, 'MCU')
*
* Delete client
*
        IF QI = 'Y' THEN
          EXECUTE 'SELECT LI.INVENTORY WITH CLIENT = "' : CLIENT.ID : '"' PASSLIST CAPTURING OUTPUT
          IF SYSTEM(11) THEN
            EXECUTE 'DELETE LI.INVENTORY'
          END
          DELETE LI.CLIENTS, CLIENT.ID
          CALL AFS.SCR.HEAD(CO.ID, FILE.ID, PERIOD, PROG.NAME, PROG.DESC, PACK.NAME, CO.NAME, TIME.DATE, 1)
          GOTO 100
        END ELSE
          GOTO 200
        END
      END
      IF NUM(QI) THEN ON QI GOSUB 1000, 2000, 3000, 6000, 5000, 4000
      GOTO 200
*
***********************************************************************
* Change client name
***********************************************************************
*
1000  HELP = "Enter name. [space] = Clear."
      MASK = CLIENT.REC<1>:STR('-', 30-LEN(CLIENT.REC<1>))
      CALL GEN.IN(21,4,MASK,'',Q,0,30,'','',2,-30,4,0,QI,HELP,0,23)
      IF Q#'' THEN CLIENT.REC<1> = Q
      IF Q = ' ' THEN CLIENT.REC<1> = ''
      CRT @(21, 4):CLIENT.REC<1>'L#30'
*
      RETURN
*
***********************************************************************
* Change client address 1
***********************************************************************
*
2000  HELP = "Enter addr 1. [space] = Clear."
      MASK = CLIENT.REC<2>:STR('-', 30-LEN(CLIENT.REC<2>))
      CALL GEN.IN(21,5,MASK,'',Q,0,30,'','',2,-30,5,0,QI,HELP,0,23)
      IF Q#'' THEN CLIENT.REC<2> = Q
      IF Q = ' ' THEN CLIENT.REC<2> = ''
      CRT @(21, 5):CLIENT.REC<2>'L#30'
*
      RETURN
*
***********************************************************************
* Change client address 2
***********************************************************************
*
3000  HELP = "Enter addr 2. [space] = Clear."
      MASK = CLIENT.REC<3>:STR('-', 30-LEN(CLIENT.REC<3>))
      CALL GEN.IN(21,6,MASK,'',Q,0,30,'','',2,-30,6,0,QI,HELP,0,23)
      IF Q#'' THEN CLIENT.REC<3> = Q
      IF Q = ' ' THEN CLIENT.REC<3> = ''
      CRT @(21, 6):CLIENT.REC<3>'L#30'
*
      RETURN
*
***********************************************************************
* Change client zip code
***********************************************************************
*
4000  HELP = "Enter zip code. [space] = Clear."
      IF CLIENT.REC<6> NE '' THEN
        MASK = CLIENT.REC<6>:STR('#', 10-LEN(CLIENT.REC<6>))
      END ELSE
        MASK = '#####-####'
      END
      CALL GEN.IN(21,9,MASK,'',Q,0,10,'','',1,-10,9,0,QI,HELP,0,23)
      IF QI = ' ' THEN Q = ''
      CLIENT.REC<6> = Q
      CODE = FIELD(Q, '-', 1)
      IF CODE#'' & NUM(CODE) THEN
        READ ZIP.REC FROM ZIPS, CODE THEN
          CLIENT.REC<4> = ZIP.REC<1>
          CLIENT.REC<5> = ZIP.REC<2>
        END
      END
      CRT @(21, 9):CLIENT.REC<6>'L#10'
      CRT @(21, 8):CLIENT.REC<5>'L#2'
      CRT @(21, 7):CLIENT.REC<4>'L#30'
*
      RETURN
*
***********************************************************************
* Change client state
***********************************************************************
*
5000  HELP = "Enter state code. [space] = Clear."
      MASK = CLIENT.REC<5>:STR('-', 2-LEN(CLIENT.REC<5>))
      CALL GEN.IN(21,8,MASK,'',Q,0,2,'','',1,-2,8,0,QI,HELP,0,23)
      IF Q#'' THEN CLIENT.REC<5> = Q
      IF Q = ' ' THEN CLIENT.REC<5> = ''
      CRT @(21, 8):CLIENT.REC<5>'L#2'
*
      RETURN
*
***********************************************************************
* Change client city
***********************************************************************
*
6000  HELP = "Enter city. [space] = Clear."
      MASK = CLIENT.REC<4>:STR('-', 30-LEN(CLIENT.REC<4>))
      CALL GEN.IN(21,7,MASK,'',Q,0,30,'','',2,-30,7,0,QI,HELP,0,23)
      IF Q#'' THEN CLIENT.REC<4> = Q
      IF Q = ' ' THEN CLIENT.REC<4> = ''
      CRT @(21, 7):CLIENT.REC<4>'L#30'
*
      RETURN
*
***********************************************************************
* Change client phone number
***********************************************************************
*
10000 HELP = "Enter client's phone number. [space] = Clear."
      CALL GEN.IN(13,11,CLIENT.REC<31>'L#13','PHONE',Q,0,13,'','',0,13,11,0,QI,HELP,0,23)
      BEGIN CASE
        CASE QI = ' '
          CLIENT.REC<31> = ''
        CASE QI NE ''
          CLIENT.REC<31> = Q
        CASE QI = ''
          NULL
        CASE 1
          GOTO 10000
      END CASE
      CRT @(13, 11):'(':CLIENT.REC<31>[1, 3]:')':CLIENT.REC<31>[4, 3]:'-':CLIENT.REC<31>[7, 4]
*
      RETURN
*
***********************************************************************
* Change client fax number
***********************************************************************
*
11000 HELP = "Enter client's fax number. [space] = Clear."
      CALL GEN.IN(13, 12, CLIENT.REC<45>'L#13', 'PHONE', Q, 0, 13, '', '', 0, 13, 12, 0, QI, HELP, 0, 23)
      BEGIN CASE
        CASE QI = ' '
          CLIENT.REC<45> = ''
        CASE QI NE ''
          CLIENT.REC<45> = Q
        CASE QI = ''
          NULL
        CASE 1
          GOTO 11000
      END CASE
      CRT @(13, 12):'(':CLIENT.REC<45>[1, 3]:')':CLIENT.REC<45>[4, 3]:'-':CLIENT.REC<45>[7, 4]
*
      RETURN
*
***********************************************************************
* Check for changes and update file
***********************************************************************
*
99000 IF OLD.CLIENT.REC # CLIENT.REC THEN
        HELP = "Enter either Y or N."
        LN = 'Client information changed. Update? -'
        CALL GEN.IN(0,22,LN,'',Q,1,1,'','',1,-1,22,1,QI,HELP,0,23)
        QI = OCONV(QI, 'MCU')
        BEGIN CASE
          CASE QI = 'Y'
            WRITE CLIENT.REC ON LI.CLIENTS, CLIENT.ID
            GOSUB 99999
            CALL AFS.SCR.REFRESH(PROG.NAME, PROG.DESC, PACK.NAME, CO.NAME, TIME.DATE, 1)
            GOTO 100
          CASE QI = 'N'
            GOSUB 99999
            CALL AFS.SCR.REFRESH(PROG.NAME, PROG.DESC, PACK.NAME, CO.NAME, TIME.DATE, 1)
            GOTO 100
          CASE 1
            GOTO 99000
        END CASE
      END
      GOSUB 99999
*
      STOP
*
***********************************************************************
* Check for changes and update file
***********************************************************************
*
99999:*
      HELP = "Enter either Y or N."
      LN = 'Do you wish to update inventory? -'
      CALL GEN.IN(0,22,LN,'',Q,1,1,'','',1,-1,22,1,QI,HELP,0,23)
      QI = OCONV(QI, 'MCU')
      IF QI = 'Y' THEN
        CALL LI.INV.ENTRY( CLIENT.ID )
      END
*
      RETURN
*
***********************************************************************
*
   END
