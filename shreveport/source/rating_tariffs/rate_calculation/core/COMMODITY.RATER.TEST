* Routine to receive input data from the Web and produce "METADATA" in return.

OPEN '','FB.COMMRATES.HDR' TO F.FB.COMMRATES.HDR ELSE ERRORS = 'CANT OPEN FB.COMMRATES.HDR'  ; RETURN
OPEN '','FB.COMMRATES.DTL' TO F.FB.COMMRATES.DTL ELSE ERRORS = 'CANT OPEN FB.COMMRATES.DTL'  ; RETURN
OPEN '','FB.TABLES' TO F.TABLES ELSE ERRORS = 'CANT OPEN FB.TABLES' ; RETURN 
OPEN '','ZIPS.CODES' TO F.ZIPS ELSE ERRORS = 'CANT OPEN ZIPS.CODES' ; RETURN

CALL GET.USER(USER)

ID.CARRIER = "00212"

ERRORS = ''

IF USER = "NPRATT" THEN
   ORIG.ZIP = '18017'
   DEST.ZIP = '74131'
   EFF.DATE   = OCONV(DATE(),"D2/")
   WEIGHT = '2780'
   DATA ORIG.ZIP
   DATA DEST.ZIP
   DATA EFF.DATE
   DATA WEIGHT
END

***  INPUTS 

INPUT ORIG.ZIP
INPUT DEST.ZIP
INPUT EFF.DATE
INPUT WEIGHT

IN.WEIGHT = WEIGHT

I.EFF.DATE = ICONV(EFF.DATE,"D2/")
*
WEIGHT = ICONV(WEIGHT,"MR2")
WEIGHT = OCONV(WEIGHT,"MR2")

OPEN '','VOC' TO F.NP.TEST THEN
   TST.REC = ORIG.ZIP
   TST.REC<2> = DEST.ZIP
   TST.REC<3> = EFF.DATE
   TST.REC<4> = IN.WEIGHT
   TST.REC<7> = WEIGHT
   TST.REC<10> = OCONV(DATE(),"D2/"):" ":OCONV(TIME(),"MTS")
   WRITE TST.REC ON F.NP.TEST, "COMMODITY.RATER.INPUT"
END

GOSUB GET.DETAIL

IF ERRORS NE "" THEN
   STOP
END

GOSUB CALC.RATE

GOSUB DO.META

STOP

*
GET.DETAIL :

ID.HDR = ID.CARRIER
READ HDR.REC FROM F.FB.COMMRATES.HDR, ID.HDR ELSE
   ERRORS = 'Carrier 00212 header record not set up'
   RETURN
END

LOCATE I.EFF.DATE IN HDR.REC<1> SETTING POS THEN
   HDR.EFF.DATE = HDR.REC<1,POS>
END ELSE
   N.DATES = DCOUNT(HDR.REC<1>,@VM)
   FND = '0'
   FOR DT = 1 TO N.DATES
      W.FROM.DT = HDR.REC<1,DT>
      W.TO.DT   = HDR.REC<2,DT>
      IF W.FROM.DT LE I.EFF.DATE THEN
         IF W.TO.DT = "" OR W.TO.DT GE I.EFF.DATE THEN
            HDR.EFF.DATE = HDR.REC<1,DT>
            FND = 1 ; DT = N.DATES
         END
      END
   NEXT DT

   IF NOT(FND) THEN
      ERRORS = 'Effective date not found on Carrier Header record'
      RETURN
   END
END


ID.DTL = ID.CARRIER:"*":HDR.EFF.DATE:"*":ORIG.ZIP:"*":DEST.ZIP

READ DTL.REC FROM F.FB.COMMRATES.DTL, ID.DTL ELSE 
   DTL.REC = ""
   ERRORS = 'Rate record not found'
   RETURN
END

ORIG.FXF.DOCK    = DTL.REC<1>
ORIG.AIRPORT.CD  = DTL.REC<2>
ABS.MIN.CHARGE1  = DTL.REC<3>
WEIGHT.STRING1   = DTL.REC<4>
W.RATE.ARRAY1    = DTL.REC<5>
DEST.FXF.DOCK    = DTL.REC<6>
DEST.AIRPORT.CD  = DTL.REC<7>
ABS.MIN.CHARGE2  = DTL.REC<8>
WEIGHT.STRING2   = DTL.REC<9>
W.RATE.ARRAY2    = DTL.REC<10>

READ ORIG.REC FROM F.ZIPS, ORIG.ZIP THEN
   ORIG.CITY = ORIG.REC<1>
   ORIG.STATE = ORIG.REC<2>
END ELSE
   ERRORS = "Origination ":ORIG.ZIP:" not found on file"
   RETURN
END

READ DEST.REC FROM F.ZIPS, DEST.ZIP THEN
   DEST.CITY = DEST.REC<1>
   DEST.STATE = DEST.REC<2>
END ELSE
   ERRORS = "Destination ":DEST.ZIP:" not found on file"
   RETURN
END

RETURN

CALC.RATE :

DEBUG.FLAG = 0    ;* SET TO 1 FOR DEBUGGING PURPOSES
*

VIN = '' ; VOUT = '' ; ERRORS = ''

VIN    = ID.CARRIER
VIN<2> = ORIG.ZIP
VIN<3> = DEST.ZIP
VIN<4> = EFF.DATE
VIN<5> = WEIGHT
VIN<6> = HDR.EFF.DATE

CALL CALC.COMMODITY.RATE(VIN,VOUT,DTL.REC,ERRORS)

RATE.TO.USE   = VOUT<1>
AMOUNT.TO.USE = VOUT<2>
WGHT.DEFICIT  = VOUT<3>

RETURN

DO.META :

      METADATA = ""
      METADATA<1, 1> = "VARCHAR" ; METADATA<1, 2> = "Errors"          ; METADATA<1 ,3> = 0

      METADATA<2 ,1> = "VARCHAR" ; METADATA<2, 2> = "OrigZip"         ; METADATA<2 ,3> = 0
      METADATA<3 ,1> = "VARCHAR" ; METADATA<3, 2> = "OrigCity"        ; METADATA<3 ,3> = 0
      METADATA<4 ,1> = "VARCHAR" ; METADATA<4, 2> = "OrigState"       ; METADATA<4 ,3> = 0
      METADATA<5 ,1> = "VARCHAR" ; METADATA<5, 2> = "DestZip"         ; METADATA<5 ,3> = 0
      METADATA<6 ,1> = "VARCHAR" ; METADATA<6, 2> = "DestCity"        ; METADATA<6 ,3> = 0
      METADATA<7 ,1> = "VARCHAR" ; METADATA<7, 2> = "DestState"       ; METADATA<7 ,3> = 0
      METADATA<8 ,1> = "VARCHAR" ; METADATA<8, 2> = "Rate"            ; METADATA<8 ,3> = 0
      METADATA<9 ,1> = "VARCHAR" ; METADATA<9, 2> = "QuoteAmt"        ; METADATA<9 ,3> = 0
      METADATA<10,1> = "VARCHAR" ; METADATA<10,2> = "EffDate"         ; METADATA<10,3> = 0
      METADATA<11,1> = "VARCHAR" ; METADATA<11,2> = "WeightDeficit"   ; METADATA<11,3> = 0
      METADATA<12,1> = "VARCHAR" ; METADATA<12,2> = "AbsMinAmount"    ; METADATA<12,3> = 0
      METADATA<13,1> = "VARCHAR" ; METADATA<13,2> = "OrigAirport"     ; METADATA<13,3> = 0
      METADATA<14,1> = "VARCHAR" ; METADATA<14,2> = "DestAirport"     ; METADATA<14,3> = 0
      METADATA<15,1> = "VARCHAR" ; METADATA<15,2> = "CarrierNo"       ; METADATA<15,3> = 0
      METADATA<16,1> = "VARCHAR" ; METADATA<16,2> = "CarrierName"     ; METADATA<16,3> = 0


************* Set up data to be sent

      ROW = ERRORS

      ROW<2 ,1> = ORIG.ZIP
      ROW<3 ,1> = ORIG.CITY
      ROW<4 ,1> = ORIG.STATE
      ROW<5 ,1> = DEST.ZIP
      ROW<6 ,1> = DEST.CITY
      ROW<7 ,1> = DEST.STATE
      ROW<8 ,1> = RATE.TO.USE
      ROW<9 ,1> = AMOUNT.TO.USE
      ROW<10,1> = EFF.DATE
      ROW<11,1> = WGHT.DEFICIT
      ROW<12,1> = ABS.MIN.CHARGE1
      ROW<13,1> = ORIG.AIRPORT.CD
      ROW<14,1> = DEST.AIRPORT.CD
      ROW<15,1> = "00212"             ;* Hard coded for now
      ROW<16,1> = "Fedex Freight"     ;* Hard coded for now

CRT @(-1) ; CRT STR("*",80) ; CRT

FOR II = 1 TO 16
   CRT METADATA<II,2> "L#25":"   >>>>>   ":ROW<II>
NEXT II

CRT ; CRT

CONVERT @AM TO CHAR(30) IN METADATA
PRINT "%METADATA:":METADATA

*CRT ; CRT STR("*",80) ; CRT

CONVERT @AM TO CHAR(30) IN ROW
PRINT ROW

RETURN
