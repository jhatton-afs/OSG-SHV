* FREIGHT BILLING PACKAGE
*---- PROGRAM: VC.REPAIR.CLIENT
* AUTHOR: JAY WEBERT
* DATE: 03/27/07
* DESCRIPTION: FIX CLIENT VEND.CUST RECORDS WITH NO VEND.CUST.NAMES POINTER IN ATTR 42
*---- 
* THIS PROGRAM IS A PROPRIETARY PRODUCT OF AUTOMATED FREIGHT SYSTEMS
* AND MAY NOT BE USED, COPIED OR MODIFIED WITHOUT THE EXPRESS
* WRITTEN CONSENT OF SAME.
*----
      PROG.NAME = 'VC.REPAIR.CLIENT'
      OPEN 'CLIENTS' TO F.CLIENTS ELSE
        CALL OPEN.ABORT('CLIENTS',PROG.NAME)
      END
      ********************
      *** ENTER CLIENT ID
      ********************
      DONE = 0
      LOOP UNTIL DONE DO
        CRT 'ENTER CLIENT ID':
        INPUT ANS
        IF LEN(ANS) > 5 THEN
          CRT 'LENGHT OF ENTRY CANNOT EXCEED 5 CHARACTERS; TRY AGAIN.'
          CRT
          CONTINUE
        END
        ANS = ANS'R%5'
        READ TEST FROM F.CLIENTS, ANS THEN
          CLIENT.ID = ANS
          DONE = 1
        END ELSE
          CRT 'CLIENT ID ':ANS:' DOES NOT EXIST; TRY AGAIN.'
          CRT
        END
      REPEAT
      CRT
      CRT 'CLIENT ':CLIENT.ID
      CRT
      ********************
      *** OPEN FILES
      ********************
      OPEN 'VEND.CUST,':CLIENT.ID TO F.VC ELSE
        CALL OPEN.ABORT('VEND.CUST,':CLIENT.ID,PROG.NAME)
      END
      OPEN 'VEND.CUST.NAMES,':CLIENT.ID TO F.VCN ELSE
        CALL OPEN.ABORT('VEND.CUST.NAMES,':CLIENT.ID,PROG.NAME)
      END
      *** FIND AND FIX VEND.CUST RECORDS
      STMT = 'SELECT VEND.CUST,':CLIENT.ID:' WITH 42 = ""'
      CRT STMT
      EXECUTE STMT RTNLIST VC.LIST CAPTURING MSG
      IF @SELECTED < 1 THEN
        CRT 'No VEND.CUST,':CLIENT.ID:' records selected; program stopped.'
        STOP
      END
      REC.CNT = @SELECTED
      CTR = 0
      DONE = 0
      LOOP UNTIL DONE DO
        READNEXT VC.ID FROM VC.LIST THEN
          CTR += 1
          IF MOD(CTR,100) = 0 THEN CRT CTR:' out of ':REC.CNT:' records processed...'
          READ VC.REC FROM F.VC, VC.ID THEN
            DESC = VC.REC<1>
            STMT = 'SELECT VEND.CUST.NAMES,':CLIENT.ID:' WITH 1 = "':DESC:'"'
            EXECUTE STMT RTNLIST VCN.LIST CAPTURING MSG
            BEGIN CASE
              CASE @SELECTED < 1
                CRT 'Description "':DESC:'" not found in VEND.CUST.NAMES,':CLIENT.ID
                CRT 'VEND.CUST,':CLIENT.ID:' record ':VC.ID:' not repaired.'
              CASE @SELECTED > 1
                CRT 'Description "':DESC:'" found more than once in VEND.CUST.NAMES,':CLIENT.ID
                CRT 'WARNING!!! You must perform a VEND.CUST.NAMES rebuild during off hours!'
                CLEARSELECT
                INPUT X
              CASE 1
                READNEXT VCN.ID FROM VCN.LIST THEN
                  READ VCN.REC FROM F.VCN, VCN.ID THEN
                    VC.REC<42> = VCN.ID
                    WRITE VC.REC TO F.VC, VC.ID
                    LOCATE VC.ID IN VCN.REC<2> SETTING POS ELSE
                      VCN.REC<2,-1> = VC.ID
                      WRITE VCN.REC TO F.VCN, VCN.ID
                    END
                  END
                END
            END CASE
          END
        END ELSE
          DONE = 1
        END
      REPEAT
      CRT CTR:' out of ':REC.CNT:' records processed...'

