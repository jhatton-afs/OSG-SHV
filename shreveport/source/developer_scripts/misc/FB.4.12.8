$OPTIONS HEADER.EJECT
      ERRMSG=''
*
$OPTIONS EXEC.EQ.PERF
$OPTIONS INFO.ABORT
*
* PROGRAM: FB.4.12.8
* PROGRAM TO CREATE CLIENT SUPPORT YEARLY SPREADSHEET
*
* AUTHOR: N.C.G. Pratt
* DATE: February 2009
*
* EDIT HISTORY:
* 101211 - RLM01 - Project 80 - Retrieve Account Manager ID from file CLIENTS instead of salesman file
*
* Initialize variables
*
      CALL GET.USER(USER.NAME)
      CALL CHANNEL(CH.NUM)
      PROG.NAME='FB.4.12.8'
      PROG.DESC='CLIENT SUPPORT SPREADSHEET(XLS)'
      PACK.NAME='FREIGHT BILLING'
      CO.NAME='AFS Logistics, LLC'
      UPL.VIN = 'FBBP' ; UPL.VIN<2> = 'FB.4.12.8' ; UPL.VOUT = ''      ; * NPR_UPL 04/23/2010
      CALL UPD.PROGRAM.LOG(UPL.VIN,UPL.VOUT)       ; * NPR_UPL 04/23/2010
*
* Open files
*
      OPEN '','CLIENTS' TO F.CLIENTS ELSE CALL OPEN.ABORT("CLIENTS",PROG.NAME)
      OPEN '','BCTRL' TO F.BCTRL ELSE CALL OPEN.ABORT("BCTRL",PROG.NAME)
      OPEN '','LOCATIONS' TO F.LOCATIONS ELSE CALL OPEN.ABORT("LOCATIONS",PROG.NAME)
      OPEN '','FB.CLIENT.SUPPORT' TO F.CLIENT.SUPPORT ELSE CALL OPEN.ABORT("FB.CLIENT.SUPPORT",PROG.NAME)

      OPEN '','AR.SLSMAN.1000.1' TO F.SLSMAN ELSE CALL OPEN.ABORT('AR.SLSMAN.1000.1',PROG.NAME)
*
      OPEN '','VOC' TO F.VOC ELSE
         CALL ELINE('UNABLE TO OPEN VOC FILE')
         STOP
      END
*
      READ BILLDATA.REC FROM F.VOC,"BILLDATA" ELSE
         CALL ELINE('UNABLE TO OPEN "VOC BILLDATA"')
         STOP
      END
*
      BILLDATA.REC<2>:='\':USER.NAME
      WRITE BILLDATA.REC ON F.VOC,"BILLDATA.":USER.NAME ELSE
         CALL ELINE("YOUR USER NAME '":USER.NAME:"' IS NOT IN THE BILLDATA FOLDER - PLEASE CALL THE HELPDESK")
         STOP
      END
      OPEN '','BILLDATA.':USER.NAME TO F.BILLDATA ELSE
         CALL ELINE('UNABLE TO OPEN "BILLDATA.":USER')
         STOP
      END

      OPEN '','FBWORK':CH.NUM TO F.WORK ELSE
         EXECUTE 'CREATE-FILE FBWORK':CH.NUM:' 1 1001' PASSLIST CAPTURING OUTPUT
         OPEN '','FBWORK':CH.NUM TO F.WORK ELSE
            CALL OPEN.ABORT('FBWORK':CH.NUM,PROG.NAME)
         END
      END
      OPEN 'DICT','FBWORK':CH.NUM TO F.DICT.WORK ELSE
         CALL OPEN.ABORT('DICT FBWORK':CH.NUM,PROG.NAME)
      END
*
      CALL SCR.REFRESH(PROG.NAME,PROG.DESC,PACK.NAME,CO.NAME,TIME.DATE,1)
*
***********************************************************************
* Clear work file
*
      CLEARFILE F.WORK

* Create work file dictionary items
*

      TEMP=''
      TEMP<1>='S'
      TEMP<2>=0
      TEMP<8>='G0*1'
      TEMP<9>='R'
      TEMP<10>='5'
      WRITE TEMP ON F.DICT.WORK,'CLIENT'
*
      TEMP=''
      TEMP<1>='S'
      TEMP<2>=0
      TEMP<8>='G1*1'
      TEMP<9>='L'
      TEMP<10>='12'
      WRITE TEMP ON F.DICT.WORK,'RANKING'
*
      TEMP=''
      TEMP<1>='S'
      TEMP<2>=0
      TEMP<8>='G2*1'
      TEMP<9>='L'
      TEMP<10>='15'
      WRITE TEMP ON F.DICT.WORK,'ACCT.MGR'

*
      TEMP=''
      TEMP<1>='S'
      TEMP<2>=16
      TEMP<8>=''
      TEMP<9>='L'
      TEMP<10>='15'
      WRITE TEMP ON F.DICT.WORK,'ACCT.MGR.NAME'

*
      ALPHABT = "ABCDEFGHIJKLMNOPQRSTUVWXYZ"       ; * EXCEL Column headers
      COL.HEADERS = ""

      FOR II = 1 TO 52
         IF II GT 26 THEN
            W.LET = ALPHABT[II-26,1]
            COL.HEADERS<1,II> = "A":W.LET
         END ELSE
            W.LET = ALPHABT[II,1]
            COL.HEADERS<1,II> = W.LET
         END
*   CRT COL.HEADERS<1,II>
*   CRT II "R#3":"  ":W.LET "L#2":"  ":COL.HEADERS<1,II>
      NEXT II

      TODAY = DATE()
      YEAR = OCONV(TODAY,"D4Y")
*
      MTH.ARRAY = 'January'
      MTH.ARRAY<1,2> = 'February'
      MTH.ARRAY<1,3> = 'March'
      MTH.ARRAY<1,4> = 'April'
      MTH.ARRAY<1,5> = 'May'
      MTH.ARRAY<1,6> = 'June'
      MTH.ARRAY<1,7> = 'July'
      MTH.ARRAY<1,8> = 'August'
      MTH.ARRAY<1,9> = 'September'
      MTH.ARRAY<1,10> = 'October'
      MTH.ARRAY<1,11> = 'November'
      MTH.ARRAY<1,12> = 'December'
*
      REPORT.TYPE = "B"
      CLIENT.ID = "ALL"
      START.DATE = ""
      END.DATE = ""
      IN.OUT = "B"
*
      GOSUB GET.SCREEN.DSP
      GOSUB INPUT.SELECTIONS
*
      GOSUB BUILD.SUMMARY.HEADERS
      GOSUB BUILD.DETAIL.HEADERS

*      STMT = \SELECT FB.CLIENT.SUPPORT WITH CALL.YEAR = "\ : YEAR: \"\

      STMT = \SSELECT FB.CLIENT.SUPPORT BY RANKING BY CLIENT BY CALL.DATE BY CALL.TIME \
      STMT := \ WITH CALL.DATE GE "\ : OCONV(START.DATE,"D2/") : \"\
      STMT := \ AND WITH CALL.DATE LE "\ : OCONV(END.DATE,"D2/") : \"\
*
      IF CLIENT.ID NE "ALL" THEN
         STMT := \ AND WITH CLIENT = "\ : CLIENT.ID : \"\
      END
*
      IF IN.OUT NE "B" THEN
         STMT := \ AND WITH CALL.DIR = "\ : IN.OUT : \"\
      END

* CRT STMT ; DEBUG
*
      CRT @(10,12) : " Now selecting Records ":@(-4)
      EXECUTE STMT RTNLIST EMP.LIST CAPTURING OUTPUT

*
      LOG.ITEMS = @SELECTED
      IF LOG.ITEMS LE 0 THEN
         CALL ELINE('ERROR - No RECORDS on file.')
         STOP
      END
*
      CRT @(10,14) : " Now processing selected Records ":@(-4)
*
      COUNTER.1=0
      PER.1=0
*
      LOOP
         READNEXT ID.CL.SUP FROM EMP.LIST ELSE ID.CL.SUP='DONE'
      UNTIL ID.CL.SUP='DONE' DO
         COUNTER.1=COUNTER.1+1
         READ CL.SUP.REC FROM F.CLIENT.SUPPORT, ID.CL.SUP THEN
            CLIENT = FIELD(ID.CL.SUP,"*",1)
            CLIENT.NAME = ""
            SUPPORT.SLSMAN = ""
            CLIENT.RANKING = ""
            READ CLIENT.REC FROM F.CLIENTS, CLIENT THEN
               CLIENT.NAME = CLIENT.REC<2>
               SUPPORT.SLSMAN = CLIENT.REC<115>
               CLIENT.RANKING = CLIENT.REC<240>
            END

            SUPP.USER = FIELD(ID.CL.SUP,"*",2)
            CALL.DATE = FIELD(ID.CL.SUP,"*",3)
            CALL.TIME = FIELD(ID.CL.SUP,"*",4)

            CALL.MONTH = OCONV(CALL.DATE,"D2M")

            CALL.IO = CL.SUP.REC<1>
            CALL.PH.NO = CL.SUP.REC<2>
            CALL.CONTACT = CL.SUP.REC<3>
            CALL.REASON = CL.SUP.REC<4>
*
            GOSUB GET.ACCT.MGR
*
            GOSUB BUILD.DETAIL.LINE
*
            ID.WORK = CLIENT:"*":CLIENT.RANKING:"*":ACCT.MGR
*
            READ WORK.REC FROM F.WORK, ID.WORK ELSE WORK.REC = ""
*
            IF CALL.IO = "I" OR CALL.IO = "O" THEN
               IF CALL.IO = "I" THEN
                  IN.CNT = WORK.REC<CALL.MONTH,1>
                  IN.CNT += 1
                  WORK.REC<CALL.MONTH,1> = IN.CNT

                  YR.IN.CNT = WORK.REC<13,1>
                  YR.IN.CNT += 1
                  WORK.REC<13,1> = YR.IN.CNT
               END

               IF CALL.IO = "O" THEN
                  OUT.CNT = WORK.REC<CALL.MONTH,2>
                  OUT.CNT += 1
                  WORK.REC<CALL.MONTH,2> = OUT.CNT

                  YR.OUT.CNT = WORK.REC<13,2>
                  YR.OUT.CNT += 1
                  WORK.REC<13,2> = YR.OUT.CNT
               END

               WORK.REC<15> = CLIENT.NAME
               WORK.REC<16> = ACCT.MGR.NAME

            END
*
            WRITE WORK.REC ON F.WORK, ID.WORK
*           ID.WORK = CLIENT:"*":CLIENT.RANKING:"*":ACCT.MGR
            ID.WORK = "99999*X*":ACCT.MGR
*
            READ WORK.REC FROM F.WORK, ID.WORK ELSE WORK.REC = ""
*
            IF CALL.IO = "I" OR CALL.IO = "O" THEN
               IF CALL.IO = "I" THEN
                  IN.CNT = WORK.REC<CALL.MONTH,1>
                  IN.CNT += 1
                  WORK.REC<CALL.MONTH,1> = IN.CNT

                  YR.IN.CNT = WORK.REC<13,1>
                  YR.IN.CNT += 1
                  WORK.REC<13,1> = YR.IN.CNT
               END

               IF CALL.IO = "O" THEN
                  OUT.CNT = WORK.REC<CALL.MONTH,2>
                  OUT.CNT += 1
                  WORK.REC<CALL.MONTH,2> = OUT.CNT

                  YR.OUT.CNT = WORK.REC<13,2>
                  YR.OUT.CNT += 1
                  WORK.REC<13,2> = YR.OUT.CNT
               END

            END

            WORK.REC<15> = ""
            WORK.REC<16> = ACCT.MGR.NAME
*
            WRITE WORK.REC ON F.WORK, ID.WORK
*
         END
*
SKIP:
         CALL PROG.BAR(20,COUNTER.1,LOG.ITEMS,PER.1)
      REPEAT
*

      CRT @(10,16) : " Now Building Spreadsheet ":@(-4)

      STMT = \SSELECT FBWORK\:CH.NUM
      STMT := \ BY ACCT.MGR.NAME BY RANKING BY CLIENT\
*
      CRT @(10,16) : " Now selecting Records ":@(-4)
      EXECUTE STMT RTNLIST STAT.LIST CAPTURING OUTPUT
*
      STAT.ITEMS = @SELECTED
      IF STAT.ITEMS LE 0 THEN
         CALL ELINE('ERROR - No RECORDS on FBWORK':CH.NUM:' file.')
         STOP
      END
*
      CRT @(10,14) : " Now processing selected Records ":@(-4)
*
      COUNTER.1=0
      PER.1=0
*
      LOOP
         READNEXT ID.WORK FROM STAT.LIST ELSE ID.WORK = 'DONE'
      UNTIL ID.WORK = 'DONE' DO
         READ WORK.REC FROM F.WORK, ID.WORK THEN
            GOSUB BUILD.SUMMARY.LINE
         END
      REPEAT
*

      CALL SCR.REFRESH(PROG.NAME,PROG.DESC,PACK.NAME,CO.NAME,TIME.DATE,1)

      GOSUB BUILD.FORMULAS

*
      IF CLIENT.ID = "ALL" THEN
         FOOTER.LINE = "All Clients - "
      END ELSE
         FOOTER.LINE = "Client ":CLIENT.ID:" - "
      END

      BEGIN CASE
         CASE IN.OUT = "I"
            FOOTER.LINE := " For incoming calls "
         CASE IN.OUT = "O"
            FOOTER.LINE := " For incoming calls "
         CASE 1
            FOOTER.LINE := " For incoming and outgoing calls "
      END CASE

      FOOTER.LINE := " from ":OCONV(START.DATE,"D2/"):"  thru ":OCONV(END.DATE,"D2/")


WRITE.RECORD.SUMMARY:
*
      IF REPORT.TYPE = "S" OR REPORT.TYPE = "B" THEN

         XLS.SUMM.REC<ROW.CNT> = ""
         ROW.CNT += 1
         XLS.SUMM.REC<ROW.CNT> = ""
         ROW.CNT += 1
         XLS.SUMM.REC<ROW.CNT> = "Client Support Analysis Summary for ":FOOTER.LINE

         CONVERT @VM TO CHAR(9) IN XLS.SUMM.REC

         ID.XLS.SUMM = "CL_SUMM_CALL_LOG_":YEAR:".XLS"
         TRY.CNT = 1
         DONE = 0

         LOOP
         UNTIL DONE DO
            WRITE XLS.SUMM.REC ON F.BILLDATA, ID.XLS.SUMM ELSE
               CALL ELINE("Could not write item to disk - try ":TRY.CNT:" of 3 - Hit ENTER to retry")
               IF TRY.CNT LT 3 THEN
                  TRY.CNT += 1
                  GOTO WRITE.RECORD.SUMMARY
               END
               CALL ELINE("Could not write Summary file ":ID.XLS.SUMM:" to disk")
               GOTO WRITE.RECORD.DETAIL
            END

            DONE = 1
         REPEAT
*

         CALL ELINE("Summary File ":ID.XLS.SUMM:" may be found in your own U: drive directory")
      END
*
WRITE.RECORD.DETAIL:

      IF REPORT.TYPE = "D" OR REPORT.TYPE = "B" THEN

         XLS.DET.REC<DROW.CNT> = ""
         DROW.CNT += 1
         XLS.DET.REC<DROW.CNT> = ""
         DROW.CNT += 1
         XLS.DET.REC<DROW.CNT> = "Client Support Analysis Detail for ":FOOTER.LINE

         CONVERT @VM TO CHAR(9) IN XLS.DET.REC

         ID.XLS.DET = "CL_DET_CALL_LOG_":YEAR:".XLS"
         TRY.CNT = 1
         DONE = 0

         LOOP
         UNTIL DONE DO
            WRITE XLS.DET.REC ON F.BILLDATA, ID.XLS.DET ELSE
               CALL ELINE("Could not write item to disk - try ":TRY.CNT:" of 3 - Hit ENTER to retry")
               IF TRY.CNT LT 3 THEN
                  TRY.CNT += 1
                  GOTO WRITE.RECORD.DETAIL
               END
               CALL ELINE("Could not write Detail file ":ID.XLS.DET:" to disk")
               STOP
            END

            DONE = 1
         REPEAT
*
         CALL ELINE("Detail File ":ID.XLS.DET:" may be found in your own U: drive directory")
      END

*
      STOP
*

*
BUILD.SUMMARY.LINE:

      D.CLIENT = FIELD(ID.WORK,"*",1)
      D.RANKING = FIELD(ID.WORK,"*",2)
      D.ACCT.MGR = FIELD(ID.WORK,"*",3)
      D.CLIENT.NAME = WORK.REC<15>
      D.ACCT.MGR.NAME = WORK.REC<16>

      SLINE = '' ; SCOL = 1
      SLINE<1,SCOL> = D.CLIENT.NAME ; SCOL += 1
      SLINE<1,SCOL> = D.CLIENT ; SCOL += 1
      SLINE<1,SCOL> = D.RANKING ; SCOL += 1
      SLINE<1,SCOL> = D.ACCT.MGR.NAME ; SCOL += 1

      FOR MTH = 1 TO 13
         D.IN.CNT = WORK.REC<MTH,1> + 0
         D.OUT.CNT = WORK.REC<MTH,2> + 0
         D.TOT.CNT = D.IN.CNT + D.OUT.CNT
         IN.COL.LET = COL.HEADERS<1,SCOL>
         SLINE<1,SCOL> = D.IN.CNT ; SCOL += 1
         OUT.COL.LET = COL.HEADERS<1,SCOL>
         SLINE<1,SCOL> = D.OUT.CNT ; SCOL += 1
         T.FORMULA = "=$":IN.COL.LET:"$":ROW.CNT:"+$":OUT.COL.LET:"$":ROW.CNT
         SLINE<1,SCOL> = T.FORMULA ; SCOL += 1
      NEXT MTH
*
      XLS.SUMM.REC<ROW.CNT> = SLINE
      ROW.CNT += 1

      RETURN

BUILD.DETAIL.LINE:

*
      READV SUPP.USER.NAME FROM F.BCTRL, SUPP.USER,13 ELSE SUPP.USER.NAME = SUPP.USER

      DLINE = '' ; DCOL = 1
      DLINE<1,DCOL> = CLIENT.NAME ; DCOL += 1
      DLINE<1,DCOL> = CLIENT ; DCOL += 1
      DLINE<1,DCOL> = CLIENT.RANKING ; DCOL += 1
      DLINE<1,DCOL> = ACCT.MGR.NAME ; DCOL += 1
      DLINE<1,DCOL> = OCONV(CALL.DATE,"D2/") ; DCOL += 1
      DLINE<1,DCOL> = OCONV(CALL.TIME,"MTS") ; DCOL += 1
      DLINE<1,DCOL> = SUPP.USER.NAME ; DCOL += 1
      DLINE<1,DCOL> = CALL.IO ; DCOL += 1
      DLINE<1,DCOL> = CALL.PH.NO ; DCOL += 1
      DLINE<1,DCOL> = CALL.CONTACT ; DCOL += 1

      FOR RS = 1 TO 7
         DLINE<1,DCOL> = CALL.REASON<1,RS> ; DCOL += 1
      NEXT RS

*
      XLS.DET.REC<DROW.CNT> = DLINE
      DROW.CNT += 1

      RETURN

*
GET.ACCT.MGR:

      ACCT.MGR = 'Unnassigned'
      ACCT.MGR.NAME = 'Unnassigned'
*
      SERVC.SLSMAN.ID = CLIENT.REC<115>

      READ SALESMAN.REC FROM F.SLSMAN,SERVC.SLSMAN.ID THEN
         SERVC.SLSMAN.NUMBER.NAME=SERVC.SLSMAN.ID:' ':SALESMAN.REC<1>'L#25'
*        ACCT.MGR = SALESMAN.REC<11>  ; * NPR08  ; * RLM01
      END

      ACCT.MGR = CLIENT.REC<264>         ; * NPR08  ; * RLM01
      READ ACCT.MGR.REC FROM F.SLSMAN,ACCT.MGR THEN
         ACCT.MGR.NAME = ACCT.MGR.REC<1>
      END

      RETURN
*
BUILD.SUMMARY.HEADERS:* Build Spreadsheet Summary column headers
*
      HLINE.SUMM1 = "Client Support Analysis "
*
      HLINE.SUMM2 = ""
      COL.NO = 1
      HLINE.SUMM2<1,COL.NO> = "Client Name" ; COL.NO += 1
      CL.COL.NO = COL.NO
      HLINE.SUMM2<1,COL.NO> = "Client #" ; COL.NO += 1
      RANK.COL.NO = COL.NO
      HLINE.SUMM2<1,COL.NO> = "Ranking" ; COL.NO += 1
      AM.COL.NO = COL.NO
      HLINE.SUMM2<1,COL.NO> = "Acct Mgr." ; COL.NO += 1
*

      MTH.ST.COL.NO = COL.NO
      COL.NO3 = COL.NO
*
      HLINE.SUMM3 = ""
*
      FOR MTH = 1 TO 12
         HLINE.SUMM2<1,COL.NO3> = MTH.ARRAY<1,MTH>
         HLINE.SUMM3<1,COL.NO3> = "I" ; COL.NO3 += 1
         HLINE.SUMM3<1,COL.NO3> = "O" ; COL.NO3 += 1
         HLINE.SUMM3<1,COL.NO3> = "Total" ; COL.NO3 += 1
      NEXT MTH
*
      HLINE.SUMM2<1,COL.NO3> = YEAR
      HLINE.SUMM3<1,COL.NO3> = "I" ; COL.NO3 += 1
      HLINE.SUMM3<1,COL.NO3> = "O" ; COL.NO3 += 1
      LAST.COL.NO = COL.NO3
      HLINE.SUMM3<1,COL.NO3> = "Total" ; COL.NO3 += 1

*XLS.SUMM.REC    = HLINE.SUMM1
*XLS.SUMM.REC<2> = HLINE.SUMM2
*XLS.SUMM.REC<3> = HLINE.SUMM3
      XLS.SUMM.REC = HLINE.SUMM2
      XLS.SUMM.REC<2> = HLINE.SUMM3
*
      ROW.CNT = 4
*
      RETURN

BUILD.DETAIL.HEADERS:* Build Spreadsheet detail column headers
*
* HLINE.DET1 = "Client Support Analysis Detail"
*
      HLINE.DET2 = ""
      DCOL.NO = 1
      HLINE.DET2<1,DCOL.NO> = "Client Name" ; DCOL.NO += 1
      CL.DCOL.NO = DCOL.NO
      HLINE.DET2<1,DCOL.NO> = "Client #" ; DCOL.NO += 1
      RANK.DCOL.NO = DCOL.NO
      HLINE.DET2<1,DCOL.NO> = "Ranking" ; DCOL.NO += 1
      AM.DCOL.NO = DCOL.NO
      HLINE.DET2<1,DCOL.NO> = "Acct Mgr." ; DCOL.NO += 1
      HLINE.DET2<1,DCOL.NO> = "Call Date" ; DCOL.NO += 1
      HLINE.DET2<1,DCOL.NO> = "Call Time" ; DCOL.NO += 1
      HLINE.DET2<1,DCOL.NO> = "AFS Person" ; DCOL.NO += 1
      HLINE.DET2<1,DCOL.NO> = "In or Out" ; DCOL.NO += 1
      HLINE.DET2<1,DCOL.NO> = "Phone #" ; DCOL.NO += 1
      HLINE.DET2<1,DCOL.NO> = "Contact" ; DCOL.NO += 1
      FOR RS = 1 TO 7
         HLINE.DET2<1,DCOL.NO> = "Reason Line ":RS ; DCOL.NO += 1
      NEXT RS

*XLS.DET.REC    = HLINE.DET1
*XLS.DET.REC<2> = HLINE.DET2
*
      XLS.DET.REC = HLINE.DET2
*
      DROW.CNT = 3

      RETURN

*
BUILD.FORMULAS:* Set up Calculation fields
*
      N.ROWS = DCOUNT(XLS.SUMM.REC,@AM)
      ST.ROW = 4
      FOR I.ROW = ST.ROW TO N.ROWS
         W.LINE = XLS.SUMM.REC<I.ROW>
         NEW.LINE = W.LINE
         W.CLIENT = W.LINE<1,CL.COL.NO>
         W.RANKING = W.LINE<1,RANK.COL.NO>

         IF W.CLIENT = "99999" THEN
            NEW.LINE<1,CL.COL.NO> = ""
            NEW.LINE<1,RANK.COL.NO> = ""
            FOR I.COL = MTH.ST.COL.NO TO LAST.COL.NO
               COL.LET = COL.HEADERS<1,I.COL>
               W.FORMULA = "=SUM($":COL.LET:"$":ST.ROW:":$":COL.LET:"$":I.ROW-1:")"
               NEW.LINE<1,I.COL> = W.FORMULA
            NEXT I.COL
            ST.ROW = I.ROW+1
         END
         XLS.SUMM.REC<I.ROW> = NEW.LINE
      NEXT I.ROW

      RETURN

INPUT.SELECTIONS:* Enter selection criteria
*

2100:

      REPORT.TYPE = "B"
      HELP="Enter S=Summary, D=Detail. B=Both or RETURN for both 'EX' to exit"
      CALL GEN.IN(0,22,'Spreadsheet format? --','',Q,0,2,'','',2,14,4,1,QI,HELP,0,23)
      QI=OCONV(QI,'MCU')
      BEGIN CASE
         CASE QI='D' ! QI='S' ! QI = 'B'
            REPORT.TYPE = QI
         CASE QI = ''
            REPORT.TYPE = "B"
         CASE QI = 'EX'
            STOP
         CASE 1
            CALL ELINE('Invalid entry.')
            GOTO 2100
      END CASE

      BEGIN CASE
         CASE REPORT.TYPE = "B"
            CRT @(14,4):"Summary and Detail" : @(-4)
         CASE REPORT.TYPE = "S"
            CRT @(14,4):"Summary Only" : @(-4)
         CASE REPORT.TYPE = "D"
            CRT @(14,4):"Detail Only" : @(-4)
      END CASE

2200:

      HELP = "Enter the client number [A]ll  [X] = Back  Exit [EX] = Exit"
      MASK = '#####'
*
      CALL GEN.IN(14,6,MASK,'',Q,0,20,'','',2,14,6,1,QI,HELP,0,23)
*
      QI = OCONV(QI,'MCU')
      IF QI = "A" OR QI = '' THEN
         CLIENT.ID = "ALL"
         CRT @(14,6):"ALL"
         GOTO 2300
      END

      BEGIN CASE
         CASE QI = 'EX'
            STOP
         CASE QI = 'X'
            GOTO 2100
         CASE QI = ''
            Q = CLIENT.ID
         CASE NOT(QI MATCHES "0N")
            VIN=0                        ; * Starting Chars In
            VOUT=''
            CALL SOUNDEX.DISPLAY.NEW(VIN,VOUT,QI,'BCUST','SDX.CLIENTS,NAME',2,ITEM.LIST)
            GOTO 2200

*            BEGIN CASE
*               CASE ITEM.LIST = ''
*                  IF CLIENT.MODE THEN STOP ELSE GOTO 2100
*               CASE DCOUNT(ITEM.LIST<1>,@VM) GT 1
*                  IF CLIENT.MODE THEN STOP ELSE GOTO 2100
*               CASE 1
*                  DATA ITEM.LIST<1>
*                  IF CLIENT.MODE THEN GOTO ENDIT ELSE GOTO 2100
*            END CASE
         CASE NUM(QI)
            Q = QI'R%5'
         CASE 1
            CALL ELINE("Please enter a Client Number")
            GOTO 2200
      END CASE

*---- Get client name

      IF CLIENT.ID # Q THEN CLIENT.ID = Q'R%5'

      READ CLIENT.REC FROM F.CLIENTS,CLIENT.ID ELSE
         CALL ELINE("ERROR - Client ":CLIENT.ID:" not on file.")
         CLIENT.ID = ''
         GOTO 2200
      END

      CLIENT.NAME = CLIENT.REC<2>
*
      CRT @(14,6):CLIENT.ID

*
** Get start/stop dates.
*

2300:

      HELP = "Enter start date of spreadsheet. 'X'=Back to Client.."
      START.DATE=ICONV('01/01/':YEAR,'D')
      DVAR = OCONV(START.DATE,'D2/')'L#8'
      CALL GEN.IN(0,22,'From ':DVAR,'DATE',Q,0,8,'','',1,14,8,0,QI,HELP,0,23)
      BEGIN CASE
         CASE QI EQ 'X'
            GOTO 2200
         CASE QI EQ ''
            Q=START.DATE
      END CASE
      START.DATE=Q
      CRT @(14,8):START.DATE'D2/'
*
2400:
*
      HELP = "Enter end date of spreadsheet. 'X'=Back to start date."
      END.DATE=ICONV('12/31/':YEAR,'D')
      DVAR = OCONV(END.DATE,'D2/')'L#8'
      CALL GEN.IN(0,22,'To ':DVAR : '   ','DATE',Q,0,10,'','',1,14,10,0,QI,HELP,0,23)
      BEGIN CASE
         CASE QI EQ 'X'
            GOTO 2300
         CASE QI EQ ''
            Q=END.DATE
      END CASE
      END.DATE=Q
      CRT @(14,10):END.DATE'D2/'

      IF END.DATE LT START.DATE THEN
         CALL ELINE('End Date must be after Start Date')
         GOTO 2300
      END

2500:

      HELP = 'Enter [I]Inbound [O]Outbound or [B]oth [X]=Back to Date Entry [EX]=Exit'
      CALL GEN.IN(0,22,'','',Q,0,2,'','',2,14,12,0,QI,HELP,0,23)
      QI = OCONV(QI,'MCU')
      BEGIN CASE
         CASE QI = ''
            Q = IN.OUT
         CASE QI = 'X'
            GOTO 2400
         CASE QI = 'EX'
            STOP
         CASE QI = 1 ! QI = 'I'
            Q = 'I'
         CASE QI = 0 ! QI = 'O'
            Q = 'O'
         CASE QI = 3 ! QI = 'B'
            Q = 'T'
         CASE QI = ''
            q = 'B'
         CASE 1
            CALL ELINE('Invalid entry.')
            GOTO 2500
      END CASE
      IN.OUT = Q
*
      RETURN
*
*
GET.SCREEN.DSP:***
*
*---- Display screen to send to subroutine
*
      CALL AFS.SCR.HEAD(CO.ID,FILE.ID,PERIOD,PROG.NAME,PROG.DESC,PACK.NAME,CO.NAME,TIME.DATE,1)

      SCREEN.DSP = ""
      SCREEN.DSP := @(0 , 4):'Report Type  : ' : REPORT.TYPE
      SCREEN.DSP := @(0 , 6):'Client #     : ' : CLIENT.ID
      SCREEN.DSP := @(0 , 8):'Start Date   : ' : START.DATE
      SCREEN.DSP := @(0 ,10):'End   Date   : ' : END.DATE
      SCREEN.DSP := @(0 ,12):'In/Outgoing  : ' : IN.OUT

      BEGIN CASE
         CASE REPORT.TYPE = "B"
            SCREEN.DSP := @(14,4):"Summary and Detail" : @(-4)
         CASE REPORT.TYPE = "S"
            SCREEN.DSP := @(14,4):"Summary Only" : @(-4)
         CASE REPORT.TYPE = "D"
            SCREEN.DSP := @(14,4):"Detail Only" : @(-4)
      END CASE

      CRT SCREEN.DSP

*
      RETURN
