$OPTIONS HEADER.EJECT
$OPTIONS EXEC.EQ.PERF
$OPTIONS INFO.ABORT
      ERRMSG=''
* PROGRAM FB.1.11.20.4 Copied from FB.1.18 and modified by NCG Pratt ON 03/16/10
* PROGRAM TO MODIFY FREIGHT BILL INTERNATIONAL ITEMS
*
* FREIGHT BILLING PACKAGE
*
* AUTHOR: OWEN HOLMAN
* DATE: FEBRUARY 13, 1995
* REVISED BY: TAMI SEAGO
* DATE: SEPTEMBER 15, 1999
*
* 12/31/08 JMK01 Add printing capability
*
* Initialize variables
*
      PROMPT''
      PROG.NAME='FB.1.11.20.4'
      PROG.DESC='INTERNATIONAL CODE MAINTENANCE'
      PACK.NAME='FREIGHT BILLING'
      CALL AFS.SCR.HEAD(CO.ID,FILE.ID,'',PROG.NAME,PROG.DESC,PACK.NAME,CO.NAME,TIME.DATE,1)
       UPL.VIN = 'FBBP' ;  UPL.VIN<2> = 'FB.1.11.20.4' ; UPL.VOUT = ''                    ;* NPR_UPL 04/23/2010
       CALL UPD.PROGRAM.LOG(UPL.VIN,UPL.VOUT)                                                           ;* NPR_UPL 04/23/2010
      ESC=CHAR(27)                       ; * JMK01
*
* Open files
*
      OPEN '','FB.TABLES' TO F.TABLES ELSE CALL OPEN.ABORT('FB.TABLES',PROG.NAME)
*
* Read Code Table from File
*
      READ DISP.REC FROM F.TABLES,'INTL.CODES' ELSE DISP.REC=''
*
* First page of codes
*
      PAGE=1
      GOSUB 9000
*
* Enter selection code
*
100:  HELP="Enter line number. 'B'=Back. 'T'=Top, ''=Next page. 'E'=End-&-Save. 'EX'=Exit."
      CALL GEN.IN(0,22,"Enter line number please. ###",'',Q,0,3,'','',1,-3,22,0,QI,HELP,0,23)
      QI=OCONV(QI,'MCU')
      BEGIN CASE
         CASE QI=''
            PAGE=PAGE+1
            GOSUB 9000
         CASE QI='B'
            PAGE=PAGE-1
            IF PAGE=0 THEN PAGE=1
            GOSUB 9000
         CASE QI='T'
            PAGE=1
            GOSUB 9000
         CASE QI='P'                     ; * JMK01
            GOSUB 9500                   ; * JMK01
         CASE QI='EX'
            STOP
         CASE NUM(QI)
            SEL=QI

            IF SEL = STOP+1 THEN   ;* NPR99
               PAGE=PAGE+1         ;* NPR99
               GOSUB 9000         ;* NPR99
            END                    ;* NPR99

            IF SEL LT START ! SEL GT STOP THEN
               CALL ELINE('Invalid selection.')
               GOTO 100
            END
            GOSUB 8000
         CASE QI='E'
            X=DCOUNT(DISP.REC<1>,CHAR(253))
            FOR I=X TO 1 STEP -1
               IF DISP.REC<1,I>='' THEN
                  DISP.REC=DELETE(DISP.REC,1,I)
                  DISP.REC=DELETE(DISP.REC,2,I)
                  DISP.REC=DELETE(DISP.REC,3,I)
               END
            NEXT I
*
            WRITE DISP.REC ON F.TABLES,'INTL.CODES'
            STOP
      END CASE
      GOTO 100
*
*------------------------------------------------------------------------
* INTERNAL SUBROUTINES
*------------------------------------------------------------------------
*
* Edit code
*
8000: ROW=3+REM(SEL,18)
      IF ROW=3 THEN ROW=21
      CODE=DISP.REC<1,SEL>
      DESC=DISP.REC<2,SEL>
      TYPE=DISP.REC<3,SEL>
8010: HELP="Enter new code. ''=Accept. 'D'=Delete. 'X'=Exit."
      CALL GEN.IN(5,ROW,CODE'L#4','',Q,0,4,'','',2,-4,ROW,0,QI,HELP,0,23)
      QI=OCONV(QI,'MCU')
      IF QI='X' THEN
         CRT @(5,ROW):CODE'L#4'
         RETURN
      END
      IF QI='' THEN
         Q=CODE
         QI=CODE
      END
      IF QI='D' THEN
         FOR X = 1 TO 3
            DISP.REC=DELETE(DISP.REC,X,SEL)
         NEXT X
         GOSUB 9000
         RETURN
      END
      CODE=QI
      CRT @(5,ROW):CODE'L#4'
*
8020: HELP="Enter code description. '<'=Back to code. 'X'=Exit."
      CALL GEN.IN(11,ROW,DESC'L#30','',Q,0,30,'','',2,-30,ROW,0,QI,HELP,0,23)
      QI=OCONV(QI,'MCU')
      BEGIN CASE
         CASE QI='X' ! QI='<'
            CRT @(11,ROW):DESC'L#30'
            GOTO 8010
         CASE QI=''
            Q=DESC
      END CASE
      DESC=Q
      CRT @(11,ROW):DESC'L#30'

      DISP.REC<1,SEL>=CODE
      DISP.REC<2,SEL>=DESC

***      WRITE DISP.REC ON F.TABLES,'INTL.CODES'


*
* Display array
*
9000: CRT @(0,3):@(-3):"  #":@(5,3):"Code  Description"
      LASTITEM = DCOUNT(DISP.REC<1>,@VM)
      START=(PAGE*18)-17
*NPR99      IF START GT LASTITEM THEN
      IF START GT LASTITEM+1 THEN       ;* NPR99 Allow paging to next page
         START=1
         PAGE=1
      END
      STOP=START+17
      FOR I=START TO STOP STEP 1
         ROW=3+REM(I,18)
         IF ROW=3 THEN ROW=21
         CODE=DISP.REC<1,I>
         DESC=DISP.REC<2,I>
         TYPE=DISP.REC<3,I>
         PRINT @(0,ROW):I"R#3":'  ':CODE"L#4":'  ':DESC"L#30":'  ':TYPE"L#1"
      NEXT I
      RETURN

*
9500: * Print report
*
      EXECUTE 'TERM ,,,,,,218,75'        ; * JMK01
      PRINTER ON                         ; * JMK01
      LASER = ''                         ; * JMK01
      LASER<1>= 0                        ; * LANDSCAPE; * JMK01
      LASER<2>= 8                        ; * Lines per inch; * JMK01
      LASER<3>= 84                       ; * Page length; * JMK01
      LASER<4>= 16.7                     ; * Characters per inch; * JMK01
      LASER<5>= 0                        ; * JMK01
      LASER<6>= 0                        ; * JMK01
      LASER<7>= 1                        ; * JMK01
      LASER<8>= 0                        ; * JMK01
      MARGIN = 0                         ; * JMK01
      PRINT ESC:"E"                      ; * JMK01
      PRINT ESC:"&l":LASER<1>:"O":       ; * Set landscape; * JMK01
      PRINT ESC:"&l":LASER<2>:"D":       ; * Lines per inch; * JMK01
      PRINT ESC:"&l":LASER<3>:"F":       ; * Page length; * JMK01
      PRINT ESC:"(s":LASER<4>:"h":       ; * Set pitch; * JMK01
      PRINT LASER<5>:"p":                ; * Proportional printing; * JMK01
      PRINT LASER<6>:"s":                ; * Italics off; * JMK01
      PRINT "0T":                        ; * Line Printer Font; * JMK01
      PRINT ESC:"&l":LASER<7>:"H":       ; * Bin selection; * JMK01
      PRINT ESC:"&a":MARGIN:"L":         ; * JMK01
      PRINT ESC:'"g':LASER<8>:'F':       ; * Paper output; * JMK01

      HED=''
      CALL PAGE.HEAD(PROG.NAME,'AFS Logistics, LLC',PROG.DESC,'','',132,HED)     ; * JMK01
      HEADING HED
      PRINT '#'"R#3":'  ':'Code'"L#4":'  ':'Description'"L#30":'  ':'Type'"L#4"  ; * JMK01
      NVAL=DCOUNT(DISP.REC<1>,@VM)       ; * JMK01
      FOR VAL=1 TO NVAL                  ; * JMK01
         CODE=DISP.REC<1,VAL>            ; * JMK01
         DESC=DISP.REC<2,VAL>            ; * JMK01
         TYPE=DISP.REC<3,VAL>            ; * JMK01
         PRINT VAL"R#3":'  ':CODE"L#4":'  ':DESC"L#30":'  ':TYPE"L#1"  ; * JMK01
      NEXT VAL                           ; * JMK01
      PRINTER OFF                        ; * JMK01
      HUSH ON                            ; * JMK01
      PRINTER CLOSE                      ; * JMK01
      HUSH OFF                           ; * JMK01
      EXECUTE 'TERM ,,,,,,132,60'        ; * JMK01
      RETURN                             ; * JMK01
