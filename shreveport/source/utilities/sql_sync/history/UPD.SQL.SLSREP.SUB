      SUBROUTINE UPD.SQL.SLSREP.SUB(VIN,VOUT,SLSREP.REC)
****************************************************
* Program name : UPD.SQL.SLSREP.SUB
* Author       : Owen Holman
* Date written : January 2002
* Library      : FBBP
* COPYRIGHT (C) 2016 - AFS LOGISTICS LLC - ALL RIGHTS RESERVED.
*
*  UPDATE SQL DB WITH AR.SLSMAN.1000.1 FILE
*
* Last updated by AFS (gbino) at 07:57:18 on 05/06/2019.
*
* Start modification history
* 061912 - JMK01 - Choose development SQL server if not production server AFS
* End modification history
******************************************************

* Include the ODBC definitions

$INCLUDE UNIVERSE.INCLUDE ODBC.H
      CALL GET.SERVER.INFO(SVR.VIN,SVR.VOUT)       ; * JMK01
      SVR.NAME=SVR.VOUT<1>               ; * JMK01

      USER=VIN<1>
      SLSREP.ID=VIN<2>

      form = "T##########"
      dash = "----------"
      Expect = ""
      NULL=""
      EOL=CHAR(13):CHAR(10)
      ERROR.CNT=0
10:   ERRORS=''

      GOSUB SETUP.SQL

* DEFINE FIELDS TO UPDATE FROM SLSREP.REC HERE

      SQL.SLSREP.ID = SLSREP.ID+0        ; * SLSREP NUMBER
      SQL.SLSREP.NAME = SLSREP.REC<1>[1,50]        ; * NAME
      SQL.SLSREP.SMGR.ID = SLSREP.REC<10>+0        ; * SALES MANAGER
      SQL.SLSREP.AMGR.ID = SLSREP.REC<11>+0        ; * ACCOUNT MANAGER
      SQL.SLSREP.EMAIL = SLSREP.REC<8>[1,50]
      IF SQL.SLSREP.EMAIL='NONE' THEN
         SQL.SLSREP.EMAIL=''
      END
      Fn = "SQLExecute"
      STATUS = SQLExecute(SLSREPENV)
      STMTENV=SLSREPENV
      ENVSRC='SLSREP'
      GOSUB CKSTMTENV


******************************************************
THE.END:***
******************************************************
*     End of program
*
* Now just clean up the environment and leave
*
      Fn = "SQLFreeStmt"
      STATUS = SQLFreeStmt(SLSREPENV, SQL.DROP)
      STMTMENV=SLSREPENV
      ENVSRC='SLSREP'
      GOSUB CKSTMTENV

      Fn = "SQLDisconnect"
      STATUS = SQLDisconnect(CONENV)
      GOSUB CKCONENV

      Fn = "SQLFreeConnect"
      STATUS = SQLFreeConnect(CONENV)
      GOSUB CKCONENV

      Fn = "SQLFreeEnv"
      STATUS = SQLFreeEnv(DBCENV)
      IF STATUS <> SQL.SUCCESS THEN
         ERRORS<1,-1>="Failed to release ODBC environment"
      END

      IF ERRORS#'' THEN
         ERROR.CNT+=1
         IF ERROR.CNT>3 THEN
            MVIN=''
            MVIN<1>='UPD.SQL.Group@afs.net'
            MVIN<4>='AFS_UPD.SQL.SLSREP.SUB'
            MVIN<5>='SQL SLSREP Update Errors'
            TEXT='On ':DATE()'D2/':' ':OCONV(TIME(),'MTHS'):', ':USER:' updated '
            TEXT<1,-1>='SLSREP ':SLSREP.ID:' ':SLSREP.REC<2>:' but did not succeed in updating SQL due to:'
            TEXT<1,-1>=' '
            TEXT<1,-1>=ERRORS
            MVIN<6>=TEXT
            MVIN<7>=1
            CALL SENDMAIL(MVIN,MVOUT)
         END ELSE
            GO 10
         END
      END

      RETURN

      STOP
******************************************************
SETUP.SQL:*
******************************************************
* Create an ODBC environment & a connection environment. Use the
* SSQLSetConnectOption to specify the Operating system user ID and
* password.

      STATUS = SQLAllocEnv(DBCENV)
      IF STATUS <> SQL.SUCCESS THEN
         ERRORS<1,-1>="Failed to allocate an ODBC environment"
         RETURN TO THE.END
      END

      STATUS = SQLAllocConnect(DBCENV,CONENV)
      IF STATUS <> SQL.SUCCESS THEN
         ERRORS<1,-1>="Failed to allocate a CONNECT environment"
         RETURN TO THE.END
      END

*
* Gather the data source name, userid and password for the server O/S and
* information for the DBMS at the server. The DBMS info is often also
* a user name and password, but not in all cases.
*

      BEGIN CASE                         ; * JMK01
         CASE SVR.NAME='AFS'             ; * JMK01
            SOURCE='PRODODS'             ; * JMK01
            UID="uv_sql"                 ; * JMK01
            PWD="4@gAbreg"               ; * JMK01
         CASE 1                          ; * JMK01
            SOURCE='DEVODS'              ; * JMK01
            UID="uv_sql"                 ; * JMK01
            PWD="4@gAbreg"               ; * JMK01
      END CASE                           ; * JMK01
*
* Establish a session to the specified data source
*
*     PRINT "Connecting to data source: ": SOURCE
      Fn = "SQLConnect"
      STATUS = SQLConnect(CONENV,SOURCE,UID,PWD)
      GOSUB CKCONENV
*
* Now allocate an SQL statement environment
* to be used for executing SQL statements
*
      Fn = "SQLAllocStmt"
      STATUS = SQLAllocStmt(CONENV,SLSREPENV)
      GOSUB CKCONENV

*
* Note that we are using parameter markers in the SQL statement to facilitate
* loading multiple rows of data efficiently
*
      OPT.STR = STR(' ?,',5)
      OPT.STR = OPT.STR[2,LEN(OPT.STR)-2]
      INST1 = "{CALL PX_UpdSalesRep (":OPT.STR:")}"          ; * 24 PLACEHOLDERS

      ROWNUM = 0
      Fn = "SQLSetParam"
*
* DEFINE THE FIELD UPDATE PARAMETERS USING THE FOLLOWING FORMAT
*     STATUS = SQLSETPARAM(STMTENV, COL, SQL.B.BASIC, SQL.DATA.TYPE, PREC, SCALE, VAR.NAME) ; GOSUB CKSTMTENV
*
      STATUS = SQLSETPARAM(SLSREPENV, 1, SQL.B.BASIC, SQL.VARCHAR, 20, 0, SQL.SLSREP.ID) ; STMTENV=SLSREPENV ; GOSUB CKSTMTENV
      STATUS = SQLSETPARAM(SLSREPENV, 2, SQL.B.BASIC, SQL.VARCHAR, 50, 0, SQL.SLSREP.NAME) ; STMTENV=SLSREPENV ; GOSUB CKSTMTENV
      STATUS = SQLSETPARAM(SLSREPENV, 3, SQL.B.BASIC, SQL.VARCHAR, 20, 0, SQL.SLSREP.SMGR.ID) ; STMTENV=SLSREPENV ; GOSUB CKSTMTENV
      STATUS = SQLSETPARAM(SLSREPENV, 4, SQL.B.BASIC, SQL.VARCHAR, 20, 0, SQL.SLSREP.AMGR.ID) ; STMTENV=SLSREPENV ; GOSUB CKSTMTENV
      STATUS = SQLSETPARAM(SLSREPENV, 5, SQL.B.BASIC, SQL.VARCHAR, 50, 0, SQL.SLSREP.EMAIL) ; STMTENV=SLSREPENV ; GOSUB CKSTMTENV

      Fn = "SQLPrepare"
      STATUS = SQLPrepare(SLSREPENV, INST1)
      STMTENV=SLSREPENV
      ENVSRC='SLSREP'
      GOSUB CKSTMTENV

      RETURN
******************************************************
CKCONENV:
******************************************************
      COUNT = -1
      IF STATUS EQ -2 THEN
         ERRORS<1,-1>="INVALID CONNECTION HANDLE"
         STMT='BBSEND GREGB UPD.SQL.SLSREP.SUB INVALID CONNECTION HANDLE ABORT'
         EXECUTE STMT CAPTURING RESULTS
         ABORT
      END
      IF STATUS NE 0 THEN
201:     *
         ST = SQLERROR(-1,CONENV,-1,STATE,NATCODE,ERRTXT)
         IF ST <> SQL.NO.DATA.FOUND
         THEN
            COUNT = 1
            IF Expect NE 0 AND STATE = Expect AND ST <> SQL.NO.DATA.FOUND THEN
               ERRORS<1,-1>="Allowed error of ":STATE:" returned for func ":Fn
               GOTO 299
            END ELSE
               ERRORS<1,-1>="Status for ":Fn:" call is: ":STATUS
               ERRORS<1,-1>="SQLSTATE,NATCOD are:" : STATE:"  ":NATCODE
               ERRORS<1,-1>="Error text is"
               ERRORS<1,-1>="    " : ERRTXT
            END
            IF ST = SQL.SUCCESS THEN GOTO 201
         END
         IF STATUS = -1 AND COUNT = 1 THEN
            ERRORS<1,-1>="EXITING CKCONENV"
            RETURN TO THE.END
         END
      END
299:  *
      RETURN
******************************************************
CKSTMTENV:
******************************************************
      IF STATUS EQ -2 THEN
         ERRORS<1,-1>="INVALID STATEMENT HANDLE"
         STMT='BBSEND GREGB UPD.SQL.SLSREP.SUB INVALID STATEMENT HANDLE ABORT'
         EXECUTE STMT CAPTURING RESULTS
         ABORT
      END

      IF STATUS EQ 100 THEN RETURN
      IF STATUS NE 0 THEN
301:     *
         ST = SQLERROR(-1,-1,STMTENV,STATE,NATCODE,ERRTXT)
         IF ST <> SQL.NO.DATA.FOUND
         THEN
            COUNT = 1
            IF Expect NE 0 AND STATE = Expect AND ST <> SQL.NO.DATA.FOUND THEN
               ERRORS<1,-1>="Allowed error of ":STATE:" returned for func ":Fn
               GOTO 399
            END ELSE
               ERRORS<1,-1>="Status for ":Fn:" call is: ":STATUS
               ERRORS<1,-1>="SQLSTATE,NATCOD are:" : STATE:"  ":NATCODE
               ERRORS<1,-1>="Error text is"
               ERRORS<1,-1>="    " : ERRTXT
            END
            IF ST = 0 THEN GOTO 301
         END
         IF STATUS = -1 AND COUNT = 1 THEN
            ERRORS<1,-1>="EXITING CKSTMTENV"
            RETURN TO THE.END
         END
      END
399:  *
      RETURN
