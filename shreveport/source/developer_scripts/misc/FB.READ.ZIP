      SUBROUTINE FB.READ.ZIP(ZIP.VAR,CITY,STATE)
***********************************************************************
* SUBROUTINE TO FIND CITY AND STATE ASSOCIATED WITH ZIP CODE
*
* FREIGHT BILLING PACKAGE
*
* AUTHOR: BRIAN M. QUEEN
* DATE: APRIL 27, 1992
*
* THIS PROGRAM IS A PROPRIETARY PRODUCT OF AUTOMATED FREIGHT SYSTEMS
* AND MAY NOT BE USED, COPIED OR MODIFIED WITHOUT THE EXPRESS
* WRITTEN CONSENT OF SAME.
***********************************************************************
* Last updated by AFS.SOURCE (valerie) at 10:16:27 on 07/21/1994.
*   UPDATED FOR UNIVERSE.
* 070602 - JMK01 - ADDED CANADIAN TO ZIP TABLE
* 100402 - JMK02 - ADD CANADIAN ZIP FILE FOR READ
* 032608 - JMK03 - ADD MORE INFORMATION TO UNKNOWN ERRORS
* 111609 - JMK04 - T091386 - Traffic Survey Canadian Zip issue
***********************************************************************
$INCLUDE PROG.ADMIN INC_OPTIONS
$INCLUDE FILE.DEFS INC_ZIP.CODES
*
      UPL.VIN = 'FBBP' ; UPL.VIN<2> = 'FB.READ.ZIP' ; UPL.VOUT = ''    ; * NPR_UPL 04/23/2010
*****      CALL UPD.PROGRAM.LOG(UPL.VIN,UPL.VOUT)       ; * NPR_UPL 04/23/2010
      MAT ZIP.REC = ''
      CITY = ''
      STATE = ''
*
      OPEN '','ZIPS.CODES' TO F.ZIPS ELSE
         CALL OPEN.ABORT("ZIPS",PROG.NAME)
      END
*
* Get zip code
*
****      USER = OCONV(SYSTEM(19),'MCU')
      BEGIN CASE
         CASE ZIP.VAR MATCHES '5N' OR ZIP.VAR MATCHES '5N"M"' OR ZIP.VAR MATCHES '1A1N1A1N1A1N'
            MATREAD ZIP.REC FROM F.ZIPS, ZIP.VAR THEN
               CITY = ZIP.CITY
               STATE = ZIP.ST
            END ELSE
****      IF USER = 'GBINO' THEN DEBUG
               VIN=ZIP.VAR
               CALL ADD.ZIP(VIN,VOUT)
               CITY=VOUT<1>
               STATE=VOUT<2>
               IF CITY='' THEN CALL ELINE(ZIP.VAR:' City unknown')     ; * JMK03
            END
         CASE LEN(ZIP.VAR) = 3
            READ ZIP3.REC FROM F.ZIPS,'3.DIGIT' ELSE ZIP3.REC = ''
            ZIP3.REC<1,-1>='CN'          ; * JMK01
            ZIP3.REC<2,-1>='A0A'         ; * JMK01
            ZIP3.REC<3,-1>='ZZZ'         ; * JMK01
            NUM.VALS = DCOUNT(ZIP3.REC<1>,CHAR(253))
            FOR I = 1 TO NUM.VALS WHILE STATE = ''
               IF ZIP.VAR => ZIP3.REC<2,I> & ZIP.VAR <= ZIP3.REC<3,I> THEN
                  STATE = ZIP3.REC<1,I>
               END
            NEXT I
            IF STATE = '' THEN
               CALL ELINE(ZIP.VAR:' State unknown')          ; * JMK03
            END
         CASE 1
      END CASE
*
      RETURN
*
************************************************************************
*
   END
