$OPTIONS EXEC.EQ.PERF
$OPTIONS INFO.ABORT
      SUBROUTINE FB.LASER.SET(FORMAT,WIDTH,DEPTH,LPI)
$OPTIONS HEADER.EJECT
ERRMSG=''
* SUBROUTINE TO OUTPUT COMMANDS TO SET LASER FOR DESIRED FORMAT
*
* FREIGHT BILLING PACKAGE
*
* AUTHOR: Brian M. Queen
* DATE: September 3, 1993
*
      PRINT.STAT=SYSTEM(1)
      BEGIN CASE
        CASE FORMAT='S'; * Sideways format
          LASER=''
          LASER<1>=1; * Landscape
          LASER<2>=LPI; * Lines per inch
          LASER<3>=DEPTH; * Page length
          LASER<4>=16.7; * Characters per inch
          LASER<5>=0
          LASER<6>=0
          LASER<7>=1
          LASER<8>=0
        CASE FORMAT='R'; * Report format
          LASER=''
          LASER<1>=0; * Portrait
          LASER<2>=LPI; * Lines per inch
          LASER<3>=DEPTH; * Page length
          LASER<4>=16.7; * Characters per inch
          LASER<5>=0
          LASER<6>=0
          LASER<7>=1
          LASER<8>=0
        CASE 1
          RETURN
      END CASE
*
      IF WIDTH GT 154 THEN TEMP=154 ELSE TEMP=WIDTH
      LENGTH=DEPTH-1
      EXECUTE 'TERM ,,,,,,':TEMP:',':LENGTH
*
      PRINTER ON
      MARGIN=0
      ESC=CHAR(27)
      PRINT ESC:"E"
      PRINT ESC:"&l":LASER<1>:"O":; * Set portrait
      PRINT ESC:"&l":LASER<2>:"D":; * Lines per inch
      PRINT ESC:"&l":LASER<3>:"F":; * Page length
      PRINT ESC:"(s":LASER<4>:"h":; * Set pitch
      PRINT LASER<5>:"p":; * Proportional printing
      PRINT LASER<6>:"s":; * Italics off
*     PRINT "7v":; * Points
      PRINT "6T":; * Font
      PRINT ESC:"&l":LASER<7>:"H"; * Bin selection
      PRINT ESC:"&a":MARGIN:"L"
      PRINT ESC:'"g':LASER<8>:'F'; * Paper output
      PRINT ESC:"&k0G"
*     PRINTER CLOSE
      IF PRINT.STAT ELSE PRINTER OFF
      RETURN
