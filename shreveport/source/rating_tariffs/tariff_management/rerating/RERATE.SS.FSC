      SUBROUTINE RERATE.SS.FSC(RVIN,RVOUT)
*
 UPL.VIN = 'FBBP' ;  UPL.VIN<2> = 'RERATE.SS.FSC' ; UPL.VOUT = ''                    ;* NPR_UPL 04/23/2010
 CALL UPD.PROGRAM.LOG(UPL.VIN,UPL.VOUT)                                                           ;* NPR_UPL 04/23/2010
* FREIGHT BILLING PACKAGE
*
* AUTHOR: OWEN HOLMAN
* DATE: JUNE 15, 2001
*
* THIS PROGRAM IS A PROPRIETARY PRODUCT OF AUTOMATED FREIGHT SYSTEMS
* AND MAY NOT BE USED, COPIED OR MODIFIED WITHOUT THE EXPRESS
* WRITTEN CONSENT OF SAME.
*
****************************************************************
* MODIFICATIONS:
****************************************************************
* Initialize variables
****************************************************************
      CRT @(0,20):@(-3):
      TEXT='Reviewing Rates...'
      CALL CENTER(TEXT,80)
      CRT @(0,20):TEXT
      DATA.START=4                       ; * Line number that 1st line of data is on
      BILLDATA.USER=RVIN<1>              ; * Name of BILLDATA FILE THAT DATA IS IN
      ITEM.NAME=RVIN<2>                  ; * Name of "File" in BILLDATA
      CLIENT.OPT=RVIN<3>                 ; * 'OLA' Client Number (Rating Files)
      RR.EFF.DATE=RVIN<4>
      CARRIER.OPT=RVIN<5>                ; * Rate all (Least Cost) or a specific carrier
      FAK.OPT=RVIN<6>
      ZIP.OPT=RVIN<7>

      FAK.ARRAY=''
      NVAL=DCOUNT(FAK.OPT<1>,@VM)
      FOR VAL=1 TO NVAL
         FAK.LVL=FAK.OPT<1,VAL>
         CLASS.RANGE=FIELD(FAK.LVL,';',1)
         CLASS.LOW=FIELD(CLASS.RANGE,'-',1)
         CLASS.HIGH=FIELD(CLASS.RANGE,'-',2)
         IF CLASS.HIGH='' THEN CLASS.HIGH=CLASS.LOW
         CLASS.FAK=FIELD(FAK.LVL,';',2)
         FAK.ARRAY<1,VAL>=CLASS.LOW
         FAK.ARRAY<2,VAL>=CLASS.HIGH
         FAK.ARRAY<3,VAL>=CLASS.FAK
      NEXT VAL

      CALL CHANNEL(CH.NUM)
      CALL GET.USER(USER)

      CLIENT.AUDIT=1
      ERROR=''
      PROG.NAME='RERATE.SS.FSC'
      PROG.DESC="(Re) Rate fuel on shipments using OLA setups"
      PACK.NAME="Freight Billing"
      CONTR.AMT=''
      BENCH.AMT=''
      CARRIER.EST.ARRAY=''
      ERROR.ARRAY=''
      ADDZIP.ARRAY=ZIP.OPT
      ADDZIP.NVAL=DCOUNT(ZIP.OPT<1>,@VM)


*
* Open general files
*
      OPEN 'BILLDATA.':BILLDATA.USER TO F.BILLDATA ELSE
         CALL OPEN.ABORT("BILLDATA.":BILLDATA.USER,PROG.NAME)
      END
      OPEN '','CLIENTS' TO F.CLIENTS ELSE
         ERROR = "CANNOT OPEN FILE CLIENTS"
         GO 9000
      END
      OPEN '','FB.TABLES' TO F.TABLES ELSE
         ERROR = "CANNOT OPEN FILE F.TABLES"
         GO 9000
      END
      OPEN '','CARRIERS' TO F.CARRIERS ELSE
         ERROR = "CANNOT OPEN FILE CARRIERS"
         GO 9000
      END
      OPEN '','FB.NFSC' TO F.NFSC ELSE
         ERROR = "CANNOT OPEN FILE FB.NFSC"
         GO 9000
      END
      OPEN '','FB.TABLES' TO F.TABLES ELSE
         ERROR = "CANNOT OPEN FILE FB.TABLES"
         GO 9000
      END

*---- Open mileage input and output files

      OPEN '','PCIN' TO F.PCIN ELSE
         ERROR = "CANNOT OPEN FILE PCIN"
         GO 9000
      END
      OPEN '','PCOUT' TO F.PCOUT ELSE
         ERROR = "CANNOT OPEN FILE  "
         GO 9000
      END
      OPEN '','ZIPS.CODES' TO F.ZIPS ELSE
         ERROR = "CANNOT OPEN ZIPS FILE "
         GO 9000
      END
      OPEN '','CAN.ZIP.CODES' TO F.CAN.ZIPS ELSE
         ERROR = "CANNOT OPEN CAN.ZIP.CODES FILE "
         GO 9000
      END

*
* Read Zip for Names
*
      FOR ADDZIP.VAL=1 TO ADDZIP.NVAL
         ZIP=ADDZIP.ARRAY<1,ADDZIP.VAL>
         READ ZIP.REC FROM F.ZIPS,ZIP THEN
            ADDZIP.ARRAY<2,ADDZIP.VAL>=ZIP.REC<1>
            ADDZIP.ARRAY<3,ADDZIP.VAL>=ZIP.REC<2>
         END
      NEXT ADDZIP.VAL
*
* Read Source Data
*

      READ DA FROM F.BILLDATA,ITEM.NAME ELSE
         CALL ELINE('BILLDATA.':BILLDATA.USER:' ':ITEM.NAME:' is not on file.. Aborting!')
         ABORT
      END
*     DA=@AM:DA

      TAB=CHAR(9)
      DA=CHANGE(DA,TAB,@VM)
      NEW.DA=''
      NROWS=DCOUNT(DA,@AM)
      ORGCOLVAL=DCOUNT(DA<4>,@VM)
      HDG.LINE=DA<3>
      HDG.LINE<1,225>='Orig Contr Amt'
      HDG.LINE<1,226>='Orig Contr FSC Amt'
      HDG.LINE<1,227>='Orig BM Amt'
      HDG.LINE<1,228>='Orig BM "N" FSC Amt'
      HDG.LINE<1,229>='Orig BM "YC" FSC Amt'
      HDG.LINE<1,230>='Orig BM "O" FSC Amt'
      HDG.LINE<1,231>='Rating Errors'
      IF ADDZIP.ARRAY#'' THEN
         FOR ADDZIP.VAL=1 TO ADDZIP.NVAL
            IF ADDZIP.ARRAY<1,ADDZIP.VAL>#'' THEN
               HDG.LINE<1,118+((ADDZIP.VAL-1)*2)+1>=ADDZIP.ARRAY<1,ADDZIP.VAL>:'-':ADDZIP.ARRAY<2,ADDZIP.VAL>:' ':ADDZIP.ARRAY<3,ADDZIP.VAL>
               HDG.LINE<1,118+((ADDZIP.VAL-1)*2)+2>='Rating Errors'
            END
         NEXT ADDZIP.VAL
      END
      LOCATE 'Client #' IN HDG.LINE<1> SETTING CLIPOS ELSE CLIPOS=0
      LOCATE 'Carrier #' IN HDG.LINE<1> SETTING CARPOS ELSE CARPOS=1
      LOCATE 'Carrier Name' IN HDG.LINE<1> SETTING CARNPOS ELSE CARNPOS=2
      LOCATE 'Pro #' IN HDG.LINE<1> SETTING PPOS ELSE PPOS=3
      LOCATE 'Bill Date' IN HDG.LINE<1> SETTING BPOS ELSE BPOS=4
      LOCATE 'Origin Zip' IN HDG.LINE<1> SETTING OPOS ELSE OPOS=6
      LOCATE 'Destin Zip' IN HDG.LINE<1> SETTING DPOS ELSE DPOS=9
      LOCATE 'I/O' IN HDG.LINE<1> SETTING IPOS ELSE IPOS=5
      LOCATE 'Benchmark Charge' IN HDG.LINE<1> SETTING BMPOS ELSE BMPOS=13
      LOCATE 'Actual Charge' IN HDG.LINE<1> SETTING ACPOS ELSE ACPOS=14
*
* Process the data
*
      BRK.CARRIER=''
      PRV.CLIENT.ID=''
      BRK.PRO=''
      ROW.PER=''
      FOR ROW = 1 TO NROWS
         CALL PROG.BAR(22,ROW,NROWS,ROW.PER)
         IF ROW < DATA.START THEN
            NEW.DA<ROW>=DA<ROW>
            IF ROW='3' THEN
               NEW.DA<ROW>=HDG.LINE
            END
            GO SKIP.LINE
         END
         LINE=DA<ROW>
         LINE<1,118>=''
         IF CLIPOS=0 THEN
            CLIENT.ID=CLIENT.OPT
         END ELSE
            CLIENT.ID=LINE<1,CLIPOS>'R%5'
         END
         IF CLIENT.ID#PRV.CLIENT.ID THEN
*
* Open client's files.
*
            AUDIT.CLIENT=1
            AUDIT.CLIENT.ID = CLIENT.ID
            OPEN '','FB.AUDIT,':CLIENT.ID TO F.AUDIT THEN
               READV SUB.CLIENT FROM F.AUDIT,'SUBSTITUTE',1 THEN
                  AUDIT.CLIENT.ID = SUB.CLIENT
                  OPEN '','FB.AUDIT,':AUDIT.CLIENT.ID TO F.AUDIT ELSE CLIENT.AUDIT=0
               END ELSE
                  AUDIT.CLIENT.ID = CLIENT.ID
               END
            END ELSE CLIENT.AUDIT=0
            IF CLIENT.AUDIT=0 THEN
               ERROR = "Client ":CLIENT.ID:" is not setup for freight estimates"
               NEW.DA<-1>=LINE
            END
            OPEN '','FB.AUDIT.TABLES,':AUDIT.CLIENT.ID TO F.AUDIT.TABLES ELSE
               ERROR = "Cannot open file FB.AUDIT.TABLES,":AUDIT.CLIENT.ID
               NEW.DA<-1>=LINE
            END
            OPEN '','FB.AUDIT.ZIPS,':AUDIT.CLIENT.ID TO F.AUDIT.ZIPS ELSE
               ERROR = "Cannot open FB.AUDIT.ZIPS,":AUDIT.CLIENT.ID:" No On-Line Audit Allowed!"
               NEW.DA<-1>=LINE
            END
            OPEN '','FB.BILLS.HIST,':AUDIT.CLIENT.ID TO F.BILLS ELSE
               ERROR = "Cannot open FB.BILLS.HIST,":AUDIT.CLIENT.ID:" No On-Line Audit Allowed!"
               NEW.DA<-1>=LINE
            END
            PRV.CLIENT.ID=CLIENT.ID
         END

         CARRIER.ID=LINE<1,CARPOS>'R%5'
*        IF CARRIER.OPT='L' THEN CARRIER.ID='ALL'
         PRO=LINE<1,PPOS>
         IOT=LINE<1,IPOS>
         OSHIP.DATE=LINE<1,BPOS>
         ORIG.ZIP=LINE<1,OPOS>
         IF NUM(ORIG.ZIP) AND ORIG.ZIP#'' THEN ORIG.ZIP=ORIG.ZIP'R%5'
         DEST.ZIP=LINE<1,DPOS>
         IF NUM(DEST.ZIP) AND DEST.ZIP#'' THEN DEST.ZIP=DEST.ZIP'R%5'
* Classes, Weights, & Codes
         PRV.CLASS=''
         CLASSES=''
         WEIGHTS=''
         MISC.CHG.CODES=''
         MISC.CHG.AMTS=''
         FOR Y = 1 TO 14
            CLASS=LINE<1,88+(Y-1)*5>
            IF CLASS='' AND PRV.CLASS#'' THEN CLASS=PRV.CLASS
            IF FAK.ARRAY#'' THEN
               LOCATE CLASS IN FAK.ARRAY<2> BY 'AR' SETTING FAKPOS THEN
                  CLASS=FAK.ARRAY<3,FAKPOS>
               END
            END
            WEIGHT=LINE<1,89+(Y-1)*5>
            CODE=LINE<1,86+(Y-1)*5>
            AMT=LINE<1,90+(Y-1)*5>
            BEGIN CASE
               CASE CODE='FRT'
               CASE CODE='TMIN'
               CASE CODE#''
                  MISC.CHG.CODES<1,-1>=CODE
                  MISC.CHG.AMTS<1,-1>=AMT
            END CASE
            IF CLASS#'' AND WEIGHT#'' THEN
               CLASSES<1,-1>=CLASS
               WEIGHTS<1,-1>=WEIGHT
               PRV.CLASS=CLASS
            END
         NEXT Y
         ERROR=''
         A.VOUT=''
         FSC.CVOUT=''
         FSC.BVOUT=''
         BILL.ID=CARRIER.ID:'*':PRO:'*0'
         READ BILL.REC FROM F.BILLS,BILL.ID ELSE BILL.REC=''
         MODE='ORIG'
         ORATE.DATE=OSHIP.DATE
         VIN.ARRAY=''
         CARRIER.ID=LINE<1,CARPOS>'R%5'
         IF CARRIER.OPT='L' THEN CARRIER.ID='ALL'
         ORIG.CARRIER.ID=''
         ORIG.CARRIER.NAME=''
*           IF CARRIER.OPT='L' THEN CARRIER.ID='ALL'
         GOSUB RATE.SHIPMENT
         IF CARRIER.OPT='L' THEN
            IF CARRIER.EST.ARRAY#'' THEN
               APP.CARRIER.ID=CARRIER.EST.ARRAY<2,1>
               APP.CARRIER.NAME=CARRIER.EST.ARRAY<3,1>
               ERROR=CARRIER.EST.ARRAY<7,1>
               CONTR.AMT=CARRIER.EST.ARRAY<8,1>
               CONTR.FSC.AMT=CARRIER.EST.ARRAY<9,1>
               BENCH.AMT=CARRIER.EST.ARRAY<10,1>
               N.BENCH.FSC.AMT=CARRIER.EST.ARRAY<11,1>
               YC.BENCH.FSC.AMT=CARRIER.EST.ARRAY<12,1>
               O.BENCH.FSC.AMT=CARRIER.EST.ARRAY<13,1>
            END ELSE
               CARRIER.ID=LINE<1,CARPOS>'R%5'
               APP.CARRIER.ID=LINE<1,CARPOS>'R%5'
               APP.CARRIER.NAME=LINE<1,CARNPOS>
               ERROR=''
               A.VOUT=''
               GOSUB RATE.SHIPMENT
            END
         END ELSE
            APP.CARRIER.ID=LINE<1,CARPOS>'R%5'
            APP.CARRIER.NAME=LINE<1,CARNPOS>
         END

         NVAL=DCOUNT(LINE<1>,@VM)
         IF NVAL>224 THEN
            FOR VAL=225 TO NVAL
               DEL LINE<1,225>
            NEXT VAL
         END

         LINE<1,225>=CONTR.AMT'R26'
         LINE<1,226>=CONTR.FSC.AMT'R26'
         LINE<1,227>=BENCH.AMT'R26'
         LINE<1,228>=N.BENCH.FSC.AMT'R26'
         LINE<1,229>=YC.BENCH.FSC.AMT'R26'
         LINE<1,230>=O.BENCH.FSC.AMT'R26'

         IF ERROR#'' THEN
            LINE<1,231>=ERROR
         END
         IF ADDZIP.ARRAY#'' THEN
            FOR ADDZIP.VAL=1 TO ADDZIP.NVAL
               BEGIN CASE
                  CASE IOT='O'
                     ORIG.ZIP=ADDZIP.ARRAY<1,ADDZIP.VAL>
                     ORIG.CITY=ADDZIP.ARRAY<2,ADDZIP.VAL>
                     ORIG.ST=ADDZIP.ARRAY<3,ADDZIP.VAL>
                  CASE IOT='I'
                     DEST.ZIP=ADDZIP.ARRAY<1,ADDZIP.VAL>
                     DEST.CITY=ADDZIP.ARRAY<2,ADDZIP.VAL>
                     DEST.ST=ADDZIP.ARRAY<3,ADDZIP.VAL>
                  CASE IOT='T'
               END CASE
               IF CARRIER.OPT='L' THEN CARRIER.ID='ALL'
               GOSUB RATE.SHIPMENT
               IF CARRIER.OPT='L' THEN
                  IF CARRIER.EST.ARRAY#'' THEN
                     APP.CARRIER.ID=CARRIER.EST.ARRAY<2,1>
                     APP.CARRIER.NAME=CARRIER.EST.ARRAY<3,1>
                     ERROR=CARRIER.EST.ARRAY<7,1>
                     BENCH.AMT=CARRIER.EST.ARRAY<8,1>
                     BENCH.FSC.AMT=CARRIER.EST.ARRAY<9,1>
                     CONTR.AMT=CARRIER.EST.ARRAY<10,1>
                     CONTR.FSC.AMT=CARRIER.EST.ARRAY<11,1>
                     N.BENCH.FSC.AMT=CARRIER.EST.ARRAY<11,1>
                     YC.BENCH.FSC.AMT=CARRIER.EST.ARRAY<12,1>
                     O.BENCH.FSC.AMT=CARRIER.EST.ARRAY<13,1>
                  END ELSE
                     CARRIER.ID=LINE<1,CARPOS>'R%5'
                     APP.CARRIER.ID=LINE<1,CARPOS>'R%5'
                     APP.CARRIER.NAME=LINE<1,CARNPOS>
                     ERROR=''
                     A.VOUT=''
                     GOSUB RATE.SHIPMENT
                  END
               END ELSE
                  APP.CARRIER.ID=LINE<1,CARPOS>'R%5'
                  APP.CARRIER.NAME=LINE<1,CARNPOS>
               END
               LINE<1,231+((ADDZIP.VAL-1)*2)+1>=CONTR.AMT'R26'
               IF ERROR#'' THEN
                  LINE<1,231+((ADDZIP.VAL-1)*2)+2>=ERROR
               END
            NEXT ADDZIP.VAL
         END
         NEW.DA<-1>=LINE
         BLS.CARR.CHG=ICONV(LINE<1,BMPOS>,'MD2')
         BLS.ACT.CHG=ICONV(LINE<1,ACPOS>,'MD2')
*         RTD.CARR.CHG=ORIG.BENCH.AMT
*         RTD.ACT.CHG=ORIG.CONTR.AMT
SKIP.LINE: ****
      NEXT ROW
      NEW.DA=CHANGE(NEW.DA,@VM,TAB)
      WRITE NEW.DA ON F.BILLDATA,'FSC-':ITEM.NAME
      RVOUT<1>='FSC-':ITEM.NAME
      ERROR=''
      RETURN
      STOP
RATE.SHIPMENT: ***
*    X=LEN(OSHIP.DATE)
*    CALL DATE.IN(OSHIP.DATE,"",1)
      RATE.DATE=ICONV(ORATE.DATE,'D')
      IF RATE.DATE='' THEN
         ERROR='Invaid Ship Date of ':ORATE.DATE
         RETURN
      END
      IF RATE.DATE > DATE() THEN
         ERROR='Ship date cannot be in the future. Use today as the date.'
         RETURN
      END
*     IF CARRIER.OPT='L' THEN CARRIER.ID='ALL'
      NUM.WEIGHTS = DCOUNT(WEIGHTS,@VM)
      TOT.WEIGHT = 0
      TOT.WEIGHT = SUM(WEIGHTS)
      IF TOT.WEIGHT GT 15000 THEN
         ERROR = "Total weight exceeds program limits. Please contact AFS"
         RETURN
      END
*----
* READ TABLES THAT MAY BE NEEDED
*----
      READ DTARIFF.DATE.XREF FROM F.TABLES,'TARIFF.DATE.XREF' ELSE DTARIFF.DATE.XREF=''
      READ DTARIFFS FROM F.TABLES,'TARIFFS' ELSE DTARIFFS=''
      READ LI.CODES FROM F.TABLES,'LI.CODES' ELSE LI.CODES=''
      READ DCLIENTS FROM F.CLIENTS,CLIENT.ID ELSE
         ERROR = "Client ":CLIENT.ID:" not on file."
         RETURN
      END
      CL.LOG.FSC=DCLIENTS<111>
      LOGISTICS=DCLIENTS<93>='Y'
      CARR.ONLY.FSC=DCLIENTS<189>
      MAX.LTL=15000
      IF DCLIENTS<150>#'' THEN MAX.LTL=DCLIENTS<150>
      IN.QUOTE.PERC = DCLIENTS<136>
      OUT.QUOTE.PERC = DCLIENTS<137>
      TP.QUOTE.PERC = DCLIENTS<138>
      BEGIN CASE
         CASE IOT = "I"
            IOT.DESC = "Inbound"
            SAVINGS.QUOTE.PERC=IN.QUOTE.PERC
         CASE IOT = "O"
            IOT.DESC = "Outbound"
            SAVINGS.QUOTE.PERC=OUT.QUOTE.PERC
         CASE IOT = "T"
            IOT.DESC = "Third Party"
            SAVINGS.QUOTE.PERC=TP.QUOTE.PERC
      END CASE
      IF SAVINGS.QUOTE.PERC = 'NO' THEN
         ERROR="Cannot Rate! Quote percent has not been set up."
         RETURN
      END


*
* Select Carrier(s) (Even if only one) to Loop thru
*
      STMT= 'SELECT FB.AUDIT,':AUDIT.CLIENT.ID
      STMT:=' WITH 1 = "LTLALL"'
      IF CARRIER.ID # 'ALL' THEN
         STMT:=" AND WITH CARRIER# '":CARRIER.ID:"'"
      END ELSE
         STMT:=' AND WITH 11 = "ENABLE"'
         STMT:=' AND WITH 13 = "Y"'      ; * Core Carrier Flag = "Y"
      END
      EXECUTE STMT RTNLIST CHECK.LIST CAPTURING OUTPUT
      IF @SELECTED < 1 THEN
         IF CARRIER.ID # 'ALL' THEN
            ERROR='Carrier ':CARRIER.ID:' is not set up for freight estimates'
         END ELSE
            ERROR='There are no core carriers set up for freight estimates'
         END
         RETURN
      END
*
* Setup AUDIT.VIN
*
*    SHIP.DATE=OSHIP.DATE
*    IF SHIP.DATE='' THEN
*      ERROR='Invaid Ship Date of ':OSHIP.DATE
*      RETURN
*    END

      READ O.ZIP.REC FROM F.ZIPS,ORIG.ZIP ELSE
         READ O.ZIP.REC FROM F.CAN.ZIPS,ORIG.ZIP ELSE
            ERROR=ORIG.ZIP:' ZIP NOT ON FILE, PLEASE TRY ANOTHER ZIP'
            RETURN
         END
      END
      ORIG.CITY = O.ZIP.REC<1>
      ORIG.ST = O.ZIP.REC<2>

      READ D.ZIP.REC FROM F.ZIPS,DEST.ZIP ELSE
         READ D.ZIP.REC FROM F.CAN.ZIPS,DEST.ZIP ELSE
            ERROR=DEST.ZIP:' ZIP NOT ON FILE, PLEASE TRY ANOTHER ZIP'
            RETURN
         END
      END
      DEST.CITY = D.ZIP.REC<1>
      DEST.ST = D.ZIP.REC<2>

      A.VIN=''
      A.VIN<3>=RATE.DATE
      A.VIN<11>=AUDIT.CLIENT.ID
      A.VIN<12>=IOT
      A.VIN<13,1>=ORIG.ZIP
      A.VIN<13,2>=ORIG.CITY
      A.VIN<13,3>=ORIG.ST
      A.VIN<14,1>=DEST.ZIP
      A.VIN<14,2>=DEST.CITY
      A.VIN<14,3>=DEST.ST
      A.VIN<16>=USER

*     A.VIN<17>=1                        ; *Misroute Flag to Ignore Misc Charge Code Errors
      A.VIN<18>=CLASSES
      A.VIN<19>=WEIGHTS
      A.VIN<21>=MISC.CHG.CODES
      A.VIN<22>=MISC.CHG.AMTS
      A.VIN<23>=MISC.CHG.AMTS
      A.VIN<24>=LOGISTICS
      A.VIN<25>=''
      A.VIN<35>=CL.LOG.FSC='N'           ; * Flag to NOT Calculate Log FSC
      A.VIN<46>=MAX.LTL                  ; * JMK02
      A.VIN<48>=DCLIENTS<188>            ; * JMK03 Forced Benchmark Minimum Flag Y/N
      A.VIN<49>=CARR.ONLY.FSC            ; * JMK05
*
**** Begin "Loop" of all Carriers *****
*
      CARRIER.EST.ARRAY=''
*<1>=Amount (MV Lowest to Highest)
*<2>=Carrier #
*<3>=Carrier Name
*<4>=Carrier Address
*<5>=Carrier City
*<6>=Carrier State
*<7>=Error (If Any)
*<8>=Flag
GET.NEXT.CARRIER:
      READNEXT CHECK.CARRIER.ID FROM CHECK.LIST ELSE
         RETURN
      END
      ERROR=''
      IF AUDIT.CLIENT.ID='01505' THEN
         CARR.ONLY.FSC=DCLIENTS<189>
         IF CARR.ONLY.FSC='Y' OR CARR.ONLY.FSC='C' THEN
            BEGIN CASE
               CASE CHECK.CARRIER.ID='00124'
                  CARR.ONLY.FSC='N'
               CASE CHECK.CARRIER.ID='01786'
                  CARR.ONLY.FSC='N'
               CASE CHECK.CARRIER.ID='05156'
                  CARR.ONLY.FSC='N'
               CASE CHECK.CARRIER.ID='08924'
                  CARR.ONLY.FSC='N'
            END CASE
         END
         A.VIN<49>=CARR.ONLY.FSC
      END
      GOSUB GET.CARRIER.INFO
      IF ERROR#'' AND CARRIER.ID # 'ALL' THEN
         ERROR='Carrier ':CARRIER.ID:' is not set up for freight estimates'
         RETURN
      END
**********************************************************************
      A.VIN<10>= CHECK.CARRIER.ID        ; * Add "Current" Carrier to AUDIT.VIN
      A.VIN<50>= CHECK.CARRIER.SCAC      ; * JMK26
      A.VIN<51>= CHECK.CARRIER.TYPE      ; * JMK26
      VIN=A.VIN
      A.VOUT=''
      TEST.VIN=CHANGE(A.VIN,@AM,@VM)
      CALL FB.AUTO(VIN,A.VOUT,D.AUDIT,CONTR.DFSC,BENCH.DFSC,DTARIFFS,DTARIFF.DATE.XREF,LI.CODES,F.AUDIT.TABLES,F.AUDIT.ZIPS,F.PCIN,F.PCOUT,F.TABLES)
      WHY=A.VOUT<24>                     ; * Save for Debugging purposes
      A.VOUT<24>=''                      ; * Kill Why So you can look at A.VOUT with Debugger
      CONTR.AMT = A.VOUT<8>
      BENCH.AMT = A.VOUT<9>
      OLA.ERROR = A.VOUT<1>
      CONTR.FSC.AMT=A.VOUT<46,7>
      N.BENCH.FSC.AMT=A.VOUT<48,7>
      YC.BENCH.FSC.AMT=A.VOUT<47,7>
      O.BENCH.FSC.AMT=A.VOUT<83,7>
*      BEGIN CASE
*         CASE A.VIN<49>='Y'
*            BENCH.FSC.AMT=A.VOUT<47,7>
*         CASE A.VIN<49>='C'
*            BENCH.FSC.AMT=A.VOUT<47,7>
*         CASE A.VIN<49>='O'
*            BENCH.FSC.AMT=A.VOUT<83,7>
*         CASE 1
*            BENCH.FSC.AMT=A.VOUT<48,7>
*      END CASE
*      IF USER='MKERBOW' THEN
*         BEGIN CASE
*            CASE OLA.ERROR#''
*               TEST.VIN<1,-1>=OLA.ERROR
*            CASE A.VOUT<2>='MA'
*               TEST.VIN<1,-1>='Shipping Point Set to N'
*            CASE CONTR.AMT#''
*               TEST.VIN<1,-1>='BA=':BENCH.AMT'R26'
*               TEST.VIN<1,-1>='BF=':BENCH.FSC.AMT'R26'
*               TEST.VIN<1,-1>='CA=':CONTR.AMT'R26'
*               TEST.VIN<1,-1>='CF=':CONTR.FSC.AMT'R26'
*         END CASE
*         VIN.ARRAY<-1>=TEST.VIN
*      END
      AUDIT=A.VOUT<2>
      IF OLA.ERROR # "" THEN
         IF CARRIER.ID # 'ALL' AND CONTR.AMT+0=0 THEN
            ERROR=OLA.ERROR
            RETURN
         END
      END
      IF AUDIT # "Y" AND CARRIER.ID="ALL" THEN GO GET.NEXT.CARRIER
      IF AUDIT = "MA" THEN OLA.ERROR = "Not an approved carrier for this shipment!"
      IF CONTR.AMT+0=0 THEN GO GET.NEXT.CARRIER
      IF LOGISTICS AND BENCH.AMT+0=0 THEN GO GET.NEXT.CARRIER
*
*** Add Quote Percentage Logic Here ***
**********************************************************************

*** Locate and set percentages for quotes and commissions

      IF LOGISTICS THEN
         LOCATE RATE.DATE IN DCLIENTS<84> BY 'AR' SETTING POS ELSE
            POS=POS-1
         END
         COMM.PERC = DCLIENTS<85,POS>
         SAVINGS = BENCH.AMT-CONTR.AMT
         LOG.COMM = SAVINGS*COMM.PERC/100
         CL.SAVINGS = SAVINGS - LOG.COMM
         SAVINGS.QUOTE.AMT = CL.SAVINGS*(1-SAVINGS.QUOTE.PERC/10000)
         QUOTE.AMT=OCONV(CONTR.AMT + LOG.COMM + SAVINGS.QUOTE.AMT,'MR0')
      END ELSE
         QUOTE.AMT=CONTR.AMT
      END
      QUOTE.AMT=CONTR.AMT
*********************************************************************
* Special deal for BFG Quincy Compressor
*********************************************************************
*    IF CLIENT.ID='00981' THEN
*      BEGIN CASE
*        CASE CLIENT.ID='00981' AND IOT='O' AND ORIG.ZIP='62305'; * Quincy
*          QUOTE.AMT=(QUOTE.AMT*1.1)'R0'
*        CASE CLIENT.ID='00981' AND IOT='O' AND ORIG.ZIP='36507'; * Bay Minette
*          QUOTE.AMT=(QUOTE.AMT*1.12)'R0'
*        CASE CLIENT.ID='00761' AND IOT='O'
*          QUOTE.AMT=(QUOTE.AMT*1.12)'R0'
*      END CASE
*    END
*********************************************************************
**********************************************************************
*

      LOCATE (QUOTE.AMT:'_':BENCH.AMT) IN CARRIER.EST.ARRAY<1> BY 'AR' SETTING POS ELSE NULL
      INS (QUOTE.AMT:'_':BENCH.AMT) BEFORE CARRIER.EST.ARRAY<1,POS>
      INS CHECK.CARRIER.ID BEFORE CARRIER.EST.ARRAY<2,POS>
      INS CHECK.CARRIER.NAME BEFORE CARRIER.EST.ARRAY<3,POS>
      INS CHECK.CARRIER.STREET BEFORE CARRIER.EST.ARRAY<4,POS>
      INS CHECK.CARRIER.CITY BEFORE CARRIER.EST.ARRAY<5,POS>
      INS CHECK.CARRIER.STATE BEFORE CARRIER.EST.ARRAY<6,POS>
      INS OLA.ERROR BEFORE CARRIER.EST.ARRAY<7,POS>
      INS CONTR.AMT BEFORE CARRIER.EST.ARRAY<8,POS>
      INS CONTR.FSC.AMT BEFORE CARRIER.EST.ARRAY<9,POS>
      INS BENCH.AMT BEFORE CARRIER.EST.ARRAY<10,POS>
      INS N.BENCH.FSC.AMT BEFORE CARRIER.EST.ARRAY<11,POS>
      INS YC.BENCH.FSC.AMT BEFORE CARRIER.EST.ARRAY<12,POS>
      INS O.BENCH.FSC.AMT BEFORE CARRIER.EST.ARRAY<13,POS>
      GO GET.NEXT.CARRIER
GET.CARRIER.INFO: ***
      READ CHECK.CARRIER.REC FROM F.CARRIERS,CHECK.CARRIER.ID ELSE
         ERROR = "Carrier ":CHECK.CARRIER.ID:" not on file."
         RETURN
      END
      CHECK.CARRIER.NAME = CHECK.CARRIER.REC<1>
      CHECK.CARRIER.STREET = TRIM(CHANGE(CHECK.CARRIER.REC<2>,@VM,' '))
      CHECK.CARRIER.CITY = CHECK.CARRIER.REC<3>
      CHECK.CARRIER.STATE = CHECK.CARRIER.REC<4>
      CHECK.CARRIER.SCAC = CHECK.CARRIER.REC<12>   ; *Added By ROB GAUDET 9/23/2003
      CHECK.CARRIER.STATUS = CHECK.CARRIER.REC<89>           ; * JMK14
      IF CHECK.CARRIER.STATUS = "I" THEN           ; * JMK14
         ERROR = "CARRIER IS NOT ACTIVE - CARRIER# ":CHECK.CARRIER.ID  ; * JMK14
         RETURN                          ; * JMK14
      END                                ; * JMK14
      CHECK.CARRIER.TYPE = 'A'           ; * JMK09
      IF CHECK.CARRIER.REC<44>='6' THEN  ; * JMK09
         CHECK.CARRIER.TYPE='B'          ; * JMK09
      END                                ; * JMK09
      CHECK.CARRIER.TEXT=TRIM(CHECK.CARRIER.ID:' ':CHECK.CARRIER.NAME:' ':CHECK.CARRIER.STREET:' ':CHECK.CARRIER.CITY)
      READ D.AUDIT FROM F.AUDIT, CHECK.CARRIER.ID ELSE
         ERROR = "CARRIER NOT SETUP FOR AUTO AUDIT - CARRIER#":CHECK.CARRIER.ID
         RETURN
      END
      IF D.AUDIT<11>#'ENABLE' THEN
         ERROR='Not Considered in On-Line Audit - Carrier Disabled'
         RETURN
      END
      READ BENCH.DFSC FROM F.NFSC,CHECK.CARRIER.ID ELSE BENCH.DFSC=''
      IF BENCH.DFSC<30>='DISABLE' THEN
         BENCH.DFSC=''                   ; ** No Effect
         CONTR.DFSC=''
      END ELSE
         READ CONTR.DFSC FROM F.NFSC,CHECK.CARRIER.ID:"*":AUDIT.CLIENT.ID ELSE CONTR.DFSC=BENCH.DFSC
         IF CONTR.DFSC<30>='DISABLE' THEN CONTR.DFSC=''
      END
      RETURN

9000: ****

      IF ERROR#'' THEN RVOUT<1>=ERROR

      STOP
