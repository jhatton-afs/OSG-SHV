$OPTIONS HEADER.EJECT
      ERRMSG=''
*
$OPTIONS EXEC.EQ.PERF
$OPTIONS INFO.ABORT
*
* PROGRAM FB.1.1.EMAIL.COLL
* PROGRAM TO MODIFY COLLECTION EMAIL ADDRESSES
*
* FREIGHT BILLING PACKAGE
*
* AUTHOR: Steve Barbee
* DATE: December 4, 2014
* 020915 DRF01 - SysAid 15204 - Gave Kim McQuiston the same access as
*                Ma. Aura Williams

*
* Initialize variables
*
      PROMPT''
      ADD.FLAG=0
      PROG.NAME='FB.1.1.EMAIL.COLL'
      PROG.DESC='CLIENT COLLECTION EMAIL MAINT.'
      PACK.NAME='FREIGHT BILLING'
      PLACE.HOLDER='!@#$%^&*()'
      UPL.VIN = 'FBBP' ; UPL.VIN<2> = 'FB.1.1.EMAIL.COLL' ; UPL.VOUT = ''
      CALL UPD.PROGRAM.LOG(UPL.VIN,UPL.VOUT)
*
* Open files
*
      OPEN '','BCUST' TO F.CLIENT ELSE CALL OPEN.ABORT("BCUST",PROG.NAME)
      OPEN 'DICT','CLIENTS' TO F.DICT.CLIENT ELSE CALL OPEN.ABORT("DICT CLIENTS",PROG.NAME)
      OPEN '','FB.ADJUST.CLIENT' TO F.ADJUST.CLIENT ELSE CALL OPEN.ABORT("FB.ADJUST.CLIENT",PROG.NAME)

      CALL GET.USER(USER)
      DISPLAY.MODE=1
      SUPER.USER=0
      VIN=USER
      CALL SUPER.USER(VIN,VOUT)
      SUPER.USER=VOUT<1>
      IF USER='KMCQUISTON' THEN SUPER.USER=1       ; * This program Only ; * DRF01
      IF USER='MAWILLIAMS' THEN SUPER.USER=1       ; * This program Only
      IF USER='MROSE' THEN SUPER.USER=1  ; * This program Only

*
* Display page heading
*
      CALL AFS.SCR.HEAD(CO.ID,FILE.ID,PERIOD,PROG.NAME,PROG.DESC,PACK.NAME,CO.NAME,TIME.DATE,1)
*
* Enter mode VIEW or CHANGE
*
50:   IF SUPER.USER THEN
         HELP="Enter [V]iew for View only or [C]hange to make changes/additions."
         CALL GEN.IN(0,22,'Enter mode V','',Q,0,1,'','',0,-1,22,0,QI,HELP,0,23)
         QI=OCONV(QI,'MCU')
         BEGIN CASE
            CASE QI='X' OR QI='EX'
               STOP
            CASE QI='V' OR QI=''
               DISPLAY.MODE=1
            CASE QI='C'
               DISPLAY.MODE=0
            CASE 1
               CALL ELINE("Invalid Entry")
               GOTO 50
         END CASE
      END
      IF DISPLAY.MODE THEN
         PROG.DESC=PROG.DESC:' - View'
      END ELSE
         PROG.DESC=PROG.DESC:' - Change'
      END
      CALL AFS.SCR.HEAD(CO.ID,FILE.ID,PERIOD,PROG.NAME,PROG.DESC,PACK.NAME,CO.NAME,TIME.DATE,1)
*
* Enter client number. If not on file then create if desired.
*
100:  CRT @(0,2):@(-3)
      HELP="Enter client number or name to search. [EX]=Exit"
      CALL GEN.IN(0,3,'Enter Client Number : #####','',Q,0,20,'','',2,-5,3,0,QI,HELP,0,23)
      QI=OCONV(QI,'MCU')
      BEGIN CASE
         CASE QI='X' OR QI='EX' OR QI=''
            STOP
         CASE ALPHA(QI)
            CALL SOUNDEX.DISPLAY(QI,'BCUST','SDX.CLIENTS,NAME',2,ITEM.LIST)
            CALL AFS.SCR.REFRESH(PROG.NAME,PROG.DESC,PACK.NAME,CO.NAME,TIME.DATE,1)
            IF ITEM.LIST='' ! DCOUNT(ITEM.LIST<1>,@VM) > 1 THEN GOTO 100
            IF NOT(NUM(ITEM.LIST<1,1>)) THEN GOTO 100
            Q=ITEM.LIST<1,1>'R%5'
         CASE NUM(QI)
            Q=QI'R%5'
         CASE 1
            GOTO 100
      END CASE
*
* Read client item
*
      CLIENT.ID=Q
      READ CLIENT.REC FROM F.CLIENT,Q ELSE
         CALL ELINE("Cannot find client ":Q:" in the CLIENT file.")
         GOTO 100
      END
      OLD.CLIENT.REC=CLIENT.REC
*
* Display menu and client data
*
      CLIENT.NUMBER.TEXT='Client Number ':Q
      CALL CENTER(CLIENT.NUMBER.TEXT,80)
      CRT @(0,2):@(-4):
      HDG.TXT='Client Number ':Q:' Collections Notification'
      VOUT<1>=CLIENT.REC<222>
      VOUT<2>=CLIENT.REC<223>
      VOUT<3>=CLIENT.REC<224>
      VIN<1>=PROG.DESC
      VIN<2>=PACK.NAME
      VIN<3>=HDG.TXT
      VIN<4>=DISPLAY.MODE
      VIN<5>=1
      VIN<6>='Y'
      VIN<7>='Y'
      VIN<8>='Y'
      VIN<9>='Y'
      CALL FB.EMAIL(VIN,VOUT)
      IF NOT(DISPLAY.MODE) THEN
         CLIENT.REC<222>=VOUT<1>
         CLIENT.REC<223>=VOUT<2>
         CLIENT.REC<224>=VOUT<3>
      END
*
* Check for changes and update file
*
99000:IF OLD.CLIENT.REC # CLIENT.REC THEN
         HELP='Options:  [Y]es [N]o'
         LN='Client information changed. Update? -'
         CALL GEN.IN(0,22,LN,'',Q,1,1,'','',2,-1,22,1,QI,HELP,0,23)
         QI=OCONV(QI,'MCU')
         BEGIN CASE
            CASE QI='Y'
               WRITE CLIENT.REC ON F.CLIENT,CLIENT.ID
               ADJUST.CLIENT.ID=CLIENT.ID:'*':DATE():'*':TIME():'*':USER
               WRITE CLIENT.REC ON F.ADJUST.CLIENT,ADJUST.CLIENT.ID
               GOTO 100
            CASE QI='N'
               RELEASE F.CLIENT,CLIENT.ID
               GOTO 100
            CASE 1
               GOTO 99000
         END CASE
      END
      CALL AFS.SCR.REFRESH(PROG.NAME,PROG.DESC,PACK.NAME,CO.NAME,TIME.DATE,1)
      IF NOT(DISPLAY.MODE) THEN RELEASE F.CLIENT,CLIENT.ID
      GOTO 100
