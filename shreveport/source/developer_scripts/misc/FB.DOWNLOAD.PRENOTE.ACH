********************************************************************************
* PROGRAM: FB.DOWNLOAD.PRENOTE.ACH
* PROGRAM TO CREATE AND DOWNLOAD THE PRENOTE TEST ACH FILE
*
* FRIEGHT BILLING PACKAGE
*
* AUTHOR: JEFF WALTON
* DATE: February 2, 2007
*
* THIS PROGRAM IS A PROPRIETARY PRODUCT OF AUTOMATED FREIGHT SYSTEMS
* AND MAY NOT BE USED, COPIED OR MODIFIED WITHOUT THE
* EXPRESS WRITTEN CONSENT OF SAME.
********************************************************************************
* 040714 - DRF01 - SysAid 9293 Added KSHELTON access as she is Dina's 
*                  back up.
* 081314 - JMK01 - SysAid#11359 - Mary Ann Rose access 
********************************************************************************

$INCLUDE PROG.ADMIN INC_OPTIONS

*
* Initialize variables
*
      PROMPT''
      CALL GET.USER(USER)
      PROG.NAME='FB.DOWNLOAD.PRENOTE.ACH'
      PROG.DESC='DOWNLOAD CARRIER PRENOTE TEST ACH FILE'
      PACK.NAME='FREIGHT PAYMENT'

       UPL.VIN = 'FBBP' ;  UPL.VIN<2> = 'FB.DOWNLOAD.PRENOTE.ACH' ; UPL.VOUT = ''                    ;* NPR_UPL 04/23/2010
       CALL UPD.PROGRAM.LOG(UPL.VIN,UPL.VOUT)                                                           ;* NPR_UPL 04/23/2010
      TB=CHAR(9)
      ESC=CHAR(27)


*
* Open files
*
      OPEN '','VOC' TO F.VOC ELSE
         CALL OPEN.ABORT('VOC',PROG.NAME)
      END
      READ BILLDATA.REC FROM F.VOC,'BILLDATA' ELSE
         CALL ELINE('Unable to open VOC BILLDATA - Call IS Department.')
         GOTO THE.END
      END
      BILLDATA.REC<2>:='\':USER
      WRITE BILLDATA.REC ON F.VOC,'BILLDATA.':USER ELSE
         CALL ELINE('Your user name ':USER:' is not in the BILLDATA folder - Call IS Department.')
         GOTO THE.END
      END
      OPEN '','BILLDATA.':USER TO F.BILLDATA ELSE
         CALL ELINE('Unable to open BILLDATA.':USER:'.')
         GOTO THE.END
      END
      BEGIN CASE
         CASE USER='OWEN'
         CASE USER='RCASON'
         CASE USER='KAREN'
         CASE USER='RWHITT'
         CASE USER='DGARRETT'  ; * Ticket 082407 DRF 10/29/08
         CASE USER='MKERBOW'             ; * JMK01
         CASE USER='JWALTON'             ; * JTW01
         CASE USER='KSHELTON'            ; * DRF01
         CASE USER='MROSE'               ; * JMK01
         CASE 1
            CALL FB.PRINT.CENT(1,80,12,'! Restricted Access Application !')
            CALL FB.PRINT.CENT(1,80,14,'Access Denied')
            CALL ELINE('')
            GOTO THE.END
      END CASE
*
* Include PRENOTE Test
*
*
100:  * Display heading information
*
      CALL AFS.SCR.HEAD(CO.ID,FILE.ID,'',PROG.NAME,PROG.DESC,PACK.NAME,CO.NAME,TIME.DATE,1)
      CRT @(0,2):STR('=',79)
      CRT @(0,6):"1 PreNote File Name..."
      CRT @(0,8):"2 PreNote Date........"

110:  ***
      HELP='Enter the PreNote source file name as saved on your U:\ drive.'
      CALL GEN.IN(24,6,'','',Q,0,20,'','',6,24,6,0,QI,HELP,0,23)
      QI=OCONV(QI,'MCU')
      IF QI='' OR QI='EX' OR QI='X' THEN GO THE.END
      PNFNAME=QI
      CRT @(24,6):PNFNAME
120:  ***
      HELP='Enter the PreNote Date to assign to the transmission file.'
      CALL GEN.IN(24,8,'','DATE',Q,0,8,'','',8,24,8,0,QI,HELP,0,23)
      QI=OCONV(QI,'MCU')
      IF QI='' OR QI='EX' THEN GO THE.END
      IF QI='X' THEN GO 110
      PNDATE=Q
      CRT @(24,8):PNDATE'D2/'
200:  ***
      CALL GEN.IN(0,22,'Process this request (Y/N)...','',Q,0,22,'','',0,28,22,0,QI,HELP,0,23)
      QI=OCONV(QI,'MCU')
      IF QI='' OR QI='EX' THEN GO THE.END
      IF QI='X' THEN GO 120
      IF QI='Y' OR QI='N' THEN NULL ELSE GO 200
      IF QI='N' THEN GO THE.END
      READ PNIN.REC FROM F.BILLDATA,PNFNAME ELSE
         MESSAGE='Cannot read ':PNFNAME:' from your U:\ drive. Please try again.'
         CALL ELINE(MESSAGE)
         GO 100
      END
      PNIN.REC=CHANGE(PNIN.REC,TB,@VM)
      REC=''
      NATT=DCOUNT(PNIN.REC,@AM)
      FOR ATT=2 TO NATT                  ; * DO NOT PROCESS HEADER ROW
*
* READ RAW DATA FIRST
*
         CUSTID=PNIN.REC<ATT,1>
         CUSTNAME=PNIN.REC<ATT,2>
         BANKID=PNIN.REC<ATT,3>
         BANKACCT=PNIN.REC<ATT,4>
*
* PERFORM ANY FORMATTING ADJUSTMENTS
*
         CUSTID=CUSTID'R%5'
         CUSTNAME=CHANGE(CUSTNAME,',','')          ; * NO COMMAS
         CUSTNAME=CHANGE(CUSTNAME,'-','')          ; * NO HYPHENS
         CUSTNAME=CHANGE(CUSTNAME,'/','')          ; * NO SLASHES
         CUSTNAME=CHANGE(CUSTNAME,'"','')          ; * NO QUOTES
         CUSTNAME=CHANGE(CUSTNAME,"'","")          ; * NO SINGLE QUOTES
         CUSTNAME=CUSTNAME[1,22]         ; * LIMIT TO 22 CHARACTERS
         BANKID=BANKID'R%9'
         LINE=''
         LINE<1,1>=CUSTNAME              ; * AFS Carrier Name
         LINE<1,2>=BANKID                ; * AFS Carrier Bank Rounting Number
         LINE<1,3>=BANKACCT              ; * AFS Carrier Bank Account Number
         LINE<1,4>=CUSTID                ; * AFS Carrier Number
         LINE<1,5>='0.01'                ; * PreNote Amount
         LINE<1,6>='C'                   ; * Bank Account Type
         LINE<1,7>='05/21/2007'
         LINE<1,8>=OCONV(PNDATE,'D4/')
         LINE<1,9>=''
         LINE<1,10>='Y'
         REC<-1>=CHANGE(LINE,@VM,',')
      NEXT ATT
      BATCH.ID='PN_ACH':OCONV(PNDATE,'D2-')
      CONVERT '-' TO '' IN BATCH.ID
      CNT=0
      LOOP
         CNT=CNT+1
         NID=BATCH.ID:(CNT 'R%3'):'.CSV'
         READV TEST FROM F.BILLDATA,NID,1 ELSE TEST=''
      UNTIL TEST='' DO REPEAT
      BATCH.ID=NID
      WRITE REC ON F.BILLDATA,BATCH.ID
      CALL ELINE('File ':BATCH.ID:' written to U:\':USER)
********************************************************************************
*
THE.END:***
      STOP
   END
