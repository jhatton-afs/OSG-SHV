*---- PROGRAM: CL.CLIENTS.RPT
* AUTHOR: JAY WEBERT
* DATE: MAY 2, 2007
* DESCRIPTION: PRODUCE REPORT OF CLAIMS CLIENTS
*
* THIS PROGRAM IS A PROPRIETARY PRODUCT OF AUTOMATED FREIGHT SYSTEMS
* AND MAY NOT BE USED, COPIED OR MODIFIED WITHOUT THE EXPRESS
* WRITTEN CONSENT OF SAME.
*-----------------------------------------------------------------------
*
* Initialize variables
*
      CALL GET.USER(USER.NAME)
      PROG.NAME='CL.CLIENTS.RPT'
      *** Set Report Variables
      OUTPUT.REC = ''
      OUTPUT.REC<1> = 'Claims Clients Report, ':DATE()'D2/'
      OUTPUT.ID = 'Claims Clients Report.XLS'
      HEAD = ''
      HEAD<-1> = 'ClientID'
      HEAD<-1> = 'ClientName'
      HEAD<-1> = 'Addr1'
      HEAD<-1> = 'Addr2'
      HEAD<-1> = 'City'
      HEAD<-1> = 'State'
      HEAD<-1> = 'Zip'
      HEAD<-1> = 'FiscalYR'
      HEAD<-1> = 'Contact'
      HEAD<-1> = 'Invoice'
      HEAD<-1> = 'Phone'
      HEAD<-1> = 'Fax'
      HEAD<-1> = 'Pre-AuditStart'
      HEAD<-1> = 'Post-AuditStart'
      HEAD<-1> = 'PayFrequency'
      HEAD<-1> = 'Status'
      HEAD<-1> = 'StayOut/Pre-Deposit'
      HEAD<-1> = 'VarianceDefault'
      HEAD<-1> = 'InvoiceCustomer'
      CONVERT CHAR(254) TO CHAR(9) IN HEAD
      OUTPUT.REC<-1> = HEAD
*
* Open files
*
      OPEN 'CL.CLIENTS' TO F.CLIENTS ELSE
        CALL OPEN.ABORT('CL.CLIENTS',PROG.NAME)
      END
      OPEN 'VOC' TO F.VOC ELSE
        CALL OPEN.ABORT('VOC',PROG.NAME)
      END
      READ BILLDATA.REC FROM F.VOC,"BILLDATA" ELSE
         CALL ELINE('UNABLE TO OPEN "VOC BILLDATA"')
         STOP
      END
      BILLDATA.REC<2>:='\':USER.NAME
      WRITE BILLDATA.REC ON F.VOC,"BILLDATA.":USER.NAME ELSE
         CALL ELINE("YOUR USER NAME '":USER.NAME:"' IS NOT IN THE BILLDATA FOLDER - PLEASE SEE OWEN/DAVID")
         STOP
      END
      OPEN '','BILLDATA.':USER.NAME TO F.BILLDATA ELSE
         CALL ELINE('UNABLE TO OPEN "BILLDATA."':USER.NAME:"'")
         STOP
      END
*
* Select Claims Clients
*
      STMT = 'SSELECT CLAIMS BY 5 5'
      CRT STMT
      EXECUTE STMT CAPTURING MSG
      IF @SELECTED < 1 THEN
        CRT 'NO CLAIMS RECORDS SELECTED'
        STOP
      END
      CLIENT.LIST = ''
      PREV.CLIENT = 'X'
      DONE = 0
      LOOP UNTIL DONE DO
        READNEXT CLIENT.ID THEN
          IF CLIENT.ID # PREV.CLIENT THEN
            PREV.CLIENT = CLIENT.ID
            READ TEST FROM F.CLIENTS, CLIENT.ID THEN
              CLIENT.LIST<-1> = CLIENT.ID
            END
          END
        END ELSE
          DONE = 1
        END
      REPEAT
*
* Write CL.CLAIMS data to spreadsheet
*
      CL.CNT = DCOUNT(CLIENT.LIST,CHAR(254))
      FOR I = 1 TO CL.CNT
        CLIENT.ID = CLIENT.LIST<I>
        READ CLIENT.REC FROM F.CLIENTS, CLIENT.ID THEN
          LINE = ''
          LINE<1> = CLIENT.ID      ; * Client ID
          LINE<2> = CLIENT.REC<2>  ; * Client Name
          LINE<3> = CLIENT.REC<3,1>  ; * Addr1
          LINE<4> = CLIENT.REC<3,2>  ; * Addr2
          LINE<5> = CLIENT.REC<4>  ; * City
          LINE<6> = CLIENT.REC<5>  ; * State
          LINE<7> = CLIENT.REC<6>  ; * Zip
          LINE<8> = CLIENT.REC<29> ; * Fiscal Year
          LINE<9> = CLIENT.REC<46> ; * Contact
          LINE<10> = CLIENT.REC<44> ; * Invoice
          LNUM=LEN(CLIENT.REC<31>)  ; * Phone
          BEGIN CASE
            CASE LNUM=10
              LINE<11> = '(':CLIENT.REC<31>[1,3]:')':CLIENT.REC<31>[4,3]:'-':CLIENT.REC<31>[7,4]
            CASE 1
              LINE<11> = CLIENT.REC<31>
          END CASE
          LNUM=LEN(CLIENT.REC<45>)  ; * Fax
          BEGIN CASE
            CASE LNUM=10
              LINE<12> = '(':CLIENT.REC<45>[1,3]:')':CLIENT.REC<45>[4,3]:'-':CLIENT.REC<45>[7,4]
            CASE LNUM = 7
              LINE<12> = CLIENT.REC<45>[1,3]:'-':CLIENT.REC<45>[4,4]
            CASE 1
              LINE<12> = CLIENT.REC<45>
          END CASE
          ADATE = CLIENT.REC<27> ; * Pre-Audit Start Date
          IF ADATE = 0 THEN ADATE = ''
          LINE<13> = ADATE'D2/'
          ADATE = CLIENT.REC<28> ; * Post-Audit Start Date
          IF ADATE = 0 THEN ADATE = ''
          LINE<14> = ADATE'D2/'
          LINE<15> = CLIENT.REC<38> ; * Pay Frequency
          IF CLIENT.REC<30> = 'A' THEN  ; * Status
            LINE<16> = 'Active'
          END ELSE
            LINE<16> = 'Inactive'
          END
          LINE<17> = CLIENT.REC<41> ; * Stay-Out/Pre-Deposit
          LINE<18> = CLIENT.REC<23> ; * Variance Default
          LINE<19> = CLIENT.REC<42> ; * Invoice Customer
          CONVERT CHAR(254) TO CHAR(9) IN LINE
          OUTPUT.REC<-1> = LINE
        END ELSE
          CRT 'COULD NOT READ CL.CLIENTS RECORD *':CLIENT.ID:'*'
        END
      NEXT I
      WRITE OUTPUT.REC TO F.BILLDATA, OUTPUT.ID
      STOP

