***
* PROGRAM NAME : FB.PROCESS.LOG.RPT
* PROGRAM DESC : Report FP Processing logging detail
*              :  For particular or ALL
*              :  For Date Range
*              :  Summary or Detail or Both
*              :  Hardcopy report or spreadsheet
*
* PROGRAM AUTH : N.C.G. Pratt.
* PROGRAM DATE : 01/27/2010.
*
*
*************************************************************************************************************************
*
      $OPTIONS HEADER.EJECT
      $OPTIONS EXEC.EQ.PERF
      $OPTIONS INFO.ABORT
*
* Initialize variables.
*
      PROMPT ''
      EQU TB TO CHAR(9)                  ; * NPR01
      VM = CHAR(253)
      SM = CHAR(252)
      PROG.NAME = 'FB.PROCESS.LOG.RPT'
      PROG.DESC='FP Processing Log report'
      PACK.NAME = "Freight Billing"

      UPL.VIN = 'FBBP' ;  UPL.VIN<2> = 'FB.PROCESS.LOG.RPT' ; UPL.VOUT = ''                    ;* NPR_UPL 04/23/2010
      CALL UPD.PROGRAM.LOG(UPL.VIN,UPL.VOUT)                                                   ;* NPR_UPL 04/23/2010
10

      ERRMSG        = ''
      CLIENT        = ''
      BEG.DATE      = ''
      END.DATE      = ''
      REPORT.TYPE   = ''
      PROG.SELECTED = ''
      OUT.DIR       = ''

      CALL GET.USER(USER)                ; * NPR01
      CALL CHANNEL(CH.NUM)               ; * NPR01
*
      CALL AFS.SCR.HEAD(CO.ID,FILE.ID,'',PROG.NAME,PROG.DESC,PACK.NAME,CO.NAME,TIME.DATE,1)
      IF FILE.ID='' THEN FILE.ID='1000.1' ; * NPR01
*
* Open general files
*
      OPEN '','FB.TABLES' TO F.TABLES ELSE
         CALL ELINE('UNABLE TO OPEN FB.TABLES ')
         STOP
      END

      OPEN '','CLIENTS' TO F.CLIENTS ELSE
         CALL ELINE('UNABLE TO OPEN CLIENTS')
         STOP
      END

      OPEN '','AFS.PROCESS.LOGFILE' TO F.PROC.LOGF ELSE
         CALL ELINE('UNABLE TO OPEN "AFS.PROCESS.LOGFILE"')
         STOP
      END

      OPEN '','BILLDATA.':USER TO BILLDATA ELSE
         OPEN 'VOC' TO F.VOC ELSE CALL OPEN.ABORT('VOC',PROG.NAME)
         READ BILLDATA.REC FROM F.VOC,"BILLDATA" ELSE
            CALL ELINE('UNABLE TO OPEN "VOC BILLDATA"')
            STOP
         END
         BILLDATA.REC<2>:='\':USER
         WRITE BILLDATA.REC ON F.VOC,"BILLDATA.":USER ELSE
            CALL ELINE("YOUR USER NAME '":USER:"' IS NOT IN THE BILLDATA FOLDER - PLEASE SEE OWEN/DAVID/MIKE")           ; * NPR01
            STOP
         END
         OPEN '','BILLDATA.':USER TO BILLDATA ELSE
            CALL ELINE('UNABLE TO OPEN "BILLDATA.":USER')
            STOP
         END
      END

20:

      GOSUB DISP.SCREEN

      HELP='Enter number of client to process, ALL or EX=Exit.'

      IF CLIENT NE "" THEN LN = CLIENT ELSE LN = "#####"
      CALL GEN.IN(18,5,'','',CLIENT,0,5,'R','',0,18,5,0,QI,HELP,0,23)

      QI = OCONV(QI,'MCU')
      IF QI='EX' OR QI='X' THEN STOP
      IF QI = '' THEN CLIENT = 'ALL'
*
      IF CLIENT = "ALL" THEN
         CRT @(18,5):"ALL Clients"
      END ELSE
         IF NUM(QI) AND QI GT "0" THEN CLIENT = CLIENT "R%5"
         READ CLIENT.REC FROM F.CLIENTS,CLIENT ELSE
            CALL ELINE('Client ':CLIENT:' not on file!')
            GOTO 20
         END
         CRT @(18,5):CLIENT:" ":CLIENT.REC<2>
      END
*
* Enter beginning date.
*
160: *

      HELP = "Enter beginning date. nn=Day. nn/nn=Month & Day. X=Exit. EX=Exit."
      IF BEG.DATE NE "" THEN LN = BEG.DATE ELSE LN = DATE()'D2/'
      CALL GEN.IN(18,7,LN,'DATE',Q,0,8,'','',0,18,7,0,QI,HELP,0,23)
      QI = OCONV(QI,'MCU')

      BEGIN CASE
         CASE QI='X'
            CRT @(18,7):BEG.DATE 'D2/'
            GOTO 20
         CASE QI='EX'
            STOP
         CASE 1
      END CASE
      BEG.DATE = Q
      CRT @(18,7):BEG.DATE'D2/'
*
* Enter ending date.
*
170: *
      HELP = "Enter ending date. nn=Day. nn/nn=Month & Day. X=Back to Beg Date. EX=Exit."
      IF END.DATE NE "" THEN LN = END.DATE ELSE LN = DATE()'D2/'
      CALL GEN.IN(18,9,LN,'DATE',Q,0,9,'','',0,18,9,0,QI,HELP,0,23)
      QI = OCONV(QI,'MCU')
      BEGIN CASE
         CASE QI = 'X'
            CRT @(18,9):END.DATE 'D2/'
            GOTO 160
         CASE QI = 'EX'
            STOP
      END CASE
      END.DATE = Q
      IF BEG.DATE GT END.DATE THEN
         CALL ELINE("Ending date may not be before beginning date")
         GOTO 170
      END
      DAYS = END.DATE-BEG.DATE+1
      CRT @(18,9):END.DATE'D2/'
*
* Summary or Detail
*
175: *

**      HELP = "Report type [S]ummary, [D]etail, [B]oth, X=Back or EX=Exit"
      HELP = "Report type [S]ummary, [D]etail, X=Back or EX=Exit"
      IF REPORT.TYPE NE "" THEN LN = REPORT.TYPE ELSE LN = "S"
      CALL GEN.IN(18,11,LN,'',Q,0,1,'','',0,18,11,0,QI,HELP,0,23)
      QI=OCONV(QI,'MCU')
      IF QI = "" THEN QI = "S"
      BEGIN CASE
         CASE QI='X'
            IF REPORT.TYPE = 'S' THEN CRT @(18,11):'S - Summary'
            IF REPORT.TYPE = 'D' THEN CRT @(18,11):'D - Detail'
            IF REPORT.TYPE = 'B' THEN CRT @(18,11):'B - Both Summary & Detail'
            GOTO 170
         CASE QI='EX'
            STOP
         CASE QI = "S" OR QI = "D" OR QI = "B"
            REPORT.TYPE = QI
         CASE 1
            CALL ELINE("Invalid Selection - ":QI)
            GOTO 175
      END CASE

      IF REPORT.TYPE = 'S' THEN CRT @(18,11):'S - Summary'
      IF REPORT.TYPE = 'D' THEN CRT @(18,11):'D - Detail'
**      IF REPORT.TYPE = 'B' THEN CRT @(18,11):'B - Both Summary & Detail'
*
* Specific Program name or ALL
*
180: *
      HELP = "Enter Program name,  [A]ll X=Back or EX=Exit"
      IF PROG.SELECTED NE "" THEN LN = PROG.SELECTED ELSE LN = "A"
      CALL GEN.IN(18,13,'','',Q,0,15,'','',0,18,13,0,QI,HELP,0,23)

      QI = OCONV(QI,'MCU')
      IF QI = '' THEN QI = 'A'
      BEGIN CASE
         CASE QI = 'X'
            IF PROG.SELECTED = 'A' THEN
               CRT @(18,13):"All Programs "
            END ELSE
               CRT @(18,13):"Program - ":PROG.SELECTED
            END
            GOTO 175
         CASE QI = 'EX'
            STOP
         CASE QI = '' OR QI='A'
            NULL
*        CASE ALPHA(QI)
*            NULL
         CASE NOT(NUM(QI))
            NULL
         CASE 1
            CALL ELINE("Invalid Selection!")
            GOTO 180
      END CASE
      PROG.SELECTED=QI
      IF PROG.SELECTED = 'A' THEN
         CRT @(18,13):"All Programs "
      END ELSE
         CRT @(18,13):"Program - ":PROG.SELECTED
      END
*
* Output to file or printer.
*
190: *
      HELP = "Output to [F]ile [S]creen or [P]rinter X=Back or EX=Exit"
      IF OUT.DIR NE "" THEN LN = OUT.DIR ELSE LN = ""
      CALL GEN.IN(18,15,LN,'',Q,0,1,'','',0,18,15,0,QI,HELP,0,23)
      QI = OCONV(QI,'MCU')
      IF QI='' THEN QI='S' ; * NPR01
      BEGIN CASE
         CASE QI = 'X'
            IF OUT.DIR = 'S' THEN CRT @(18,15):'S - Screen'
            IF OUT.DIR = 'P' THEN CRT @(18,15):'P - Printer'
            IF OUT.DIR = 'F' THEN CRT @(18,15):'F - Excel File'
            GOTO 180
         CASE QI = 'EX'
            STOP
         CASE QI = ''
            OUT.DIR = "F"
         CASE QI = 'S' OR QI='F' OR QI='P'
            OUT.DIR = QI
         CASE 1
            CALL ELINE("Invalid Selection!")
            GOTO 190
      END CASE

      OUT.DIR=QI

      IF OUT.DIR = 'S' THEN CRT @(18,15):'S - Screen'
      IF OUT.DIR = 'P' THEN CRT @(18,15):'P - Printer'
      IF OUT.DIR = 'F' THEN CRT @(18,15):'F - Excel File'

*
* Verify selection criteria is correct.
*
300: *
      HELP = "X=Back. EX=Exit. RETURN=Continue."
      CALL GEN.IN(0,22,'Enter selection please. --','',Q,0,2,'','',0,-2,22,1,QI,HELP,0,23)
      QI = OCONV(QI,'MCU')
      BEGIN CASE
         CASE QI = 'X'
            CRT @(0,22):@(-3):
            GOTO 190
         CASE QI = 'EX'
            STOP
         CASE QI = ''
            NULL
         CASE 1
            CALL ELINE("Invalid Selection!")
            GOTO 300
      END CASE

      IF OUT.DIR EQ "F" THEN
         GOSUB BUILD.FILE
      END ELSE
         GOSUB RUN.REPORT
      END

      CALL ELINE("") ; GOTO 10
*
***********************************************************************
* Start processing
***********************************************************************
*
DISP.SCREEN:
*
      CRT @(0, 5):" Client Number  : "
      CRT @(0, 7):" From Date      : "
      CRT @(0, 9):" To Date        : "
      CRT @(0,11):" Report type    : "
      CRT @(0,13):" Program Name   : "
      CRT @(0,15):" Output to      : "
*
      RETURN

RUN.REPORT:
*
** Possible variables to use
*
*      CLIENT
*      BEG.DATE
*      END.DATE
*      REPORT.TYPE
*      PROG.SELECTED
*      OUT.DIR

      STMT = "SORT AFS.PROCESS.LOGFILE"
      STMT := \ WITH LOG.DATE GE "\ : OCONV(BEG.DATE,"D2/") : \"\
      STMT := \ AND WITH LOG.DATE LE "\ : OCONV(END.DATE,"D2/") : \"\

      IF CLIENT = "ALL" THEN
         NULL
      END ELSE
         STMT := \ AND WITH LOG.CLIENT = "\:CLIENT:\"\
      END

      IF PROG.SELECTED = "A" THEN
         NULL
      END ELSE
         STMT := \ AND WITH LOG.PROG = "[\:PROG.SELECTED:\]"\
      END

      IF REPORT.TYPE = "S" THEN
         STMT := \ AND WITH LOG.DETNO = ""\
         STMT := \ BY LOG.DATE BY LOG.CLIENT BY LOG.PROG LOG.CLIENT LOG.PROG LOG.DATE HDR.2 HDR.4 TOTAL ELAPSED HDR.5\
         STMT := \ HEADING "FP Processing log Summary report \:SPACE(10):\ 'D'  'PL' " ID-SUPP HDR-SUPP\
      END ELSE
         STMT := \ AND WITH LOG.DETNO NE ""\
***       STMT := \ BY LOG.DATE BY LOG.CLIENT BY LOG.PROG BY LOG.DIV BY LOG.DETNO LOG.CLIENT DET.2 DET.4 TOTAL ELAPSED\
***       STMT := \ BY LOG.DATE BY LOG.CLIENT BY LOG.PROG BY LOG.DIV BY LOG.DETNO LOG.CLIENT LOG.DIV LOG.DETNO DET.2 TOTAL ELAPSED\
         STMT := \ BY LOG.DATE BY LOG.CLIENT BY LOG.PROG BY LOG.DIV BY LOG.DETNO LOG.CLIENT LOG.DIV LOG.DETNO TOTAL DET.ACT.ELAPSED\
         IF OUT.DIR = 'P' THEN STMT := \ DET.5.LONG\ ELSE  STMT := \ DET.5\
         STMT := \ HEADING "FP Processing log Detail report \:SPACE(10):\ 'D'  'PL' " ID-SUPP HDR-SUPP\
      END

      IF OUT.DIR = 'P' THEN STMT := \ LPTR\

*** CRT ; CRT STMT

      EXECUTE STMT

      RETURN
*
BUILD.FILE:

      STMT = "SSELECT AFS.PROCESS.LOGFILE"
      STMT := \ WITH LOG.DATE GE "\ : OCONV(BEG.DATE,"D2/") : \"\
      STMT := \ AND WITH LOG.DATE LE "\ : OCONV(END.DATE,"D2/") : \"\

      IF CLIENT = "ALL" THEN
         NULL
      END ELSE
         STMT := \ AND WITH LOG.CLIENT = "\:CLIENT:\"\
      END

      IF PROG.SELECTED = "A" THEN
         NULL
      END ELSE
         STMT := \ AND WITH LOG.PROG = "[\:PROG.SELECTED:\]"\
      END


      STMT := \ AND WITH LOG.DETNO = ""\
      STMT := \ BY LOG.DATE BY LOG.CLIENT BY LOG.PROG\

      IF REPORT.TYPE = "S" THEN
         XLS.FOOTER = \FP Processing log Summary report. Run on \:OCONV(DATE(),"D2/"):\ at \:OCONV(TIME(),"MTS"):" by " : USER
      END ELSE
*       STMT := \ AND WITH LOG.DETNO NE ""\
*       STMT := \ BY LOG.DATE BY LOG.CLIENT BY LOG.PROG BY LOG.DIV BY LOG.DETNO\
         XLS.FOOTER = \FP Processing log Detail report. Run on \:OCONV(DATE(),"D2/"):\ at \:OCONV(TIME(),"MTS"):" by " : USER
      END

      COL.CNT = 1
      XLS.HDR = "Client"                          ; COL.CNT += 1
      XLS.HDR<1,COL.CNT> = "Client Name"          ; COL.CNT += 1
      XLS.HDR<1,COL.CNT> = "Program"              ; COL.CNT += 1
      XLS.HDR<1,COL.CNT> = "User"                 ; COL.CNT += 1
      XLS.HDR<1,COL.CNT> = "Program Start Date"   ; COL.CNT += 1
      XLS.HDR<1,COL.CNT> = "Program End Date"     ; COL.CNT += 1
      XLS.HDR<1,COL.CNT> = "Program Start Time"   ; COL.CNT += 1
      XLS.HDR<1,COL.CNT> = "Program End Time"     ; COL.CNT += 1
      XLS.HDR<1,COL.CNT> = "Program Elapsed Time" ; COL.CNT += 1

      XLS.DET            = XLS.HDR
      XLS.DET<1,COL.CNT> = "Log Seq#"             ; COL.CNT += 1
      XLS.DET<1,COL.CNT> = "Process Start Date"   ; COL.CNT += 1
      XLS.DET<1,COL.CNT> = "Process End Date"     ; COL.CNT += 1
      XLS.DET<1,COL.CNT> = "Process Start Time"   ; COL.CNT += 1
      XLS.DET<1,COL.CNT> = "Process End Time"     ; COL.CNT += 1
      XLS.DET<1,COL.CNT> = "Process Elapsed Time" ; COL.CNT += 1
      XLS.DET<1,COL.CNT> = "Division"             ; COL.CNT += 1
      XLS.DET<1,COL.CNT> = "Process Ref1"         ; COL.CNT += 1
      XLS.DET<1,COL.CNT> = "Process Ref2"         ; COL.CNT += 1
      XLS.DET<1,COL.CNT> = "Process Ref3"         ; COL.CNT += 1
      XLS.DET<1,COL.CNT> = "Process Ref4"         ; COL.CNT += 1
      XLS.DET<1,COL.CNT> = "Process Ref5"         ; COL.CNT += 1

      EXECUTE STMT CAPTURING OUTPUT

      EOF = "N"
      CNT = 0
      CRT @(-1)

      LOOP
         READNEXT ID.LOG.HDR ELSE EOF = "Y"
      UNTIL EOF = "Y" DO
         READ HDR.REC FROM F.PROC.LOGF, ID.LOG.HDR ELSE HDR.REC = ''

         LOG.PROG   = FIELD(ID.LOG.HDR,"*",1)
         LOG.CLIENT = FIELD(ID.LOG.HDR,"*",2)
         READV LOG.CL.NAME FROM F.CLIENTS, LOG.CLIENT,2 ELSE LOG.CL.NAME = " "
         LOG.DATE   = FIELD(ID.LOG.HDR,"*",3)

         HDR.ST.DATE   = HDR.REC<1>
         HDR.ST.TIME   = HDR.REC<2>
         HDR.END.DATE  = HDR.REC<3>
         HDR.END.TIME  = HDR.REC<4>
         HDR.USER      = HDR.REC<5>
         HDR.CH.NUM    = HDR.REC<6>
         HDR.ELAPSED   = HDR.END.TIME - HDR.ST.TIME

         HCNT = 1
         HLINE = LOG.CLIENT                ; HCNT += 1
         HLINE<1,HCNT> = LOG.CL.NAME :" "  ; HCNT += 1
         HLINE<1,HCNT> = LOG.PROG  :" "    ; HCNT += 1
         HLINE<1,HCNT> = HDR.USER :" "     ; HCNT += 1
         HLINE<1,HCNT> = HDR.ST.DATE"D2/"  ; HCNT += 1
         HLINE<1,HCNT> = HDR.END.DATE"D2/" ; HCNT += 1
         IF HDR.ST.TIME NE "" THEN
            HLINE<1,HCNT> = OCONV(HDR.ST.TIME,"MTS")    ; HCNT += 1
         END ELSE
            HLINE<1,HCNT> = " "                         ; HCNT += 1
         END
         IF HDR.END.TIME NE "" THEN
            HLINE<1,HCNT> = OCONV(HDR.END.TIME,"MTS")   ; HCNT += 1
         END ELSE
            HLINE<1,HCNT> = " "                         ; HCNT += 1
         END
         HLINE<1,HCNT> = HDR.ELAPSED       ; HCNT += 1

         XLS.HDR<-1> = HLINE

         IF REPORT.TYPE = "S" THEN
         END ELSE
            N.RECS = DCOUNT(HDR.REC<10>,@VM)
            FOR II = 1 TO N.RECS
               SEQ.NO = HDR.REC<10,II>
               DIVISION = FIELD(SEQ.NO,"_",2)
               ID.LOG.DET = ID.LOG.HDR:"*":SEQ.NO
               DET.REC = ''
               READ DET.REC FROM F.PROC.LOGF, ID.LOG.DET THEN
                  DET.ST.DATE   = DET.REC<1>
                  DET.ST.TIME   = DET.REC<2>
                  DET.END.DATE  = DET.REC<3>
                  DET.END.TIME  = DET.REC<4>
                  DET.PROC.DESC = DET.REC<5>
                  DET.REF1      = DET.REC<6>
                  DET.REF2      = DET.REC<7>
                  DET.ELAPSED   = DET.END.TIME - DET.ST.TIME

                  DCNT = 1
                  DLINE         = SEQ.NO             ; DCNT += 1
                  DLINE<1,DCNT> = DET.ST.DATE "D2/"  ; DCNT += 1
                  DLINE<1,DCNT> = DET.END.DATE "D2/" ; DCNT += 1
                  IF DET.ST.TIME NE "" THEN
                     DLINE<1,DCNT> = OCONV(DET.ST.TIME,"MTS")    ; DCNT += 1
                  END ELSE
                     DLINE<1,DCNT> = " "                         ; DCNT += 1
                  END
                  IF DET.END.TIME NE "" THEN
                     DLINE<1,DCNT> = OCONV(DET.END.TIME,"MTS")   ; DCNT += 1
                  END ELSE
                     DLINE<1,DCNT> = " "                         ; DCNT += 1
                  END
                  DLINE<1,DCNT> = DET.ELAPSED:" "    ; DCNT += 1
                  DLINE<1,DCNT> = DIVISION:" "       ; DCNT += 1
                  DLINE<1,DCNT> = DET.PROC.DESC:" "  ; DCNT += 1
                  DLINE<1,DCNT> = DET.REF1:" "       ; DCNT += 1
                  DLINE<1,DCNT> = DET.REF2:" "       ; DCNT += 1
*            DLINE<1,DCNT> = DET.REF3:" "       ; DCNT += 1
*            DLINE<1,DCNT> = DET.REF4:" "       ; DCNT += 1
                  XLS.DET<-1> = HLINE:@VM:DLINE
               END
            NEXT II
         END
      REPEAT

      W.DATE = OCONV(DATE(),"D2/")
      CONVERT "/" TO "" IN W.DATE

      CONVERT @VM TO CHAR(9) IN XLS.HDR
      ID.XLS.HDR = "LOG_PROG_SUMMARY_":W.DATE:".XLS"
      WRITE XLS.HDR ON BILLDATA, ID.XLS.HDR

      IF REPORT.TYPE = "D" THEN
         CONVERT @VM TO CHAR(9) IN XLS.DET
         ID.XLS.DET = "LOG_PROG_DETAIL_":W.DATE:".XLS"
         WRITE XLS.DET ON BILLDATA, ID.XLS.DET
      END

      RETURN

