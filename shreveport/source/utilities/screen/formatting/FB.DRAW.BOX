$OPTIONS EXEC.EQ.PERF
$OPTIONS INFO.ABORT
*
*
SUBROUTINE FB.DRAW.BOX(BOX.X, BOX.Y, BOX.W, BOX.H, FILLER, BORDER)

$OPTIONS HEADER.EJECT
ERRMSG=''
*
*----------------------------------------------------------------------
* PROCEDURE:  FB.DRAW.BOX
*
* AUTHOR: JIM T. ROW
*
* PURPOSE: To draw a box ANYWHERE on the user's screen
*
* INPUT: BOX.X  - Starting Screen X position (upper left corner of box)
* ~~~~~  BOX.Y  - Starting Screen Y position (upper left corner of box)
*        BOX.W  - Width of box (this width includes the border size)
*        BOX.H  - Height of box (this includes the border size)
*        FILLER - Char to use in the center of the box
*                 o IF ='' then the center is NOT cleared.
*                 o IF =' ' then the center is cleared with spaces
*        BORDER - Border set to use
*                 o IF ='' then no border is drawn
*                 o IF =' ' then the border is spaces
*
* NOTES: 1. Cool! My first SUBROUTINE!
* ~~~~~  2. Remember the background is not saved where the box is drawn!
*        3. REMEBER! Parameters are passed by 'REFERENCE' (or..in techno
*           terms, the Address is passed not the Value)
*
*----------------------------------------------------------------------

    *
    * Do an Idiot Check!
    *
    IF (BOX.W < 2 OR BOX.H < 2) THEN RETURN

    *
    * Do the borders
    *
    DRAW.BORDER = (BORDER NE '')

    IF (DRAW.BORDER) THEN
        *
        * Init some locals -
        *
        X = BOX.X
        Y = BOX.Y
        W = BOX.W-2
        H = BOX.H-2
        IF BORDER=' ' THEN
            BORDER='+-+|+-+|'
        END

        * draw upper left corner
        *
        CRT @(X,Y):BORDER[1,1]

        * draw top right corner
        *
        CRT @(X+W+1,Y):BORDER[3,1]

        * draw bottom right corner
        *
        CRT @(X+W+1,Y+H+1):BORDER[5,1]

        * draw bottom left corner
        *
        CRT @(X, Y+H+1):BORDER[7,1]

        *
        * Draw Box Top and Botom edges
        *
        IF (W) THEN
           * DRAW TOP EDGE OF BOX

           CRT @(X+1,Y):STR(BORDER[2,1],W)

           * DRAW BOTTOM EDGE OF BOX

           CRT @(X+1,Y+H+1):STR(BORDER[6,1],W)
        END

        *
        * draw right and left edges of box
        *
        LOOP
        WHILE (H)
            Y+=1
            H-=1
            IF (FILLER NE '') THEN
                CRT @(X,Y):BORDER[8,1]:STR(FILLER[1,1],W):BORDER[4,1]
            END ELSE
                CRT @(X+W+1,Y):BORDER[4,1]
                CRT @(X,Y):BORDER[8,1]
            END
        REPEAT

        RETURN
    END

    *
    * Do the Box Center
    *
    IF (FILLER NE '') THEN
        X = BOX.X
        Y = BOX.Y
        W = BOX.W
        H = BOX.H

        LOOP
        WHILE (H)
            CRT @(X,Y):STR(FILLER[1],W)
            Y+=1
            H-=1
        REPEAT
    END


RETURN
*
*ÿ
