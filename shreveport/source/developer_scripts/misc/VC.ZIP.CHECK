* FREIGHT BILLING PACKAGE

*---- PROGRAM: VC.ZIP.CHECK
* AUTHOR: JAY WEBERT
* DATE: MARCH 14, 2007
* DESCRIPTION: GATHER AND SAVE DATA ABOUT THE ZIP CODES IN VEND.CUST
*
* THIS PROGRAM IS A PROPRIETARY PRODUCT OF AUTOMATED FREIGHT SYSTEMS
* AND MAY NOT BE USED, COPIED OR MODIFIED WITHOUT THE EXPRESS
* WRITTEN CONSENT OF SAME.
*-----------------------------------------------------------------------
* TOTAL - Number of VEND.CUST records for that client
* CAN.LONG - 6-Character Canadian format zip
* CAN.SHORT - 3-Character Candian format zip
* STANDARD - 5-Digit US format zip
* NINE.DIGIT - 9-Digit US format zip with hyphen
* HYPHEN.END - 5-Digit US format zip with hyphen on the end
* NO.REC - VEND.CUST ID was selected, but records could not be read
* NULL - zip code field is blank
* UNDERSCORE - 5-Digit or 9-Digit US format zip with underscore (_) where hyphen should be
* HYPHEN.ONLY - zip is the hyphen character
* CAN.PAIR - 6-Character Canadian format zip with a space added after the third character
* OTHER - zip value that fits none of the above descriptions
*-----------------------------------------------------------------------
      PROG.NAME = 'VC.ZIP.CHECK'
      OPEN 'ZIP.DATA' TO F.ZIP ELSE
        CALL OPEN.ABORT('ZIP.DATA',PROG.NAME)
      END
      ********************
      *** 'SELECT CLIENT ID'S
      ********************
      STMT = 'SELECT VOC "VEND.CUST" *A7'
      EXECUTE STMT RTNLIST CAPTURING MSG
      IF @SELECTED < 1 THEN
        CRT 'NO CLIENTS SELECTED.'
        STOP
      END
      STMT = 'SELECT CLIENTS'
      EXECUTE STMT PASSLIST RTNLIST CLIENT.LIST CAPTURING MSG
      IF @SELECTED < 1 THEN
        CRT 'NO CLIENT RECORDS SELECTED.'
        STOP
      END
      ********************
      *** LOOP THROUGH ALL CLIENTS
      ********************
      DONE = 0
      LOOP UNTIL DONE DO
        READNEXT CLIENT.ID FROM CLIENT.LIST THEN
          CRT CLIENT.ID
          *** INITIALIZE COUNTERS
          CAN.LONG = 0
          CAN.SHORT = 0
          STANDARD = 0
          NINE.DIGIT = 0
          HYPHEN.END = 0
          NO.REC = 0
          NULL = 0
          UNDERSCORE = 0
          HYPHEN.ONLY = 0
          CAN.PAIR = 0
          OTHER = 0
          ZIP.REC = ''
          OPEN 'VEND.CUST,':CLIENT.ID TO F.VC ELSE
            CRT 'COULD NOT OPEN VEND.CUST,':CLIENT.ID
            GOTO NEXT.CLIENT
          END
          STMT = 'SELECT VEND.CUST,':CLIENT.ID
          EXECUTE STMT RTNLIST VC.LIST CAPTURING MSG
          IF @SELECTED < 1 THEN GOTO NEXT.CLIENT
          TOTAL = @SELECTED
          DONE1 = 0
          LOOP UNTIL DONE1 DO
            READNEXT VC.ID FROM VC.LIST THEN
              READV ZIP FROM F.VC, VC.ID, 7 THEN
                BEGIN CASE
                  CASE ZIP MATCHES '1A1N1A1N1A1N'
                    CAN.LONG += 1
                  CASE ZIP MATCHES '1A1N1A'
                    CAN.SHORT += 1
                  CASE NUM(ZIP) AND LEN(ZIP) = 5
                    STANDARD += 1
                  CASE LEN(ZIP) = 6 AND ZIP[6,1] = '-' AND NUM(ZIP[1,5])
                    HYPHEN.END += 1
                  CASE ZIP[6,1] = '-' AND NUM(FIELD(ZIP,'-',1)) AND NUM(FIELD(ZIP,'-',2))
                    NINE.DIGIT += 1
                  CASE ZIP = ''
                    NULL += 1
                  CASE ZIP[6,1] = '_'
                    UNDERSCORE += 1
                  CASE ZIP = '-'
                    HYPHEN.ONLY += 1
                  CASE ZIP MATCHES '1A1N1A 1N1A1N'
                    CAN.PAIR += 1
                  CASE 1
                    OTHER += 1
                END CASE
              END ELSE
                NO.REC += 1
              END
            END ELSE
              DONE1 = 1
            END
          REPEAT
          *** Display client results
          CRT 'TOTAL = ':TOTAL
          CRT 'CAN.LONG = ':CAN.LONG
          CRT 'CAN.SHORT = ':CAN.SHORT
          CRT 'STANDARD = ':STANDARD
          CRT 'NINE.DIGIT = ':NINE.DIGIT
          CRT 'HYPHEN.END = ':HYPHEN.END
          CRT 'NO.REC = ':NO.REC
          CRT 'NULL = ':NULL
          CRT 'UNDERSCORE = ':UNDERSCORE
          CRT 'HYPHEN.ONLY = ':HYPHEN.ONLY
          CRT 'CAN.PAIR = ':CAN.PAIR
          CRT 'OTHER = ':OTHER
          CRT
          *** Save client results
          ZIP.REC<1> = TOTAL
          ZIP.REC<2> = CAN.LONG
          ZIP.REC<3> = CAN.SHORT
          ZIP.REC<4> = STANDARD
          ZIP.REC<5> = NINE.DIGIT
          ZIP.REC<6> = HYPHEN.END
          ZIP.REC<7> = NO.REC
          ZIP.REC<8> = NULL
          ZIP.REC<9> = UNDERSCORE
          ZIP.REC<10> = HYPHEN.ONLY
          ZIP.REC<11> = CAN.PAIR
          ZIP.REC<12> = OTHER
          WRITE ZIP.REC TO F.ZIP, CLIENT.ID ELSE
            CRT '****** COULD NOT WRITE ZIP RECORD!**********'
            INPUT X
          END
        END ELSE
          DONE = 1
        END
NEXT.CLIENT:
      REPEAT
STOP

