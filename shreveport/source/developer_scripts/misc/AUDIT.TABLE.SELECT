*=================================================
*
*  SUBROUTINE: AUDIT.TABLE.SELECT()
*
*  PURPOSE   :
*
*  PARAMETERS: VIN<1> = CLIENT
*              VIN<2> = CARRIER
*
*
*  RETURNS: VOUT<1> = TABLE NUMBER  OR EXIT KEY USED.
*
*
*
*   DATE   | USER | COMMENT
* ---------+------+--------------------------------------
* 02/04/98 | JTR  | Unit Created
* 06/17/98 | JTR  | Modified to display a shorter table description
*          |      | and show the CutBack code and short description
*          |      |
*
*======================================================================
* COPYRIGHT (C) 1998 - AFS LOGISTICS LLC - ALL RIGHTS RESERVED
*       ----------------------------------------------------
*  This program is sole property of AFS Logistics, LLC (AUTOMATED
*  FREIGHT SYSTEMS) and may not be used, copied, or modified without
*  the express written consent of Brian Barker of AFS Logistics, LLC.
*======================================================================


SUBROUTINE AUDIT.TABLE.SELECT(VIN,VOUT)

$OPTIONS HEADER.EJECT
$OPTIONS EXEC.EQ.PERF
$OPTIONS INFO.ABORT

   ERRMSG=''
   PROG.NAME='AUDIT.TABLE.SELECT'
   PROG.DESC='SELECT TABLES'
   PACK.NAME='FREIGHT AUDITING'
   PROMPT''
    UPL.VIN = 'FBBP' ;  UPL.VIN<2> = 'AUDIT.TABLE.SELECT' ; UPL.VOUT = ''                    ;* NPR_UPL 04/22/2010
    CALL UPD.PROGRAM.LOG(UPL.VIN,UPL.VOUT)                                                           ;* NPR_UPL 04/22/2010
   FIND.LOCKED.NAME=''
   PAGE=1
   TABLE.NUMBER = ''
   CLIENT.ID=VIN<1>
   CARRIER.ID=VIN<2>

   CALL AFS.SCR.HEAD(CO.ID,FILE.ID,'',PROG.NAME,PROG.DESC,PACK.NAME,CO.NAME,TIME.DATE,1)

   *
   * Open files
   *
   OPEN '','FB.AUDIT.TABLES,':CLIENT.ID TO F.AUDIT.TABLES ELSE CALL OPEN.ABORT("FB.AUDIT.TABLES,":CLIENT.ID,PROG.NAME)
   OPEN '','FB.CUT.BACKS' TO F.CUT.BACKS ELSE CALL OPEN.ABORT("FB.CUT.BACKS",PROG.NAME)

   *
   * Build display array..
   *
   STMT = 'SSELECT FB.AUDIT.TABLES,':CLIENT.ID:' WITH CARRIER# = "':CARRIER.ID:'" BY TABLE#'
   EXECUTE STMT CAPTURING OUTPUT
   IF SYSTEM(11) ELSE
      CALL ELINE('No tables on file for this client.')
      GOTO EXIT.PROGRAM
   END
   NUM.ITEMS = @SELECTED

   TABLE.ARRAY=''

   LINE.COUNT=0
   DONE=0
   LOOP
      READNEXT ID ELSE DONE=1
   UNTIL DONE DO
      READ TABLE.REC FROM F.AUDIT.TABLES,ID THEN
         LINE.COUNT+=1
         TABLE.ARRAY<1,LINE.COUNT>=FIELD(ID,'*',2)         ;* Table Number
         T=TABLE.REC<31>                                   ;* Description
         IF T='' THEN T='(* No Description Entered for this table *)'
         TABLE.ARRAY<2,LINE.COUNT>=T
         T.CODE=TABLE.REC<32>                              ;* Cut Back Code
         IF T.CODE # '' THEN
            READV T.DESC FROM F.CUT.BACKS,T.CODE,1 ELSE T.DESC = 'Cutback Code "':T.CODE:'" is not on file!'
         END ELSE
            T.CODE = '----'
            T.DESC = '*No cutback entered*'
         END
         TABLE.ARRAY<3,LINE.COUNT>=T.CODE
         TABLE.ARRAY<4,LINE.COUNT>=T.DESC                  ;* Description
      END
   REPEAT
   LAST.PAGE = INT(LINE.COUNT/18)
   IF REM(LINE.COUNT,18)>0 THEN LAST.PAGE+=1
   PAGE = 1


   *
   * SCREEN.TOP the picking!
   *
DRAW.SCREEN:
   GOSUB UPDATE.DISPLAY

USER.INPUT:
   *
   * Enter selection code.
   *
   HELP="Select Table:  [BACK], [TOP], [Enter]=Next Page [X]=Exit"
   CALL GEN.IN(0,22,"Enter table number please. #####",'',Q,0,5,'','',2,-5,22,0,QI,HELP,0,23)
   QI=OCONV(QI,'MCU')
   BEGIN CASE
      CASE QI=''
         IF PAGE < LAST.PAGE THEN PAGE+=1

      CASE QI='BACK'
         IF PAGE>1 THEN PAGE-=1

      CASE QI='TOP'
         PAGE=1

      CASE QI='EX' OR QI='X'
         TABLE.NUMBER=QI

      CASE 1
         SEL=QI
         LOCATE SEL IN TABLE.ARRAY<1> SETTING WHERE THEN
            TABLE.NUMBER=SEL
         END ELSE
            CALL ELINE('Table entry ':SEL:' does not exist.')
         END
   END CASE

   IF TABLE.NUMBER='' THEN GOTO DRAW.SCREEN

EXIT.PROGRAM:
   *
   *
   *
   VOUT=TABLE.NUMBER

RETURN


*-----------------------------------------------------------------------
* UPDATE.DISPLAY
*-----------------------------------------------------------------------
UPDATE.DISPLAY:

   TITLE = 'TBL DESCRIPTION                                 CUTBAK  DESCRIPTION'
   CRT @(1,3):TITLE
   SCREEN.TOP=(PAGE-1)*18
   SCREEN.BOTTOM=SCREEN.TOP+18
   IF SCREEN.BOTTOM>LINE.COUNT THEN SCREEN.BOTTOM=LINE.COUNT
   SCREEN.TOP+=1
   LINE = 4
   FOR I=SCREEN.TOP TO SCREEN.BOTTOM
      T = TABLE.ARRAY<1,I>'R#3'
      T:=' ':TABLE.ARRAY<2,I>'L#45'
      T:=TABLE.ARRAY<3,I>'R#4'
      T:='   ':TABLE.ARRAY<4,I>
      CRT @(2,LINE):T'L#75':
      LINE+=1
   NEXT I

RETURN





*
*
*
