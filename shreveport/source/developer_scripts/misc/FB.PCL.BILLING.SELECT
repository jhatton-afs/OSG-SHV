      SUBROUTINE FB.PCL.BILLING.SELECT(VIN,VOUT,PCL.SLS.ARRAY,PCL.SOC.ARRAY,PCL.INV.ARRAY,F.PCL.BILL)

      CLIENT.ID=VIN<1>
      DIV.ID=VIN<2>
      BEG.DATE=VIN<3>
      END.DATE=VIN<4>
      FYR.DATE=VIN<5>
      DIV.REG=VIN<6>
      BEGIN CASE
         CASE DIV.REG='D' ; CYCLE='DIV'
         CASE DIV.REG='R' ; CYCLE='REG'
         CASE 1 ; CYCLE=''
      END CASE
      VOUT=''
      PCL.SAVINGS=''
      PCL.CONTROL.ARRAY=''
      BILLING.REC=0

*     STMT='SELECT PCL.BILLING WITH INV.DATE GE "':FYR.DATE'D2/':'"'   ; * JMK153
      STMT='SELECT PCL.BILLING WITH INV.DATE GE "':BEG.DATE'D2/':'"'   ; * JMK153
      STMT=STMT:' AND WITH INV.DATE LE "':END.DATE'D2/':'"'  ; * JMK153
      STMT=STMT:' AND WITH BILL.CLIENT# EQ "':CLIENT.ID:'"'  ; * JMK153
      IF DIV.ID#'' THEN                  ; * JMK153
         IF CYCLE='DIV' THEN             ; * JMK153
            STMT:=' AND WITH DIVISION EQ "':DIV.ID:'"'       ; * JMK153
         END                             ; * JMK153
         IF CYCLE='REG' THEN             ; * JMK153
            STMT:=' AND WITH REGION EQ "':DIV.ID:'"'       ; * JMK153
         END                             ; * JMK153
      END                                ; * JMK153
      EXECUTE STMT CAPTURING OUTPUT      ; * JMK153
      NUM.PCL.ITEMS=@SELECTED            ; * JMK153
      IF NUM.PCL.ITEMS>0 THEN BILLING.REC=1        ; * JMK153

      PCL.DONE = 0                       ; * JMK153
      LOOP                               ; * JMK153
         READNEXT PCL.ID ELSE PCL.DONE = 1         ; * JMK153
      UNTIL PCL.DONE = 1 DO              ; * JMK153
         READ PCL.REC FROM F.PCL.BILL, PCL.ID THEN           ; * JMK153
            PCL.BILL.CODE=PCL.REC<13>    ; * JMK153
            PCL.BILL.DESC=PCL.REC<14>    ; * JMK153
            PCL.BILL.RATE=PCL.REC<15>+0  ; * JMK153
            PCL.BILL.QTY=PCL.REC<16>+0   ; * JMK153
            PCL.BILL.AMT=PCL.REC<17>+0   ; * JMK153

            PCL.SLS.AMT=PCL.REC<17>+0    ; * JMK153
            PCL.SLS.CD=PCL.REC<24>       ; * JMK153
            PCL.INV.DATE=PCL.REC<19>     ; * JMK153
            PCL.DIV=PCL.REC<23>          ; * JMK153
            IF PCL.INV.DATE GE BEG.DATE THEN       ; * JMK153
               PCL.SAVINGS=PCL.REC<4>    ; * JMK153
               PCL.CONTROL=PCL.REC<5>    ; * JMK153
               LOCATE PCL.CONTROL IN PCL.CONTROL.ARRAY<1> BY 'AR' SETTING PCPOS ELSE
                  INS PCL.CONTROL BEFORE PCL.CONTROL.ARRAY<1,PCPOS>
               END
            END                          ; * JMK153

            IF PCL.BILL.AMT#0 THEN       ; * JMK153

               LOCATE PCL.SLS.CD IN PCL.SLS.ARRAY<1> BY 'AR' SETTING PCL.POS ELSE          ; * JMK155
                  INS PCL.SLS.CD BEFORE PCL.SLS.ARRAY<1,PCL.POS>       ; * JMK155
                  INS 0 BEFORE PCL.SLS.ARRAY<2,PCL.POS>      ; * JMK155
                  INS 0 BEFORE PCL.SLS.ARRAY<3,PCL.POS>      ; * JMK155
               END                       ; * JMK155
               IF PCL.INV.DATE GE BEG.DATE THEN    ; * JMK153
                  PCL.SLS.ARRAY<2,PCL.POS>+=PCL.SLS.AMT      ; * JMK155
               END                       ; * JMK153
               PCL.SLS.ARRAY<3,PCL.POS>+=PCL.SLS.AMT         ; * JMK155

               LOCATE PCL.BILL.CODE IN PCL.SOC.ARRAY<1> BY 'AL' SETTING PCL.POS ELSE       ; * JMK153
                  INS PCL.BILL.CODE BEFORE PCL.SOC.ARRAY<1,PCL.POS>    ; * JMK153
                  INS 0 BEFORE PCL.SOC.ARRAY<2,PCL.POS>      ; * JMK153
                  INS 0 BEFORE PCL.SOC.ARRAY<3,PCL.POS>      ; * JMK153
                  INS 0 BEFORE PCL.SOC.ARRAY<4,PCL.POS>      ; * JMK153
                  INS 0 BEFORE PCL.SOC.ARRAY<5,PCL.POS>      ; * JMK153
                  INS 0 BEFORE PCL.SOC.ARRAY<6,PCL.POS>      ; * JMK153
                  INS 0 BEFORE PCL.SOC.ARRAY<7,PCL.POS>      ; * JMK153
               END                       ; * JMK153
               IF PCL.INV.DATE GE BEG.DATE THEN    ; * JMK153
                  PCL.SOC.ARRAY<2,PCL.POS>+=PCL.BILL.QTY     ; * JMK153
                  PCL.SOC.ARRAY<3,PCL.POS>=PCL.BILL.RATE     ; * JMK153
                  PCL.SOC.ARRAY<4,PCL.POS>+=PCL.BILL.AMT     ; * JMK153
               END                       ; * JMK153
               PCL.SOC.ARRAY<5,PCL.POS>+=PCL.BILL.QTY        ; * JMK153
               PCL.SOC.ARRAY<6,PCL.POS>=PCL.BILL.RATE        ; * JMK153
               PCL.SOC.ARRAY<7,PCL.POS>+=PCL.BILL.AMT        ; * JMK153

               IF PCL.INV.DATE GE BEG.DATE THEN    ; * JMK153
                  LOCATE PCL.BILL.DESC IN PCL.INV.ARRAY<1> BY 'AL' SETTING PCL.POS ELSE    ; * JMK153
                     INS PCL.BILL.DESC BEFORE PCL.INV.ARRAY<1,PCL.POS>           ; * JMK153
                     INS 0 BEFORE PCL.INV.ARRAY<2,PCL.POS>   ; * JMK153
                     INS 0 BEFORE PCL.INV.ARRAY<3,PCL.POS>   ; * JMK153
                     INS 0 BEFORE PCL.INV.ARRAY<4,PCL.POS>   ; * JMK153
                  END                    ; * JMK153
                  PCL.INV.ARRAY<2,PCL.POS>+=PCL.BILL.QTY     ; * JMK153
                  PCL.INV.ARRAY<3,PCL.POS>=PCL.BILL.RATE     ; * JMK153
                  PCL.INV.ARRAY<4,PCL.POS>+=PCL.BILL.AMT     ; * JMK153
               END                       ; * JMK153

            END                          ; * JMK153
         END                             ; * JMK153
      REPEAT                             ; * JMK153
      IF DCOUNT(PCL.CONTROL.ARRAY,@VM) = 1 THEN
         PCL.CONTROL=PCL.CONTROL.ARRAY
      END ELSE
         PCL.CONTROL=PCL.CONTROL.ARRAY'R#4'
      END
      VOUT<1>=BILLING.REC                ; * JMK153
      VOUT<2>=PCL.SAVINGS                ; * JMK153
      VOUT<3>=PCL.CONTROL                ; * JMK153
      RETURN
      END
