* GENERATE FREIGHT BILLS FOR CLIENTS
*
* AUTHOR: ROB GAUDET
* DATE: AUGUST 2, 2001
*
* THIS PROGRAM IS A PROPRIETARY PRODUCT OF AUTOMATED FREIGHT SYSTEMS
* AND MAY NOT BE USED, COPIED OR MODIFIED WITHOUT THE EXPRESS
* WRITTEN CONSENT OF SAME.
*

    PROG.NAME=''
    RESULT.DATA = ''
    PROG.DESC='GENERATE FREIGHT BILLS FOR CLIENTS'

    INPUT CLIENTNUM:
    CLIENT.NUM = OCONV(CLIENTNUM,'MCU')

    INPUT BILLDATE:
    BILL.WEEK.DATE = OCONV(BILLDATE,'MCU')

	FREIGHT.BILL.HIST.CLIENT = 'FB.BILLS.HIST,':CLIENT.NUM
	OPEN FREIGHT.BILL.HIST.CLIENT TO F.FREIGHT.BILL.HIST.CLIENT ELSE
	  ERROR.MSG = 'ERROR: Could not open FREIGHT.BILL.HIST.CLIENT file. Client Number - ':CLIENT.NUM
	  ERROR.TYPE = 'FATAL'
	  GOSUB ERROR.HANDLER
	END

	OPEN 'CARRIERS' TO F.CARRIERS ELSE
	  ERROR.MSG = 'ERROR: Could not open CARRIERS file.'
	  ERROR.TYPE = 'FATAL'
	  GOSUB ERROR.HANDLER
	END

    STMT = 'SSELECT ':FREIGHT.BILL.HIST.CLIENT:' WITH 37 = ':BILL.WEEK.DATE:' AND WITH VERSION = "0" AND WITH 39 # "R" BY CARRIER BY 2 BY PRO'

	FREIGHT.BILL.LIST = ''
	EXECUTE STMT FREIGHT.BILL.LIST CAPTURING OUTPUT
	NUM.RECORDS = DCOUNT(OUTPUT,@AM)
	IF NUM.RECORDS = 0 THEN
	  ERROR.MSG = 'ERROR: NO FREIGHT BILL RECORDS FOUND. TCL STATEMENT WAS - ':STMT
	  ERROR.TYPE = 'FATAL'
	  GOSUB ERROR.HANDLER
	END

    METADATA = ""
    METADATA<1,1> = "VARCHAR" ;   METADATA<1,2> = "Carrier Name";      METADATA<1,3> = 0
    METADATA<2,1> = "VARCHAR" ;   METADATA<2,2> = "IOT"  ;      METADATA<2,3> = 0
    METADATA<3,1> = "VARCHAR" ;   METADATA<3,2> = "Pro Number"  ;      METADATA<3,3> = 0
    METADATA<4,1> = "VARCHAR" ;   METADATA<4,2> = "Amount Paid"  ;      METADATA<4,3> = 0
    METADATA<5,1> = "VARCHAR" ;   METADATA<5,2> = "Check Number"  ;      METADATA<5,3> = 0
    METADATA<6,1> = "VARCHAR" ;   METADATA<6,2> = "Check Date"  ;      METADATA<6,3> = 0
    METADATA<7,1> = "VARCHAR" ;   METADATA<7,2> = "Reference #"  ;      METADATA<7,3> = 0
	CONVERT @AM TO CHAR(30) IN METADATA
	PRINT "%METADATA:":METADATA

	LOOP

		READNEXT ID ELSE GO 500

		READ DFREIGHT.BILL.HIST.CLIENT FROM F.FREIGHT.BILL.HIST.CLIENT,ID THEN
			CARRIER=FIELD(ID,'*',1)
			READ DCARRIERS FROM F.CARRIERS,CARRIER THEN
				CARRIER.NAME = DCARRIERS<1>
			END

			PRO.NUM		  =  ID
			ACTUAL.CHARGE =  DFREIGHT.BILL.HIST.CLIENT<8>
			CHECK.NUM	  =	 DFREIGHT.BILL.HIST.CLIENT<14>
			CHECK.DATE	  =	 DFREIGHT.BILL.HIST.CLIENT<15>
			BOL			  =	 DFREIGHT.BILL.HIST.CLIENT<16>
			IOT			  =  DFREIGHT.BILL.HIST.CLIENT<2>

			ROW = ''
			ROW<1> = CARRIER.NAME
			ROW<2> = IOT
			ROW<3> = FIELD(PRO.NUM,'*',2)
			ROW<4> = ACTUAL.CHARGE'R26'
			ROW<5> = FIELD(CHECK.NUM,'*',1)
			ROW<6> = CHECK.DATE'D2/'
			ROW<7> = BOL
			CONVERT @AM TO CHAR(30) IN ROW
			PRINT ROW
		END ELSE
			ERROR.MSG = 'ERROR: NO RECORD FOUND IN F.FREIGHT.BILL.HIST.CLIENT FOR ':ID
			ERROR.TYPE = 'FATAL'
			GOSUB ERROR.HANDLER
		END
	REPEAT
500:
STOP


ERROR.HANDLER:***
		METADATA = ''
		METADATA<1,1> = "VARCHAR" ; METADATA<1,2> = "Flag"			; METADATA<1,3> = 0
		METADATA<2,1> = "VARCHAR" ; METADATA<2,2> = "ErrorType"		; METADATA<2,3> = 0
		METADATA<3,1> = "VARCHAR" ; METADATA<3,2> = "ErrorMsg"		; METADATA<3,3> = 0
		METADATA<4,1> = "VARCHAR" ; METADATA<4,2> = "ClientNum"		; METADATA<4,3> = 0
		CONVERT @AM TO CHAR(30) IN METADATA
		PRINT "%METADATA:":METADATA
		ROW=''
		ROW<1> = "ERROR"
		ROW<2> = ERROR.TYPE
		ROW<3> = ERROR.MSG
		ROW<4> = CLIENT.NUM
		CONVERT @AM TO CHAR(30) IN ROW
		PRINT ROW
		STOP
RETURN
END