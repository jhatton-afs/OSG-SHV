$OPTIONS HEADER.EJECT
$OPTIONS EXEC.EQ.PERF
$OPTIONS INFO.ABORT
ERRMSG=''
* PROGRAM FB.1.8
* PROGRAM TO MODIFY FREIGHT BILL STATUS
*
* FREIGHT BILLING PACKAGE
*
* AUTHOR: Stephen B. Cook
* DATE:   09.21.94
*
*---- cloned from FB.1.8
* Initialize variables
*
      PROMPT''
      PROG.NAME='FB.1.16'
      PROG.DESC='EDIT FREIGHT BILL STATUS CODES'
      PACK.NAME='FREIGHT BILLING'
      CALL AFS.SCR.HEAD(CO.ID,FILE.ID,'',PROG.NAME,PROG.DESC,PACK.NAME,CO.NAME,TIME.DATE,1)
       UPL.VIN = 'FBBP' ;  UPL.VIN<2> = 'FB.1.16' ; UPL.VOUT = ''                    ;* NPR_UPL 04/23/2010
       CALL UPD.PROGRAM.LOG(UPL.VIN,UPL.VOUT)                                                           ;* NPR_UPL 04/23/2010
*
* Open files
*
      OPEN '','BCUST' TO F.CLIENT ELSE CALL OPEN.ABORT("BCUST",PROG.NAME)
      OPEN '','FB.STATUS' TO F.FILE ELSE
        EXECUTE 'CREATE-FILE FB.STATUS 1 7' PASSLIST CAPTURING OUTPUT
        OPEN '','FB.STATUS' TO F.FILE ELSE CALL OPEN.ABORT("FB.STATUS",PROG.NAME)
      END
*
* Select codes and build display array
*
      DISP.REC=""
      SELECT F.FILE
      LOOP
        READNEXT ID ELSE ID=""
      UNTIL ID="" DO
        READV DESC FROM F.FILE,ID,1 ELSE DESC='~~~ Not on file ~~~'
        IF DISP.REC="" THEN
          DISP.REC<1>=ID
          DISP.REC<2>=DESC
        END ELSE
          LOCATE(ID,DISP.REC,1;POS;"AR") ELSE
            DISP.REC=INSERT(DISP.REC,1,POS,0,ID)
            DISP.REC=INSERT(DISP.REC,2,POS,0,DESC)
          END
        END
      REPEAT
*
* First page of codes
*
      PAGE=1
      GOSUB 9000
*
* Enter selection code
*
100   HELP="Enter line number. 'B'=Back. 'T'=Top, ''=Next page. 'E'=End. 'EX'=Exit."
      CALL GEN.IN(0,22,"Enter line number please. ###",'',Q,0,3,'','',1,-3,22,0,QI,HELP,0,23)
      QI=OCONV(QI,'MCU') 
      BEGIN CASE
        CASE QI=''
          PAGE=PAGE+1
          GOSUB 9000
        CASE QI='B'
          PAGE=PAGE-1
          IF PAGE=0 THEN PAGE=1
          GOSUB 9000
        CASE QI='T'
          PAGE=1
          GOSUB 9000
        CASE QI='EX'
          STOP
        CASE NUM(QI)
          SEL=QI
          IF SEL LT START ! SEL GT STOP THEN
            CALL ELINE('Invalid selection.')
            GOTO 100
          END
          GOSUB 8000
        CASE QI='E'
          X=DCOUNT(DISP.REC<1>,CHAR(253))
          FOR I=X TO 1 STEP -1
            IF DISP.REC<1,I>='' THEN
              DISP.REC=DELETE(DISP.REC,1,I)
              DISP.REC=DELETE(DISP.REC,2,I)
            END
          NEXT I
*
          Z=0
          LOOP
            Z=Z+1
            CODE=DISP.REC<1,Z>
          UNTIL CODE="" DO
            READ ITEM FROM F.FILE,CODE ELSE ITEM=''
            ITEM<1>=DISP.REC<2,Z>
            WRITE ITEM ON F.FILE,CODE
          REPEAT
          STOP
      END CASE
      GOTO 100
*
*------------------------------------------------------------------------
* INTERNAL SUBROUTINES
*------------------------------------------------------------------------
*
* Edit code
*
8000  ROW=3+REM(SEL,18)
      IF ROW=3 THEN ROW=21
      CODE=DISP.REC<1,SEL>
      DESC=DISP.REC<2,SEL>
8010  HELP="Enter new code. ''=Accept. 'D'=Delete. 'X'=Exit."
      CALL GEN.IN(5,ROW,CODE'L#4','',Q,0,4,'','',2,-4,ROW,0,QI,HELP,0,23)
      QI=OCONV(QI,'MCU')
      IF QI='X' THEN
        CRT @(5,ROW):CODE'L#4'
        RETURN
      END
      IF QI='' THEN
        Q=CODE
      END
      IF QI='D' THEN
        DELETE F.FILE,CODE
        DISP.REC=DELETE(DISP.REC,1,SEL)
        DISP.REC=DELETE(DISP.REC,2,SEL)
        GOSUB 9000
        RETURN
      END
      CODE=Q
      CRT @(5,ROW):CODE'L#4'
*
      HELP="Enter new code description. Use ~ for input points. '<'=Back to code. 'X'=Exit."
      CALL GEN.IN(11,ROW,DESC'L#40','',Q,0,40,'','',2,-40,ROW,0,QI,HELP,0,23)
      QI=OCONV(QI,'MCU') 
      BEGIN CASE
        CASE QI='X' ! QI='<'
          CRT @(11,ROW):DESC'L#40'
          GOTO 8010
        CASE QI=''
          Q=DESC
      END CASE
      DESC=Q
      CRT @(11,ROW):DESC'L#40'
      DISP.REC<1,SEL>=CODE
      DISP.REC<2,SEL>=DESC
      RETURN
*
* Display array
*
9000  CRT @(0,3):@(-3):"  #":@(5,3):"Code  Description"
      START=(PAGE*18)-17
      IF START GT 108 THEN
        START=1
        PAGE=1
      END
      STOP=START+17
      FOR I=START TO STOP STEP 1
        ROW=3+REM(I,18)
        IF ROW=3 THEN ROW=21
        CODE=DISP.REC<1,I>
        DESC=DISP.REC<2,I>
        PRINT @(0,ROW):I"R#3":'  ':CODE"L#4":'  ':DESC"L#40"
      NEXT I
      RETURN
