***********************************************************************
* Analyze bills acted upon by auditors
* Copyright (c) AFS Logistics, LLC., 2002
*
* AUTHOR : Owen Holman
* Date   : January 2002
***********************************************************************
***********************************************************************
!
* Include the ODBC definitions
!
$INCLUDE UNIVERSE.INCLUDE ODBC.H

      form = "T##########"
      dash = "----------"
      Expect = ""
      NULL=""
      TOT.NUM.BILLS=0

      CALL GET.USER(USER)
*     IF USER#'MKERBOW' THEN STOP
      GET (ARG.,1) CLIENT.ID ELSE CLIENT.ID=''
      GET (ARG.,2) BILL.LIST ELSE BILL.LIST=''
      IF CLIENT.ID='' THEN
         CRT 'USAGE: UPD.SQL.FBHIST.BILLS {CLIENT.ID} {FB.LIST.NAME}'
         CRT
         CRT 'Enter the Client # : ':
         INPUT CLIENT.ID
         IF CLIENT.ID='' THEN GOTO THE.END
      END
      IF BILL.LIST='' THEN
         CRT 'Enter the FB select list name : ':
         INPUT BILL.LIST
         IF BILL.LIST='' THEN GOTO THE.END
      END

      OPEN '','CLIENTS' TO F.CLIENT ELSE ABORT 201,'CLIENTS'
      OPEN '','FB.TABLES' TO F.TABLES ELSE ABORT 201,'FB.TABLES'
      READ LI.CODE.REC FROM F.TABLES,'LI.CODES' ELSE LI.CODE.REC=''

      READ CLIENT.REC FROM F.CLIENT,CLIENT.ID THEN
         OPEN '','FB.BILLS.HIST,':CLIENT.ID TO F.BILL THEN
            EDI.FOUND=0
            VC.FOUND=0
            DIV.FOUND=0

            OPEN '','EDI.BILLS.HIST,':CLIENT.ID TO F.EDI THEN EDI.FOUND=1
            OPEN '','VEND.CUST,':CLIENT.ID TO F.VC THEN VC.FOUND=1
            OPEN '','DIVISION.CODE,':CLIENT.ID TO F.DIV THEN DIV.FOUND=1

            STMT = 'GET-LIST ':BILL.LIST
            EXECUTE STMT RTNLIST BILL.LIST CAPTURING GETLIST.OUTPUT
            IF @SELECTED < 1 THEN GOTO THE.END
            NUM.BILLS=@SELECTED
            CRT @(0,0):@(-1):
            TOT.NUM.BILLS+=NUM.BILLS
            CRT @(0,10):@(-3):' #Bills - ':NUM.BILLS'R#12':' - ':TOT.NUM.BILLS'R#15'
            BILL.EOF=0
            BILL.CNT=0
            BILL.PER=0
            LOOP
               READNEXT BILL.ID FROM BILL.LIST ELSE BILL.EOF=1
            UNTIL BILL.EOF DO
               BILL.CNT+=1
               BILL.PCT=ICONV(BILL.CNT/NUM.BILLS*100,'MR2')
               CALL PROG.BAR(13,BILL.CNT,NUM.BILLS,BILL.PER)
               READ BILL.REC FROM F.BILL,BILL.ID THEN
                  EDI.REC=''
                  IF EDI.FOUND THEN
                     READ EDI.REC FROM F.EDI,BILL.ID ELSE EDI.REC=''
                  END
* DEFINE FIELDS TO UPDATE FROM CLIENT.REC HERE
                  CARRIER.ID=FIELD(BILL.ID,'*',1)
                  PRO=FIELD(BILL.ID,'*',2)
                  DIV.REC=''
                  IF DIV.FOUND THEN
                     DIV.ID=BILL.REC<9>
                     READ DIV.REC FROM F.DIV,DIV.ID ELSE DIV.REC=''
                  END
                  VC.REC=''
                  IF VC.FOUND THEN
                     VC.ID=BILL.REC<17>
                     READ VC.REC FROM F.VC,VC.ID ELSE VC.REC=''
                  END
                  SQLVIN=''
                  SQLVIN<1>=USER
                  SQLVIN<2>=CLIENT.ID
                  SQLVIN<3>=BILL.ID
                  SQLVIN<4>='111101'
                  SQLVIN<5>=LI.CODE.REC<1>
                  SQLVIN<6>=LI.CODE.REC<3>
                  SQLVOUT=''
                  CALL UPD.SQL.FBHIST.SUB(SQLVIN,SQLVOUT,CLIENT.REC,DIV.REC,VC.REC,BILL.REC,EDI.REC)
                  IF MOD(BILL.CNT,500)=0 THEN
                     CRT @(0,22):'Sleeping...zzz...zzz...'
                     EXECUTE 'SLEEP 3'
                  END
               END
SKIP.BILL:     *
            REPEAT
         END
      END


THE.END:***
*     End of program
      STOP
