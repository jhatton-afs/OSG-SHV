*
* PROGRAM TO PRINT ADJUSTMENTS TO FREIGHT BILLS AFTER PROCESSING
* VALERIE DOTY
* NOVEMBER 22, 1993
*
* FREIGHT BILLING PACKAGE
*
***********************************************************************
*
$OPTIONS HEADER.EJECT
$OPTIONS EXEC.EQ.PERF
$OPTIONS INFO.ABORT
*
* Initialize variables.
*
      PROMPT ''
      VM = CHAR(253)
      SM = CHAR(252)
      PROG.NAME = 'FB.3.13'
      PROG.DESC='Active Clients - Lead and Second Salesman Listing'
      PACK.NAME = "Client Maintenance"
      CALL GET.USER(USER)
       UPL.VIN = 'FBBP' ;  UPL.VIN<2> = 'FB.3.13' ; UPL.VOUT = ''                    ;* NPR_UPL 04/23/2010
       CALL UPD.PROGRAM.LOG(UPL.VIN,UPL.VOUT)                                                           ;* NPR_UPL 04/23/2010
         BDATA = ''
         BDATA<1,1> = 'Client No'
         BDATA<1,2> = 'Client Name'
         BDATA<1,3> = 'Lead Sls'
         BDATA<1,4> = 'Salesman Name'
         BDATA<1,5> = 'Sec Sls'
         BDATA<1,6> = 'Salesman Name'
         LINE = ''
         BDEXIST = 1
*
      CALL AFS.SCR.HEAD(CO.ID,FILE.ID,'',PROG.NAME,PROG.DESC,PACK.NAME,CO.NAME,TIME.DATE,1)
*
*
* Open general files
*
      OPEN '','AR.SLSMAN.1000.1' TO F.SLSMAN ELSE
         CALL OPEN.ABORT( 'AR.SLSMAN.1000.1', PROG.NAME )
      END
      OPEN '','CLIENTS' TO F.CLIENTS ELSE
         CALL OPEN.ABORT( 'CLIENTS', PROG.NAME )
      END
         OPEN '','BILLDATA.':USER TO F.BILLDATA ELSE
            BDEXIST = 0
         END
*
*
300:*
      HELP = "EX=Exit. RETURN=Continue."
      CALL GEN.IN(0,22,'Enter selection please. --','',Q,0,2,'','',0,-2,22,1,QI,HELP,0,23)
      QI = OCONV(QI,'MCU')
      BEGIN CASE
         CASE QI = 'EX'
            STOP
         CASE QI = ''
            NULL
         CASE 1
            GOTO 300
      END CASE
*
***********************************************************************
* Start processing
***********************************************************************
*
400:*
      TEXT = "Processing Report - Please Wait."
      CALL CENTER(TEXT,80)
      CRT @(0,13):@(-3):TEXT
*
* Set up report heading.
*
      RPT.DESC = PROG.DESC
      HEAD1 = "'LL'" : PROG.NAME:SPACE(22):RPT.DESC:SPACE(22):"'TL'"
      CO.NAME = 'AFS Logistics, LLC'
      TAB = INT(55-(LEN(CO.NAME)/2))
      HEAD2 = SPACE(TAB):CO.NAME:SPACE(TAB):"Page 'PL'"
      HEAD3 = STR('-',127):"'L'"
      HEAD3 = HEAD3:"Client No" 'L#10 ':"Client Name" 'L#30 '
      HEAD3 = HEAD3:"Lead Sls" 'L#8 ':"Salesman Name" 'L#20 '
      HEAD3 = HEAD3:"Sec Sls" 'L#8 ':"Salesman Name" 'L#20 ':"'L'"
      HEAD3 = HEAD3:STR('-',127):"'L'"
*
* Select clients to process.
*
      STMT = 'SSELECT CLIENTS BY 2'
      STMT = STMT:' WITH 30 = "A" AND WITH @ID # "99]"'
      EXECUTE STMT PASSLIST CAPTURING OUTPUT
      NUM.ITEMS = @SELECTED
*
* Start processing report.
*
      VIN=''
      VIN<3>='R'
      CALL LASER.CODE(VIN,VOUT)
      IF VOUT<1>#'' THEN
         CALL ELINE(VOUT<1>)
         STOP
      END
      IF VOUT<3>#'' THEN EXECUTE VOUT<3>
      PRINTER ON
      IF VOUT<2>#'' THEN PRINT VOUT<2>:
      HEADING HEAD1:HEAD2:HEAD3
*
***********************************************************************
* Get next item to process.
***********************************************************************
*
      COUNTER = 0
      DONE = 0
      LOOP
         READNEXT CLIENT.ID ELSE DONE = 1
      UNTIL DONE = 1 DO
         COUNTER = COUNTER+1
         CALL PROG.BAR(23,COUNTER,NUM.ITEMS,PER)
*
         GOSUB READ.CLIENT
         GOSUB PRINT.DETAIL
         IF BDEXIST THEN GOSUB WRITE.BILLDATA
      REPEAT
         IF BDEXIST THEN
           BDATA=CHANGE(BDATA,@VM,CHAR(9))
           WRITE BDATA ON F.BILLDATA,'SLSMAN.XLS'
         END

      PRINTER OFF
      PRINTER CLOSE
      STOP
*
*----------------------------------------------------------------------
* INTERNAL SUBROUTINES
*----------------------------------------------------------------------
*
***********************************************************************
PRINT.DETAIL:*
***********************************************************************
*
      PRINT.LINE = SPACE(132)
      PRINT.LINE[1,10] = CLIENT.ID 'R#10'
      PRINT.LINE[12,30] = CLIENT.NAME 'L#30'
      PRINT.LINE[43,8] = LEAD.SLSMAN 'L#8'
      PRINT.LINE[52,20] = LEAD.SLSMAN.NAME 'L#20'
      PRINT.LINE[73,8] = SECOND.SLSMAN 'L#8'
      PRINT.LINE[82,20] = SECOND.SLSMAN.NAME 'L#20'
*
      PRINT PRINT.LINE
*
      RETURN
***********************************************************************
READ.CLIENT:*
***********************************************************************
*
      READ CLIENT.REC FROM F.CLIENTS, CLIENT.ID ELSE
         CLIENT.REC = ''
         CLIENT.REC<2> = CLIENT.ID : ' not on file.'
      END
      CLIENT.NAME = CLIENT.REC<2>
      LEAD.SLSMAN = CLIENT.REC<114>
      READV LEAD.SLSMAN.NAME FROM F.SLSMAN,LEAD.SLSMAN,1 ELSE LEAD.SLSMAN.NAME=''
      SECOND.SLSMAN = CLIENT.REC<115>
      READV SECOND.SLSMAN.NAME FROM F.SLSMAN,SECOND.SLSMAN,1 ELSE SECOND.SLSMAN.NAME=''

*
      RETURN
***********************************************************************
WRITE.BILLDATA:*
***********************************************************************
*
      LINE<1,1> = CLIENT.ID
      LINE<1,2> = CLIENT.NAME
      LINE<1,3> = LEAD.SLSMAN
      LINE<1,4> = LEAD.SLSMAN.NAME
      LINE<1,5> = SECOND.SLSMAN
      LINE<1,6> = SECOND.SLSMAN.NAME
      BDATA<-1> = LINE
*
      RETURN
   END
