****************************************
* Program name : CLEANUP.FBWORK
* Author       : Greg Bino
* Date written : Jan 28, 2016
* Library      : FBBP
* COPYRIGHT (C) 1998 - AFS LOGISTICS LLC - ALL RIGHTS RESERVED.
*
*
* This program clears out files used to send emails
* Last updated by afs (gbino) at 08:48:26 on 03/29/2016.
*
* Start modification history
* Mod nn  mm/dd/yy, init, req #, description
* End modification history
*
****************************************
$OPTIONS HEADER.EJECT
$OPTIONS EXEC.EQ.PERF
$OPTIONS INFO.ABORT
      ERRMSG=''
*
      COMMON PROGPARAMS(24)
      EQU PROGFROM TO PROGPARAMS(1)
      EQU USER TO PROGPARAMS(2)
      EQU PROCDATE TO PROGPARAMS(3)
      EQU CHNUM TO PROGPARAMS(4)
*
      CRT @(0,0):@(-3):
      CALL GET.USER.NEW(USER,CH.NUM)
* Begin GRB01
      VIN1=""
      VIN1<1> = "CLEANUP.FBWORK"
      VIN1<2> = USER
      VIN1<3> = CH.NUM
      VIN1<4> = "S"
      CALL PROGTRACK(VIN1,VOUT1)
* End GRB01
      EXECUTE "WHO" CAPTURING WHO
      IF INDEX(OCONV(WHO,'MCU'),'AFS',1)=0 AND INDEX(OCONV(WHO,'MCU'),'SV.AUTO',1)=0 THEN
         STMT='BBSEND GREGB CLEANUP.FBWORK CAN ONLY BE RUN IN THE AFS ACCOUNT!-':WHO
         EXECUTE STMT CAPTURING RESULTS
         CALL ELINE('THIS CAN ONLY BE RUN IN THE AFS ACCOUNT!')
         STOP
      END
      EXECUTE "LISTU NOPAGE" CAPTURING OUTPUT
      LISTU.OUTPUT=CHANGE(OUTPUT,@AM,@VM)
**********************************************************
      TIMES=DCOUNT(OUTPUT,@AM)
      ISON=''
* ISON <1> NAME
* ISON <2> PORT
* ISON <3> TIME
      FOR X = 5 TO TIMES-3
         LINE=OUTPUT<X>
         SHV.POS=INDEX(LINE,'AFSLOGISTICS',1)
         NAME.POS=SHV.POS+13
         NAME.LENGTH=9
         IF SHV.POS=0 THEN
            NAME.POS=23
            NAME.LENGTH=20
         END
         NAME=TRIM(LINE[NAME.POS,NAME.LENGTH])
         PID=TRIM(LINE[15,6])
         PORT=MOD(PID,32768)'R%5'
         TIME=TRIM(LINE[62,12])
         LOCATE PORT IN ISON<1> BY 'AR' SETTING PPOS ELSE
            INS PORT BEFORE ISON<1,PPOS>
            INS NAME BEFORE ISON<2,PPOS>
            INS TIME BEFORE ISON<3,PPOS>
            INS PID BEFORE ISON<4,PPOS>
         END
      NEXT X

      PER=''
      OPEN '','VOC' TO F.VOC ELSE ABORT 201,'VOC'
      FOR TIMES=1 TO 2
         BEGIN CASE
            CASE TIMES=1
               STMT='SSELECT VOC = "FBWORK]" WITH F1 = "F]"'
            CASE TIMES=2
               STMT='SSELECT AFS_DIR = "FBWORK]"'
         END CASE
         EXECUTE STMT RTNLIST WORK.LIST CAPTURING RESULTS
         NUM.ITMS=@SELECTED
         EOF=0
         CNT=0
         LOOP
            READNEXT FNAME FROM WORK.LIST ELSE EOF=1
         UNTIL EOF DO
            CNT+=1
            CHANNEL=FNAME[7,5]
            VALID.CHAN=1
            IF CHANNEL='' THEN VALID.CHAN=0
            CHAN.TEST=OCONV(CHANNEL,'MCN')
            IF CHAN.TEST='' THEN VALID.CHAN=0
            IF LEN(FNAME)#11 THEN VALID.CHAN=0
            IF VALID.CHAN THEN
               LOCATE CHANNEL IN ISON<1> SETTING CPOSS ELSE
****                  CRT @(0,10):@(-4):'Clearing port number ':CHANNEL:'...':
                  CRT @(0,11):@(-4):
                  CRT @(0,12):@(-4):
                  CRT @(0,13):@(-4):
                  CRT @(0,14):@(-4):
                  CRT @(0,15):@(-4):
                  CALL PROG.BAR(20,CNT,NUM.ITMS,PER)
                  TERMFIL=CHANNEL:'P'
                  OPEN '','LAST.LOGINS' TO F.LAST.LOGINS THEN
****                     CRT @(0,11):'Clearing Last Logins file...':
                     DELETE F.LAST.LOGINS,CHANNEL
                  END
                  OPEN '','FBWORK':CHANNEL TO F.WORK THEN
                     CLOSE F.WORK
                  END ELSE
                  END
****                  CRT @(0,12):'Deleting P File...':
                  OPEN '',TERMFIL TO F.TF THEN
                     CLOSE F.TF
                     EXECUTE "DELETE-FILE ":TERMFIL CAPTURING OUTPUT
                  END ELSE
                     DELETE F.VOC,TERMFIL
                     DELETE F.VOC,'D_':TERMFIL
                     DOS.STMT="erase ":TERMFIL
                     EXECUTE "dos /c ":DOS.STMT CAPTURING DOS.RESULTS
                     DOS.STMT="erase D_":TERMFIL
                     EXECUTE "dos /c ":DOS.STMT CAPTURING DOS.RESULTS
                  END
****                  CRT @(0,13):'Deleting R File...':
                  OPEN '','R':CHANNEL TO F.RF THEN
                     CLOSE F.RF
                     EXECUTE "DELETE-FILE R":CHANNEL CAPTURING OUTPUT
                  END ELSE
                     DELETE F.VOC,'R':CHANNEL
                     DELETE F.VOC,'D_R':CHANNEL
                     DOS.STMT="erase R":CHANNEL
                     EXECUTE "dos /c ":DOS.STMT CAPTURING DOS.RESULTS
                     DOS.STMT="erase D_R":CHANNEL
                     EXECUTE "dos /c ":DOS.STMT CAPTURING DOS.RESULTS
                  END
****                  CRT @(0,14):'Deleting T File...':
                  OPEN '','T':CHANNEL TO F.TF THEN
                     CLOSE F.TF
                     EXECUTE "DELETE-FILE T":CHANNEL CAPTURING OUTPUT
                  END ELSE
                     DELETE F.VOC,'T':CHANNEL
                     DELETE F.VOC,'D_T':CHANNEL
                     DOS.STMT="erase T":CHANNEL
                     EXECUTE "dos /c ":DOS.STMT CAPTURING DOS.RESULTS
                     DOS.STMT="erase D_T":CHANNEL
                     EXECUTE "dos /c ":DOS.STMT CAPTURING DOS.RESULTS
                  END
****                  CRT @(0,15):'Deleting FB Work File...':
                  OPEN '','FBWORK':CHANNEL TO F.FW THEN
                     CLOSE F.FW
                     EXECUTE "DELETE-FILE FBWORK":CHANNEL CAPTURING OUTPUT
                  END ELSE
                     DELETE F.VOC,'FBWORK':CHANNEL
                     DELETE F.VOC,'D_FBWORK':CHANNEL
                     DOS.STMT="erase FBWORK":CHANNEL
                     EXECUTE "dos /c ":DOS.STMT CAPTURING DOS.RESULTS
                     DOS.STMT="erase D_FBWORK":CHANNEL
                     EXECUTE "dos /c ":DOS.STMT CAPTURING DOS.RESULTS
                  END
               END
            END
         REPEAT
      NEXT TIMES
999:  ******************************************************************
* Begin GRB01
      VIN1=''
      VIN1<1> = "CLEANUP.FBWORK"
      VIN1<2> = USER
      VIN1<3> = CH.NUM
      VIN1<4> = "E"
      CALL PROGTRACK(VIN1,VOUT1)
* End GRB01
   END
