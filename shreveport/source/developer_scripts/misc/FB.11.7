$OPTIONS HEADER.EJECT
      ERRMSG=''
*
$OPTIONS EXEC.EQ.PERF
$OPTIONS INFO.ABORT
*************************************************************************
* ROUTINE TO ENTER CLIENT/CARRIER CUSTOM SCAC CODES
*
* FREIGHT BILLING PACKAGE
*
* AUTHOR: JMK
* DATE: DECEMBER 15, 2004
*
* THIS PROGRAM IS A PROPRIETARY PRODUCT OF AUTOMATED FREIGHT SYSTEMS
* AND MAY NOT BE USED, COPIED OR MODIFIED WITHOUT THE EXPRESS
* WRITTEN CONSENT OF SAME.
*
********************************************************************
* 070506 - JMK01 - EXPAND CUSTOMER SCAC CODE TO 10 CHARACTERS
* 111006 - JMK02 - T200611100011 / priority 2 / SMATTHEWS attempted to change a custom SCAC code
*
*
************************************************************************
* Initialize variables
************************************************************************
      PROMPT ''
      PROG.NAME="FB.11.7"
      PROG.DESC="Client/Carrier Custom SCAC Code"
      PACK.NAME="Freight Payment"
************************************************************************
       UPL.VIN = 'FBBP' ;  UPL.VIN<2> = 'FB.11.7' ; UPL.VOUT = ''                    ;* NPR_UPL 04/23/2010
       CALL UPD.PROGRAM.LOG(UPL.VIN,UPL.VOUT)                                                           ;* NPR_UPL 04/23/2010
* Open files
************************************************************************
      OPEN '','CLIENTS' TO F.CLIENTS ELSE CALL OPEN.ABORT("CLIENTS",PROG.NAME)
      OPEN '','CLIENT.SCAC' TO F.CSCAC ELSE CALL OPEN.ABORT("CLIENT.SCAC",PROG.NAME)
      OPEN '','CARRIERS' TO F.CARRIERS ELSE CALL OPEN.ABORT("CARRIERS",PROG.NAME)
      CALL GET.USER(USER)


************************************************************************
* Enter client number.
************************************************************************
100:  CALL AFS.SCR.HEAD(CO.ID,FILE.ID,'',PROG.NAME,PROG.DESC,PACK.NAME,CO.NAME,TIME.DATE,1)
      HELP="Enter client number. Word of name for search. X=Exit. EX=Exit."
      CALL GEN.IN(0,3,'Enter Client Number: #####','',Q,0,20,'','',2,-5,3,0,QI,HELP,0,23)
      QI=OCONV(QI,'MCU')
      BEGIN CASE
         CASE QI='' ! QI='EX' ! QI='X'
            STOP
         CASE NOT(QI MATCHES "0N")
            CALL SOUNDEX.DISPLAY(QI,'BCUST','SDX.CLIENTS,NAME',2,ITEM.LIST)
            CALL AFS.SCR.REFRESH(PROG.NAME,PROG.DESC,PACK.NAME,CO.NAME,TIME.DATE,1)
            BEGIN CASE
               CASE ITEM.LIST=''
                  GOTO 100
               CASE DCOUNT(ITEM.LIST<1>,@VM) GT 1
                  GOTO 100
               CASE ITEM.LIST NE ''
                  IF NUM(ITEM.LIST<1,1>) THEN DATA ITEM.LIST<1,1>
                  GOTO 100
            END CASE
         CASE NUM(QI)
            Q=QI'R%5'
         CASE 1
            GOTO 100
      END CASE
      CLIENT.ID=Q
      READ CLIENT.REC FROM F.CLIENTS,Q ELSE
         CALL ELINE('ERROR - Client ':CLIENT.ID:' not on file.')
         GOTO 100
      END
      CLIENT.NAME=CLIENT.REC<2>
      CRT @(21,3):CLIENT.ID:' ':CLIENT.NAME

************************************************************************
* Enter carrier number.
************************************************************************
200:  HELP="Enter carrier number. Word of name for search. [L]ist current [X]Back [EX]it."
      CALL GEN.IN(0,5,'Enter Carrier Number: #####','',Q,0,20,'','',2,-5,5,0,QI,HELP,0,23)
      QI=OCONV(QI,'MCU')
      BEGIN CASE
         CASE QI='EX'
            STOP
         CASE QI='X'
            CRT @(0,5):@(-3):
            GOTO 100
         CASE QI = 'L'
            EXECUTE 'SORT CLIENT.SCAC CARRIER.NAME 1 WITH CLIENT =':CLIENT.ID:' BY CARRIER.NAME'
            INPUT NULL
            CALL AFS.SCR.REFRESH(PROG.NAME,PROG.DESC,PACK.NAME,CO.NAME,TIME.DATE,1)
            CRT @(0,3):'Enter Client Number: '
            CRT @(21,3):CLIENT.ID:' ':CLIENT.NAME
            GO 200
         CASE NOT(QI MATCHES "0N")
            CALL SOUNDEX.DISPLAY(QI,'BCUST','SDX.CARRIER,NAME',2,ITEM.LIST)
            CALL AFS.SCR.REFRESH(PROG.NAME,PROG.DESC,PACK.NAME,CO.NAME,TIME.DATE,1)
            BEGIN CASE
               CASE ITEM.LIST=''
                  GOTO 200
               CASE DCOUNT(ITEM.LIST<1>,@VM) GT 1
                  GOTO 200
               CASE ITEM.LIST NE ''
                  IF NUM(ITEM.LIST<1,1>) THEN DATA ITEM.LIST<1,1>
                  GOTO 200
            END CASE
         CASE NUM(QI)
            Q=QI'R%5'
         CASE 1
            GOTO 200
      END CASE
      CARRIER.ID=Q
      READ CARRIER.REC FROM F.CARRIERS,Q ELSE
         CALL ELINE('ERROR - Carrier ':CARRIER.ID:' not on file.')
         GOTO 200
      END
      CARRIER.NAME=CARRIER.REC<1>
      CRT @(22,5):CARRIER.ID:' ':CARRIER.NAME:@(-4)


************************************************************************
* Check for existing custom SCAC code
************************************************************************
      CSCAC.ID = CLIENT.ID'R%5':'*':CARRIER.ID'R%5'
      READ START.REC FROM F.CSCAC,CSCAC.ID ELSE START.REC = ''
      CSCAC = START.REC<1>


************************************************************************
* Enter client/carrier custom SCAC code.
************************************************************************

300:  CRT @(0,7): 'Enter the custom SCAC code for reporting on this client and carrier.'
      IF CSCAC = '' THEN
*        LN = '######' ; * JMK01
         LN = '##########' ; * JMK01
      END ELSE
*        LN = CSCAC'L#6' ; * JMK01
         LN = CSCAC'L#10' ; * JMK01
      END
      HELP="Enter custom SCAC code for client/carrier. [X]Back [EX]it"
*     CALL GEN.IN(0,8,LN,'',Q,0,6,'','',0,-6,8,0,QI,HELP,0,23) ; * JMK01
      CALL GEN.IN(0,8,LN,'',Q,0,10,'','',0,-10,8,0,QI,HELP,0,23) ; * JMK01
      QI=OCONV(QI,'MCU')
      BEGIN CASE
         CASE QI='X'
            CRT @(0,8):@(-3):
            GOTO 200
         CASE QI='EX'
            STOP
      END CASE
      CSCAC=Q
*     CRT @(0,8):CSCAC'L#6' ; * JMK01
      CRT @(0,8):CSCAC'L#10' ; * JMK01


************************************************************************
* Verify selection criteria is correct.
************************************************************************
400:  HELP="X=Change. EX=Exit. RETURN=Continue."
      CALL GEN.IN(0,22,'Enter selection please. --','',Q,0,2,'','',0,-2,22,0,QI,HELP,0,23)
      QI=OCONV(QI,'MCU')
      BEGIN CASE
         CASE QI='X'
            CRT @(0,22):@(-3):
            GOTO 300
         CASE QI='EX'
            STOP
         CASE QI=''
            NULL
         CASE 1
            CALL ELINE('Invalid entry. Must be X, EX, or RETURN.')
            GOTO 400
      END CASE

      BEGIN CASE
         CASE USER='OWEN' ; NULL
         CASE USER='KAREN' ; NULL
         CASE USER='BRIAN' ; NULL
         CASE USER='DAVID' ; NULL
         CASE USER='MYRA' ; NULL
         CASE USER='MARY' ; NULL
         CASE USER='MKERBOW' ; NULL
         CASE USER='JESPOSITO' ; NULL
         CASE USER='JAUDIT' ; NULL
         CASE USER='NFASONE' ; NULL
         CASE USER='SMATTHEWS' ; * JMK02
         CASE 1
            EMAIL.OUT=''
            EMAIL.OUT<1,1>='KGilbert@afs.net'
            EMAIL.OUT<1,2>='OHolman@afs.net'
            EMAIL.OUT<1,3>='SMatthews@afs.net'
****            EMAIL.OUT<1,4>='MButts@afs.net'
            EMAIL.OUT<1,5>='MMcKay@afs.net'
            IF USER='OWEN' THEN EMAIL.OUT<1>='OHolman@afs.net'
            EMAIL.OUT<4>='FB.11.7@afs.net'          ; * From Email address
            EMAIL.OUT<5>=USER:' attempted to change a custom SCAC code'
            EMAIL.OUT<6>=USER:' attempted to change a custom SCAC code for the Following:'
            EMAIL.OUT<6,-1>="Client: ":CLIENT.ID:"-":CLIENT.NAME
            EMAIL.OUT<6,-1>="Carrier: ":CARRIER.ID:"-":CARRIER.NAME
            EMAIL.OUT<6,-1>="Custom SCAC Code: ":CSCAC'L#8'
            IF START.REC<1>#'' THEN
               EMAIL.OUT<6,-1>="Current custom SCAC code is: ":START.REC<1>'L#8'
            END
            EMAIL.OUT<6,-1>="The Attempt was detected and the Update was Denied,"
            EMAIL.OUT<6,-1>="No changes were allowed"
            CALL SENDMAIL(EMAIL.OUT,VOUT)
            CALL ELINE('You are not authorized to modify custom SCAC codes!')
            GO 300
      END CASE

************************************************************************
* Save start Date
************************************************************************
      IF CSCAC # START.REC<1> THEN
         START.REC<5,-1> = START.REC<1>
         START.REC<1> = CSCAC
         START.REC<2,-1> = USER
         START.REC<3,-1> = DATE()
         START.REC<4,-1> = TIME()
         WRITE START.REC ON F.CSCAC,CSCAC.ID
      END
      GO 100
