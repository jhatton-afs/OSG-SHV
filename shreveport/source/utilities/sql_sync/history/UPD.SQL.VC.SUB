      SUBROUTINE UPD.SQL.VC.SUB(VIN,VOUT,VC.REC)
***********************************************************************
* Program name : UPD.SQL.VC.SUB
* Author       : Owen Holman
* Date written : January 2002
* Library      : FBBP
* COPYRIGHT (C) 2016 - AFS LOGISTICS LLC - ALL RIGHTS RESERVED.
*
* VEND/CUST SQL UPDATE
*
* Last updated by afs (AFSLOGISTICS\gbino) at 08:45:33 on 12/19/2018.
*
* Start modification history     
* 070709 - JMK01 - ADD CONSCODE TO TRANSFER LIST FOR CUSTOMERS
* 080410 - NPR01 - LIMIT CONSCODE TO 20 CHARS MAX
* 011911 - JMK02 - T110042 - FB Entry frozen at Customer/Vendor, client #1832 H&K Canada
* 061912 - JMK03 - Choose development SQL server if not production server AFS
***********************************************************************

***RETURN

$INCLUDE UNIVERSE.INCLUDE ODBC.H
  CALL GET.USER.NEW(USER,CH.NUM)
 UPL.VIN = 'FBBP' ; UPL.VIN<2> = 'UPD.SQL.VC.SUB' ; UPL.VOUT = ''  
       UPL.VIN<3> = USER
       UPL.VIN<4> =  CH.NUM 
      CALL UPD.PROGRAM.LOG(UPL.VIN,UPL.VOUT)       
      CALL GET.SERVER.INFO(SVR.VIN,SVR.VOUT)       ; * JMK03
      SVR.NAME=SVR.VOUT<1>               ; * JMK03

      USER=VIN<1>
      CLIENT.ID=VIN<2>
      VC.ID=VIN<3>

      form = "T##########"
      dash = "----------"
      Expect = ""
      NULL=""
      ERROR.CNT=0
10:   ERRORS=''

      GOSUB SETUP.SQL

* DEFINE FIELDS TO UPDATE FROM VC.REC HERE

      SQL.CLIENT.ID=CLIENT.ID+0
      SQL.VC.ID = VC.ID+0
      SQL.VC.NAME = VC.REC<1>[1,50]      ; * NAME
      SQL.VC.ADDR1 = VC.REC<3>[1,50]     ; * ADDRESS
      SQL.VC.ADDR2 = ''[1,50]            ; * JMK02
      SQL.VC.CITY = VC.REC<5>[1,50]      ; * CITY
      SQL.VC.ST = VC.REC<6>[1,20]        ; * STATE
      SQL.VC.ZIP = VC.REC<7>[1,20]       ; * ZIP
      SQL.VC.COUNTRY = ''[1,20]          ; * COUNTRY ; * JMK02
      SQL.VC.FLAG = VC.REC<4>[1,1]       ; * FLAG (VCIR)
      SQL.VC.CONSCODE = VC.REC<2>[1,20]  ; * JMK01 ; NPR01

      Fn = "SQLExecute"
      STATUS = SQLExecute(STMTENV)
      GOSUB CKSTMTENV

************************************************************
THE.END:***
************************************************************
*     End of program
*
* Now just clean up the environment and leave
*
      Fn = "SQLFreeStmt"
      STATUS = SQLFreeStmt(STMTENV, SQL.DROP)
      GOSUB CKSTMTENV

      Fn = "SQLDisconnect"
      STATUS = SQLDisconnect(CONENV)
      GOSUB CKCONENV

      Fn = "SQLFreeConnect"
      STATUS = SQLFreeConnect(CONENV)
      GOSUB CKCONENV

      Fn = "SQLFreeEnv"
      STATUS = SQLFreeEnv(DBCENV)
      IF STATUS <> SQL.SUCCESS THEN
         ERRORS<1,-1>="Failed to release ODBC environment"
      END

      IF ERRORS#'' THEN
         ERROR.CNT+=1
         IF ERROR.CNT>3 THEN
            MVIN=''
            MVIN<1>='UPD.SQL.Group@afs.net'
            MVIN<4>='AFS_UPD.SQL.VC.SUB'
            MVIN<5>='SQL Client Vendor/Customer Update Errors'
            TEXT='On ':DATE()'D2/':' ':OCONV(TIME(),'MTHS'):', ':USER:' updated '
            TEXT<1,-1>='Client ':CLIENT.ID:' Vendor/Customer ':VC.ID:', but did not succeed in updating SQL due to:'
            TEXT<1,-1>=' '
            TEXT<1,-1>=ERRORS
            MVIN<6>=TEXT
            MVIN<7>=1
            CALL SENDMAIL(MVIN,MVOUT)
         END ELSE
            GO 10
         END
      END

      RETURN

      STOP
************************************************************
SETUP.SQL:*
************************************************************
* Create an ODBC environment & a connection environment. Use the
* SSQLSetConnectOption to specify the Operating system user ID and
* password.

      STATUS = SQLAllocEnv(DBCENV)
      IF STATUS <> SQL.SUCCESS THEN
         ERRORS<1,-1>="Failed to allocate an ODBC environment"
         RETURN TO THE.END
      END

      STATUS = SQLAllocConnect(DBCENV,CONENV)
      IF STATUS <> SQL.SUCCESS THEN
         ERRORS<1,-1>="Failed to allocate a CONNECT environment"
         RETURN TO THE.END
      END

*
* Gather the data source name, userid and password for the server O/S and
* information for the DBMS at the server. The DBMS info is often also
* a user name and password, but not in all cases.
*

      BEGIN CASE                         ; * JMK03
         CASE SVR.NAME='AFS'             ; * JMK03
            SOURCE='PRODODS'             ; * JMK03
            UID="uv_sql"                 ; * JMK03
            PWD="4@gAbreg"               ; * JMK03
         CASE 1                          ; * JMK03
            SOURCE='DEVODS'              ; * JMK03
            UID="uv_sql"                 ; * JMK03
            PWD="4@gAbreg"               ; * JMK03
      END CASE                           ; * JMK03

* Establish a session to the specified data source
*
*     PRINT "Connecting to data source: ": SOURCE
      Fn = "SQLConnect"
      STATUS = SQLConnect(CONENV,SOURCE,UID,PWD)
      GOSUB CKCONENV
*
* Now allocate an SQL statement environment
* to be used for executing SQL statements
*
      Fn = "SQLAllocStmt"
      STATUS = SQLAllocStmt(CONENV,STMTENV)
      GOSUB CKCONENV

*
* Note that we are using parameter markers in the SQL statement to facilitate
* loading multiple rows of data efficiently
*

      OPT.STR = STR(' ?,',11)            ; * JMK01
      OPT.STR = OPT.STR[2,LEN(OPT.STR)-2]
      INST1 = "{CALL PX_UpdCustomerVendor (":OPT.STR:")}"    ; * 10 PLACEHOLDERS

      ROWNUM = 0
      Fn = "SQLSetParam"
*
* DEFINE THE FIELD UPDATE PARAMETERS USING THE FOLLOWING FORMAT
*     STATUS = SQLSETPARAM(STMTENV, COL, SQL.B.BASIC, SQL.DATA.TYPE, PREC, SCALE, VAR.NAME) ; GOSUB CKSTMTENV
*
      STATUS = SQLSETPARAM(STMTENV, 1, SQL.B.BASIC, SQL.VARCHAR, 50, 0, SQL.CLIENT.ID) ; GOSUB CKSTMTENV
      STATUS = SQLSETPARAM(STMTENV, 2, SQL.B.BASIC, SQL.VARCHAR, 50, 0, SQL.VC.ID) ; GOSUB CKSTMTENV
      STATUS = SQLSETPARAM(STMTENV, 3, SQL.B.BASIC, SQL.VARCHAR, 50, 0, SQL.VC.NAME) ; GOSUB CKSTMTENV
      STATUS = SQLSETPARAM(STMTENV, 4, SQL.B.BASIC, SQL.VARCHAR, 50, 0, SQL.VC.ADDR1) ; GOSUB CKSTMTENV
      STATUS = SQLSETPARAM(STMTENV, 5, SQL.B.BASIC, SQL.VARCHAR, 50, 0, SQL.VC.ADDR2) ; GOSUB CKSTMTENV
      STATUS = SQLSETPARAM(STMTENV, 6, SQL.B.BASIC, SQL.VARCHAR, 50, 0, SQL.VC.CITY) ; GOSUB CKSTMTENV
      STATUS = SQLSETPARAM(STMTENV, 7, SQL.B.BASIC, SQL.VARCHAR, 20, 0, SQL.VC.ST) ; GOSUB CKSTMTENV
      STATUS = SQLSETPARAM(STMTENV, 8, SQL.B.BASIC, SQL.VARCHAR, 20, 0, SQL.VC.ZIP) ; GOSUB CKSTMTENV
      STATUS = SQLSETPARAM(STMTENV, 9, SQL.B.BASIC, SQL.VARCHAR, 20, 0, SQL.VC.COUNTRY) ; GOSUB CKSTMTENV
      STATUS = SQLSETPARAM(STMTENV, 10, SQL.B.BASIC, SQL.CHAR, 1, 0, SQL.VC.FLAG) ; GOSUB CKSTMTENV
      STATUS = SQLSETPARAM(STMTENV, 11, SQL.B.BASIC, SQL.VARCHAR, 20, 0, SQL.VC.CONSCODE) ; GOSUB CKSTMTENV    ; * JMK01

      Fn = "SQLPrepare"
      STATUS = SQLPrepare(STMTENV, INST1)
      GOSUB CKSTMTENV

      RETURN
************************************************************
CKCONENV:
************************************************************
      COUNT = -1
      IF STATUS EQ -2 THEN
         ERRORS<1,-1>="INVALID CONNECTION HANDLE FOR USER ":USER
         STMT='BBSEND GREGB UPD.SQL.VC.SUB CLIENT ':CLIENT.ID:' VENDOR/CUSTOMER ':VC.ID:' INVALID CONNECTION HANDLE ABORT - USER ':USER
         EXECUTE STMT CAPTURING RESULTS
         RETURN TO THE.END
      END
      IF STATUS NE 0 THEN
201:     *
         ST = SQLERROR(-1,CONENV,-1,STATE,NATCODE,ERRTXT)
         IF ST <> SQL.NO.DATA.FOUND
         THEN
            COUNT = 1
            IF Expect NE 0 AND STATE = Expect AND ST <> SQL.NO.DATA.FOUND THEN
               ERRORS<1,-1>="Allowed error of ":STATE:" returned for func ":Fn
               GOTO 299
            END ELSE
               ERRORS<1,-1>="Status for ":Fn:" call is: ":STATUS
               ERRORS<1,-1>="SQLSTATE,NATCOD are:" : STATE:"  ":NATCODE
               ERRORS<1,-1>="Error text is"
               ERRORS<1,-1>="    " : ERRTXT
            END
            IF ST = SQL.SUCCESS THEN GOTO 201
         END
         IF STATUS = -1 AND COUNT = 1 THEN
            ERRORS<1,-1>="EXITING CKCONENV"
            RETURN TO THE.END
         END
      END
299:  *
      RETURN
************************************************************
CKSTMTENV:
************************************************************
      IF STATUS EQ -2 THEN
         ERRORS<1,-1>="INVALID STATEMENT HANDLE FOR USER ":USER
         STMT='BBSEND GREGB UPD.SQL.VC.SUB CLIENT ':CLIENT.ID:' VENDOR/CUSTOMER ':VC.ID:' INVALID STATEMENT HANDLE ABORT - USER ':USER
         EXECUTE STMT CAPTURING RESULTS
         RETURN TO THE.END
      END

      IF STATUS EQ 100 THEN RETURN
      IF STATUS NE 0 THEN
301:     *
         ST = SQLERROR(-1,-1,STMTENV,STATE,NATCODE,ERRTXT)
         IF ST <> SQL.NO.DATA.FOUND
         THEN
            COUNT = 1
            IF Expect NE 0 AND STATE = Expect AND ST <> SQL.NO.DATA.FOUND THEN
               ERRORS<1,-1>="Allowed error of ":STATE:" returned for func ":Fn
               GOTO 399
            END ELSE
               ERRORS<1,-1>="Status for ":Fn:" call is: ":STATUS
               ERRORS<1,-1>="SQLSTATE,NATCOD are:" : STATE:"  ":NATCODE
               ERRORS<1,-1>="Error text is"
               ERRORS<1,-1>="    " : ERRTXT
            END
            IF ST = 0 THEN GOTO 301
         END
         IF STATUS = -1 AND COUNT = 1 THEN
            ERRORS<1,-1>="EXITING CKSTMTENV"
            RETURN TO THE.END
         END
      END
399:  *
      RETURN
