$OPTIONS HEADER.EJECT
      ERRMSG=''
*
$OPTIONS EXEC.EQ.PERF
$OPTIONS INFO.ABORT
*
* PROGRAM: FB.5.12
* PROGRAM TO PROCESS CLEARED CHECKS WITH ERRORS REPORT
*
* PACKAGE: FREIGHT BILLING
*
* AUTHOR: BRIAN M. QUEEN
* DATE: MAY 1, 1992
*
* 110104 - JMK01 - ADD BANK INFO TO REPORT HEADING
* 050806 - JCW01 - CHANGES TO '@SELECTED', 'CLEARSELECT', AND 'STMT<X>'
* 061906 - JMK02 - ADD DOWNLOAD OPTIONS FOR REPORT
* 090806 - JCW02 - USE NEW FB.CHECKS FILES
***********************************************************************
*
* Initialize variables
*
      PROMPT''
      PROG.NAME='FB.5.12'
      PROG.DESC='Checks Cleared with Errors Report'
      PACK.NAME='Freight Billing'
      GRAND.TOT=''
       UPL.VIN = 'FBBP' ;  UPL.VIN<2> = 'FB.5.12' ; UPL.VOUT = ''                    ;* NPR_UPL 04/23/2010
       CALL UPD.PROGRAM.LOG(UPL.VIN,UPL.VOUT)                                                           ;* NPR_UPL 04/23/2010
      CALL GET.USER(USER)                ; * JMK02
*
* Open files
*
*     OPEN '','FB.CHECKS' TO F.CHECKS ELSE CALL OPEN.ABORT("FB.CHECKS",PROG.NAME)         ; * JCW02
      OPEN '','FB.CHECKS.HDR' TO F.CHECKS ELSE CALL OPEN.ABORT("FB.CHECKS.HDR",PROG.NAME) ; * JCW02
      OPEN '','CARRIERS' TO F.CARRIER ELSE CALL OPEN.ABORT("CARRIERS",PROG.NAME)
      OPEN '','FB.BANKS' TO F.BANKS ELSE CALL OPEN.ABORT("FB.BANKS",PROG.NAME)
      OPEN '','VOC' TO F.VOC ELSE        ; * JMK02
         CALL ELINE('Unable to open VOC file - Call IS Department.')   ; * JMK02
         STOP                            ; * JMK02
      END                                ; * JMK02
      READ BILLDATA.REC FROM F.VOC,'BILLDATA' ELSE           ; * JMK02
         CALL ELINE('Unable to open VOC BILLDATA - Call IS Department.')         ; * JMK02
         STOP                            ; * JMK02
      END                                ; * JMK02
      BILLDATA.REC<2>:='\':USER          ; * JMK02
      WRITE BILLDATA.REC ON F.VOC,'BILLDATA.':USER ELSE      ; * JMK02
         CALL ELINE('Your user name ':USER:' is not in the BILLDATA folder - Call IS Department.')   ; * JMK02
         STOP                            ; * JMK02
      END                                ; * JMK02
      OPEN '','BILLDATA.':USER TO F.BILLDATA ELSE  ; * JMK02
         CALL ELINE('Unable to open BILLDATA.':USER:'.')     ; * JMK02
         STOP                            ; * JMK02
      END                                ; * JMK02
*
* Display screen heading
*
50:   CALL AFS.SCR.HEAD('','','',PROG.NAME,PROG.DESC,PACK.NAME,CO.NAME,TIME.DATE,1)
*
* Get beginning date
*
100:  HELP="Enter beginning date. ''=Accept. 'X'=Exit."
      CALL GEN.IN(0,3,'Enter beginning date.....':DATE()'D2/','DATE',BEG.DATE,0,8,'','',1,-8,3,0,QI,HELP,0,23)
      QI=OCONV(QI,'MCU')
      IF QI='X' ! QI='EX' THEN STOP
      CRT @(25,3):BEG.DATE'D2/'
*
* Get ending date
*
150:  HELP="Enter ending date. ''=Accept. 'X'=Back to beginning date."
      CALL GEN.IN(0,5,'Enter ending date........':DATE()'D2/','DATE',END.DATE,0,8,'','',1,-8,5,0,QI,HELP,0,23)
      QI=OCONV(QI,'MCU')
      BEGIN CASE
         CASE QI='X'
            CRT @(0,5):@(-4)
            GOTO 100
         CASE QI='EX'
            STOP
      END CASE
      CRT @(25,5):END.DATE'D2/'
*
* Get bank account
*
200:  HELP="Enter bank ID. F=Freight Payment. B=Brokerage. V=Vouchers. X=Back, or EX=Exit"
      CRT @(0,7):@(-4)
      CALL GEN.IN(0,7,'Enter bank selection.....- ','',Q,0,2,'','',1,-2,7,0,QI,HELP,0,23)
      QI=OCONV(QI,'MCU')
      BEGIN CASE
         CASE QI='X'
            CRT @(0,7):@(-4)
            GOTO 150
         CASE QI='EX'
            STOP
         CASE ALPHA(QI)
            READV BANK.DESC FROM F.BANKS,"BANK*":QI,1 ELSE
               CALL ELINE("Invalid bank - ":QI)
               GOTO 200
            END
            BANK=QI
         CASE 1
            GOTO 200
      END CASE
      CRT @(25,7):BANK'L#2':BANK.DESC'L#20'
*
* Get display options
*
300:  HELP="S=Screen. P=Printer. X=Back, or EX=Exit"         ; * JMK02
      HELP="S=Screen. P=Printer. D=Download, X=Back, or EX=Exit"       ; * JMK02
      SELECTION=''
      CALL GEN.IN(0,9,'Enter display selection. P ','',Q,0,2,'','',1,-2,9,0,QI,HELP,0,23)
      QI=OCONV(QI,'MCU')
      BEGIN CASE
         CASE QI='X'
            CRT @(0,9):@(-4)
            GOTO 200
         CASE QI='EX'
            STOP
         CASE QI='P' ! QI=''
            SELECTION='P'
         CASE QI='S'
            SELECTION='S'
         CASE QI='D'                     ; * JMK02
            SELECTION='D'                ; * JMK02
         CASE 1
            GOTO 300
      END CASE
*
* Display processing message
*
      TEXT="Processing Checks Cleared with Errors Report - Please Wait."
      CALL CENTER(TEXT,80)
      CRT @(0,12):TEXT
      IF SELECTION='S' THEN EXECUTE '132'
      IF SELECTION='D' THEN EXP.REC=''   ; * JMK02
*
* Select checks with errors
*
*     PREC='SSELECT FB.CHECKS WITH 6 AND WITH 5 GE "':BEG.DATE'D2/':'" AND WITH 5 LE "':END.DATE'D2/':'" AND WITH BANK.ID = "':BANK:'"'     ; * JCW02
      PREC='SSELECT FB.CHECKS.HDR WITH 6 AND WITH 5 GE "':BEG.DATE'D2/':'" AND WITH 5 LE "':END.DATE'D2/':'" AND WITH BANK.ID = "':BANK:'"' ; * JCW02
      EXECUTE PREC PASSLIST CAPTURING OUTPUT
      IF @SELECTED < 1 THEN              ; * JCW01
         CALL ELINE("No checks with errors on file")
         GOTO 50
      END
      NUM.ITMS = @SELECTED
      COUNTER=0
*
* Create report heading
*
      ODATE=BEG.DATE'D2/':' to ':END.DATE'D2/'
      HDG1=''                            ; * JMK01
      HDG2=''                            ; * JMK01
      BEGIN CASE                         ; * JMK01
         CASE BANK='F' OR BANK='V' OR BANK='B'     ; * JMK01
            HDG1='AFS Logistics, LLC'    ; * JMK01
         CASE 1                          ; * JMK01
            HDG1=BANK.DESC               ; * JMK01
            HDG2='A Division of AFS Logistics, LLC'          ; * JMK01
      END CASE                           ; * JMK01
      IF SELECTION='D' THEN              ; * JMK02
         EXP.REC<1>=HDG1:' ':HDG2:' ':PROG.DESC:' ':ODATE:' '          ; * JMK02
      END                                ; * JMK02
*     CALL AFS.PAGE.HEAD(PROG.NAME,'AFS Logistics, LLC','','Checks Cleared with Errors Report',ODATE,132,HED) ; * JMK01
      CALL AFS.PAGE.HEAD(PROG.NAME,HDG1,HDG2,PROG.DESC,ODATE,132,HED)  ; * JMK01
      HED=HED:" 'L'Bank................ Check# Date.... Carrier Name..................    Check Amount  Cleared Amount Amount of Error"
      IF SELECTION='P' THEN PRINTER ON
      IF SELECTION='D' THEN              ; * JMK02
         EXP.REC<3,1>='Bank'             ; * JMK02
         EXP.REC<3,2>='Check#'           ; * JMK02
         EXP.REC<3,3>='Check Date'       ; * JMK02
         EXP.REC<3,4>='Carrier Name'     ; * JMK02
         EXP.REC<3,5>='Check Amount'     ; * JMK02
         EXP.REC<3,6>='Cleared Amount'   ; * JMK02
         EXP.REC<3,7>='Error Amount'     ; * JMK02
      END ELSE                           ; * JMK02
         HEADING HED
      END                                ; * JMK02
*
      LOOP
         READNEXT ID ELSE ID=''
      UNTIL ID='' DO
         COUNTER=COUNTER+1
         IF SELECTION NE 'S' THEN
            PRINTER OFF
            CALL PROG.BAR(22,COUNTER,NUM.ITMS,PER)
            PRINTER ON
         END
         READ CHECK.REC FROM F.CHECKS,ID THEN
            CHECK.NUM=CHECK.REC<1>'R%6'
            CHECK.DATE=CHECK.REC<2>
            CARRIER=CHECK.REC<3>
            READV CARRIER.NAME FROM F.CARRIER,CARRIER,1 ELSE CARRIER.NAME='~~~ Unknown ~~~'
            CHECK.AMT=CHECK.REC<4>'R0'
            CLEARED.AMT=CHECK.REC<6>'R0'
            ERROR.AMT=CHECK.AMT-CLEARED.AMT
            IF ERROR.AMT NE 0 THEN
               GRAND.TOT<1>=GRAND.TOT<1>+CHECK.AMT
               GRAND.TOT<2>=GRAND.TOT<2>+CLEARED.AMT
               GRAND.TOT<3>=GRAND.TOT<3>+ERROR.AMT
               IF SELECTION='D' THEN     ; * JMK02
                  EXP.LINE=''
                  EXP.LINE<1,1>=BANK.DESC          ; * JMK02
                  EXP.LINE<1,2>=CHECK.NUM          ; * JMK02
                  EXP.LINE<1,3>=CHECK.DATE'D2/'    ; * JMK02
                  EXP.LINE<1,4>=CARRIER.NAME       ; * JMK02
                  EXP.LINE<1,5>=CHECK.AMT'R26,'    ; * JMK02
                  EXP.LINE<1,6>=CLEARED.AMT'R26,'  ; * JMK02
                  EXP.LINE<1,7>=ERROR.AMT'R26,'    ; * JMK02
                  EXP.REC<-1>=EXP.LINE   ; * JMK02
               END ELSE                  ; * JMK02
                  PRINT BANK.DESC'L#20':' ':CHECK.NUM:' ':CHECK.DATE'D2/':' ':CARRIER.NAME'L#30':CHECK.AMT'R26,#16':CLEARED.AMT'R26,#16':ERROR.AMT'R26,#16'
               END                       ; * JMK02
            END
         END
      REPEAT
*
      IF SELECTION='D' THEN              ; * JMK02
         EXP.ID='CLRE':BANK:OCONV(BEG.DATE,'DYMD[4,2,2]':@VM:'MCN'):'-':OCONV(END.DATE,'DYMD[4,2,2]':@VM:'MCN'):'.XLS'   ; * JMK02
         EXP.REC=CHANGE(EXP.REC,@VM,CHAR(9))       ; * JMK02
         WRITE EXP.REC ON F.BILLDATA,EXP.ID        ; * JMK02
         CALL ELINE('Your file is called ':EXP.ID:', located on your "U:\" drive')         ; * JMK02
      END ELSE                           ; * JMK02
         PRINT SPACE(68):STR('-',15):' ':STR('-',15):' ':STR('-',15)
         PRINT
         PRINT '   Grand totals':SPACE(52):GRAND.TOT<1>'R26,#16':GRAND.TOT<2>'R26,#16':GRAND.TOT<3>'R26,#16'
         PRINT SPACE(68):STR('=',15):' ':STR('=',15):' ':STR('=',15)
      END                                ; * JMK02
*
      PRINTER OFF
      PRINTER CLOSE
      IF SELECTION='S' THEN
         CALL ELINE('')
         EXECUTE '80'
      END
      STOP
