$OPTIONS HEADER.EJECT
      ERRMSG=''
*
$OPTIONS EXEC.EQ.PERF
$OPTIONS INFO.ABORT
*
* PROGRAM: FB.4.5
* PROGRAM TO REBUILD FREIGHT BILLS CROSS REFERENCE FILE
*
* AUTHOR: BRIAN M. QUEEN
* DATE: SEPTEMBER 19, 1991
*
*******************************************************
* 05/08/06 : JCW01 : CHANGES TO '@SELECTED', 'CLEARSELECT', AND 'STMT<X>'
*******************************************************
* INITIALIZE VARIABLES
*
      PROG.NAME='FB.4.5'
      PROG.DESC='REBUILD FREIGHT BILL CROSS REFERENCE'
      PACK.NAME='FREIGHT BILLING'
      CO.NAME='AFS Logistics, LLC'
      UPL.VIN = 'FBBP' ; UPL.VIN<2> = 'FB.4.5' ; UPL.VOUT = ''         ; * NPR_UPL 04/23/2010
      CALL UPD.PROGRAM.LOG(UPL.VIN,UPL.VOUT)       ; * NPR_UPL 04/23/2010
*
* OPEN FILES
*
      OPEN '','FB.BILLS.CRX' TO F.CRX ELSE CALL OPEN.ABORT("FB.BILLS.CRX",PROG.NAME)
      OPEN '','CLIENTS' TO F.CLIENTS ELSE CALL OPEN.ABORT("CLIENTS",PROG.NAME)
      CALL AFS.SCR.REFRESH(PROG.NAME,PROG.DESC,PACK.NAME,CO.NAME,TIME.DATE,1)
      PRINT @(0,15):'This Program Clears and Rebuilds the FB.BILLS.CRX (Cross Reference File).':
      PRINT @(0,16):'It will take SEVERAL HOURS to complete. No data entry will be allowed':
      PRINT @(0,17):'until the program is finished':
      PRINT @(0,18):'Enter "I-UNDERSTAND" to begin processing: ':
      INPUT NUL
      IF NUL#'I-UNDERSTAND' THEN STOP
*
* SELECT CLIENT FILE
*
      TEXT='Selecting Clients.'
      CALL CENTER(TEXT,80)
      CRT @(0,10):TEXT
*
      EXECUTE 'SSELECT CLIENTS WITH @ID # "99]"' PASSLIST CAPTURING OUTPUT
      ITEMS = @SELECTED
      IF @SELECTED < 1 THEN              ; * JCW01
         CALL ELINE('ERROR - No clients on file.')
         STOP
      END
      SELECT F.CLIENTS TO CLIENT.LIST
*
* REBUILD CROSS REFERENCE FILE
*
      CLEARFILE F.CRX
      COUNTER.1=0
      PER=0
      LOOP
         READNEXT CLIENT.ID FROM CLIENT.LIST ELSE CLIENT.ID=''
         COUNTER.1=COUNTER.1+1
         CRT @(0,17):CLIENT.ID:@(-4):
      UNTIL CLIENT.ID='' DO
         CALL PROG.BAR(20,COUNTER.1,ITEMS,PER.1)
         OPEN '','FB.BILLS.HIST,':CLIENT.ID TO F.BILLS THEN
            EXECUTE 'SELECT FB.BILLS.HIST,':CLIENT.ID PASSLIST CAPTURING OUTPUT
            NUM.BILLS = @SELECTED
            SELECT F.BILLS TO BILLS.LIST
            COUNTER.2=0
            LOOP
               READNEXT PRO.ID FROM BILLS.LIST ELSE PRO.ID=''
            UNTIL PRO.ID='' DO
               COUNTER.2=COUNTER.2+1
               CRT @(0,17):CLIENT.ID:' ':PRO.ID:@(-4):
               CALL PROG.BAR(22,COUNTER.2,NUM.BILLS,PER.2)
               READ PRO.REC FROM F.BILLS,PRO.ID THEN
                  REC=''
                  REC<1>=CLIENT.ID
                  REC<2>=PRO.REC<1>
                  REC<7>=PRO.REC<7>
                  REC<8>=PRO.REC<8>
                  REC<10>=PRO.REC<10>
                  REC<11>=PRO.REC<11>
                  REC<12>=PRO.REC<12>
                  REC<13>=PRO.REC<13>
                  REC<14>=PRO.REC<14>
                  REC<15>=PRO.REC<15>
                  REC<30>=PRO.REC<30>
                  REC<37>=PRO.REC<37>
                  REC<39>=PRO.REC<39>
                  REC<41>=PRO.REC<41>
                  REC<43>=PRO.REC<43>
                  REC<53>=PRO.REC<53>
                  REC<54>=PRO.REC<54>
                  REC<55>=PRO.REC<55>
                  REC<63>=PRO.REC<63>
                  WRITE REC ON F.CRX,PRO.ID
               END
            REPEAT
         END
      REPEAT
      STOP
