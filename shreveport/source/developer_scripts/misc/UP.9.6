$OPTIONS HEADER.EJECT
$OPTIONS EXEC.EQ.PERF
$OPTIONS INFO.ABORT
ERRMSG=''
* PROGRAM UP.9.6
* PROGRAM TO MODIFY FREIGHT BILL LINE ITEM CODES
*
* FREIGHT BILLING PACKAGE
*
* AUTHOR: OWEN HOLMAN AND TAMI SEAGO
* DATE: FEBRUARY 13, 1995 REVISED MARCH 7, 2000
*
* Initialize variables
*
      PROMPT''
      PROG.NAME='UPS.9.6'
      PROG.DESC='TRUCKLOAD ACCESSORIAL CHARGE MAINTENANCE'
      PACK.NAME='UTILITY PROGRAMS'
      CALL AFS.SCR.HEAD(CO.ID,FILE.ID,'',PROG.NAME,PROG.DESC,PACK.NAME,CO.NAME,TIME.DATE,1)
       UPL.VIN = 'FBBP' ;  UPL.VIN<2> = 'UP.9.6' ; UPL.VOUT = ''                    ;* NPR_UPL 04/23/2010
       CALL UPD.PROGRAM.LOG(UPL.VIN,UPL.VOUT)                                                           ;* NPR_UPL 04/23/2010
*
* Open files
*
      OPEN '','FB.TABLES' TO F.TABLES ELSE CALL OPEN.ABORT('FB.TABLES',PROG.NAME)
*
* Read Code Table from File
*
      READ DISP.REC FROM F.TABLES,'TL.ACCESSORIAL.CHRGS' ELSE DISP.REC=''
*
* First page of codes
*
      PAGE=1
      GOSUB 9000
*
* Enter selection code
*
100   HELP="Enter line number. 'B'=Back. 'T'=Top, ''=Next page. 'E'=End-&-Save. 'EX'=Exit."
      CALL GEN.IN(0,22,"Enter line number please. ###",'',Q,0,3,'','',1,-3,22,0,QI,HELP,0,23)
      QI=OCONV(QI,'MCU') 
      BEGIN CASE
        CASE QI=''
          PAGE=PAGE+1
          GOSUB 9000
        CASE QI='B'
          PAGE=PAGE-1
          IF PAGE=0 THEN PAGE=1
          GOSUB 9000
        CASE QI='T'
          PAGE=1
          GOSUB 9000
        CASE QI='EX'
          STOP
        CASE NUM(QI)
          SEL=QI
          IF SEL LT START ! SEL GT STOP THEN
            CALL ELINE('Invalid selection.')
            GOTO 100
          END
          GOSUB 8000
        CASE QI='E'
          X=DCOUNT(DISP.REC<1>,CHAR(253))
          FOR I=X TO 1 STEP -1
            IF DISP.REC<1,I>='' THEN
              DISP.REC=DELETE(DISP.REC,1,I)
              DISP.REC=DELETE(DISP.REC,2,I)
            END
          NEXT I
*
          WRITE DISP.REC ON F.TABLES,'TL.ACCESSORIAL.CHRGS'
          STOP
      END CASE
      GOTO 100
*
*------------------------------------------------------------------------
* INTERNAL SUBROUTINES
*------------------------------------------------------------------------
*
* Edit code
*
8000  ROW=3+REM(SEL,18)
      IF ROW=3 THEN ROW=21
      CODE=DISP.REC<1,SEL>
      DESC=DISP.REC<2,SEL>
8010  HELP="Enter new code. ''=Accept. 'D'=Delete. 'X'=Exit."
      CALL GEN.IN(5,ROW,CODE'L#4','',Q,0,4,'','',2,-4,ROW,0,QI,HELP,0,23)
      QI=OCONV(QI,'MCU')
      IF QI='X' THEN
        CRT @(5,ROW):CODE'L#4'
        RETURN
      END
      IF QI='' THEN
        Q=CODE
        QI=CODE
      END
      IF QI='D' THEN
        FOR X = 1 TO 3
          DISP.REC=DELETE(DISP.REC,X,SEL)
        NEXT X
        GOSUB 9000
        RETURN
      END
      CODE=QI
      CRT @(5,ROW):CODE'L#4'
*
8020  HELP="Enter code description. '<'=Back to code. 'X'=Exit."
      CALL GEN.IN(11,ROW,DESC'L#30','',Q,0,30,'','',2,-30,ROW,0,QI,HELP,0,23)
      QI=OCONV(QI,'MCU') 
      BEGIN CASE
        CASE QI='X' ! QI='<'
          CRT @(11,ROW):DESC'L#30'
          GOTO 8010
        CASE QI=''
          Q=DESC
      END CASE
      DESC=Q
      CRT @(11,ROW):DESC'L#30'
      DISP.REC<1,SEL>=CODE
      DISP.REC<2,SEL>=DESC
      RETURN
*
* Display array
*
9000  CRT @(0,3):@(-3):"  #":@(5,3):"Code  Description"
      START=(PAGE*18)-17
      IF START GT 108 THEN
        START=1
        PAGE=1
      END
      STOP=START+17
      FOR I=START TO STOP STEP 1
        ROW=3+REM(I,18)
        IF ROW=3 THEN ROW=21
        CODE=DISP.REC<1,I>
        DESC=DISP.REC<2,I>
        PRINT @(0,ROW):I"R#3":'  ':CODE"L#4":'  ':DESC"L#30"
      NEXT I
      RETURN
