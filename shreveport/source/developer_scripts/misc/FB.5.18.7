* PROGRAM: FB.5.18.7
* PROGRAM TO CHANGE A VOID DATE
*
* PACKAGE: FREIGHT BILLING
*
* AUTHOR: DAVID R. FRIEND
* DATE: MAY 14, 2003
*
* 091306 : JCW01 : USE NEW FB.CHECKS FILES
*
***********************************************************************
*
*
$INCLUDE PROG.ADMIN INC_OPTIONS
*
* Initialize variables
*
      PROMPT''
      ERRMSG=''
      PROG.NAME='FB.5.18.7'
      PROG.DESC='Change Date of Void'
      PACK.NAME='Freight Billing'
      ORIG.VOID.DATE=''
       UPL.VIN = 'FBBP' ;  UPL.VIN<2> = 'FB.5.18.7' ; UPL.VOUT = ''                    ;* NPR_UPL 04/23/2010
       CALL UPD.PROGRAM.LOG(UPL.VIN,UPL.VOUT)                                                           ;* NPR_UPL 04/23/2010
      NEW.VOID.DATE=''
      CALL GET.USER(USER)
*
* Open files
*
*     OPEN '','FB.CHECKS' TO F.CHECKS ELSE          ; * JCW01
*       CALL OPEN.ABORT("FB.CHECKS",PROG.NAME)      ; * JCW01
*     END                                           ; * JCW01
      OPEN '','FB.CHECKS.HDR' TO F.CHECKS ELSE      ; * JCW01
        CALL OPEN.ABORT("FB.CHECKS.HDR",PROG.NAME)  ; * JCW01
      END                                           ; * JCW01
      OPEN '','FB.MAN.CHECKS' TO F.MAN.CHECKS ELSE
        CALL OPEN.ABORT("FB.MAN.CHECKS",PROG.NAME)
      END
      OPEN '','FB.BANKS' TO F.BANK ELSE
        CALL OPEN.ABORT("FB.BANKS",PROG.NAME)
      END
*
* Display screen heading
*
      CALL AFS.SCR.HEAD('','','',PROG.NAME,PROG.DESC,PACK.NAME,CO.NAME,TIME.DATE,1)
*
* Get beginning check number
*
100   CRT @(0,3):@(-3)
      HELP="Enter the beginning check number to void. ''=Exit."
      CALL GEN.IN(0,3,'Enter Beginning Check Number ######','',Q,0,6,'','',0,-6,3,0,QI,HELP,0,23)
      QI=OCONV(QI,'MCU')
      BEGIN CASE
        CASE QI='' ! QI='EX' ! QI='X'
          STOP
        CASE NUM(QI)
          CHECK.NUM=QI'R%6'
        CASE 1
          CHECK.NUM = QI
      END CASE
      CRT @(29,3):CHECK.NUM'R%6'
*
* Get ending check number
*
200   HELP="Enter the ending check number to void. "
      END.CHECK.NUM=CHECK.NUM
      CALL GEN.IN(0,5,'Enter Ending Check Number ':END.CHECK.NUM'R%6','',Q,0,6,'','',0,-6,5,0,QI,HELP,0,23)
      QI=OCONV(QI,'MCU')
      IF QI='' THEN QI=END.CHECK.NUM
      BEGIN CASE
        CASE QI='EX'
          STOP
       CASE QI='X'
          CRT @(0,5):@(-4)
          GOTO 100
        CASE NUM(QI)
          END.CHECK.NUM=QI'R%6'
        CASE 1
          END.CHECK.NUM = QI
      END CASE
      IF END.CHECK.NUM < CHECK.NUM THEN
        CALL ELINE('Ending Check Number is Lower Than the Beginning Check Number')
        GOTO 200
      END
      CRT @(26,5):END.CHECK.NUM'R%6'
*
* Get bank account
*
300   HELP="Enter bank ID. X=Back or EX=Exit"
      CALL GEN.IN(0,7,'Enter bank selection. - ','',Q,0,2,'','',0,-2,7,0,QI,HELP,0,23)
      QI=OCONV(QI,'MCU')
      BEGIN CASE
        CASE QI='X'
          CRT @(0,7):@(-4)
          GOTO 200
        CASE QI='EX'
          STOP
        CASE ALPHA(QI)
          READ DUMMY FROM F.BANK,'BANK*':QI ELSE
            CALL ELINE("Invalid bank ID.")
            GOTO 300
          END
          BANK=QI
        CASE 1
          GOTO 300
      END CASE
*
* Get orignal void date
*
400   HELP="Enter the orignal date the check was voided.  Must be within 45 days."
      IF ORIG.VOID.DATE='' THEN
        MASK='--/--/--'
      END ELSE
        MASK=ORIG.VOID.DATE'D2/'
      END
      CALL GEN.IN(0,9,'Orignal Void Date. ':MASK'L#8','DATE',Q,0,8,'','',0,-8,9,0,QI,HELP,0,23)
      QI=OCONV(QI,'MCU')
      IF QI='' THEN Q=ORIG.VOID.DATE
      BEGIN CASE
        CASE QI='X'
          CRT @(0,9):@(-4)
          GOTO 300
        CASE QI='EX'
          STOP
        CASE Q > DATE() OR Q < (DATE() - 45)
          CALL ELINE( 'INVALID DATE.' )
          GOTO 400
        CASE 1
          NULL
      END CASE
      ORIG.VOID.DATE=Q
      CRT @(19,9):ORIG.VOID.DATE'D2/'
*
* Get new void date
*
500   HELP="Enter the new date the check should be voided.  Must be within 45 days."
      IF NEW.VOID.DATE='' THEN
        MASK='--/--/--'
      END ELSE
        MASK=NEW.VOID.DATE'D2/'
      END
      CALL GEN.IN(0,11,'New Void Date. ':MASK'L#8','DATE',Q,0,8,'','',0,-8,11,0,QI,HELP,0,23)
      QI=OCONV(QI,'MCU')
      IF QI='' THEN Q=NEW.VOID.DATE
      BEGIN CASE
        CASE QI='X'
          CRT @(0,11):@(-4)
          GOTO 400
        CASE QI='EX'
          STOP
        CASE Q > DATE() OR Q < (DATE() - 45)
          CALL ELINE( 'INVALID DATE.' )
          GOTO 500
        CASE 1
          NULL
      END CASE
      NEW.VOID.DATE=Q
      CRT @(15,11):NEW.VOID.DATE'D2/'
*
* Ask if information is correct
*
600   HELP="Enter Y if the information is correct. 'X'=Back"
      CALL GEN.IN(0,22,"Information Correct? Y",'',Q,0,1,'','',0,-1,22,1,QI,HELP,0,23)
      QI=OCONV(QI,'MCU')
      BEGIN CASE
        CASE QI='EX'
          STOP
        CASE QI='X'
          GOTO 500
        CASE QI='Y' OR QI = ''
          NULL
        CASE QI='N'
           GOTO 500
        CASE 1
          GOTO 500
      END CASE
*
* Check to see if the check range has been voided and the void date is
* the orignal void date
*
      OK.TO.PROCESS=1
      FOR XX = CHECK.NUM TO END.CHECK.NUM
        CHECK.ID=XX:'*':BANK
        READ CHECK.REC FROM F.CHECKS, CHECK.ID THEN
          IF CHECK.REC<7>='Void' AND CHECK.REC<9>=ORIG.VOID.DATE ELSE
            OK.TO.PROCESS=0
          END
        END ELSE
          OK.TO.PROCESS=0
        END
      NEXT XX
     IF NOT(OK.TO.PROCESS) THEN
       CALL ELINE('The range cannot be processed. Check range for only void check of orignal void date')
       GOTO 100
     END
      FOR XX = CHECK.NUM TO END.CHECK.NUM
        CHECK.ID=XX:'*':BANK
        READU CHECK.REC FROM F.CHECKS, CHECK.ID THEN
          CHECK.REC<5>=NEW.VOID.DATE
          CHECK.REC<9>=NEW.VOID.DATE
          WRITE CHECK.REC ON F.CHECKS,CHECK.ID
        END ELSE
          RELEASE
          CALL ELINE(CHECK.ID:' does not exist skipping')
        END
      NEXT XX
      CALL ELINE('Process Has Complete')
*
    GOTO 100
*
********************************************************************
*
 END
