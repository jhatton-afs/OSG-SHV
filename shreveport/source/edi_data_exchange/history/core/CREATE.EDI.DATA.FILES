*******************************************************
* Program name : CREATE.EDI.DATA.FILES
* Author       : JAY WEBERT
* Date written : JULY 20, 2006
* Library      : EDIBP
* COPYRIGHT (C) 2016 - AFS LOGISTICS LLC - ALL RIGHTS RESERVED.
*
* THIS PROGRAM COPIES RECORDS FROM EDI.BILLS.HIST AND WRITES THEM
* TO THEIR NEW LOCATION IS SUBFILES UNDER EDI.BILLS.HIST.NEW
*
* Last updated by afs (AFSLOGISTICS\nmeddaugh) at 09:22:51 on 03/21/2019.
*
* Start modification history
* 03/21/19 : NKH01 : ACCU-316 Modified CREATE-FILE file sizes
* End modification history
*
*******************************************************
      * INITIALIZATION
      *----------------------
      STMT = 'SLEEP 23:00'
      EXECUTE STMT
      EXECUTE 'DATE'
      EXECUTE 'DATE' CAPTURING MSG
      START.TIME = MSG
      PROG.NAME = 'CREATE.EDI.DATA.FILES'
      REC.CNT = 0
      OPEN 'EDI.BILLS.HIST' TO F.EDI.OLD ELSE
        CALL OPEN.ABORT('EDI.BILLS.HIST', PROG.NAME)
      END
      *-----------------
      * MAIN
      *-----------------
      *** SORT/SELECT ALL RECORDS ***
      STMT = 'SSELECT EDI.BILLS.HIST'
      CRT STMT
      EXECUTE STMT
      IF @SELECTED < 1 THEN
        CRT 'NO RECORDS SELECTED; PROGRAM STOPPED.'
        STOP
      END
      EXECUTE 'DATE'
      *** MAIN LOOP ***
      PREV.CLIENT.ID = 'X'
      DONE = 0
      LOOP
        READNEXT EDI.ID ELSE DONE = 1
      UNTIL DONE DO
        ***
        *** PROGRESS DISPLAY
        ***
        REC.CNT += 1
        IF MOD(REC.CNT,300000) = 0 THEN
          EXECUTE 'DATE' CAPTURING MSG
          CRT REC.CNT:' RECORDS PROCESSED AT ':MSG
        END
        ***
        *** HANDLE NEW CLIENT ID
        ***
        CALL PARSE.EDI.ID(EDI.ID, CLIENT.ID, EDI.HIST.ID)
        IF PREV.CLIENT.ID # CLIENT.ID THEN
          PREV.CLIENT.ID = CLIENT.ID
          OPEN 'EDI.BILLS.HIST.NEW,':CLIENT.ID TO F.EDI.HIST ELSE
            STMT = 'CREATE-FILE DATA EDI.BILLS.HIST.NEW,':CLIENT.ID:' 1009,2'    ; * NKH01
            CRT STMT
            EXECUTE STMT
            OPEN 'EDI.BILLS.HIST.NEW,':CLIENT.ID TO F.EDI.HIST ELSE
              CALL OPEN.ABORT('EDI.BILLS.HIST.NEW,':CLIENT.ID, PROG.NAME)
            END
          END
        END
        ***
        *** COPY RECORD TO NEW DATA FILE
        ***
        READ EDI.REC FROM F.EDI.OLD, EDI.ID THEN
          WRITE EDI.REC TO F.EDI.HIST, EDI.HIST.ID ELSE
            CRT 'COULD NOT WRITE ':EDI.ID:' TO DATA FILE FOR ':CLIENT.ID
          END
        END ELSE
          CRT 'COULD NOT READ ':EDI.ID:' RECORD FROM ORIGINAL FILE'
        END
      REPEAT
      ***
      *** FINALIZE
      ***
      STOP.TIME = OCONV(TIME(),'MT')
      CRT 'START TIME WAS ':START.TIME
      CRT 'STOP TIME WAS ':STOP.TIME

