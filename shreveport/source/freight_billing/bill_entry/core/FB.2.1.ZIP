      SUBROUTINE FB.2.1.ZIP(ZIP.ARR, QI)
*
 UPL.VIN = 'FBBP' ;  UPL.VIN<2> = 'FB.2.1.ZIP' ; UPL.VOUT = ''                    ;* NPR_UPL 04/23/2010
 CALL UPD.PROGRAM.LOG(UPL.VIN,UPL.VOUT)                                                           ;* NPR_UPL 04/23/2010
* SUBROUTINE TO ENTER ZIP CODES
*
* FREIGHT BILLING PACKAGE
*
* AUTHOR:      VALERIE DOTY
* DATE:        JULY 21, 1994
* DESCRIPTION: THIS PROGRAM ALLOWS THE ENTRY OF UP TO 50 ZIP CODES FOR
*              THE ROUTE OF THIS SHIPMENT.  THE FIRST ZIP MUST BE THE
*              ORIGIN, THE LAST MUST BE THE DESTINATION, AND ALL THE
*              ZIPS MUST BE IN ORDER SO THE MILEAGE WILL CALCULATE
*              CORRECTLY.  COPIED FROM FB.2.1.DIST.
*
* THIS PROGRAM IS A PROPRIETARY PRODUCT OF AUTOMATED FREIGHT SYSTEMS
* AND MAY NOT BE USED, COPIED OR MODIFIED WITHOUT THE EXPRESS
* WRITTEN CONSENT OF SAME.
*
*********************************************************************
* 040909 - JMK01 - T090486 - Hussey Copper ; can't pick up additional mileage with canadian stopoffs
*
$INCLUDE PROG.ADMIN INC_OPTIONS
*
* Initialize variables
*
      PROMPT ''
      OPT = QI
      IF OPT = 'I' THEN
         INQUIRE = 1
         MODE = 'EDIT'
      END ELSE
         INQUIRE = 0
         MODE = 'INPUT'
      END
      LN = 0
      CALL FB.CLEAR.WINDOW
*
* Display headings
*
      IF OPT = 'V' THEN
         CRT @(39,6):"--------------- Zip Codes --------------"
         CRT @(39,7):" Zip Code":@(56):"City and State"
         CRT @(40,8):STR("-",39)
      END ELSE
         CRT @(36,6):"---------------- Zip Codes ----------------"
         CRT @(36,7):"Lin Zip Code":@(56):"City and State"
         CRT @(36,8):STR("-",43)
      END
*
* Start input
*
      PAGE = 1
      GOSUB 9000
*
* Enter zip code line number.
*
100:  IF MODE = 'INPUT' THEN
         LN += 1
         QI = LN
         IF LN GT STOP THEN
            PAGE += 1
            GOSUB 9000
         END
      END ELSE
         HELP = "Enter Line Num. ''=End. 'B'=Back. 'F'=Forward page. 'EX'=Exit."
         CALL GEN.IN(36,18,'Enter line number. ###','',Q,0,3,'','',0,-3,
            18,0,QI,HELP,0,23)
      END
      QI = OCONV(QI,'MCU')
      BEGIN CASE
         CASE QI = 'B'
            PAGE -= 1
            IF PAGE = 0 THEN PAGE=1
            GOSUB 9000
         CASE QI = 'F'
            PAGE += 1
            GOSUB 9000
         CASE QI = ''
            GOTO 9900
         CASE QI = 'X'
            CALL FB.CLEAR.WINDOW
            RETURN
         CASE QI = 'EX'
            RELEASE
            STOP
         CASE NUM(QI) AND INQUIRE
            CALL ELINE('Cannot access detail in inquiry.')
         CASE NUM(QI)
            SEL = QI
            GOSUB 8000
            BEGIN CASE
               CASE QI = '' & OPT='V'
                  RETURN
               CASE QI = 'X'
                  CALL FB.CLEAR.WINDOW
                  RETURN
               CASE LN + 1 > 50 AND MODE = 'INPUT'
                  MODE = 'EDIT'
               CASE 1
                  NULL
            END CASE
      END CASE
*
      GOTO 100
*
*-----------------------------------------------------------------------
*
* INTERNAL SUBROUTINES
*
*-----------------------------------------------------------------------
*
* Enter zip codes
*
8000: IF SEL LT START ! SEL GT STOP THEN
         CALL ELINE('Invalid entry.')
         RETURN
      END
*
      GOSUB 9100
*
8010: HELP = "Enter Zip Code. "
      IF OPT = 'V' THEN
         MASK = SPACE(5)
         HELP := "''=End. 'X'=Back. 'EX'=Exit."
      END ELSE
         HELP := "''=Accept. 'D'=Delete. 'E'=Change Mode. 'X'=Back. 'EX'=Exit."
         MASK = ZIP.ARR<1,SEL>'L#5'
      END
*     CALL GEN.IN(40,LINE,MASK,'',Q,0,5,'','',2,-5,LINE,0,QI,HELP,0,23)          ; * JMK01
      CALL GEN.IN(40,LINE,MASK,'',Q,0,6,'','',2,-6,LINE,0,QI,HELP,0,23)          ; * JMK01
      QI = OCONV(QI,'MCU')
      BEGIN CASE
         CASE QI = 'EX'
            RELEASE
            STOP
         CASE QI = 'X'
*           IF OPT NE 'V' THEN CRT @(40,LINE):ZIP.ARR<1,SEL>'L#5'      ; * JMK01
            IF OPT NE 'V' THEN CRT @(40,LINE):ZIP.ARR<1,SEL>'L#6'      ; * JMK01
            RETURN
         CASE QI = 'D' & OPT NE 'V'
            DEL ZIP.ARR<1,SEL>
            DEL ZIP.ARR<2,SEL>
            DEL ZIP.ARR<3,SEL>
            GOSUB 9000
            RETURN
         CASE QI = 'E' & OPT NE 'V'
            MODE = 'EDIT'
*           CRT @(40,LINE):ZIP.ARR<1,SEL>'L#5'     ; * JMK01
            CRT @(40,LINE):ZIP.ARR<1,SEL>'L#6'     ; * JMK01
            RETURN
         CASE QI = '' & OPT NE 'V'
            Q = ZIP.ARR<1,SEL>
            IF Q = '' & MODE = 'INPUT' THEN
               MODE = 'EDIT'
               RETURN
            END
            IF Q = '' THEN RETURN
         CASE QI = ''
            X = DCOUNT(ZIP.ARR<1>,CHAR(253))
            FOR I = 1 TO X
               IF ZIP.ARR<4,I> NE '*' THEN
                  CALL ELINE('All zip codes have not been verified.')
                  GOTO 8010
               END
            NEXT I
            RETURN
         CASE OPT = 'V'
            IF QI # ZIP.ARR<1,SEL> THEN
               CALL ELINE('INVALID ZIP CODE.')
               GOTO 8010
            END
            ZIP.ARR<4,SEL> = '*'
*        CASE NUM(QI) AND LEN(QI) < 6    ; * JMK01
         CASE NUM(QI) AND LEN(QI) < 7    ; * JMK01
            ZIP.ARR<1,SEL> = Q
         CASE 1
            CALL ELINE('INVALID ZIP CODE.')
            GOTO 8010
      END CASE
*
      IF OPT NE 'V' THEN
         CODE = ZIP.ARR<1,SEL>
         GOSUB 8090
         IF ACK = 'ERR' THEN GOTO 8010
*        CRT @(40,LINE):ZIP.ARR<1,SEL>'L#5'        ; * JMK01
         CRT @(40,LINE):ZIP.ARR<1,SEL>'L#6'        ; * JMK01
      END
*
      RETURN
*
* Check for valid zip code
*
8090: IF CODE # '' THEN
         CITY='' ; STATE=''
         CALL FB.READ.ZIP( CODE, CITY, STATE )
         IF CITY='' THEN ACK='ERR' ; RETURN
         ZIP.ARR<2,SEL> = CITY
         ZIP.ARR<3,SEL> = STATE
      END
      CRT @(56,LINE) : ZIP.ARR<2,SEL> : ' ' : ZIP.ARR<3,SEL>:@(-4):
      ACK = ''
*
      RETURN
*
* Print page
*
9000: START = (PAGE * 9) - 8
      STOP = START + 8
      FOR SEL = START TO STOP
         GOSUB 9100
         IF OPT NE 'V' THEN
            IF ZIP.ARR<1,SEL> = '' THEN
               CRT @(36,LINE):SEL'L#3':@(-4):
            END ELSE
*              CRT @(36,LINE):SEL'L#3':' ':ZIP.ARR<1,SEL>'L#5':        ; * JMK01
               CRT @(36,LINE):SEL'L#3':' ':ZIP.ARR<1,SEL>'L#6':        ; * JMK01
               IF ZIP.ARR<2,SEL> = '' AND ZIP.ARR<3,SEL> = '' THEN
                  CODE = ZIP.ARR<1,SEL>
                  GOSUB 8090
               END ELSE
                  CRT @(56,LINE):ZIP.ARR<2,SEL>:' ':ZIP.ARR<3,SEL>:@(-4):
               END
            END
         END
      NEXT SEL
*
      RETURN
*
* Calculate line
*
9100: X = REM(SEL,9)
      IF X = 0 THEN X = 9
      LINE = 8 + X
*
      RETURN
*
* Compress zip codes and return
*
9900: IF NOT(INQUIRE) THEN
         TIMES = DCOUNT(ZIP.ARR<1>,CHAR(253))
         NEW.ZIP.ARR=''
         LAST.ZIP='FIRST'
         FOR I = 1 TO TIMES
           IF ZIP.ARR<1,I>#'' THEN
             IF LAST.ZIP=ZIP.ARR<1,I> AND LAST.ZIP#'FIRST' ELSE
               FOR X = 1 TO 4
                 NEW.ZIP.ARR<X,-1>=ZIP.ARR<X,I>
               NEXT X
*              LAST.ZIP=ZIP.ARR<1,I> ; * Allow Dups Per Paul
             END
           END
         NEXT I
      END
      ZIP.ARR=NEW.ZIP.ARR
      CALL FB.CLEAR.WINDOW
*
      RETURN
*
**********************************************************************
*
   END
